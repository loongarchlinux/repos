# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=(
  lib32-libxkbcommon
  lib32-libxkbcommon-x11
)
pkgver=1.3.1
pkgrel=1
pkgdesc='Keymap handling library for toolkits and window systems (32-bit)'
url=https://xkbcommon.org/
arch=(loongarch64 x86_64)
license=(custom)
makedepends=(
  git
  lib32-libxcb
  lib32-libxml2
  lib32-wayland
  meson
  wayland-protocols
  xorg-util-macros
)
checkdepends=(
  xorg-server-xvfb
  libgl
)
_tag=e97297b5ce1201a3992f2de5fb1118d73eb904e4
source=(git+https://github.com/xkbcommon/libxkbcommon#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd libxkbcommon

  git describe --tags | sed 's/^xkbcommon-//'
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libxkbcommon build \
    --libdir=/usr/lib32 \
    -D enable-docs=false
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_lib32-libxkbcommon() {
  depends=(
    lib32-libxml2
    libxkbcommon
  )
  provides=(
    libxkbcommon.so
    libxkbregistry.so
  )

  meson install -C build --destdir "${pkgdir}"
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

  mkdir -p x11/usr/lib32/pkgconfig
  mv "${pkgdir}"/usr/lib32/*x11* x11/usr/lib32
  mv "${pkgdir}"/usr/lib32/pkgconfig/*x11* x11/usr/lib32/pkgconfig
}

package_lib32-libxkbcommon-x11() {
  pkgdesc='Keyboard handling library using XKB data for X11 XCB clients (32-bit)'
  depends=(
    lib32-libxkbcommon
    libxkbcommon.so
    lib32-libxcb
    libxkbcommon-x11
  )
  provides=(
    libxkbcommon-x11.so
  )

  mv x11/* "${pkgdir}"/
  find "${pkgdir}" -type d -exec chmod 755 {} +

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
