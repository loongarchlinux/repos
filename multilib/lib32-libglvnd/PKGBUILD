# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=1.4.0
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('loongarch64' 'x86_64')
url="https://gitlab.freedesktop.org/glvnd/libglvnd"
license=('custom:BSD-like')
makedepends=('lib32-libx11' 'lib32-libxext' 'xorgproto' 'python' 'meson')
provides=('lib32-libgl' 'lib32-libegl' 'lib32-libgles')
options=('debug')
source=("https://gitlab.freedesktop.org/glvnd/libglvnd/-/archive/v$pkgver/libglvnd-v$pkgver.tar.gz")
sha512sums=('ca741bdd7ac2fba919f1f85e685e92396bc1cdc436e77bca921d87738bb273a6d48f77650e2781593ce9084dcec6b1fa8f78dc84534a225629e7f61d92df8d89')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libglvnd-v$pkgver build \
    --libdir=/usr/lib32 \
    -D headers=false \
    -D gles1=false

  meson compile -C build
}

package() {
  # lib32-libglvnd needs lib32-mesa for indirect rendering
  depends=('lib32-libxext' 'libglvnd' 'lib32-mesa' 'lib32-opengl-driver')

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/include

  mkdir -p "$pkgdir"/usr/share/licenses
  ln -s libglvnd "$pkgdir"/usr/share/licenses/$pkgname
}
