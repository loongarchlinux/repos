# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-gdk-pixbuf2
pkgver=2.40.0+6+g5432316df
pkgrel=1
pkgdesc="An image loading library (32-bit)"
arch=(loongarch64 x86_64)
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=(LGPL2.1)
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg lib32-libx11 gdk-pixbuf2)
makedepends=(git meson)
install=lib32-gdk-pixbuf2.install
_commit=5432316df6b8106d279c4972ebade240edd66d13  # gdk-pixbuf-2-40
source=("git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=$_commit"
        gdk-pixbuf-query-loaders-32.hook)
sha256sums=('SKIP'
            '60f0f8c2c022f9adef0902bdb4b89881e81f3388cd2e2e4830456eea10f49bae')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gdk-pixbuf
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson gdk-pixbuf build \
    --libdir /usr/lib32 \
    -D gir=false \
    -D installed_tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set ts=2 sw=2 et:
