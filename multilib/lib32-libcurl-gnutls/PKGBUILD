# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lizao (Larry) Li <lzlarryli@gmail.com>

pkgname=lib32-libcurl-gnutls
pkgver=7.73.0
pkgrel=1
pkgdesc='An URL retrieval library (linked against gnutls)'
arch=('loongarch64' 'x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('lib32-curl' 'lib32-glibc' 'lib32-gnutls' 'lib32-libidn2'
         'lib32-libpsl' 'lib32-nettle' 'lib32-libssh2' 'lib32-zlib'
         'lib32-zstd' 'libcurl-gnutls')
makedepends=('gcc-multilib')
options=('strip')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha512sums=('86996f7ce537cfc39be18a6a93af0524c7a878d9e003ce0106eb4ff6ce2dea99da1cb9af297b87f93b204b42a3d793198a6e01764aa2a06f5b6fe0623b008f88'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd curl-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --without-gssapi \
    --with-libssh2 \
    --without-libidn \
    --without-ssl \
    --with-gnutls='/usr' \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4.7.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0; do
    ln -s libcurl-gnutls.so.4.7.0 "${pkgdir}"/usr/lib32/libcurl-gnutls.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libcurl-gnutls "${pkgdir}"/usr/share/licenses/lib32-libcurl-gnutls
}

# vim: ts=2 sw=2 et:
