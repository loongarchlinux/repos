# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-libxcrypt
pkgver=4.4.27
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('loongarch64' 'x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('gcc-multilib')
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("${url}/releases/download/v${pkgver}/libxcrypt-${pkgver}.tar.xz"{,.asc})
sha256sums=('500898e80dc0d027ddaadb5637fa2bf1baffb9ccd73cd3ab51d92ef5b8a1f420'
            'SKIP')

build() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 
}

package() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
