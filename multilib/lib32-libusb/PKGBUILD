# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

_name=libusb
pkgname=lib32-libusb
pkgver=1.0.23
pkgrel=2
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=('x86_64')
url="http://libusb.info/"
license=('LGPL2.1')
depends=('lib32-glibc' 'lib32-systemd' 'libusb')
provides=('libusb-1.0.so')
makedepends=('gcc-multilib')
source=("https://github.com/${_name}/${_name}/releases/download/v$pkgver/${_name}-$pkgver.tar.bz2")
sha512sums=('73430ad31dd405e76a9c6c55086961003989c44964e2735b5076f9ca849e3f6fe71d10a66b74b111f0c34a0d2cb744b9ca105863c552123fce4ec0bbdd2ec15b')
b2sums=('4eaf8cce0047b85c3057c67b53769739c50a39c4d34e94659b030fb11f02309f9217e93d35d78f699c55256346cf8b14827c8ddf495caeeacc522c51f473abc2')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
