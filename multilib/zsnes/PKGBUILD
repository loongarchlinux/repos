# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=1.51
pkgrel=20
pkgdesc="Super Nintendo emulator"
url="http://www.zsnes.com/"
arch=('x86_64')
license=('GPL')
options=(!buildflags)

makedepends=('nasm' 'gendesk' 'mesa')
depends_x86_64=('lib32-gcc-libs' 'lib32-sdl' 'lib32-libpng>=1.5.0' 'lib32-libgl' 'lib32-ncurses')
optdepends_x86_64=('lib32-alsa-lib: sound support')
makedepends_x86_64=('gcc-multilib')

depends_i686=('gcc-libs' 'sdl' 'libpng>=1.5.0' 'libgl' 'ncurses')
optdepends_i686=('alsa-lib: sound support')
makedepends_i686=('nasm' 'gcc' 'gendesk' 'mesa')

source=(http://downloads.sourceforge.net/project/zsnes/zsnes/ZSNES%20v1.51/zsnes151src.tar.bz2
        zsnes.patch
        zsnes-1.51-libpng15.patch
        zsnes.png)
sha512sums=('b10373cf81b201a33dc60c728431d4f69d36de31fcf6f137b623ea206a2998f722dcd6132daec1d0bf14a05ae52f3ee9ecee4334517039be554db85c9f7b8274'
            'a89bd37a800627f80e5385be83dde0db7da2570d1479a4d265c2493033bc57e46d220ee496eff9353e7c61b4b5e321b7db8199334227bfb9b7f65b25fec52012'
            '4191987cad2f8dd7d7fd1d4f8f0c60de07059f18a60cb9bb94f1e5c35c06122032de9887762ebcf2d255f24c3e90aa5b4f6a7540a8e4dbdc4ce5f7d3b3e3b532'
            '50a3392de384f3279ec2c8aa0d7e4cedca4b0ff0b8dc6b1b265a84d8e5d91589b23fa52ae0f083c93e638ee9a785b2f18636de6146145dc09d13174bef2511f6')

prepare() {
  cd "$srcdir/${pkgname}_1_51"

  patch -p1 -i ../zsnes.patch
  patch -p0 -i ../zsnes-1.51-libpng15.patch
}

build() {
  cd "$srcdir/${pkgname}_1_51/src"

  if [ $CARCH == "x86_64" ]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
  fi

  ./configure \
    --prefix=/usr \
    --enable-release \
    x_libraries=/usr/lib \
    force_arch=i686

  make
}

package() {
  cd "$srcdir"

  gendesk "$startdir/PKGBUILD"

  cd "${pkgname}_1_51/src"

  make DESTDIR=${pkgdir} install

  # adding --mandir= to autoconf line fails
  install -dm755 ${pkgdir}/usr/share

  install -Dm644 ${srcdir}/zsnes.desktop ${pkgdir}/usr/share/applications/zsnes.desktop
  install -Dm644 ${srcdir}/zsnes.png ${pkgdir}/usr/share/pixmaps/zsnes.png

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}

# vim:set ts=2 sw=2 et:
