# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-glib2
pkgver=2.66.1
pkgrel=2
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
arch=(x86_64)
depends=(lib32-pcre lib32-libffi lib32-util-linux lib32-zlib glib2)
makedepends=(gettext gtk-doc shared-mime-info python lib32-libelf git util-linux
             meson lib32-dbus)
checkdepends=(desktop-file-utils lib32-glib2)
_commit=b2f8ba8a11482dc702a470e92552cbb97765dbc9  # tags/2.66.1^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        1683.patch
        multilib.diff
        gio-querymodules-32.hook)
sha256sums=('SKIP'
            'e1913090c7cdd4c7db12651858a8381be28ae61f19d5e5e02a33f4c7c74c926d'
            '2611ec4c97346c3c4eea61786ee20c801207135a9d52a771f5f340b41bf69fbc'
            'c37b2777d6c7b9d2eb6003d8bdb72175f889013bc4d582b3f7a12fc7419a2d6d')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib

  # https://gitlab.gnome.org/GNOME/glib/-/issues/2219
  git apply -3 ../1683.patch

  # Test suite workaround
  git apply -3 ../multilib.diff
}

build() {
  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  arch-meson glib build \
    -D selinux=disabled \
    --libdir=/usr/lib32
  meson compile -C build
}

check() {
  # Spurious wakeups in g_cond_wait_until, only on lib32
  meson test -C build --no-suite flaky --no-suite slow --print-errorlogs || :
}

package() {
  depends+=(libmount.so)
  provides+=(libgio-2.0.so libglib-2.0.so libgmodule-2.0.so libgobject-2.0.so
             libgthread-2.0.so)

  DESTDIR="$pkgdir" meson install -C build

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 et:
