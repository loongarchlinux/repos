# Maintainer: Daniel Wallace <danielwallace@aur.archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lib32-curl
_pkgname=curl
pkgver=7.77.0
pkgrel=1
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('loongarch64' 'x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('lib32-libidn2' 'lib32-libssh2' 'lib32-krb5' 'lib32-openssl'
         'lib32-libpsl' 'lib32-zlib' 'lib32-zstd' "${_pkgname}")
source=("https://curl.haxx.se/download/$_pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('cacd85eb9b2ad90f59595b497035f4660b6c5cff2d653da939c9ceac6dd8dd4bc7bdcb2dfd251862af0c29b9299312bf7271ed0249734fca979c9588799635d6'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd "$_pkgname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-dependency-tracking \
    --disable-ldap \
    --disable-ldaps \
    --enable-ipv6 \
    --enable-manual \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-libssh2 \
    --with-openssl \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --libdir=/usr/lib32

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{share,bin,include}

  # license
  install -d "$pkgdir/usr/share/licenses"
  ln -s "$_pkgname" "$pkgdir/usr/share/licenses/$pkgname"
}
