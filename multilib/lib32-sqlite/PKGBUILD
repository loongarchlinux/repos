# Maintainer: Biru Ionut <ionut@archlinux.ro>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Contributor: Kaos < gianlucaatlas dot gmail dot com >

_pkgbasename=sqlite
pkgname=lib32-sqlite
_srcver=3360000
pkgver=3.36.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine (32-bit)"
arch=('loongarch64' 'x86_64')
license=('custom')
url="https://www.sqlite.org/"
depends=(lib32-glibc $_pkgbasename)
makedepends=('tcl' 'lib32-readline')
source=(https://www.sqlite.org/2021/sqlite-src-${_srcver}.zip)
# upstream now switched to sha3sums - currently not supported by makepkg
sha256sums=('25a3b9d08066b3a9003f06a96b2a8d1348994c29cc912535401154501d875324')

prepare() {
  cd "$srcdir"/sqlite-src-$_srcver
  autoreconf -vfi
}

build() {
  cd "$srcdir"/sqlite-src-$_srcver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 \
                         -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                         -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                         -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
                         -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
                         -DSQLITE_ENABLE_RTREE=1 \
                         -DSQLITE_SECURE_DELETE \
                         -DSQLITE_MAX_VARIABLE_NUMBER=250000 \
                         -DSQLITE_MAX_EXPR_DEPTH=10000"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-tcl \
    --disable-static

  make
}

package() {
  cd "$srcdir"/sqlite-src-$_srcver

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}"/usr/share/licenses
  ln -s $_pkgbasename "${pkgdir}"/usr/share/licenses/$pkgname
}
