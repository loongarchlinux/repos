# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgbasename=util-linux
pkgname=lib32-$_pkgbasename
_pkgmajor=2.36
pkgver=${_pkgmajor}
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url='https://github.com/karelzak/util-linux'
arch=('loongarch64' 'x86_64')
depends=('lib32-glibc' "$_pkgbasename" 'lib32-libxcrypt')
provides=('libuuid.so' 'libblkid.so' 'libfdisk.so' 'libmount.so' 'libsmartcols.so')
license=('GPL2')
options=('!emptydirs')
validpgpkeys=('B0C64D14301CC6EFAEDF60E4E4B71D5EEC39C284')  # Karel Zak
source=("https://www.kernel.org/pub/linux/utils/util-linux/v$_pkgmajor/$_pkgbasename-$pkgver.tar."{xz,sign})
sha256sums=('9e4b1c67eb13b9b67feb32ae1dc0d50e08ce9e5d82e1cccd0ee771ad2fa9e0b1'
            'SKIP')

build() {
  cd "$_pkgbasename-$pkgver"

  ./configure \
      CC="${CC:-cc} -m32" \
      PKG_CONFIG_PATH="/usr/lib32/pkgconfig" \
      --libdir=/usr/lib32

  make lib{uuid,blkid,fdisk,mount,smartcols}.la
}

package() {
  make -C "$_pkgbasename-$pkgver" \
    DESTDIR="$pkgdir" \
    install-usrlib_execLTLIBRARIES \
    install-pkgconfigDATA
}
