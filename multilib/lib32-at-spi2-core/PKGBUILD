# Maintainer:m Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-at-spi2-core
pkgver=2.40.2
pkgrel=1
pkgdesc='Protocol definitions and daemon for D-Bus at-spi'
arch=(loongarch64 x86_64)
url=https://www.gnome.org
license=(GPL2)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-libxtst
)
makedepends=(
  git
  gtk-doc
  intltool
  meson
)
_tag=45ab48720072d263798124f7e6dcc3ad3dc62256
source=(git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd at-spi2-core

  git describe --tags | sed 's/^AT_SPI2_CORE_//; s/_/./g'
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson at-spi2-core build \
    --libdir=/usr/lib32 \
    -D enable_docs=false \
    -D introspection=no
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim: ts=2 sw=2 et:
