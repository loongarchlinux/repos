# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
# Contributor: vigo <vigo.the.unholy.carpathian@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lib32-libwrap
pkgver=7.6.31
pkgrel=3
pkgdesc='Monitors and controls incoming TCP connections'
arch=(loongarch64 x86_64)
url=ftp://ftp.porcupine.org/pub/security/index.html
license=(BSD)
depends=(
  lib32-glibc
  lib32-libnsl
  libwrap
)
provides=(lib32-tcp_wrappers-libs)
conflicts=(lib32-tcp_wrappers-libs)
options=(!makeflags)
source=(
  ftp://ftp.porcupine.org/pub/security/tcp_wrappers_${pkgver%.*}.tar.gz
  http://http.debian.net/debian/pool/main/t/tcp-wrappers/tcp-wrappers_${pkgver%.*}.q-${pkgver##*.}.debian.tar.xz
  tcp-wrappers-redhat-bug11881.patch
)
sha256sums=('9543d7adedf78a6de0b221ccbbd1952e08b5138717f4ade814039bb489a4315d'
            '20fec7826bde1262007e6dd43f365b5802bacc283942d93b78385ea634dc594a'
            '2e527e54c1ea208de10a206c667f751e54651eb77c973271d213e3459d690403')

prepare() {
  cd tcp_wrappers_${pkgver%.*}

  for patch in $(cat ../debian/patches/series); do
    patch -Np1 -i ../debian/patches/${patch}
  done
  patch -Np1 -i ../tcp-wrappers-redhat-bug11881.patch
}

build() {
  cd tcp_wrappers_${pkgver%.*}

  make \
    COPTS="$CFLAGS -m32" \
    LDOPTS="$LDFLAGS -m32" \
    REAL_DAEMON_DIR=/usr/bin \
    STYLE=-DPROCESS_OPTIONS \
    linux
}

package() {
  cd tcp_wrappers_${pkgver%.*}

  install -Dm 644 shared/libwrap.so.0.${pkgver%.*} -t "${pkgdir}"/usr/lib32/
  ln -s libwrap.so.0.${pkgver%.*} "${pkgdir}"/usr/lib32/libwrap.so.0
  ln -s libwrap.so.0 "${pkgdir}"/usr/lib32/libwrap.so

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libwrap "${pkgdir}"/usr/share/licenses/lib32-libwrap
}

# vim: ts=2 sw=2 et:
