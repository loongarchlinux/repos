# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-expat
pkgver=2.2.9
pkgrel=1
pkgdesc='An XML Parser library'
arch=(x86_64)
url=https://libexpat.github.io/
license=(custom)
depends=(
  expat
  lib32-glibc
)
makedepends=(
  docbook2x
  git
)
source=(git+https://github.com/libexpat/libexpat.git#tag=a7bc26b69768f7fb24f0c7976fae24b157b85b13)
sha512sums=(SKIP)

pkgver() {
  cd libexpat

  git describe --tags | sed 's/R_//; s/_/./g'
}

prepare() {
  cd libexpat/expat

  autoreconf -fiv
}

build() {
  cd libexpat/expat

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --mandir=/usr/share/man
  make
}

package() {
  cd libexpat/expat

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share/man,share/doc}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s expat "${pkgdir}"/usr/share/licenses/lib32-expat
}

# vim: ts=2 sw=2 et:
