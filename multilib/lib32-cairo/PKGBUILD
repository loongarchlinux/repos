# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-cairo
pkgver=1.17.6
pkgrel=2
pkgdesc="2D graphics library with support for multiple output devices (32-bit)"
url="https://cairographics.org/"
arch=(loong64 x86_64)
license=(LGPL MPL)
depends=(lib32-zlib lib32-libpng lib32-fontconfig lib32-freetype2 lib32-libx11
         lib32-libxext lib32-libxrender lib32-libxcb lib32-glib2 lib32-pixman
         cairo)
makedepends=(valgrind git meson)
options=(debug)
_commit=b43e7c6f3cf7855e16170a06d3a9c7234c60ca94  # tags/1.17.6^0
source=("git+https://gitlab.freedesktop.org/cairo/cairo.git#commit=$_commit"
        0001-Fix-type1-subset-indexing.patch)
sha256sums=('SKIP'
            '296be3c73638314bea08fa51b5f1650ea0a2aab2a037ea55e41c319d64ca4c3c')

pkgver() {
  cd cairo
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd cairo

  patch -Np1 -i ../0001-Fix-type1-subset-indexing.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson cairo build \
    --libdir=/usr/lib32 \
    -D spectre=disabled \
    -D tee=enabled \
    -D tests=disabled \
    -D symbol-lookup=disabled \
    -D gtk_doc=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/usr/{include,bin}
}
