# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=giac
_pkgver=1.9.0-21
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc='A free computer algebra system'
arch=(loong64 x86_64)
url='http://www-fourier.ujf-grenoble.fr/~parisse/giac.html'
license=(GPL3)
depends=(fltk mpfi gsl pari ntl lapack curl glpk libao gmp-ecm libsamplerate)
makedepends=(python libjpeg texlive-core texlive-science hevea nauty)
optdepends=('perl: for pgiac')
replaces=(libgiac xcas)
provides=(libgiac xcas)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        giac-test-pari-2.11.patch
        giac-pari-2.15.patch)
sha256sums=('120cd16f2e033817302d2a03a22bd413dc1d85df8b225ba4f76b93a7805b07fe'
            'c5f091986f0a0807fc1ae5a3b7454132816e9bc3ed64411f0a7bdf90a8c8e624'
            'cb1d6e783f2a1f05c2ec7319874089e6a2ab6316f65661087796452b9b8efb21')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../giac-test-pari-2.11.patch # Fix tests with pari 2.11
  patch -p1 < ../giac-pari-2.15.patch # Fix build with pari 2.15
}

build() {
  cd $pkgname-${pkgver%.*}
  CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS" # Fix crashes
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-${pkgver%.*}

#  make check
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
