# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='faraday-httpclient'
pkgname="ruby-${_gemname}"
pkgver=2.0.1
pkgrel=1
pkgdesc='Faraday adapter for HTTPClient'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=('ruby' 'ruby-httpclient')
makedepends=('ruby-rspec')
checkdepends=('ruby-faraday' 'ruby-webmock' 'ruby-multipart-parser')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('034f8dc7fff8ea214a78208a57bf92e16259ab1b515936a647d50ce3bae7361bcef891eb1baa0ce4a1f8cc4afa14b564f743aee2a0c215d3cbebabab8c41c537')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # drop broken/cirular dependencies
  sed --in-place '/add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  rspec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CODE_OF_CONDUCT.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
