# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ilya Elenskiy <elenskiy.ilya@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=python-rx
pkgver=4.0.2
pkgrel=1
pkgdesc='Reactive Extensions for Python'
arch=(any)
url=http://reactivex.io
license=(APACHE)
depends=(
  python
  python-typing-extensions
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry
)
checkdepends=(
  python-coverage
  python-nose
  python-pytest
  python-pytest-asyncio
)
_tag=430cfabbf11eb8e07bc7a875e3d9a26dc5823311
source=(git+https://github.com/ReactiveX/RxPY.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd RxPY
  _pkgver=$(git describe --tags | sed 's/^v//')
  poetry version -q ${_pkgver}
  echo ${_pkgver}
}

build() {
  cd RxPY
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd RxPY
  python -m pytest
}

package() {
  cd RxPY
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
