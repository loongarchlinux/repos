# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=14.0.1
pkgrel=2
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=(any)
url="https://apps.nextcloud.com/apps/spreed"
license=(AGPL3)
groups=(nextcloud-apps)
makedepends=(nextcloud)
checkdepends=(ripgrep yq)
source=(https://github.com/nextcloud-releases/$_name/releases/download/v$pkgver/$_name-v$pkgver.tar.gz)
sha512sums=('b3467aa5d77900ab8d1a2c302128d06668889212b9598bc455a3ef69fcf5702d4f999df3ac107febe9d6e7b80fd23b3cbec3545e9490238882b329c6a06e3e04')
b2sums=('bb1e05dd2593ec8f78e7c8fcd8aa3ad1e7fe5bf2aeb4f0336744d90969151e61f3cd946a019ff871773d2d678b6e2e260eb7a69736c77e812c8018d1d17a48eb')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

_get_php_versions() {
  local _phps=(php7 php)

  _app_min_php="$(xq '.info.dependencies.php["@min-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_php="$(xq '.info.dependencies.php["@max-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"

  if [[ $_app_max_php != 'null' ]]; then
    _app_max_php="$(echo $_app_max_php | awk -F '.' '{print $1"."$2+1}')"
  fi

  _system_php=""
  for _php in "${_phps[@]}"; do
    if command -v "$_php" > /dev/null; then
      if [[ -z "$_system_php" ]]; then
        _system_php="$_php"
      fi
    fi
  done
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  _get_php_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "$_nextcloud_major_version" "$_app_min_major_version")"<0)) || (("$(vercmp "$_nextcloud_major_version" "$_app_max_major_version")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "$_app_min_major_version" "$_app_max_major_version" "$_nextcloud_major_version"
    exit 1
  fi

  local _php_version="$($_system_php --version |head -n1 |cut -d ' ' -f2 |sed 's/.[0-9]*$//g')"
  if [[ "$(vercmp "$_php_version" "$_app_min_php" )" -lt 0 ]]; then
    printf "%s requires php-interpreter >= %s, but %s is provided\n" $pkgname $_app_min_php $_php_version
    exit 1
  fi
  if [[ $_app_max_php != 'null' ]]; then
    if [[ "$(vercmp "$_php_version" "$_app_max_php" )" -ge 0 ]]; then
      printf "%s requires php-interpreter < %s, but %s is provided\n" $pkgname $_app_max_php $_php_version
      exit 1
    fi
  fi
}

package() {
  _get_nextcloud_versions
  _get_php_versions

  depends=(
    "nextcloud>=$_app_min_major_version"
    "nextcloud<$_app_max_major_version"
  )
  if [[ "$_app_min_php" != 'null' ]]; then
    depends+=(
      "php-interpreter>=$_app_min_php"
    )
  fi
  if [[ "$_app_max_php" != 'null' ]]; then
    depends+=(
      "php-interpreter<$_app_max_php"
    )
  fi
  if [[ "$_app_min_php" == 'null' ]] && [[ "$_app_max_php" == 'null' ]]; then
    depends+=(php-interpreter)
  fi

  install -vdm 755 "$pkgdir/usr/share/webapps/nextcloud/apps/"
  cp -av $_name "$pkgdir/usr/share/webapps/nextcloud/apps/"
}
