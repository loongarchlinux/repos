# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.3.0
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-isodate' 'python-jsonschema' 'python-six')
makedepends=('python-build' 'python-installer' 'python-poetry')
checkdepends=('python-pytest-cov' 'python-pytest-flake8' 'python-rfc3339-validator'
              'python-strict-rfc3339')
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('003ea97a1cac598eb3728d96291d8be56c6ba9e2d3cd4f77c1258962f67b460a887f4473fed39587870583e13a83403623c3b2f96fa8d02345583cd3bdaf008b')

build() {
  cd openapi-schema-validator-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
