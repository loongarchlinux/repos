# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=3.8.12
pkgrel=2
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-packaging' 'python-pydantic' 'python-mutagen' 'qt5-svg'
         'xdg-user-dirs')
makedepends=('python-setuptools')
checkdepends=('python-pytest-benchmark' 'python-pytest-cov')
optdepends=('feeluown-netease' 'feeluown-qqmusic' 'feeluown-kuwo' 'feeluown-bilibili')
conflicts=('feeluown-local')
provides=('feeluown-local')
replaces=('feeluown-local')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown_issue_715.patch" # https://github.com/feeluown/FeelUOwn/issues/715
        "feeluown.desktop")
sha512sums=('14011a86cb943ff7aa0f60a84e6825e0c1d3b1ced139b64231d6ec8805dedcc802062251788d4d3d508eeb8772a3f1f534e260e056135511d98dfeb2f387ef0f'
            '8ccd6f46f8e334938b0fe7734788805c0b98a6f4b76e93950db416038d2d1f6bea0f3f8fad20137508cfe2336b31aec90b686946fbe5a4332edf532058851bac'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087')

prepare() {
  cd $pkgname-$pkgver
  # Fixes upstream compatability with Pydantic>=2.0
  # Issue: https://github.com/feeluown/FeelUOwn/issues/715
  # PR: https://github.com/feeluown/FeelUOwn/pull/718
  patch -t -s -p1 < $srcdir/feeluown_issue_715.patch
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/gui/assets/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
