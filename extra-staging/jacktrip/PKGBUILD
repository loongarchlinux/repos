# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=2.0.2
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(loong64 x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(
  LGPL-3.0-only
  GPL-3.0
  MIT
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-declarative
  qt6-networkauth
  qt6-quickcontrols2
  qt6-svg
  qt6-webchannel
  qt6-webengine
  qt6-websockets
)
makedepends=(
  help2man
  meson
  jack
  python-jinja
  python-yaml
  rtaudio
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('19ba4ee4ccad1f07416d34019731c40111c787ffd6f7fe93d8fbe7a40aa8298b4c4be05718ad0bb96a4a1cebdb2cd16b3689ef05b9d2fd2a6cd3d55daecd23d6')
b2sums=('0c76920a631e82f8902c3888b6b22ae38ecde5fa90d4272e3b7b403bf58caf74f76ff4d8d9bbedf45430a27624ff54b0a1893c4e77fed57d5bdd9b19fb392a82')

build() {
  local meson_options=(
    -D noupdater=true
  )

  arch-meson "${meson_options[@]}" build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    jack libjack.so
    rtaudio librtaudio.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
