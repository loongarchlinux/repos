# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
pkgname=hipify-clang
pkgver=5.5.1
pkgrel=1
pkgdesc='Convert CUDA to Portable C++ Code'
arch=('loong64' 'x86_64')
url='https://docs.amd.com/bundle/HIP-Programming-Guide-v5.4/page/Transitioning_from_CUDA_to_HIP.html'
license=('MIT')
depends=('rocm-llvm' 'cuda')
makedepends=('cmake')
_git='https://github.com/ROCm-Developer-Tools/HIPIFY'
source=("${pkgname}-${pkgver}.tar.gz::$_git/archive/rocm-$pkgver.tar.gz"
        "hipify-fix-install-cmake.patch")
sha256sums=('35b9c07a7afaf9cf6f3bbe9dd147fa81b1b297af3e5e26e60c55629e83feaa48'
            '49fcf9d1c9f79bf726d483fea76e5f133128af01e88231ebefb027aa2feb049a')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"

prepare() {
    cd "$_dirname"
    patch -Np1 -i "$srcdir/hipify-fix-install-cmake.patch"
}

build() {
  cmake \
    -Wno-dev -B build \
    -S "$_dirname" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/opt/rocm \
    -DSWDEV_375013=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
