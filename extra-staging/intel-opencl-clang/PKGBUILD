# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
epoch=1
pkgver=14.0.0.r18+gcf95b33
pkgrel=1
pkgdesc="Wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(loong64 x86_64)
url="https://github.com/intel/opencl-clang"
license=(custom)
depends=(clang14 llvm14-libs spirv-llvm-translator-14)
makedepends=(cmake llvm14 git)
# Current point in the used LLVM 14 branch
_commit=cf95b338d14685e4f3402ab1828bef31d48f1fd6
source=(git+${url}.git#commit=$_commit
	intel-opencl-use-clang14.patch)
sha256sums=('SKIP'
            '1badfdfbc91ba328bebb2a547a329710dd1e13b1a8d75ae7185e67f4fabcff82')

pkgver() {
  cd ${_srcname}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./;s/-/+/'
}

prepare() {
  cd ${_srcname}
  patch -Np1 -i ../intel-opencl-use-clang14.patch
}

build() {
  cmake -B build -S ${_srcname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_PREFIX_PATH=/usr/lib/llvm14/lib/cmake/ \
    -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
    -DSPIRV_TRANSLATOR_DIR=/usr \
    -DLLVM_NO_DEAD_STRIP=ON \
    -DPREFERRED_LLVM_VERSION=14.0.6 \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_srcname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
