# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=neochat
pkgver=24.04.80
pkgrel=2
pkgdesc='A client for matrix, the decentralized communication protocol'
url='https://apps.kde.org/neochat/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(cmark
         gcc-libs
         glibc
         icu
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kirigami
         kirigami-addons
         kitemmodels
         knotifications
         kquickcharts
         kquickimageeditor
         kservice
         kstatusnotifieritem
         kwindowsystem
         libquotient
         prison
         purpose
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         qt6-webview
         qtkeychain-qt6
         sonnet
         syntax-highlighting)
makedepends=(extra-cmake-modules
             kdoctools
             python
             qcoro-qt6)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2098d0382d9e5d0ad136f0c9c0c596d8b2ff7ef8b7de55b62829f01c7f6c7922'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
