# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.7
pkgrel=2
pkgdesc="Qt oriented code checker based on clang framework"
url="https://www.kdab.com/"
license=(GPL2)
depends=(clang)
makedepends=(cmake llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        clazy-llvm11.patch::"https://invent.kde.org/sdk/clazy/-/commit/30d6a2b6.patch"
        clazy-llvm11b.patch::"https://invent.kde.org/sdk/clazy/-/commit/25aa102c.patch")
sha256sums=('754da5815f769dd6b72a040a2430525c93f294eb7769c78271603df9614f1b21'
            'SKIP'
            'b097b430723dd654a773e9154c86834a802284bf84d0dd1dc4c43727cebe5c5f'
            '59b938ffb0ef27e67ea1a838f0ab0b26dc09b07650c0dfc9eafc21b77b44faa1')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../clazy-llvm11.patch # Fix build with LLVM 11
  patch -d $pkgname-$pkgver -p1 -i ../clazy-llvm11b.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
