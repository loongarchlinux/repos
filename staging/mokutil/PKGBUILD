# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=mokutil
pkgver=0.6.0
pkgrel=2
pkgdesc="Shim.efi Machine Owner Key management tool"
arch=(i686 loong64 x86_64)
url="https://github.com/lcp/mokutil"
license=(GPL3)
depends=(efivar openssl keyutils libxcrypt)
makedepends=(git)
_commit=b4d8693f04bcf43986aba061845947534b384152
source=("git+https://github.com/lcp/mokutil#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=()

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
  cd "$pkgname"
  autoreconf -f -i
}

build() {
  cd "$pkgname"
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       ;
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2:sw=2:et
