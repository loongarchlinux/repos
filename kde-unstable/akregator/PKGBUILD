# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akregator
pkgver=22.11.90
pkgrel=1
pkgdesc='A Feed Reader by KDE'
arch=(loong64 x86_64)
url='https://apps.kde.org/akregator/'
license=(GPL2)
depends=(kontactinterface messagelib knotifyconfig kuserfeedback)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6c049f31bc8ad0fef7db38145b1ba7efa59ccc304b8acc64e5ef37476e837776'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
