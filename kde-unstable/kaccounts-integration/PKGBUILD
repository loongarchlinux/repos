# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=kaccounts-integration
pkgver=22.07.80
pkgrel=1
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, 
Jabber and others'
arch=(loong64 x86_64)
url='https://www.kde.org/'
license=(GPL)
depends=(kcmutils signon-kwallet-extension signon-plugin-oauth2 signon-ui)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4f1f3cbadb3b72b40d14b44f6cfee63c9e3907e3e10b372eba701c675da0a933'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
