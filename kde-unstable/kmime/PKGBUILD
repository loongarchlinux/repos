# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=22.11.90
pkgrel=1
pkgdesc='Library for handling mail messages and newsgroup articles'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('61b5a1f8b60114d2b474f10f3ded66a538fafcd48794fe4e17b643e88e74d2de'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
