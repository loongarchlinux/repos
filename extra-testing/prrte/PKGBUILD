# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=prrte
pkgname=(prrte{,-docs})
pkgver=3.0.3
pkgrel=1
pkgdesc="PMIx Reference RunTime Environment"
arch=(loong64 x86_64)
url="https://github.com/openpmix/prrte"
license=(BSD-3-Clause)
makedepends=(
  glibc
  hwloc
  libevent
  openpmix
  perl
)
conflicts=(putty)  # putty also provides a pterm: https://gitlab.archlinux.org/archlinux/packaging/packages/prrte/-/issues/1
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('b2443fa225b279ab31a5714b3f89c50e1b12667bc58357f885eee1e4fb8a6d22e533dea5cc2a2b6618334e0ed13b5dbc4c3ea88446dd454480f4300e08293a4d')
b2sums=('5c68f6ca10089cc13fc326c2e65c86254f8063a4b5d3844c26c77d9ed47f4a939d72dd668959f7351a95144bf8c6289b5223cc262c8847913430263bcb6b77ac')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd $pkgname-$pkgver
  ./autogen.pl
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc/$pkgname
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

check() {
  make -k check -C $pkgname-$pkgver
}

package_prrte() {
  depends=(
    glibc
    hwloc
    libevent
    openpmix libpmix.so
  )
  optdepends=(
    'prrte-docs: for documentation'
  )
  provides=(libprrte.so)
  backup=(
    etc/$pkgname/prte-default-hostfile
    etc/$pkgname/prte-mca-params.conf
    etc/$pkgname/prte.conf
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/{doc,prte/rst}
  )
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_prrte-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
