# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>

pkgname=rocm-core
_rocm_major=6
_rocm_minor=0
_rocm_patch=0
_pkgver_magic=91
pkgver=$_rocm_major.$_rocm_minor.$_rocm_patch
pkgrel=2
pkgdesc='AMD ROCm core package (version files)'
license=('Unlicense')
arch=('loong64' 'x86_64')
source=('local://LICENSE')
b2sums=('e4c94a21ea352227919ba41e0e461b61b700cfb469a0cc1019d7daee85b009410edb4078987dc163d0504b0385162c0f4988ba6c4d5cab23c059486f8bdb4db3')
url='https://rocm.docs.amd.com/'

package() {
  # AMD hasn't released the complete source code of the package,
  # so we only package the version file that is required by some libraries.
  echo "$pkgver-$_pkgver_magic" > version-rocm
  install -Dm644 version-rocm "$pkgdir/opt/rocm/.info/version"
  install -Dm644 version-rocm "$pkgdir/opt/rocm/.info/version-dev"

  echo "#pragma once" > rocm_version.h
  echo "#define ROCM_VERSION_MAJOR $_rocm_major" >> rocm_version.h
  echo "#define ROCM_VERSION_MINOR $_rocm_minor" >> rocm_version.h
  echo "#define ROCM_VERSION_PATCH $_rocm_patch" >> rocm_version.h
  echo "#define ROCM_BUILD_INFO \"$_rocm_major.$_rocm_minor.$_rocm_patch.0-$_pkgver_magic\"" >> rocm_version.h
  install -Dm644 rocm_version.h "$pkgdir/opt/rocm/include/rocm_version.h"
  install -Dm644 rocm_version.h "$pkgdir/opt/rocm/include/rocm-core/rocm_version.h"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
