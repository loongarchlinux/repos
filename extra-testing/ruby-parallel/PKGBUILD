# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='parallel'
pkgname="ruby-${_gemname}"
pkgver=1.26.1
pkgrel=1
pkgdesc='Run any kind of code in parallel processes'
arch=('any')
url="https://github.com/grosser/${_gemname}"
license=('MIT')
depends=('ruby')
checkdepends=('ruby-bump' 'ruby-rspec' 'ruby-rspec-rerun' 'ruby-rake' 'ruby-ruby-progressbar' 'ruby-sqlite3' 'ruby-bundler' 'lsof' 'procps-ng')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('92404eff2c39bb742fda8b746930262c546c03df9b4633106858e0b968c86589172e4b080f1ba50dff9d3e0873ca817950d08c8b521549ad4d71731aaad80083')

prepare() {
  cd "${_gemname}-${pkgver}"

  # we build based on a tar archive, not a git repo
  sed --in-place --regexp-extended 's|git ls-files lib MIT-LICENSE.txt|find lib MIT-LICENSE.txt -type f|' "${_gemname}.gemspec"

  rm --verbose Gemfile.lock

  # Remove dependency on activerecord
  sed --in-place --regexp-extended '/activerecord|rubocop|legacy_formatters|mysql/d' Gemfile

  # Remove certain tests as they use activerecord which Arch cannot ship right now.
  rm spec/cases/map_with_ar.rb
  rm spec/cases/each_with_ar_sqlite.rb
  sed --in-place --regexp-extended '/works with SQLite/,/end/d' spec/parallel_spec.rb

  # This test is simply broken
  sed --in-place --regexp-extended '/does not leave processes behind while running/,/end/d' spec/parallel_spec.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}


check() {
  cd "${_gemname}-${pkgver}"

  GEM_HOME="tmp_install/${_gemdir}" rspec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
