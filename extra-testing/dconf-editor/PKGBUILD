# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dconf-editor
pkgver=45.0.1
pkgrel=1
pkgdesc="GSettings editor for GNOME"
url="https://wiki.gnome.org/Apps/DconfEditor"
arch=(loong64 x86_64)
license=(GPL3)
depends=(
  dconf
  libhandy
)
makedepends=(
  git
  meson
  vala
)
groups=(gnome-extra)
_commit=e9e1b3a8d292e0e49d884f5f498534202306af90  # tags/45.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
