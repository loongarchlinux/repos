# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=45.0
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  dconf
  hicolor-icon-theme
  geoclue
  geocode-glib-2
  gjs
  glib2
  gtk4
  libadwaita
  libgweather-4
  libportal
  librest
  libshumate
  libxml2
)
makedepends=(
  git
  gobject-introspection
  meson
  yelp-tools
)
groups=(gnome)
_commit=9ba1c9ab31037402bfa8d58954f826587fe2bf8c  # tags/v45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
