# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='faraday-net_http_persistent'
pkgname="ruby-${_gemname}"
pkgver=2.0.1
pkgrel=1
pkgdesc='About Faraday adapter for NetHttpPersistent'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rspec')
checkdepends=('ruby-faraday' 'ruby-webmock' 'ruby-multipart-parser')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('f198ebb42cf0aee6ee8b6c598e437112f620bd145dad609e64845e555f4f51034a8f8670ef448b46742f7f5a83bc8681943c6ba817430b1519fd683f57d0b7fa')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # drop broken/cirular dependencies
  sed --in-place '/add_development_dependency/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # tests currently don't pass because we don't have net-http-persistent in the repos
  # we run the tests in ruby-faraday
  #rspec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
