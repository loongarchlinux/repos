# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-application-manager
pkgver=1.1.1
pkgrel=1
pkgdesc='App manager of Deepin Desktop Environment'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-application-manager"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('qt6-base' 'glibc' 'gcc-libs' 'systemd-libs' 'startdde')
makedepends=('cmake' 'ninja' 'gtest')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-application-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('98b0a778af29ef6e1730584948ab6d845bd81bc306596b12852299d159b8bd30bd7ac519ec8fcab8d20e0c0b61f48ff5d8de7995725d3e2ebdc6a196128d8a16')

build() {
  cd dde-application-manager-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-application-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
  rm -r "$pkgdir"/etc/dpkg
  rmdir "$pkgdir"/etc
}
