# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=45.0
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  gjs
  gnome-keyring
  gtk4
  libadwaita
  libsoup3
  telepathy-glib
  telepathy-idle
  telepathy-logger
  telepathy-mission-control
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  yelp-tools
)
groups=(gnome-extra)
_commit=9ea71de3d20fdc96e76bb55362418b9118e7cf07  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/polari.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd polari
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd polari
}

build() {
  arch-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
