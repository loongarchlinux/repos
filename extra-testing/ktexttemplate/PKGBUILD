# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktexttemplate
pkgver=6.5.0
pkgrel=1
pkgdesc='Library to allow application developers to separate the structure of documents from the data they contain'
arch=(loong64 x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         qt6-base
         qt6-declarative)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
groups=(kf6)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('295a3f87ff08af17f83496fd66b3f634917bcac6466c79be1f4cb6786ecb04c6'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D # Jonathan Esk-Riddell <jr@jriddell.org>
              90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1 # Nicolas Fella <nicolas.fella@kde.org>
              )

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
