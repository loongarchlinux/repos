# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Dylan Delgado <dylan1496 at live dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=sakura
pkgver=3.8.7
pkgrel=2
pkgdesc="A terminal emulator based on GTK and VTE"
arch=('loong64' 'x86_64')
url="https://launchpad.net/sakura"
license=('GPL-2.0-or-later')
depends=('vte3' 'libxft')
makedepends=('cmake')
source=("https://launchpad.net/sakura/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('ba7009ff03cacc19149d82f280413defb019540d2409e6cb9243f0d7ac812922')

prepare() {
  cd "$srcdir/"

  # Set default font size a bit smaller
  sed -i 's|#define DEFAULT_FONT "Bitstream Vera Sans Mono 14"|#define DEFAULT_FONT "Bitstream Vera Sans Mono 10"|g' "${pkgname}-${pkgver}/src/sakura.c"
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
