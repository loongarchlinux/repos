# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=7.1.3.r68.5644c667
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(loong64 x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libcanberra
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libhandy-1.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
#_tag=167862ae223b3483336ad3ed45bc2326ac3c167a
_commit=5644c66724a70983c5abed0a28cfb21733d0e11a
#source=(git+https://github.com/elementary/gala.git#tag=${_tag})
source=(git+https://github.com/elementary/gala.git#commit=${_commit})
b2sums=('SKIP')

pkgver() {
  cd gala
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
