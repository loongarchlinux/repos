# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-connections
pkgver=45.0
pkgrel=1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/GNOME/connections'
license=('GPL3')
depends=('cairo' 'dconf' 'freerdp' 'fuse3' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3'
         'gtk-vnc' 'hicolor-icon-theme' 'libhandy' 'libsecret' 'libxml2')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'vala')
groups=('gnome')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('b9fab525b90a3e27d113c16fb868c2b9c47bf8149310d14db862ea1912c06fb8')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
