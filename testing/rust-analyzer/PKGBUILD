# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2021-11-08
pkgver=${_pkgver//-}
pkgrel=2
pkgdesc='Rust compiler front-end for IDEs'
arch=('loongarch64' 'x86_64')
url=https://rust-analyzer.github.io/
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust-src')
makedepends=('git')
source=("git+https://github.com/rust-analyzer/rust-analyzer.git#tag=$_pkgver")
b2sums=('SKIP')

prepare() {
  cd $pkgname
  cargo fetch --locked
}

build() {
  cd $pkgname
  RUST_ANALYZER_REV=$_pkgver cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
