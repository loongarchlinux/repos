# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=gst-editing-services
pkgver=1.18.5
pkgrel=3
pkgdesc='GStreamer library for creating audio/video editors'
arch=('loongarch64' 'x86_64')
license=('LGPL')
url='https://gstreamer.freedesktop.org/modules/gst-editing-services.html'
depends=('gst-plugins-base')
makedepends=('bash-completion' 'gobject-introspection' 'gtk-doc' 'meson' 'python-gobject')
checkdepends=('gst-plugins-bad' 'gst-plugins-good')
source=("https://gstreamer.freedesktop.org/src/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5' # Sebastian Dröge
              'D637032E45B8C6585B9456565D2EEE6F6F349D7C') # Tim-Philipp Müller
sha256sums=('8af4a8394d051f3e18280686db49a6efaccc95c0c59a17f0f564e32000590df5'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build \
    -D doc=disabled \
    -D validate=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
