# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-sekizai
pkgname=python-django-sekizai
pkgver=3.0.1
pkgrel=1
pkgdesc="Django Template Blocks with extra functionality"
arch=(any)
url="https://github.com/django-cms/django-sekizai"
license=(BSD)
depends=(python-django python-django-classy-tags)
makedepends=(python-setuptools)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('1435cdaf5d871c502066e5db203464f9ca9bbcdb17d5630c2d132389d1e390f5deea7729b899a834639965c48b47c13765ea2d8f0d86e3a176b9a94fd04704d5')
b2sums=('2b3941000c54f708906031f1335dae4e74474f3aa3ec76d21909bf3a56ce0cbcec77b4063f32ec214d60354e4b02687311ac58ced4e74623b0b37dcdf31e7479')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  # project uses deprecated way of running tests: https://github.com/django-cms/django-sekizai/issues/116
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
