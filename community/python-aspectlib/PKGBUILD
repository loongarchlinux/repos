# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aspectlib
pkgver=1.5.1
pkgrel=1
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://github.com/ionelmc/python-aspectlib"
depends=('python-fields')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-tornado' 'python-process-tests'
              'python-profilestats' 'python-pytest-runner') # 'mysql-python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-aspectlib/archive/v$pkgver.tar.gz")
sha512sums=('24e700a14096042f2382910eb1245a9b64d99670c8bd60e2df4afe41113e8f813633bae10c8c2487c52c556f537a77ee43f6672a00af1f05d2ed4a129a92332e')

build() {
  cd python-aspectlib-$pkgver
  python setup.py build
}

check() {
  cd python-aspectlib-$pkgver
  # https://github.com/ionelmc/python-aspectlib/issues/15
  python setup.py pytest --addopts '--deselect tests/test_integrations_py3.py::test_decorate_tornado_coroutine'
}

package() {
  cd python-aspectlib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
