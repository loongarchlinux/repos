# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=cosign
pkgver=0.3.1
pkgrel=2
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('loongarch64' 'x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache')
depends=('pcsclite')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('61bd6b0a0eaf646e0e3fe47589b30e4e38a57532abb905e884d65d93557a8e22')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

test() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/cosign "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 build/copasetic "${pkgdir}/usr/bin/copasetic"
}
