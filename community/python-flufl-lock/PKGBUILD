# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=5.0.1
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://gitlab.com/warsaw/flufl.lock"
license=('Apache')
depends=('python-atpublic' 'python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
replaces=('python-flufl.lock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f937fb6c7f7bef2b85674225f83f3162f34db3fe2df3d9a896f772c4489c3e0611361e3d1d9efda7727112d780f31022d3045d3a3b8e78e9412aca2f2418ca24')
b2sums=('2651898e881719f24bc61e14f697877ca39f2747fadf52de1a5fc917b26e23001df5f0a4ea2e48271da69198e1a1336e69cc0167a4f46fddd21f464b355fa4ca')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't use coverage with pytest
  sed -s '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove top-level tests and docs
  # https://gitlab.com/warsaw/flufl.lock/-/issues/22
  rm -rfv "${pkgdir}/${site_packages}/"{docs,tests}
}
