# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=7.0
pkgrel=2
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=(any)
url="https://gitlab.com/warsaw/flufl.lock"
license=(Apache)
depends=(python-atpublic python-psutil)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-pytest python-sybil)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('b1c5151d30aa3e9afc12fe0dbd9be05ad1f9133b32f5e3bacf98532fefb08fbf354bf4b9a4b9fc90e448861941eba358e8f67218daf9797eb031b487cc717181')
b2sums=('72c763e50e0a28f02fe6d9a8d88b1c64626a60310b203f9bbcc82e950f8ac494640748dece4c63542d4b89512a1f98bb1d92bda145028ebd18a8f254c2e2c1eb')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="src:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
