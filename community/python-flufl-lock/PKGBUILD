# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=7.1
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=(any)
url="https://gitlab.com/warsaw/flufl.lock"
license=(Apache)
depends=(python-atpublic python-psutil)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-pytest python-sybil)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('6f073ffd7ec825b9483b70e68b86e6f8842056334126bd54778e8182489df5f659afcdfdec103fdc516d7a4a3cf8ba918d337f01816b6b688ef8706d52627314')
b2sums=('e6a93c4bba8edf65cad505b52d8f57731410b4b3d8e293407123468820d9eba585f09c1683f191c035f5b97d59b8e45a921218107a32e3c49a1acaf20890b997')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="src:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
