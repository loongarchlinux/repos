# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.6
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=(loongarch64 x86_64)
url="http://vmpk.sourceforge.net/"
license=(GPL3)
groups=(pro-audio)
depends=(gcc-libs glibc libxcb qt5-base qt5-x11extras)
makedepends=(cmake docbook-xsl drumstick qt5-tools)
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('0b77aabf1dda99c9149a5353fc9d0363469de2678e6089fc7dc13dc349587fd1b1630cf511c4d9ef384a7523b8f023e451d6c543a805dcda0bf5cf9d7a4b4277')
b2sums=('a2145394a9ad90f567e292e23bd9b7f62e26160b6375a48cea2a84818139865a65fdde172f6a8b6e42015e0ce14127c1f97558e00b56ebc288fa1e3d3bd11eab')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libdrumstick-rt.so libdrumstick-widgets.so)

  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
