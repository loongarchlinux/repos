# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=('python-jsbeautifier' 'python-cssbeautifier')
pkgbase=python-jsbeautifier
pkgver=1.14.3
pkgrel=1
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-editorconfig' 'python-six')
makedepends=('python-setuptools')
source=("https://github.com/beautify-web/js-beautify/archive/v$pkgver/js-beautify-v$pkgver.tar.gz")
b2sums=('927cfbf2b5993017ea03bf220e07b62888f52dce6370a2f5dfdce6e1a3060f4ab206989ecf0dcae7310a52a2d47bca9dd6bd4524808ce0c95b2638dab2020217')

prepare() {
  cd js-beautify-$pkgver
  cp -a python python-css
}

build() {
  cd js-beautify-$pkgver/python
  python setup-js.py build
  cd ../python-css
  python setup-css.py build
}

check() {
  cd js-beautify-$pkgver
  python python/js-beautify-test.py
}

package_python-jsbeautifier() {
  pkgdesc='JavaScript unobfuscator and beautifier'
  cd js-beautify-$pkgver/python
  python setup-js.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" ../LICENSE
}

package_python-cssbeautifier() {
  pkgdesc='CSS unobfuscator and beautifier'
  depends+=('python-jsbeautifier')
  cd js-beautify-$pkgver/python-css
  python setup-css.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" ../LICENSE
}
