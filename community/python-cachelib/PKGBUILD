# Maintainer:
# Contributor: Filipe Nascimento <flipee at tuta dot io>
# Contributor: Julien Nicoulaud <julien DOT nicoulaud AT gmail DOT com>

pkgname=python-cachelib
_name=${pkgname#python-}
pkgver=0.8.0
pkgrel=2
pkgdesc='A collection of cache libraries in the same API interface. Extracted from werkzeug'
arch=(any)
url='https://github.com/pallets/cachelib'
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest-xprocess)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('e358f6f82cf633891f0dbd3996e300b4657716ac44b301df33b9cb2cfd6eea6d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  PYTHONPATH="$PWD"/src \
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
