# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Dan Serban
# Contributor: Richard Murri

pkgname=python-werkzeug
pkgver=2.0.1
pkgrel=1
pkgdesc='Swiss Army knife of Python web development'
url='http://werkzeug.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-timeout' 'python-requests' 'python-pytest-xprocess')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/werkzeug/archive/${pkgver}.tar.gz")
sha512sums=('721a6c3cf8f803c80873f68d94b273da2ddda79b5e83d9283b9af3d3c971bb8c7ac7828c7a60c33b9bded7b05f063810df12fc266ee7d264dc6261100fc97708')

build() {
  cd "werkzeug-$pkgver"

  export PYTHONHASHSEED=0
  python setup.py build
}

# check() {
#   cd "werkzeug-$pkgver"
#   PYTHONPATH="$PWD/build/lib" pytest
# }

package() {
  cd "werkzeug-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
