# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-libcharon
pkgver=4.6.2
pkgrel=1
pkgdesc="Python File metadata and streaming library"
url="https://github.com/Ultimaker/libCharon"
arch=(any)
license=("AGPL")
depends=('python' 'python-dbus')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(libcharon-$pkgver.tar.gz::https://github.com/Ultimaker/libCharon/archive/${pkgver}.tar.gz)
sha512sums=('c3b5f62027fa8f9764b744a3becf0f9083ac08ad4bff8f38cdad57d038638640eb444f011985969a01dc8f84606fce13efc8aa669fd5c088d8748508514cb69d')

build() {
  cd libCharon-$pkgver
  python setup.py build
}

check() {
  cd libCharon-$pkgver
  pytest tests
}

package() {
  cd libCharon-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
