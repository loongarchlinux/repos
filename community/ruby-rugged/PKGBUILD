# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: ChaosKid42 <christoph.scholz@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

_name=rugged
# ideally this should match $pkgver, as package() might fail otherwise. thanks, github
_libgit2_pkgver=1.4.3
pkgname=ruby-rugged
pkgver=1.4.3
pkgrel=2
pkgdesc="A Ruby binding to the libgit2 linkable library"
arch=(loong64 x86_64)
url="https://github.com/libgit2/rugged"
license=(MIT)
depends=(glibc ruby)
makedepends=(libgit2 ruby-rdoc)
checkdepends=(git ruby-minitest ruby-rake ruby-rake-compiler)
options=(!emptydirs)
source=(
  $_name-$pkgver.tar.gz::https://github.com/libgit2/$_name/archive/v$pkgver.tar.gz
  libgit2-$_libgit2_pkgver.tar.gz::https://github.com/libgit2/libgit2/archive/v$_libgit2_pkgver.tar.gz
)
sha512sums=('412116c07b00554f2fe2b73212c455e05008143555ba9704b1ffc19494b0533a3c42ac7112b3762771aec41321ff354f5f330dcb00443697bb463512426f3f2a'
            '3dfa16c1c705b99fbda55e5e86e67703621d6f8fb62a03cfe998c834d242f33f4200fc0b6bc5d32fe5254c8b8e845259688e4bea9c2dbfc2b7df91841c335d28')
b2sums=('56d7651a0ee2e5542a39f08786e0e97a77f1cce284efe52b72bf85658ad096d2936cca7c340498261b0468f2ca5962ae0554abeb060c48354c97984d152898ac'
        '9308100ede912eae9d1fa4011c67f88f225aa11a49adf845389559fcbc213f31b0f91e4a5b496d282d42ac7f843f03c4ef7236f2d200331c615013d1250f6c31')

prepare() {
  cd $_name-$pkgver
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i $_name.gemspec
  # remove broken and useless libgit2 "version check":
  # https://github.com/libgit2/rugged/issues/698
  sed '68,90d' -i ext/rugged/extconf.rb
  # source tarball doesn't include vendored libgit2 version, which is only
  # needed for test fixtures (resources): https://github.com/libgit2/rugged/issues/801
  mv -v ../libgit2-$_libgit2_pkgver/* vendor/libgit2
  # remove all tests requiring an internet connection
  rm -rv test/online/*
}

build() {
  cd $_name-$pkgver
  export CI_BUILD=true
  export RUGGED_USE_SYSTEM_LIBRARIES=true
  export CMAKE_FLAGS=" --use-system-libraries"
  rake compile
  rake gem
}

check(){
  cd $_name-$pkgver
  # ConfigTest#test_read_global_config_file fails, if no gitconfig is set for
  # user...
  # https://github.com/libgit2/rugged/issues/432
  git config --global user.name "Foo Bar"
  rake test --trace --verbose TESTOPTS="--verbose"
}

package() {
  local _gemdir="$(gem env gemdir)"

  depends+=(libgit2.so)

  cd $_name-$pkgver
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" pkg/$_name-$pkgver.gem
  install -vDm 644 "$pkgdir/$_gemdir/gems/$_name-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$pkgname"

  rm -frv \
    "$pkgdir/$_gemdir/cache/" \
    "$pkgdir/$_gemdir/gems/$_name-$pkgver/vendor/" \
    "$pkgdir/$_gemdir/doc/$_name-$pkgver/ri/ext/"

  find "$pkgdir/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete

  find "$pkgdir/$_gemdir/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}
