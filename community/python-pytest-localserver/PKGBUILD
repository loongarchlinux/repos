# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-localserver
pkgver=0.6.0
pkgrel=1
pkgdesc='py.test plugin to test server connections locally'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-localserver'
depends=('python-aiosmtpd' 'python-pytest' 'python-werkzeug')
makedepends=('python-setuptools')
checkdepends=('python-requests')
source=(https://files.pythonhosted.org/packages/source/p/pytest-localserver/pytest-localserver-$pkgver.tar.gz)
sha512sums=('149796393af260fa5d69560852e9d11b569515b8486859302af2ed369a93bbb7cad11f54210851bd4030d8f7f5936a0306fe9599bc8477bdfc61d3d0a83cb205')

build() {
  cd pytest-localserver-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-localserver-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-localserver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
