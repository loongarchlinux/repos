# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-localserver
pkgver=0.5.1
pkgrel=3
_name=${pkgname#python-}
pkgdesc='py.test plugin to test server connections locally'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-localserver'
depends=('python-pytest' 'python-werkzeug')
makedepends=('python-setuptools')
checkdepends=('python-requests')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('dad8b21020cf2f08ad03398b82bad7b47e553a9c6b489b94186bdc61c22f57e7151f8dbeab38ef42d03658104ec42920ec363288a1bdbea34dab6aee1da03c48')

build() {
  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-localserver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
