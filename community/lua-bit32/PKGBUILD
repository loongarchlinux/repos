# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stephen Smith <stephen304@gmail.com>

pkgbase=lua-bit32
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
_compatrel=0.10
pkgver=5.3.5.1_$_compatrel
_rockrel=1
pkgrel=3
pkgdesc='Lua 5.2 bit manipulation library'
arch=(loongarch64 x86_64)
url='https://github.com/keplerproject/lua-compat-5.3'
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="lua-compat-5.3-$_compatrel"
_rockspec="$_rockname-${pkgver%_*}-$_rockrel.rockspec"
source=("$_archive.tar.gz::$url/archive/v$_compatrel.tar.gz"
        "${url/github/raw.githubusercontent}/1e31f3ddc517b4e521c73f7d6eaecd5e1787daa6/rockspecs/$_rockspec")
sha256sums=('d1ed32f091856f6fffab06232da79c48b437afd4cd89e5c1fc85d7905b011430'
            'a1c88310b5d5776c9f4c1ff4a934be497b03237c21d5aaf88422d1582b04af86')

prepare() {
	# Upstream forgot to commit this before tagging the release
	cp "$_rockspec" "$_archive/rockspecs"
}

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version=$1 --tree="$pkgdir/usr/" \
		CFLAGS="$CPPFLAGS $CFLAGS -fPIC" \
		LIBFLAG="$LDFLAGS -shared" \
		make --deps-mode=none --no-manifest "rockspecs/$_rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-bit32() {
	_package 5.4
}

package_lua51-bit32() {
	_package 5.1
}

package_lua52-bit32() {
	_package 5.2
}

package_lua53-bit32() {
	_package 5.3
}
