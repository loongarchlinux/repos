# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-checkdocs
pkgver=2.7.1
pkgrel=3
pkgdesc='Check the README when running tests'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-checkdocs'
depends=('python-docutils' 'python-importlib-metadata' 'python-pep517')
makedepends=('python-pip' 'python-setuptools-scm' 'python-toml')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-checkdocs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b42d63a4ddbb6835d07d457fd5758a06cafc071ee61f0216eadf7f62eedf27a18f51a7c7694c84ab32975c76c3fd1f642140522d21737a2f83f7bf63e0983204')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-checkdocs-$pkgver
  python setup.py build
}

check() {
  cd pytest-checkdocs-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-checkdocs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
