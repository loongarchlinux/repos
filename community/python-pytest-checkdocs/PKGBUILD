# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-checkdocs
pkgver=2.7.0
pkgrel=1
pkgdesc='Check the README when running tests'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-checkdocs'
depends=('python-docutils' 'python-importlib-metadata' 'python-pep517')
makedepends=('python-pip' 'python-setuptools-scm' 'python-toml')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-checkdocs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ebd7f06c46b2dbe996564a080b8c030aa21af025da91eec916a772ad4eaf9eb11c1f1734bafbf5bbd2de2f16b041788e28377ea99a4c5adfef8287a810650a45')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-checkdocs-$pkgver
  python setup.py build
}

check() {
  cd pytest-checkdocs-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-checkdocs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
