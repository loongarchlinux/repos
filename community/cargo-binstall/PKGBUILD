# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-binstall
pkgver=0.17.0
pkgrel=1
pkgdesc="Binary installation for Rust projects"
arch=('loong64' 'x86_64')
url="https://github.com/cargo-bins/cargo-binstall"
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e9dcf989c8eeaf3d8fda29a32e1dddf25e4bbc16d97fabca4deafa3f41814631')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=" -ffat-lto-objects"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
