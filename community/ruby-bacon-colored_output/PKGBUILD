# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bacon-colored_output
pkgver=1.1.1
pkgrel=1
pkgdesc='Colored output for Bacon test framework'
arch=(any)
url='https://github.com/whitequark/bacon-colored_output'
license=(MIT)
depends=(ruby-bacon)
options=(!emptydirs)
source=(https://github.com/whitequark/bacon-colored_output/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('90e0e15f33991da2c8ffb9e2478fa05cc8a60c495a5193fdefa89181029a7677')

prepare() {
  cd bacon-colored_output-$pkgver
  sed -e 's|git ls-files|find -type f|' -i bacon-colored_output.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bacon-colored_output-$pkgver
  gem build bacon-colored_output.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bacon-colored_output-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bacon-colored_output-$pkgver
  # No tests
}

package() {
  cd bacon-colored_output-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
