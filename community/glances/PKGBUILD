# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=3.2.7
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
makedepends=('git' 'python-setuptools')
depends=('python' 'python-psutil' 'python-future' 'python-defusedxml')
optdepends=('hddtemp: HDD temperature monitoring support'
            'python-bottle: web server support'
            'python-docker: for the Docker monitoring support'
            'python-matplotlib: for graphical/chart support'
            'python-netifaces: for the IP plugin'
            'python-zeroconf: for the autodiscover mode'
            'python-pystache: templating engine')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/nicolargo/glances/archive/v${pkgver}.tar.gz"
        'glances.service')
sha512sums=('12edf99cf30c72a60eb7a604bce99bdac3ae290e910d0b88fc0b8b99045512dafd8d39bc61e8c768191335727ee5e2db4fa7d17ad92f63e849413a233c2ba5be'
            '49f0d185a37a5c5837e5beb463770c943ede40b2f1b8405e338129e897e97d9fc58373a8586fabc506266e6343cfea3c91b9787ac6832cc97a1ab63d6ad058d4')
b2sums=('b76632787d39cb0f9388c00cc5838a9f553020b7ce1b502f205bbbf2255d7e2e02a2f2d629a84b23951b02a019d20b121dea7ea2cb7dec0a77b7c88ea4679cb1'
        'ecc44f8c06b1e8624cec92e41422a65d11e024b9fc23bae09b4e52fbedeb172a5034e5b612bbff7ba93d45189fb25eda0d54bc47b22b7f3f0acba984391e4017')

package() {
  cd "glances-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 "${srcdir}"/glances.service \
    "${pkgdir}"/usr/lib/systemd/system/glances.service
}

# vim:set ts=2 sw=2 et:
