# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=4.2.0
pkgrel=1
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("https://github.com/kevin1024/vcrpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('61eeec585fc78be1ae6d4f9047123c9680611e436bb5980fb7c6b885e16b2ef9bd5ce921efff7e87a222b3662c7009dbf0a8df24cdb9849d09deb24957ba4889')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
