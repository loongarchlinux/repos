# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=4.1.1
pkgrel=5
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz"
        fix-urllib-redirect-tests.patch)
sha512sums=('afa1904bff37bfd2d278ed49fa3982a357eb6bb113818b532044da0f11a9dd877cc152d7f0d81d88bdc0f870521b1241f6aecf959f668211904e3c6c7cea4ad8'
            'abe9cbb99e83e8bc521ec7e744f52580b410ed88c1dfaa441174ced526460389ffba0decccc4483e50fd9ec68c94388ec5cc1abd668abd88ce057db25a8e9b8d')

prepare() {
  cd vcrpy-$pkgver
  patch -Np1 -i ../fix-urllib-redirect-tests.patch
}

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
