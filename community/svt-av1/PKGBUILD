# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=svt-av1
pkgver=0.8.5
pkgrel=1
pkgdesc='Scalable Video Technology AV1 encoder'
arch=(x86_64)
url=https://github.com/AOMediaCodec/SVT-AV1
license=(BSD)
makedepends=(
  cmake
  git
  nasm
  ninja
)
_tag=45081abaeebd488946b576452e46ddac92c6381d
source=(git+https://github.com/AOMediaCodec/SVT-AV1.git#tag=${_tag})
md5sums=(SKIP)

pkgver() {
  cd SVT-AV1

  git describe --tags | sed 's/^v//'
}

prepare() {
  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-AV1/CMakeLists.txt
}

build() {
  export LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

  cmake -S SVT-AV1 -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 SVT-AV1/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-av1/
}

# vim: ts=2 sw=2 et:
