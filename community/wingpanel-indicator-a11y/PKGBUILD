# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-a11y
pkgver=1.0.0
pkgrel=2
pkgdesc='Wingpanel Universal Access Indicator'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-a11y
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  meson
  vala
  wingpanel
)
_tag=951885a75c32f3c7e9e174f52ff0845ed51e027b
source=(git+https://github.com/elementary/wingpanel-indicator-a11y.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-a11y
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-a11y build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
