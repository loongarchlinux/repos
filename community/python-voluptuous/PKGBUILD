# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-voluptuous
pkgver=0.13.0
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://pypi.io/packages/source/v/voluptuous/voluptuous-$pkgver.tar.gz")
sha512sums=('a4b76a7d7e3f1f2697c09b025c2386b77c48e7f9d001e05ab0b93c91167e438352ac682d52527e2cc01d009787fedd4674cf7c926ed908022fe381c09d488f29')

prepare() {
  # https://github.com/alecthomas/voluptuous/issues/333
  rm -r voluptuous-$pkgver/*.egg-info
}

build() {
  cd voluptuous-$pkgver
  python setup.py build
}

check() {
  cd voluptuous-$pkgver
  python setup.py nosetests
}

package() {
  cd voluptuous-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
