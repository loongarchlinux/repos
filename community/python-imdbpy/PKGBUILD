# Maintainer: David Runge <dvzrv@archlinux.org>

_name=IMDbPY
pkgname=python-imdbpy
pkgver=2020.9.25
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5cea5146ace93b525be97ce5f3d8bb037be2e43280b3893c5475b8b2f84265e61db72ef935b1a50c341577a22ce4c98ac6d052eb047868fa88fb2dd3fab6d86a')
b2sums=('a703682d11d798c182bb4fe0a93f8dd50c9e673ff8bb6f1acbea768121c32ec6c3293a559b48381e547c21b12103732d537c6402f0f9c3851016ae0d36376d32')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# flaky tests are flaky: https://github.com/alberanid/imdbpy/issues/240
check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
