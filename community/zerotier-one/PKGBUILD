# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.8.10
pkgrel=1
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('loongarch64' 'x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz")
sha512sums=('56311f95681050f998bb67112efee78e66821850d9544ab57b0284ef032f5b098455dee80acbffa8e71c48b6bc9eaaf20e935e087f096f533dd0305cf0338cfe')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
