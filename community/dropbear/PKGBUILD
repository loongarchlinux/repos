# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <pierce.json@gmail.com>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur@sanxion.net>

pkgbase=dropbear
pkgname=(dropbear dropbear-scp)
pkgver=2020.80
pkgrel=1
pkgdesc='Lightweight SSH server'
arch=(loongarch64 x86_64)
url='https://github.com/mkj/dropbear'
license=(MIT)
options=(emptydirs)
makedepends=('git')
validpgpkeys=('F7347EF2EE2E07A267628CA944931494F29C6773')
source=("git+$url#commit=80e928170150f2f11ce6aeaa030870608bb6323b" # tag: DROPBEAR_2020.80
        localoptions.h
        $pkgname.service)
sha256sums=('SKIP'
            'a284c2a2570d5b1f98f38fc044ab6fd45b9834be7a91216df7f159257b54e0bc'
            '0c0e5b9d121b9d728fed92d6574f2a96d74e4f8a61169dc1e452488f43bb1484')

prepare() {
  cp -f localoptions.h "$pkgname/"
}

build() {
  cd $pkgname
  autoconf
  autoheader
  ./configure --bindir=/usr/bin --prefix=/usr --sbindir=/usr/bin
  make \
    PROGRAMS='dbclient dropbear dropbearconvert dropbearkey scp' SCPPROGRESS=1
}

package_dropbear() {
  install -d "$pkgdir/etc/$pkgname"
  make -C $pkgbase install DESTDIR="$pkgdir"
  install -Dm644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 $pkgbase/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_dropbear-scp() {
  pkgdesc='Lightweight application for copying files over SSH'
  provides=(scp)
  conflicts=(openssh)

  make -C $pkgbase install PROGRAMS=scp DESTDIR="$pkgdir"
  install -Dm644 $pkgbase/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rmdir "$pkgdir/usr/share/man"{/man1,}
}

# vim: ts=2 sw=2 et:
