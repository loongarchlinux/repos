# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: <boromil@gmail.com>

pkgname=libopenraw
pkgver=0.2.3
pkgrel=2
pkgdesc='Library for decoding RAW files'
arch=('x86_64')
url='https://libopenraw.freedesktop.org/'
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('boost' 'libxml2' 'cargo')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere <hub@mozilla.com>
source=("https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2"{.asc,})
sha256sums=('SKIP'
            'aef60cf4d91e8428c82adcf50a5bda80cacae484b1433dc965650232fde3db9a')

prepare() {
# Fix libopenraw dependency in -gnome pc file
  sed -e 's|libopenraw-0.1|libopenraw-0.2|' -i $pkgname-$pkgver/gnome/libopenraw-gnome-0.2.pc.in
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: libopenraw.freedesktop.org/wiki
# vim: ts=2 sw=2 et:
