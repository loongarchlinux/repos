# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=laz-perf
pkgver=3.0.0
pkgrel=1
epoch=2
pkgdesc="Alternative LAZ implementation for C++ and JavaScript"
arch=(any)
url="https://github.com/hobu/laz-perf"
license=('LGPL')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hobu/laz-perf/archive/${pkgver}.tar.gz")
sha512sums=('57fcbf661c306b01f86254c4471de68f7359d050cc562ff549ab3560f54f9a4455624740335009d2dfd6d9b3298c4742198106886ba1b0c97d4dfdeddeb180c0')

build() {
  cd laz-perf-$pkgver

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd laz-perf-$pkgver

  ninja -C build test
}

package() {
  cd laz-perf-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
