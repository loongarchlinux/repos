# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-gopkg-yaml.v2
pkgver=2.4.0
pkgrel=1
pkgdesc='YAML support for the Go language'
arch=('any')
url="https://github.com/go-yaml/yaml"
license=('Apache')
depends=('go')
checkdepends=('golang-gopkg-check.v1')
source=("$pkgname-$pkgver.tar.gz::https://github.com/go-yaml/yaml/archive/v$pkgver.tar.gz")
sha512sums=('6408fb2c4641c35ee5c8b2ec8e4a6dc89a089f3cccb509dc46e531c23d268a77f501e493753dea2d4232cf7e8817c6388124e7174e4d7dd5dcd100b2b57455d1')

check() {
  export GO111MODULE=off
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$srcdir"/build/src/gopkg.in/yaml.v2
  cd "$srcdir"/build/src/gopkg.in/yaml.v2
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$pkgdir"/usr/share/gocode/src/gopkg.in/yaml.v2
}
