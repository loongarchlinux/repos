# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=userpath
pkgname=python-$_pkgname
pkgver=1.7.0
pkgrel=3
pkgdesc='Cross-platform tool for adding locations to the user PATH, no elevated privileges required!'
arch=('any')
url='https://github.com/ofek/userpath'
license=('MIT' 'Apache')
depends=('python' 'python-click' 'python-distro')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('faabda97ae4be23350ecf9e3be9b21b3b66b6873f31e094f5bd839b5ddd28d0968799217678b91fdf80a3b829aa1f7f6b451c55b0fc94f770e62f17dd6192b5e')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:set ts=2 sw=2 et:
