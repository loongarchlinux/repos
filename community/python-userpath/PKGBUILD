# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=userpath
pkgname=python-$_pkgname
pkgver=1.6.0
pkgrel=1
pkgdesc='Cross-platform tool for adding locations to the user PATH, no elevated privileges required!'
arch=('any')
url='https://github.com/ofek/userpath'
license=('MIT' 'Apache')
depends=('python' 'python-click' 'python-distro')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('13e101b1596641b8e199e222bb877c7a630d384c060a0b22b01348b4aa21b9dedcf22f162c6df2f17b50b26aaabf279b7d6ef04e310b7e738c5b4318c5f14740')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:set ts=2 sw=2 et:
