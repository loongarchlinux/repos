# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgver=35
pkgrel=1
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice.'
url='http://ngspice.sourceforge.net'
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('libxaw' 'libedit' 'gcc-libs')
source=("https://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-$pkgver.tar.gz"
        "http://ngspice.sourceforge.net/docs/ngspice-$pkgver-manual.pdf")
sha1sums=('61a39d0aa75f43a2325d444f7d837c978053ec39'
          '0ac5d62eee5a641a69509e5e1d2ab6424704787e')

# figure out why libedit is okay, but readline isn't

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=no \
              --enable-xspice \
              --enable-cider \
              --enable-openmp \
              --enable-oldapps \
              --with-ngshared
  make
  #strip ./src/.libs/libngspice.so.0.0.0

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=no \
              --enable-xspice \
              --enable-cider \
              --enable-oldapps \
              --enable-openmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644  "$srcdir/ngspice-$pkgver-manual.pdf" "$pkgdir/usr/share/doc/$pkgname/manual.pdf"

  install -Dm755 src/.libs/libngspice.so.0.0.1 "$pkgdir/usr/lib/libngspice.so.0.0.1"
  ln -s "libngspice.so.0.0.1" "$pkgdir/usr/lib/libngspice.so.0"
  ln -s "libngspice.so.0.0.1" "$pkgdir/usr/lib/libngspice.so"
  # FS#70563, probably remove on v35
  install -Dm644 ngspice.pc "$pkgdir/usr/lib/pkgconfig/ngspice.pc"
  install -Dm644 src/include/ngspice/sharedspice.h "$pkgdir/usr/include/ngspice/sharedspice.h"
}
