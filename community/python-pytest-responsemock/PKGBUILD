# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-responsemock
pkgname=python-pytest-responsemock
pkgver=1.0.1
pkgrel=3
pkgdesc="Simplified requests calls mocking for pytest"
arch=(any)
url="https://github.com/idlesign/pytest-responsemock"
license=(BSD)
depends=(python-pytest python-responses)
makedepends=(python-setuptools)
checkdepends=(python-requests)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4b04f5e1e7608640cf8310d33ce6ad53cdf416f0a05a087b86288abff53bd9ce2ecbfaf83320b1356e4aecfc7f3d7bf12dbfb30d1488aabf96e32f5e37612c8a')
b2sums=('b62184c382b9110b5a0479c1fe5aaf24a7fc0342aa69f8d21559a870c937ec635a3e18bf803ff3544843af664ab0b3b996bbba2eb2c3796cf7a5b3a6f862c001')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable flaky tests
  pytest -v -k "not test_bypass"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
