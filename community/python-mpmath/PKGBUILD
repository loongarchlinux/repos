# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Rafael G. Martins <rafael@rafaelmartins.com>
# Contributor: Iván Pulido <ijpulidos@riseup.net>

pkgname=python-mpmath
pkgver=1.2.1
pkgrel=2
pkgdesc='Python library for arbitrary-precision floating-point arithmetic.'
arch=(any)
url='http://mpmath.org/'
license=(BSD)
depends=(python-gmpy2)
makedepends=(python-setuptools-scm)
source=(https://pypi.python.org/packages/source/m/mpmath/mpmath-$pkgver.tar.gz
        mpmath-sage-detection.patch::"https://github.com/fredrik-johansson/mpmath/commit/84ca137f.patch")
sha256sums=('79ffb45cf9f4b101a807595bcb3e72e0396202e0b1d25d689134b48c4216a81a'
            '16b91c3b2197fce6a0e1a431aa3ad6a5854ec4985bdc479f37382d79e35adb35')

prepare() {
  patch -d mpmath-$pkgver -Rp1 < mpmath-sage-detection.patch # Fix Sage detection
}

build() {
  cd mpmath-$pkgver
  python setup.py build
}

package() {
  cd mpmath-$pkgver  

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
