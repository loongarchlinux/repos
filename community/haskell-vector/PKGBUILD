# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.12.3.0
pkgrel=23
pkgdesc="Efficient Arrays"
url="https://hackage.haskell.org/package/vector"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-primitive')
makedepends=('ghc' 'haskell-base-orphans' 'haskell-hunit' 'haskell-quickcheck' 'haskell-random'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha512sums=('7078f5179913f385f67e21150d1d0a1d80f6a5419a8be54728ecd0eadd1c26be262736df8f345e88d9cffeadb0248f19256d7df282fcdd524d36691cb2563fff')

build() {
  cd "$srcdir"/vector-${pkgver}

  if (( CHECKFUNC )); then
      _opts=('--enable-tests')
  else
      _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd vector-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
