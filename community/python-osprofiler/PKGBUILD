# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-osprofiler
pkgver=3.4.1
pkgrel=1
pkgdesc="OpenStack Profiler Library"
arch=('any')
url="https://docs.openstack.org/osprofiler/latest/"
license=('Apache')
depends=('python-netaddr' 'python-oslo-concurrency' 'python-oslo-serialization' 'python-oslo-utils'
         'python-prettytable' 'python-requests' 'python-six' 'python-webob')
checkdepends=('python-ddt' 'python-docutils' 'python-elasticsearch' 'python-jaeger-client'
              'python-pymongo' 'python-redis' 'python-stestr')
source=("https://github.com/openstack/osprofiler/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b3a561756bd25398485862bc4244e4229b86de3a5f33e22d984d9d96ab97aa0b453aa7d8585267a7d1b117ab0ad55a28fc8d6c537b7f6889dec746900abaa7cc')

export PBR_VERSION=$pkgver

build() {
  cd osprofiler-$pkgver
  python setup.py build
}

check() {
  cd osprofiler-$pkgver
  stestr run
}

package() {
  cd osprofiler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
