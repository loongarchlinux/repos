# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=zim-tools
pkgver=3.1.1
pkgrel=1
pkgdesc="Various ZIM command line tools"
license=('GPL3')
arch=('loong64' 'x86_64')
provides=('zimwriterfs')
conflicts=('zimwriterfs')
replaces=('zimwriterfs')
depends=('file' 'libzim' 'docopt' 'gumbo-parser')
makedepends=('meson' 'mustache')
url='https://github.com/openzim/zim-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('980352ded328cadb41fce43706b679065b4933169d3909e82ea52b07e2cc18d5')

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
