# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-samovar
pkgver=2.1.4
pkgrel=2
pkgdesc='A flexible option parser excellent support for sub-commands and help documentation'
arch=(any)
url='https://github.com/ioquatix/samovar'
license=(MIT)
depends=(ruby-console ruby-mapping)
checkdepends=(ruby-covered ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/ioquatix/samovar/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('cf2fc2ac131eb9a869401d2125c224225950150ca86ec49d1b5056fb5a7c36e8')

prepare() {
  cd samovar-$pkgver
  sed -e 's|git ls-files|find -type f|' -i samovar.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd samovar-$pkgver
  rake build
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/samovar-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd samovar-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd samovar-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
