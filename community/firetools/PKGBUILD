# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: ajs124 < aur AT ajs124 DOT de > 

pkgname=firetools
pkgver=0.9.62
pkgrel=1
pkgdesc='Graphical user interface of Firejail'
arch=('x86_64')
url='https://firejail.wordpress.com/'
license=('GPL')
depends=('gcc-libs' 'firejail' 'qt5-base')
source=("https://sourceforge.net/projects/firejail/files/firetools/${pkgname}-${pkgver}.tar.xz"{,.asc})
#source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/netblue30/firetools/archive/${pkgver}.tar.gz")
sha256sums=('1f91272b465f590d74bace9c1e9192ec4cf23543d95628cff4ff31b0e5d86db4'
            'SKIP')
validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7') # netblue (firejail key)

build() {
    cd "${pkgname}-${pkgver}"
    
    # fix build: 'undefined reference to...' and 'DSO missing from command line'
    # this is caused by upstream commit:
    # https://github.com/netblue30/firetools/commit/15a4571cffaba9b649d614994c5d00d1206bc2bc
    export LDFLAGS="-lrt ${LDFLAGS}"
    
    ./configure --prefix='/usr'
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
