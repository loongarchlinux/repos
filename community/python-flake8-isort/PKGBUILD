# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

_name=flake8-isort
pkgname=python-flake8-isort
pkgver=5.0.0
pkgrel=1
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=('any')
url=https://github.com/gforcada/flake8-isort
license=('GPL3')
depends=('flake8' 'python-isort')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest' 'python-toml')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('e336f928c7edc509684930ab124414194b7f4e237c712af8fcbdf49d8747b10c')
b2sums=('80a3856a040a1d6b670eda54cf94d95786375e8f9be83276a975304ea28ecaacd0aed33349897064354ce68c87c0de1eac8a9251ac514aa8e550a4fa387c57d1')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest run_tests.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
