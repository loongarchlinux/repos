# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

_name=flake8-isort
pkgname=python-flake8-isort
pkgver=5.0.3
pkgrel=1
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=('any')
url=https://github.com/gforcada/flake8-isort
license=('GPL3')
depends=('flake8' 'python-isort')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest' 'python-toml')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('0951398c343c67f4933407adbbfb495d4df7c038650c5d05753a006efcfeb390')
b2sums=('0a8d233f0dc70a0e8edf578f473c2819b83edf836f36332cfb4ae5e2422fe1f86e75217203a5bf8a26cb58cabdbc1dd05697d21c3ee865fb1c5279c4cca7a0cf')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest run_tests.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
