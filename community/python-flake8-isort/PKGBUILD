# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

_name=flake8-isort
pkgname=python-flake8-isort
pkgver=4.1.1
pkgrel=2
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=('any')
url=https://github.com/gforcada/flake8-isort
license=('GPL3')
depends=('flake8' 'python-isort' 'python-testfixtures')
checkdepends=('python-pytest' 'python-setuptools' 'python-toml')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('d814304ab70e6e58859bc5c3e221e2e6e71c958e7005239202fee19c24f82717')
b2sums=('8edc1321a8a72a36f203601c8bcf73467b583940bed1c13899e2b3f5748d4268433116efe27192d903bcd0bc68d580ab85b7c37bc4903e1aacc130643cd5d427')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
