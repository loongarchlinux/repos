# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=flake8-typing-imports
pkgname=python-$_pkgname
pkgver=1.10.0
pkgrel=1
pkgdesc='flake8 plugin which checks that typing imports are properly guarded'
arch=('any')
url='https://github.com/asottile/flake8-typing-imports'
license=('MIT')
depends=('flake8')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'flake8' 'python-pyflakes' 'python-pycodestyle' 'python-mccabe')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a248504215445f33d0a4598b9b5e2411897bc651aee7913b49d3c01840f6f0dbf7762133f6f6a879f992f060c69f2bc9c68bef9900865bda05b59f5b28761ad9')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
