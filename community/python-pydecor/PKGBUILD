# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pydecor
pkgname=python-$_pkgname
pkgver=2.0.1
pkgrel=3
pkgdesc='Easy peasy Python decorators'
arch=('any')
url='https://github.com/mplanchard/pydecor'
license=('MIT')
depends=('python' 'python-six' 'python-dill')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('3cd26ac107a9edc5e2219388e144ec11eb1adf296b70d6a144815ea31508652da7da0842004ec0a302d69d5878e4414304810da05e2d4314baec31ae1d7df0ac')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=src pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
