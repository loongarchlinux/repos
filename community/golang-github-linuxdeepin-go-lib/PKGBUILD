# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-linuxdeepin-go-lib
pkgver=5.8.9.2
pkgrel=1
pkgdesc='A library containing many useful go routines for things such as glib, gettext, archive, graphic, etc.'
arch=('any')
url="https://github.com/linuxdeepin/go-lib"
license=('GPL3')
depends=('dbus' 'go' 'libpulse' 'alsa-lib' 'libcanberra' 'gdk-pixbuf2' 'gdk-pixbuf-xlib'
         'mobile-broadband-provider-info' 'libx11' 'golang-gopkg-alecthomas-kingpin.v2' 'golang-deepin-gir'
         'golang-github-linuxdeepin-go-x11-client' 'golang-golang-x-net' 'golang-golang-x-image')
         # 'golang-github-cryptix-wav' not packaged yet, paused until our go packaging standards formed
checkdepends=('iso-codes' 'golang-gopkg-check.v1' 'git') # git needed only for go get
replaces=('golang-deepin-lib')
conflicts=('golang-deepin-lib')
provides=('golang-deepin-lib')
source=("https://github.com/linuxdeepin/go-lib/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('193c27177e381468e380616bbf4c972fca17e2a94be05170b961130eca6d4c4b58a97a83ecc9d3fc7a30a1a1fc4e675d8deb7c63417920113905fbbedbca3b40')

export GO111MODULE=off

prepare() {
  cd go-lib-$pkgver
  sed -i 's/int connect_timeout;/extern int connect_timeout;/' pulse/dde-pulse.h
}

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-lib-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-lib
  cd "$srcdir"/build/src/github.com/linuxdeepin/go-lib
  # TODO: make packages for them
  go get github.com/cryptix/wav github.com/smartystreets/goconvey/convey github.com/mozillazg/go-pinyin gopkg.in/yaml.v3 github.com/godbus/dbus github.com/fsnotify/fsnotify
  # TODO: figure out why pulse tests hang
  # passwd: test needs to access /etc/passwd
  # group & timer & log & dbus: build failed
  # shell: TestEncode failed
  go test -v $(go list ./... | grep -v -e lib/pulse -e lib/users/passwd -e lib/users/group -e lib/timer -e lib/log -e lib/dbus -e lib/shell)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-lib-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-lib

  rm -r "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-lib/debian
}
