# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=1.11.3
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
depends=('python-elementpath')
makedepends=('python-pip')
checkdepends=('python-lxml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")
sha512sums=('39677ff186b662977bf960ea467183416309cc6b278cc3225265e47bbe72c5fd1c027bc23c8da4813bd2ac2d3aac573e6c2272c3378c3ba60cf5ecd4fac9627a')

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m unittest
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
