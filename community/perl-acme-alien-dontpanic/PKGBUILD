# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-acme-alien-dontpanic
pkgver=2.1100
pkgrel=3
pkgdesc="Test Module for Alien::Base"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Acme-Alien-DontPanic"
depends=('dontpanic' 'perl-alien-build' 'perl-file-sharedir')
makedepends=('perl-alien-base-modulebuild')
checkdepends=('perl-test2-suite')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Acme-Alien-DontPanic-$pkgver.tar.gz")
sha512sums=('3c9168b3ec67a6f499928b964834bccc3e027974025bc3f077e2769548b0184a8e96714d0ef99e62dc60e82b9d481e9123c59654ee2c7058cbbb74a1cd92bd16')

build() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build test
}

package() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build install destdir="$pkgdir"
}
