# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=jsonmerge
pkgname=python-$_pkgname
pkgver=1.8.0
pkgrel=3
pkgdesc='Merge a series of JSON documents'
arch=('any')
url='https://github.com/avian2/jsonmerge'
license=('MIT')
depends=('python-jsonschema')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgname-$pkgver.tar.gz")
sha512sums=('c6bc20267653f74718f728442d31a8712845963f1035ce170c3712df5fca4ed3ce5407d65ab429a93e0e23eb379ced19528ae7533387eb3f52f826fc82ea4627')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py test
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
