# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-discover
pkgname=haskell-tasty-discover
pkgver=4.2.2
pkgrel=82
pkgdesc="Test discovery for the tasty framework"
url="https://github.com/haskell-works/tasty-discover"
license=('MIT')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-glob')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-hspec' 'haskell-tasty' 'haskell-tasty-hedgehog'
             'haskell-tasty-hspec' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-tasty-smallcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        tasty-hspec-1.1.7.patch)
sha512sums=('42a0d9c79e3c80780fbf65b9069586d908eaea8cdad62b35120369cfc1fa14f7155b8aba27356d6f454c6fb292bcd3f637ce59827795bdf5cce4e9ab5f9e7792'
            '396fec6196fabe5e12894a60e621b47eacb59d7effe7a1b4b9145be8acae4c0afc93a46d8495eea1dd8752195948c049e34d3260fedb9d10a425646a55b07acb')

prepare() {
    patch -d $_hkgname-$pkgver -p1 < tasty-hspec-1.1.7.patch
}

build() {
    cd $_hkgname-$pkgver

    # Hack to allow ghc execute tasty-discover when building tests
    export PATH="$PWD/dist/build/$_hkgname:$PATH"
    export LD_LIBRARY_PATH="$PWD/dist/build"

    # Test could be built before executable, so build an executable first
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla
    runhaskell Setup build $MAKEFLAGS

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
