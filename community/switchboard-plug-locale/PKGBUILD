# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=2.5.2
pkgrel=1
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL3)
depends=(
  accountsservice
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libibus
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=1d0ceef7e90072b13da05f744573e0d4eb47f817)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-locale

  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
