# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Platon Pronko <platon7pronko@gmail.com>

pkgname=python-lsp-server
pkgver=1.3.2
pkgrel=2
pkgdesc="Fork of the python-language-server project, maintained by the Spyder IDE team and the community"
arch=(any)
url="https://github.com/python-lsp/python-lsp-server"
license=(MIT)
depends=(python-jedi python-lsp-jsonrpc python-pluggy python-ujson python-setuptools)
checkdepends=(
    python-pytest
    python-flaky
    python-matplotlib
    python-mccabe
    python-numpy
    python-pandas
    python-pycodestyle
    python-pydocstyle
    python-pyflakes
    python-pylint
    python-pyqt5
    python-rope
    autopep8
    flake8
    yapf
)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-py3.10.patch::${url}/pull/133.patch)
sha256sums=('c768f5714880b38111516339384bbb0f7328cd1c50090038c915210f3f6b01cb'
            '2faa399a1dc34f6956d6d0196cecf5ece53cbff5ec2f2cc8bea8400997f9957e')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|autopep8>=1.6.0,<1.7.0|autopep8>=1.6.0|g' -i setup.py
  sed 's|flake8>=4.0.0,<4.1.0|flake8>=4.0.0|g' -i setup.py
  sed 's|mccabe>=0.6.0,<0.7.0|mccabe>=0.6.0|g' -i setup.py
  sed 's|pycodestyle>=2.8.0,<2.9.0|pycodestyle>=2.8.0|g' -i setup.py
  sed 's|pyflakes>=2.4.0,<2.5.0|pyflakes>=2.4.0|g' -i setup.py
  patch -p1 < ../${pkgname}-fix-py3.10.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  sed -i '/addopts =$/,$d' setup.cfg
  pytest -v --color=yes
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
