# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dogpile.cache
pkgver=1.1.7
pkgrel=1
pkgdesc="A caching front-end based on the Dogpile lock"
arch=('any')
url="https://pypi.python.org/pypi/dogpile.cache"
license=('BSD')
depends=('python-decorator' 'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('memcached' 'pifpaf' 'redis' 'python-mako' 'python-redis' 'python-pytest' 'python-mock'
              'python-pytest-cov' 'python-pylibmc')
source=("https://pypi.io/packages/source/d/dogpile.cache/dogpile.cache-$pkgver.tar.gz")
sha512sums=('2c7e8dfc252241ad075a073ad1c1688865943f6e33a317618a47e95132b65ac891b00d5aa0bf3bcced005a6157b50754b086192470b3ce867461bf183947d603')

build() {
  cd dogpile.cache-$pkgver
  python setup.py build
}

check() {
  cd dogpile.cache-$pkgver
  python -m pytest -k 'not test_memcached_backend and not test_redis_backend'
  pifpaf --env-prefix DOGPILE run memcached --port 11234 -- python -m pytest tests/cache/test_memcached_backend.py
  pifpaf --env-prefix DOGPILE run redis --port 11234 -- python -m pytest tests/cache/test_redis_backend.py
}

package() {
  cd dogpile.cache-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
