# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Hector Mtz-Seara <hseara.#at#.gmail*.*com>

pkgname=pyzo
pkgver=4.10.2
pkgrel=1
pkgdesc="Cross-platform Python IDE focused on interactivity and introspection, very suitable for scientific computing."
arch=(any)
url="https://pyzo.org"
license=(BSD)
depends=(python pyside2)
makedepends=(python-setuptools)
optdepends=('python-pyqt5: to use PyQt5 as backend instead of PySide2')
source=(https://github.com/pyzo/pyzo/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7f7f7d6990da32190a2f05e469674fa3cc92cb41c9e5c5ebb9309ba96e483f96')

build(){
    cd ${pkgname}-${pkgver}
    python setup.py build
}

package(){
    cd ${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    # Clean
    rm "${pkgdir}"/usr/{LICENSE.md,README.md,pyzo.appdata.xml,pyzolauncher.py}
    install -Dm644 LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
    install -Dm644 pyzo.appdata.xml -t "${pkgdir}"/usr/share/metainfo/
    install pyzolauncher.py "${pkgdir}"/usr/bin/pyzo

    install -Dm644 pyzo/resources/pyzo.desktop -t "${pkgdir}"/usr/share/applications/
    for i in 16 32 48 64 128 256 ; do
        install -Dm644 pyzo/resources/appicons/pyzologo${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/pyzologo.png
    done
}
