# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=jalv
pkgver=1.6.4
pkgrel=2
pkgdesc="A simple but fully featured LV2 host for Jack"
arch=('x86_64')
url="https://drobilla.net/software/jalv"
license=('custom:ISC')
groups=('pro-audio')
depends=('glibc' 'libjack.so' 'liblilv-0.so' 'libserd-0.so' 'libsord-0.so'
'libsratom-0.so' 'libsuil-0.so')
makedepends=('gtk3' 'gtkmm' 'lv2' 'qt5-tools' 'waf')
optdepends=('atk: for jalv.gtkmm and jalv.gtk3'
            'cairo: for jalv.gtkmm and jalv.gtk3'
            'fontconfig: for jalv.gtkmm and jalv.gtk'
            'gcc-libs: for jalv.qt5 and jalv.gtkmm'
            'glib2: for jalv.gtkmm and jalv.gtk3'
            'qt5-base: for jalv.qt5'
            'gtk2: for jalv.gtkmm and jalv.gtk'
            'gtk3: for jalv.gtk3'
            'gtkmm: for jalv.gtkmm'
            'harfbuzz: for jalv.gtkmm and jalv.gtk3'
            'gdk-pixbuf2: for jalv.gtkmm, jalv.gtk and jalv.gtk3'
            'libsigc++: for jalv.gtkmm'
            'libfreetype.so: for jalv.gtkmm and jalv.gtk'
            'pango: for jalv.gtkmm, jalv.gtk and jalv.gtk3'
            'zlib: for jalv.gkt3')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('bef75f422cae7d536e0caa620ea47872277e42001044a2682deb2c1aa3ecd8fc51056b4a49a950cbd04c6694751579f9932e7dd0f7868f9825ccc2d8e2110c1b'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

prepare() {
  cd "${pkgname}-${pkgver}"
  # copying custom waf script to separate directory, removing outdated waflib
  # and fixing wscript to use the custom tools location instead.
  mkdir -pv tools
  mv -v waflib/extras/autowaf.py tools/
  rm -rfv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr
  waf
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
