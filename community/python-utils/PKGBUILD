# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.3.3
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-loguru python-pytest-runner)
optdepends=('python-loguru: for logging support')
source=(https://pypi.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('6becb1873da57f66aec26b98dab22c7917dacddd1a843a0c6e4f2939986a33888cb3b6addfa3b98660f9f35ca54763cf6da34e4099cd3e21987660f12a574b27'
            'SKIP')
b2sums=('d491e5ba1c8e2ab8d7c7797757cb6163f5f4cff223b898b4a9502f85b740df3c128234fd0034fc422d17efb38cfc7056aa7fb3aee9441f1f763fc742930da5b3'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
