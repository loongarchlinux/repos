# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.2.3
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-loguru python-pytest-runner)
optdepends=('python-loguru: for logging support')
source=(https://pypi.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('85f1607faacf210c8e377a438c5d2f66e237cc870a91a9ed1ec7150aa8a7bb8f8fd5b3b0fb5e9585afb81801e7048f92b7ea46943205ddd9e271d6087b5e05d5'
            'SKIP')
b2sums=('b4e65b647b42288111edc7a90f7c1b1c3a222a999edacc2f0764e378f7c5f0b5c512842d73ea9f2e70052ba43ac48f9eb914a8a410b564bcd965c357672eda57'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
