# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pyVHDLModel
pkgname=python-pyvhdlmodel
pkgver=0.12.0
pkgrel=3
pkgdesc='An abstract language model of VHDL written in Python'
arch=('any')
url='https://github.com/VHDL/pyVHDLModel'
license=('Apache2')
depends=('python' 'python-pydecor')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d3e6bb9a0e04c742a4cb724b399860b0d4b4fcaecb157d0bfdc8686f80a4aa6223813b4a33ceea87406465893ea4c0d8d555f59285e4220324e78902c6e5d7ca')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest tests
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
