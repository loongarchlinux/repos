# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>

pkgname=lxcfs
pkgver=4.0.4
pkgrel=2
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse2' 'dbus-glib')
makedepends=('help2man')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"
	"lxcfs-0001.patch::https://github.com/lxc/lxcfs/commit/7cf2b4146811fc238452e65d1cd573ea5f473970.patch"
	"lxcfs.service")
sha256sums=('4e75373654667f8cb0d27fea675bd2054af1773574fd3ca7594d2e6741c05996'
            'acc5993d1e64d459d297dc2c3a93c8ff510d4da0e9bf66e051fcc385ebad21cc'
            '3549e83ebbd6ec5934be0c3137b0dbb1101cec098bc49d135fea29f733b9e2a1')

prepare() {
    patch -d "$srcdir/$pkgname-$pkgver" -p1 -i "$srcdir"/lxcfs-0001.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-pamdir=/usr/lib/security \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
