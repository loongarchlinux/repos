# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=2.4.1
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL3)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=8d30ede20a01f1197aa9b5ccc0d135b1c122124a
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd switchboard-plug-security-privacy

  git cherry-pick -n c8e422e630bbee0badcf4df26364c9e83e06bad0
}

pkgver() {
  cd switchboard-plug-security-privacy

  git describe --tags
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
