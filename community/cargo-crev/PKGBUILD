# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.21.3
pkgrel=2
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/crev-dev/cargo-crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('loongarch64' 'x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/crev-dev/cargo-crev/archive/v${pkgver}.tar.gz")
sha512sums=('9342586a5c7fa5d3d1889889470f0e62dd36934062d35bd8be628f5e65e9de754bd4cee656d1098574412968dd2ccbc0111f126566e24ba5395ffeedcc780fdf')
b2sums=('c622878895734d0e07475f93710aa3ef4ee7db95d77cb60df1bf80ef7ac8fd5269642f20319be9d1315f96ef88bcd7fc391852f8c55785b72af7207849db1531')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --frozen --release --no-default-features
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
