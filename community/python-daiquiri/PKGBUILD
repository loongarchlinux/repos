# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.0.1
pkgrel=3
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
depends=('python-json-logger')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('4ccf04ae22b422a0a94eadc07ea28e397f27aa0052cf32eb42ecbd1273c20349f90325da91e3cd2bed8c7820f54966978c65f139303624b23f155404957bfa30')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd daiquiri-$pkgver
  python setup.py build
}

check() {
  cd daiquiri-$pkgver
  python -m pytest
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
