# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.1.0
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/Mergifyio/daiquiri'
depends=('python-json-logger')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Mergifyio/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('5ddf4e0987130545f26090e629fb4f569f5d29fc7579d51869ad2046fdfec97275cad55f2a1aadc241c94f63e13fcda5951bcda5bca759c962d5a312575c52bc')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd daiquiri-$pkgver
  python setup.py build
}

check() {
  cd daiquiri-$pkgver
  python -m pytest
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
