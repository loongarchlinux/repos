# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.1.1
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/Mergifyio/daiquiri'
depends=('python-json-logger')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Mergifyio/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('9cff5f2a323921710d571f76de78a46c2b625a44ea0cf80991adad827680a28919f2cd2b1f608fa426c525bdc58d49b45bbb891281333d46d152c83007a0e432')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd daiquiri-$pkgver
  python setup.py build
}

check() {
  cd daiquiri-$pkgver
  python -m pytest
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
