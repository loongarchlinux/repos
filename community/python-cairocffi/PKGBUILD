# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.3.0
pkgrel=1
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo' 'python-xcffib')
makedepends=('python-setuptools' 'gdk-pixbuf2' 'python-pytest-runner')
checkdepends=('python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort'
              'python-numpy')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('3d054abc51078624f6b5c1d0d3e25547809dabc1f27b530567dab3711bbef892403bcd6d73b1edd3425caad0e9916392973b81fb2057e6bad95bc7bd4fe42b2b')


build() {
  cd cairocffi-$pkgver
  python setup.py build
}

check() {
  cd cairocffi-$pkgver
  python setup.py pytest
}

package() {
  cd cairocffi-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
