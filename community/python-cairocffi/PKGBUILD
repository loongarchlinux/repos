# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.3.0
pkgrel=2
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo' 'python-xcffib')
makedepends=('python-setuptools' 'gdk-pixbuf2' 'python-pytest-runner')
checkdepends=('python-pytest' 'python-numpy' 'python-pikepdf')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz"
        $pkgname-test-fixes-for-new-cairo.patch::https://github.com/Kozea/cairocffi/pull/205.patch)
sha512sums=('3d054abc51078624f6b5c1d0d3e25547809dabc1f27b530567dab3711bbef892403bcd6d73b1edd3425caad0e9916392973b81fb2057e6bad95bc7bd4fe42b2b'
            '623ce970997ebee9fd2ea4f2f83ce4c3443f7a6c8fe7fc458ba91f7d107aab0a9efe638e9cf25fab5b8265afc977013e3bb65f5321d84e2bbfc8a40179dc9954')

prepare() {
  cd cairocffi-$pkgver
  patch -p1 -i ../$pkgname-test-fixes-for-new-cairo.patch
  sed -i 's/addopts = --flake8 --isort//' setup.cfg
}

build() {
  cd cairocffi-$pkgver
  python setup.py build
}

check() {
  cd cairocffi-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest build
}

package() {
  cd cairocffi-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
