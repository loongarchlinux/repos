# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>

pkgname=just
pkgver=1.3.0
pkgrel=1
pkgdesc="A handy way to save and run project-specific commands"
arch=("loong64" "x86_64")
url="https://github.com/casey/just"
license=("custom:CC0")
depends=("gcc-libs")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/casey/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('0214a95b7a46c6a812d3257a976114a6971892bc2129ebc87ee3094a329d71e6')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo check --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "completions/just.zsh" "${pkgdir}/usr/share/zsh/site-functions/_just"
  install -Dm644 "completions/just.bash" "${pkgdir}/usr/share/bash-completion/completions/just"
  install -Dm644 "completions/just.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/just.fish"
}
