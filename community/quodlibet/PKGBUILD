# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=4.4.0
pkgrel=3
pkgdesc="Music player and music library manager"
arch=(any)
url="https://quodlibet.readthedocs.io/"
license=(GPL2)
depends=(dbus-python gtk3 python-cairo python-feedparser python-gobject python-mutagen)
makedepends=(python-sphinx_rtd_theme)
# python-raven python-senf are currently vendored
checkdepends=(gst-plugins-base gst-plugins-good python-pytest python-xvfbwrapper)
optdepends=('gst-libav: Extra media codecs for the GStreamer backend'
            'gst-plugins-bad: Extra media codecs for the GStreamer backend; Audio Pitch / Speed, Crossfeed, Submit Acoustic Fingerprints plugins'
            'gst-plugins-base: Default audio backend'
            'gst-plugins-good: Extra media codecs, PulseAudio support for the GStreamer backend; Audio Compressor, Karaoke, Replay Gain plugins'
            'gst-plugins-ugly: Extra media codecs for the GStreamer backend'
            'gtksourceview3: Undo and redo support in multiline text fields'
            'kakasi: Kana/Kanji Simple Inverter plugin'
            'libappindicator-gtk3: Appindicator support in the Tray Icon plugin'
            'libkeybinder3: Multimedia key support under non Gnome setups'
            'libmodplug: MOD support'
            'python-musicbrainzngs: MusicBrainz Lookup, MusicBrainz Sync plugins'
            'python-paho-mqtt: MQTT Publisher plugin'
            'python-pyinotify: Automatic Library Update plugin'
            'rygel: UPnP AV Media Server plugin'
            'webkit2gtk: Web Lyrics plugin'
            'xine-lib: Alternative audio backend')
source=(
  "https://github.com/${pkgname}/${pkgname}/releases/download/release-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig}
  "${pkgname}-4.4.0-collections.patch::https://github.com/quodlibet/quodlibet/commit/49dd6474484f9f157adae485a51afc37e6c884f4.patch"
)
sha256sums=('a03318d2767e4959551763d0a87fad977387af712608fe572714176a24bbf367'
            'SKIP'
            '6a5665838f01c30b2df0456985779e55a2e05d4ce76b7e3f1438c36e089ebc90')
validpgpkeys=(0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC) # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  # use collections.abc
  # backport of https://github.com/quodlibet/quodlibet/commit/49dd6474484f9f157adae485a51afc37e6c884f4
  patch -Np1 -i ../"${pkgname}-4.4.0-collections.patch"
  # Fix zsh completions dir
  sed -e 's|vendor-completions|site-functions|' -i gdist/zsh_completions.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  export PYTHONPATH="build:${PYTHONPATH}"
  # not running useless linter checks
  # disable tests that are broken due to telnetlib (SqueezeboxPlaylistPlugin)
  pytest -v -k 'not TFlake8 and not test_remove_all and not test_plugin_pref'
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -vDm 644 {README,NEWS}.rst -t "${pkgdir}"/usr/share/doc/${pkgname}
}
