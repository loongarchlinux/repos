# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sébastien Luttringer

_pkgname=LDoc
pkgname=${_pkgname,,}
pkgver=1.4.6
pkgrel=5
pkgdesc='LuaDoc-compatible documentation generation system'
url="https://github.com/lunarmodules/$_pkgname"
arch=(any)
license=(MIT)
depends=(lua
         # lua-markdown # claimed in rockspec but vendored?
         lua-penlight)
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('4b73e78a0325fb3c295d015ddb60b5cee5b647cecb5c50ce8f01319b53bd536f')

package() {
  cd "$_archive"
  install -dm755 "$pkgdir/usr/share/lua/5.4"
  cp -a ldoc "$pkgdir/usr/share/lua/5.4"
  install -Dm0755 "$pkgname.lua" "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md doc/doc.md
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYRIGHT
}
