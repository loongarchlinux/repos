# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=python-beautifulsoup4
pkgver=4.10.0
pkgrel=1
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="https://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
depends=('python-soupsieve')
optdepends=('python-chardet: to autodetect character encodings'
            'python-lxml: alternative HTML parser'
            'python-html5lib: alternative HTML parser')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/b/beautifulsoup4/beautifulsoup4-$pkgver.tar.gz")
sha512sums=('92be99ed0fd479b86519395143516e28f7b80f54a8251223d6e7b88dc98f8dfca9585079f5f1d89bfe68f577de3dff433711642486303203f46bc76754959a18')

build() {
  cd beautifulsoup4-$pkgver
  python setup.py build
}

check() {
  cd beautifulsoup4-$pkgver
  pytest
}

package() {
  cd beautifulsoup4-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
