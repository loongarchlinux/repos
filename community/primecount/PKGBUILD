# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primecount
pkgver=7.4
pkgrel=1
pkgdesc='Fast C++ prime counting function implementation'
arch=(loong64 x86_64)
url='https://github.com/kimwalisch/primecount'
license=(BSD)
depends=(primesieve)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primecount/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('6362887e80e849dd1c396ccec773e5a3a7904371b46f1d495d320d31f9c1ae28')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_LIBPRIMESIEVE=OFF \
    -DBUILD_STATIC_LIBS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_POPCNT=OFF \
    -DWITH_FLOAT128=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
