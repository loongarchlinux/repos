# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=4.0.1
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-six' 'python-requests')
optdepends=('python-keystoneclient: for Auth versions 2.0 and 3 support')
makedepends=('python-setuptools' 'python-pbr')
checkdepends=('python-stestr' 'python-mock' 'python-keystoneauth1' 'python-keystoneclient')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('d71a9c01fa1e4db74d4672d2ba44e6827baad67584eea30c847bec23c5bb7e84ef115f20fbeda72d2e604471f2e9ee84d18b8f814c8a70541a089b719715cf57')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
