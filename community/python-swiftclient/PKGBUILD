# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=3.12.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-six' 'python-requests')
optdepends=('python-keystoneclient: for Auth versions 2.0 and 3 support')
makedepends=('python-setuptools' 'python-pbr')
checkdepends=('python-stestr' 'python-mock' 'python-keystoneauth1' 'python-keystoneclient')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('2832c77ebb71f7200f6165557961dc750f70988afa59d2c18b5dc1ba42f305ded35177cc94fd8f47286dd5f84b88f0613233c27172c08251ccf2cf7e2ed6a8aa')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
