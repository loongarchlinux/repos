# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-alerting-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Alerting Plugin"
arch=('loongarch64' 'x86_64')
url="https://docs-beta.opensearch.org/monitoring-plugins/alerting"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/alerting/archive/${pkgver}.tar.gz"
)
sha256sums=('952e613e181609aecf86fd0b9fd07f836aeb9eca08cafde26fd314c0cf4073e6')

build() {
  cd "alerting-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # alertingBwcCluster and integTest (Reaper) requires JDK 14
  ./gradlew build \
    --exclude-task ":alerting:alertingBwcCluster#fullRestartClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#mixedClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#oldVersionClusterTask0" \
    --exclude-task ":alerting:alertingBwcCluster#oldVersionClusterTask1" \
    --exclude-task ":alerting:alertingBwcCluster#rollingUpgradeClusterTask" \
    --exclude-task ":alerting:alertingBwcCluster#twoThirdsUpgradedClusterTask" \
    --exclude-task ":alerting:integTest" \
    --exclude-task ":alerting:jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-alerting"
  unzip "${srcdir}/alerting-${pkgver}/alerting/build/distributions/opensearch-alerting-${pkgver}.zip"

  install -Dm644 "${srcdir}/alerting-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
