# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: flaccid aka Chris Fordham <chris@fordham.id.au>
# Contributor: Sparadox <etienne.lafarge at gmail.com>

pkgname=cloud-init
pkgver=21.4
pkgrel=2
pkgdesc="The standard for customising cloud instances"
arch=(any)
url="https://cloud-init.io"
license=(GPL3)
depends=(dhclient netplan python-configobj python-jinja
python-jsonpatch python-jsonschema python-netifaces python-oauthlib
python-requests python-yaml sudo systemd)
makedepends=(python-setuptools)
checkdepends=(python-httpretty python-prettytable python-pytest)
optdepends=('cloud-guest-utils: for growpart')
backup=(
  etc/cloud/cloud.cfg
  etc/cloud/cloud.cfg.d/05_logging.cfg
)
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-21.3-lib_dir.patch"
        "${pkgname}-21.3-python-configobj.patch"
)
sha512sums=('ee23dfab6720063f37a14100956b823d6b070281259248a09988fcf986190ca76db1a263fce430426f6643f76f378a3b9de0e33c674a65c11296cda3c15410bd'
            '3d23d060779e2bb82a7044ee6e00169baa7ea4cda756ea32c80329b73fd69521f7dd76a4079cb1c7a941859389a8d67dec7bfb5c2c9eb236a3950eca92f446ca'
            'e4404702b51b2f199026bf8e8a75fe18cf216ce91da47f5faa644b6ed6251f49c49a85772e48f2bbfcf283292d67234b05d588ad69e5804bb4fb6b7a63f2dad5')
b2sums=('e5075ea88d0be8f92237ce182eeb80d188ea3438e9b219bc0bcfb5ae2bcf6c2a59d14b7b6d48c14cfb9a70eb44f39f007cf6154c6e5fa4fdd70d71f2bc0964a4'
        'a7845162108cdc90f576d2408c6244fbddac13431395740e02c8915b5a6a4b70f3dd5d44de27e13fb6d7a24d2e916c474bdce842b6992410385a786914e4da0f'
        '7e370e2ba8c159195586ae839a450368eeb6f558366714e873743d871bda9613dbdb2391db365f66f613fbfa60cb36a13cff990a5b92c17b31f711a098e5747f')

prepare() {
  cd "${pkgname}-${pkgver}"
  # we use /usr/lib
  patch -Np1 -i ../"${pkgname}-21.3-lib_dir.patch"
  # we have python-configobj > 5.0.6, which does not provide ConfigObj._a_to_u() anymore
  patch -Np1 -i ../"${pkgname}-21.3-python-configobj.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --init-system=systemd --root="${pkgdir}"

  # /etc/systemd is for administrative purposes, not for vendoring purposes
  mv -v "${pkgdir}/"{etc,usr/lib}/systemd/system/sshd-keygen@.service.d/
  rm -rf "${pkgdir}/etc/systemd"
}
