# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

_name=Pound
pkgname=pound
pkgver=2.8
pkgrel=3
pkgdesc="A reverse proxy, load balancer, and SSL wrapper"
arch=('x86_64')
url="http://www.apsis.ch/pound/index_html"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'pcre' 'openssl-1.0')
makedepends=('gperftools')
backup=('etc/pound/pound.cfg')
# switch to https://github.com/graygnuorg/pound for openssl
source=("http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.tgz"
        "${_name}-${pkgver}.tgz.asc::http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.asc"
        "${pkgname}.service"
        "${pkgname}.cfg")
sha512sums=('cf0b865b17d3628e273626e07733f1320e4768702c0f64c8ef0f78d46667f770b223bdc7dca88016a95e5ebd23ae646f95a9b2f4a54a5a80001a10047f07eacc'
            'SKIP'
            'c4b47825e3f394db9e8e784f6342d7912081c7de94638e51d27c6a7de0b13fd9665f5540412c8ddaf3157040f9b83d234e01d93ad3a61be45955aaf3afc6f543'
            '32d33474a115dfc9d5ccc094ffdb3c367108a48976cf3e58442642dab08167cd0a1808eefa1879e3c38b607d2a6a1cd28142dbd690244368760daba1f95526f6')
b2sums=('2e4526fb78bb8bf1206a60318fef23925f2eb3b2d72c56895e6cc839e944ad8a58820be8c6c54ff2f12cd8e34ee8500dc8f7555c968fe4cd42ae7cc51ef6feec'
        'SKIP'
        '41fade7a7dc90d4de479b95748f272be4d4ed1e0226539b1dacb20caeef20b30f66b62afc401b4e5fb43b9cbfe107d22dad88a772469f4963ecb564357cd4f2a'
        '1dc6854854fcc1e0ce32249f6dbfe89b9ccb5ebe614bc700252090a1bb1e072d763859f42bdc1e3a8aa5ddded271cecb5d4c82ef0a90ed98c113ca019cbb255b')
validpgpkeys=('8BB562A0F1DB703FB7EB1E95AB72C62A8691DD02') # Robert Segall <roseg@apsis.ch>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # use openssl-1.0 to generate dhparam
  sed -e 's/openssl/openssl-1.0/' -i Makefile.in
}

build() {
  cd "${pkgname}-${pkgver}"
  # use openssl-1.0
  CPPFLAGS+=" -I/usr/include/openssl-1.0" \
  LDFLAGS+=" -L/usr/lib/openssl-1.0" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc/pound \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --with-ssl='/usr/lib/openssl-1.0' \
              --with-owner=root \
              --with-group=root
  make
}

package() {
  depends+=('libtcmalloc.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # configuration
  install -vDm 644 "${srcdir}/${pkgname}.cfg" \
    "${pkgdir}/etc/${pkgname}/${pkgname}.cfg"
  # systemd service
  install -vDm 644 "$srcdir/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  # docs
  install -vDm 644 {CHANGELOG,FAQ,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
