# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=record-dot-preprocessor
pkgname=haskell-record-dot-preprocessor
pkgver=0.2.14
pkgrel=26
pkgdesc="Preprocessor to allow record.field syntax"
url="https://github.com/ndmitchell/record-dot-preprocessor#readme"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-uniplate')
makedepends=('ghc' 'haskell-record-hasfield')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('9759be4d05c6c57a3c6fc016a043b93d19a149885eb418bfb4d986adc1289c6b2dd2f091ff3930e459cd1d4190d328c4b98f0fb22cc63a4f20075c88280e2b8b')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
