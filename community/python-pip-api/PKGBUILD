# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-api
pkgver=0.0.18
pkgrel=2
pkgdesc="An unofficial, importable pip API"
url="https://github.com/di/pip-api"
license=('Apache')
arch=('any')
depends=('python-packaging' 'python-pyparsing' 'python-pip')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pretend')  # 'python-virtualenv' needs < 20
source=("$pkgname-$pkgver.tar.gz::https://github.com/di/pip-api/archive/$pkgver.tar.gz")
sha512sums=('f70bb2f8448f0f1d461b85210a60a0f5b94106c89bb39c0fc6ed543d653f4bb74b81d6f862b12d16f07dd05a2773c335d7521ab1a909fd28e665d0bc3a59b203')

prepare() {
  cd pip-api-$pkgver
  # Devendor
  sed -i 's/pip_api._vendor.//' pip_api/*.py tests/*.py

  # Make it install an old virtualenv for tests
  sed -i '/install_requires/a \    tests_require=["virtualenv<20"],' setup.py

  rm -r pip_api/_vendor
}

build() {
  cd pip-api-$pkgver
  python setup.py build
}

check() {
  cd pip-api-$pkgver
  python setup.py pytest
}

package() {
  cd pip-api-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
