# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-api
pkgver=0.0.22
pkgrel=1
pkgdesc="An unofficial, importable pip API"
url="https://github.com/di/pip-api"
license=('Apache')
arch=('any')
depends=('python-packaging' 'python-pyparsing' 'python-pip')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pretend' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/di/pip-api/archive/$pkgver.tar.gz")
sha512sums=('7f718d0be10fc20a3313b183dddacd1231af0dca8bf5c581437f2da8c987c3f1d5e5c960322d32e71400bd01980ace1ab59884286e16658b7d7c8d4ad2ddde47')

prepare() {
  cd pip-api-$pkgver
  # Devendor
  sed -i 's/pip_api._vendor.//' pip_api/*.py tests/*.py

  rm -r pip_api/_vendor
}

build() {
  cd pip-api-$pkgver
  python setup.py build
}

check() {
  cd pip-api-$pkgver
  python setup.py pytest
}

package() {
  cd pip-api-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
