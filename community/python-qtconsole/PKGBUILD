# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=qtconsole
pkgname=python-qtconsole
pkgver=5.2.1
pkgrel=2
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=(any)
url="https://qtconsole.readthedocs.io/"
license=(BSD)
depends=(
    python-ipykernel
    python-jupyter_core
    python-jupyter_client
    python-traitlets
    python-pygments
    python-pyqt5
    python-pyzmq
    python-qtpy
    qt5-svg
)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-qt python-flaky xorg-server-xvfb)
source=(https://github.com/jupyter/${_pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-py3.10.patch::https://github.com/jupyter/qtconsole/pull/513.patch)
sha256sums=('90e6eedb84f1e3ddc775599d9f14fe7fb68e55f2b849e9006a5c4706f869f7c4'
            '971fc34f2eada8ca6c0d1260dd7c6ce6be1082ae3eab2d31912b125e35eb239c')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -p1 < ../${pkgname}-py3.10.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  # Tests passes but then xvfb-run segfaults
  xvfb-run -s '-nolisten local' pytest -x -vv --color=yes qtconsole || echo "Tests segfaulted"
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

  cd examples
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  sed -i "s|^Icon=.*$|Icon=${site_packages}/qtconsole/resources/icon/JupyterConsole.svg|" jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop -t "${pkgdir}"/usr/share/applications/
}
