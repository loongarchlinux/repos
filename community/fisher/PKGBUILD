# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>
# Contributor: Daniel Maslowski <info@orangecms.org>

pkgname=fisher
pkgver=4.3.0
pkgrel=2
pkgdesc="A package manager for the fish shell"
arch=('any')
url="https://github.com/jorgebucaran/fisher"
license=('MIT')
depends=('fish' 'curl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('f8f15aadb1d75aac4fb0bab3d8a64e70bbb3c73d02273e9479aec510ae8503a738689018c65f54bd137a2eca4ca4fa5da7fbc8e45eb0575eb270c5fabc0bd17e')

package() {
  cd "$pkgname-$pkgver"

  # install to global fish directory
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" functions/fisher.fish
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" completions/fisher.fish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
