# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
epoch=2
pkgver=0.30.14
_basever=${pkgver%.*}
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment (odd versions are unstable!)"
arch=('loongarch64' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'webkit2gtk' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2' 'libgudev'
	 'gcr' 'libgdata')
makedepends=('intltool' 'vala>=0.17.2' 'itstool' 'meson' 'ninja')
# odd versions are unstable!
source=("https://download.gnome.org/sources/shotwell/${_basever}/shotwell-$pkgver.tar.xz")
sha256sums=('31c2e4833908d3619cb2c3675a05f0da59c2baa76e28b905385fd56c00c12895')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
