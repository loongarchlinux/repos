# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mario Finelli <mario at finel dot li>

pkgname=go-yq
pkgver=4.29.2
pkgrel=1
pkgdesc='Portable command-line YAML processor'
arch=('loong64' 'x86_64')
url=https://github.com/mikefarah/yq
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=('yq')
source=("$url/archive/v$pkgver/yq-$pkgver.tar.gz")
b2sums=('17293c70c59b0110da6b0300a6a51bdf69074bf8d7dda953fbc96726e454a709d30463e1660422a292d45b7b5c3a85b9fd3e643f2a2c214165dee5cd90770c08')

prepare() {
  cd yq-$pkgver
  go mod vendor
}

build() {
  cd yq-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"
  go build .
}

check() {
  cd yq-$pkgver
  go test ./...
}

package() {
  cd yq-$pkgver
  install -Dt "$pkgdir"/usr/bin yq
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md

  ./yq shell-completion bash | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/bash-completion/completions/yq
  ./yq shell-completion zsh | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/zsh/site-functions/_yq
  ./yq shell-completion fish | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/fish/vendor_completions.d/yq.fish
}

# vim:set ts=2 sw=2 et:
