# Maintainer : Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-genshi
pkgver=0.7.5
pkgrel=4
pkgdesc="Python toolkit for stream-based generation of output for the web."
arch=('any')
url="https://genshi.edgewall.org/"
license=("BSD")
depends=('python-six')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/edgewall/genshi/archive/${pkgver}.tar.gz"
        ${pkgname}-python310.patch::https://github.com/edgewall/genshi/commit/a23f3054b96b.patch)
sha256sums=('3c9419a4988a59c566b43e60980a28f63b6850c3fd8e1e513f8ca415be7e5081'
            '90b8b57199c9262be33a770f0f1b334fab7873a1f833e37c85d063ff0f42e641')

prepare() {
    cd genshi-${pkgver}
    patch -Np1 -i ../${pkgname}-python310.patch
}

build() {
    cd genshi-${pkgver}

    python setup.py build
}

check() {
    cd genshi-${pkgver}

    python setup.py test
}

package() {
    cd genshi-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
