# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=intelhex
pkgname=python-$_pkgname
pkgver=2.2.1
pkgrel=2
pkgdesc='Python library for Intel HEX files manipulations'
arch=('any')
url='https://github.com/python-intelhex/intelhex'
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "fix-version.patch::$url/commit/1597874d2bce3e5cefa1c0bf10cf2e98b6181337.patch")
sha512sums=('8534f71e7bc1f6c92fb766bbffb52f93fab1c6c426d8fdaca92d3d02f2b95624620456f8e95e55d1c4d974a1e3a39bf9d0abb73888ae1d13ceb5144ef6daca14'
            'eb932bee043c683fa1b2d3c3aaa5cbccf2216908c8863f9d49bd39d5a727b5f5283abb4a963983d4b87f0182c48b2a3607eaa0e9b127e69d0c3f6230a15f7416')

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../fix-version.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py test
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
