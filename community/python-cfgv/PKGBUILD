# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cfgv
pkgname=python-cfgv
pkgver=3.3.0
pkgrel=2
pkgdesc="Validate configuration and produce human readable error messages"
arch=('any')
url="https://github.com/asottile/cfgv"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# pypi sdist tarballs don't contain tests:
# https://github.com/asottile/cfgv/issues/23
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/asottile/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('23a238c76635404b150bbc3bb1eafd8eb521171e69bb3f3fee55a0a24c12d2ee152cc7453a41952d2f41a6cb72575dacf4b990dbd443f69026a770c87a1e081f')
b2sums=('d4cf565884aaab6a81716382ba8e3244e36a3381b771f7e206053fa80500205bd3b70b62b611ce4a7f0cb143a763d51eb10b1e29333b7f06e29cb406f9f7f6b8')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
