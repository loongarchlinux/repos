# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger
pkgver=1.19.1
pkgrel=11
pkgdesc="Command-line interface for the hledger accounting system"
url="http://hledger.org"
license=("GPL")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base-compat-batteries'
         'haskell-cmdargs' 'haskell-data-default' 'haskell-decimal' 'haskell-diff'
         'haskell-hashable' 'haskell-hledger-lib' 'haskell-lucid' 'haskell-math-functions'
         'haskell-megaparsec' 'haskell-old-time' 'haskell-pretty-show' 'haskell-regex-tdfa'
         'haskell-safe' 'haskell-shakespeare' 'haskell-split' 'haskell-tabular' 'haskell-tasty'
         'haskell-temporary' 'haskell-unordered-containers' 'haskell-utf8-string'
         'haskell-utility-ht' 'haskell-wizards')
makedepends=('ghc' 'haskell-extra' 'haskell-timeit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('503bd900ff9fea84ba4045402ea60dce1601cab2f768994a5ad441fe3968c0f31d8c90882ee31ea27c88d2e92822d2130dba4a8c34e1e0130b61bf14ebefaaf4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fterminfo -fthreaded --ghc-option='-pie'
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
