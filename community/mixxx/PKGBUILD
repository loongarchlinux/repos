# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.3.1
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=('loongarch64' 'x86_64')
url="https://www.mixxx.org"
license=('GPL2')
groups=('pro-audio')
# TODO: package libshout-idjc
depends=('gcc-libs' 'glibc' 'hidapi' 'lame' 'libglvnd' 'libmad' 'libmodplug'
'libx11' 'openssl' 'opusfile' 'qt5-base' 'qt5-script' 'qt5-svg' 'qt5-x11extras'
'qtkeychain' 'soundtouch' 'speex' 'sqlite' 'taglib' 'upower' 'wavpack')
makedepends=('chromaprint' 'cmake' 'faad2' 'ffmpeg' 'fftw' 'flac' 'glib2' 'glu'
'gperftools' 'libid3tag' 'libogg' 'libsndfile' 'libusb' 'libvorbis' 'lilv'
'lv2' 'qt5-tools' 'portaudio' 'portmidi' 'protobuf' 'rubberband'
'vamp-plugin-sdk')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}dj/${pkgname}/archive/${pkgver}.tar.gz"
        add-missing-include.patch)
sha512sums=('17cf8dee2aaca2ed85067474d40b0955abd22268767a9c68dbc8126554e6b216ddab9a42b6c888606aca61fdcccccda3062f5b4f1f550722c5033c516e046816'
            '3a3de0ca03652cb203c5627f37f7cf1fa661f8f0adb542f2569feff61d39232f9eb92e4ba86bae044dfd10c915d65fac6bd9330c93893643633dce0ccc45321c')
b2sums=('08edfe19f5d0f16072762c3800e34ff37dcad478bde04356b0aae6a02ab99b115a5d1e8a33846f7f142228860bb5b19ded16b52dd464e4738462f6634b9fd6be'
        'ff844bb91a284883e54daf3912962cc256bbabc530c2dff9693081165ecf4e7b85f70fb6a49691b5dedb91347b038fc69ea3e78a81f51bd1869c502eaaf22848')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../add-missing-include.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='Release' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run build/${pkgname}-test
}

package() {
  depends+=('libFLAC.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'libchromaprint.so' 'libfftw3.so' 'libglib-2.0.so' 'libid3tag.so'
  'libgobject-2.0.so' 'liblilv-0.so' 'libogg.so' 'libportaudio.so'
  'libportmidi.so' 'libprotobuf-lite.so' 'librubberband.so' 'libsndfile.so'
  'libusb-1.0.so' 'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so')

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
