# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.5.2
pkgrel=1
pkgdesc="File identification library for Python"
arch=(any)
url="https://github.com/pre-commit/identify"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-ukkonen python-pytest)
optdepends=('python-editdistance-s: for returning license identifiers')
optdepends=('python-ukkonen: for returning license identifiers')
source=($_name-$pkgver.tar.gz::https://github.com/pre-commit/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('72aaf406e5aa7acc83189820c389e82e90cf775541013e4ad91e5d25abdecd8526d1c47d203baa8ab8e5e028836da755a7045e74fded7e8e3167397557d4b1f0')
b2sums=('f0ef048c6543b32016ad47a84e4b4b049a88424e496b159ce6ec1a1358172a87849e132b045a7345e818b02aee359e6d9458c458430a4d5d2b9f918cba685ed7')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
