# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.5.1
pkgrel=1
pkgdesc="File identification library for Python"
arch=(any)
url="https://github.com/pre-commit/identify"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-ukkonen python-pytest)
optdepends=('python-editdistance-s: for returning license identifiers')
optdepends=('python-ukkonen: for returning license identifiers')
source=($_name-$pkgver.tar.gz::https://github.com/pre-commit/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('8b61dd03bcdb8908a1a7f3b13d66121bb4f4bb3aab57672bff55a84232dcff310214ed0a90cfcf79846da9abc111bff08761ec9dd40c8b9b183bfbbf365efac4')
b2sums=('8586721838c427b00a7c70e842d2ab1a2f0437909d84d5cba6614aff3f8265f17b5137628bdd78e5f324fbf6a0fef8b5ca0246e6861af023c2d9a1a323cc43f6')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
