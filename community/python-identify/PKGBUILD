# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.5.5
pkgrel=1
pkgdesc="File identification library for Python"
arch=(any)
url="https://github.com/pre-commit/identify"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-ukkonen python-pytest)
optdepends=('python-editdistance-s: for returning license identifiers')
optdepends=('python-ukkonen: for returning license identifiers')
source=($_name-$pkgver.tar.gz::https://github.com/pre-commit/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('e02688805310a4f92d0435af562383a10297afa7fc468e44f73be40118c73e2ceac1b4ba7525c375fd0b671ea2700ba66307115c78558a370c525de8cba33c83')
b2sums=('b3a4dc2273435cf9244af64b08b9a9a922025f7d32f479fb2641fcdd9f8a05d2778db126104b1e3b765b82af7ecc1660163de1f71098900296135cd1ccd129c5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
