# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.4.0
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/pre-commit/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-ukkonen' 'python-pytest')
optdepends=('python-ukkonen: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/pre-commit/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('f0806d3785ef5db9cb1b5354c146fc0d33b4a4f04b3ff899945e9e93273a0bf8e4c8ff07ace22b5d71721c953148c007630dc400b82849321e8399ecdfffbc85')
b2sums=('1ba4fcf235518427fba8a59eb30ceb0c20ed23f5a3c4f56d6039ab530aaaba6b83ee395626a7d086f193f57f00a8d34a659883d897a21dc0fae5018cb0a4e87a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
