# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi
pkgver=4.54
pkgrel=2
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' perl-html-parser)
checkdepends=('perl-test-deep>=0.11' perl-test-nowarnings
              'perl-test-warn>=0.3')
url=https://metacpan.org/release/CGI
source=("https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
md5sums=(d12ddcd7d3db6410e9316ed9b9b5c80f)
sha512sums=(be7ecdd9eab81ad95d527aac2f10ef7a15322675fe002558c6ab4951f496a8964025b7d0426241fb3f61aba103964a40f99acc05a39c84a2434f70d90ac47be6)
_ddir="CGI-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
