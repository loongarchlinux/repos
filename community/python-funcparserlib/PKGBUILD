# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-funcparserlib
pkgver=1.0.0
pkgrel=1
pkgdesc="Recursive descent parsing library based on functional combinators"
url="https://funcparserlib.pirx.ru"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vlasovskikh/funcparserlib/archive/$pkgver.tar.gz")
sha512sums=('431d6b639c46dc84a0a12c8de976cb2c1369f0172a18d1eadbffba31aff12e9cfeded5ebec3ec63065384547000144d65d60003c55e2b23a1a40818d910aaef3')

build() {
  cd funcparserlib-$pkgver
  python -m build -nw
}

check() {
  cd funcparserlib-$pkgver
  python -m unittest discover
}

package() {
  cd funcparserlib-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
