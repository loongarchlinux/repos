# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-traitlets
pkgver=5.6.0
pkgrel=1
pkgdesc="A configuration system for Python applications"
arch=('any')
url="https://traitlets.readthedocs.io/en/stable/"
license=('BSD')
depends=('python-decorator')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest')
source=("https://pypi.python.org/packages/source/t/traitlets/traitlets-$pkgver.tar.gz")
sha256sums=('10b6ed1c9cedee83e795db70a8b9c2db157bb3778ec4587a349ecb7ef3b1033b')

build() {
  cd traitlets-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd traitlets-$pkgver
  pytest -v
}

package() {
  cd traitlets-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
