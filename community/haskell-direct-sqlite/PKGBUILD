# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=direct-sqlite
pkgname=haskell-direct-sqlite
pkgver=2.3.26
pkgrel=30
pkgdesc="Low-level binding to SQLite3.  Includes UTF8 and BLOB support."
url="https://github.com/IreneKnapp/direct-sqlite"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'sqlite' 'haskell-semigroups')
makedepends=('ghc' 'haskell-hunit' 'haskell-base16-bytestring' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('7e18237a0129b69b68db394bf1d912b664edf16ce335cbd65330d10eb1e2f9fc')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
      -fsystemlib

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/IreneKnapp/direct-sqlite/issues/85
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
