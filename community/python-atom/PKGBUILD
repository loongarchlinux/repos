# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=atom
pkgname=python-atom
pkgver=0.5.2
pkgrel=1
pkgdesc='Memory efficient Python objects'
arch=(x86_64)
url='https://github.com/nucleic/atom'
license=('BSD' 'custom')
depends=(python python-setuptools)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::https://github.com/nucleic/atom/archive/$pkgver.tar.gz)
sha512sums=('c8a7442ae65a68ebe0aa1905ddce6db63ff9b03011c73f71e37cfd5ab366e0142408e80cd9466dd42e9e27c24767ee3443c83ce2d6f6e99d083b15f6b89cd111')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  cd ${_name}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}" pytest
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
