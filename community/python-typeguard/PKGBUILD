# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.12.1
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('mypy' 'python-pytest-cov' 'python-typing_extensions')
source=("https://github.com/agronholm/typeguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ea91140ba0ebfe7cc83bf212ae20aca777279fc88ff806bf6a1f4208581bb726c6091d0777371d487e7422887814d81c84ebac9f06bfacc5a929f5a2657312a8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  # https://github.com/agronholm/typeguard/issues/176
  PYTHONPATH="$PWD/build/lib" MYPYPATH="$PWD/build/lib" pytest --deselect tests/mypy/test_type_annotations.py::test_positive
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
