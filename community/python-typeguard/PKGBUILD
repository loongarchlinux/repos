# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.13.2
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('mypy' 'python-pytest-cov' 'python-typing_extensions')
source=("https://github.com/agronholm/typeguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a11c1b9ee417a99a8bd09f4b5b05c7cdf319748b8221a49d1ff76854e2f432768224437f82a41c0166e5b148a550b58afbabfa1935cdc2a049cda3010edd052a')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  # https://github.com/agronholm/typeguard/issues/176
  PYTHONPATH="$PWD/build/lib" MYPYPATH="$PWD/build/lib" pytest --deselect tests/mypy/test_type_annotations.py::test_positive
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
