# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce
pkgver=1102
pkgrel=2
pkgdesc='NEC PC Engine/SuperGrafx/CD core'
arch=(loongarch64 x86_64)
url=https://github.com/libretro/beetle-pce-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
  zlib
)
makedepends=(git)
_commit=f2f09241d8c5a22dac22ed9ce20437c2c782bca8
source=(libretro-beetle-pce::git+https://github.com/libretro/beetle-pce-libretro.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-beetle-pce

  git rev-list --count HEAD
}

build() {
  make \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-pce
}

package() {
  install -Dm 644 libretro-beetle-pce/mednafen_pce_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
