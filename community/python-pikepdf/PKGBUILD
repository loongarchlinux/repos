# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=5.3.1
pkgrel=1
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(loong64 x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-build python-installer python-wheel python-toml python-setuptools-scm)
depends=(python-lxml python-pillow python-deprecation qpdf)
checkdepends=(python-pytest-xdist python-hypothesis)
source=(https://pypi.python.org/packages/source/p/pikepdf/pikepdf-$pkgver.tar.gz)
sha256sums=('b8c9e5cc1c73fa4d0dae9f4b184adbc72949973a011f2746c851affec3c0ede3')

build() {
  cd pikepdf-$pkgver
# Work around https://github.com/pikepdf/pikepdf/issues/367
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver \
  python -m build --wheel --no-isolation
}

check() {
  cd pikepdf-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd pikepdf-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
