# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=5.1.3
pkgrel=1
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(loongarch64 x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-build python-installer python-wheel python-toml python-setuptools-scm-git-archive)
depends=(python-lxml python-pillow qpdf)
checkdepends=(python-pytest-xdist python-hypothesis)
source=(https://pypi.python.org/packages/source/p/pikepdf/pikepdf-$pkgver.tar.gz)
sha256sums=('c34e4239661d2ddf23caa1c4256f636c866ce8069a5052a2bf8ee06e3cae22f3')

build() {
  cd pikepdf-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pikepdf-$pkgver
  PYTHONPATH="${PWD}/$(echo build/lib.*)" \
  pytest -v
}

package() {
  cd pikepdf-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
