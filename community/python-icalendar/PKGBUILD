# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgname=python-icalendar
pkgver=4.0.9
pkgrel=3
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('BSD')
depends=('python-dateutil' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('cc73fa9c848744843046228cb66ea86cd8c18d73a51b140f7c003f760b84a997')
b2sums=('c1ca7a7730404f7079e8fac8270f98a60ecd4e657f2b2b3d376d620f12c11a0db6dcd5713f7d2d4935c859d544497fa544b22ffb1a83be9c11ec2ea3923da93b')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH="${PWD}:${PYTHONPATH}"
  pytest -v "src/${_name}/tests"
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
