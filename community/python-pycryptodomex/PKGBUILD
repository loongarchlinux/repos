# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-pycryptodomex
pkgname=('python-pycryptodomex' 'python2-pycryptodomex')
pkgver=3.9.7
pkgrel=1

pkgdesc='A self-contained Python package of low-level cryptographic primitives'
url='https://www.pycryptodome.org/'
arch=('x86_64')
license=('BSD')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/p/pycryptodomex/pycryptodomex-$pkgver.tar.gz")
sha512sums=('50964f3fb1baa771d63eedf7f9b976670341eaeed3adf6935ab180aa5e866d88ba714314f570746cc1f62893a8c799f9126c09653591650137cf1f2d5d4e0a25')

prepare() {
	cp -a pycryptodomex-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/pycryptodomex-$pkgver
	python setup.py build

	cd "$srcdir"/pycryptodomex-$pkgver-python2
	python2 setup.py build
}

package_python-pycryptodomex() {
	depends=('python')

	cd pycryptodomex-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-pycryptodomex() {
	depends=('python2')

	cd pycryptodomex-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
