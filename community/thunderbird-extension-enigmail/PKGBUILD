# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Daniel Landau <daniel.landau@iki.fi>
# Contributor: Einhard Leichtfu√ü <alguien@respiranto.de>
# Contributor: Xyne
# Contributor: David Manouchehri <d@32t.ca>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Thomas Jost <schnouki schnouki net>
# Contributor: Hinrich Harms <arch hinrich de>

pkgname=thunderbird-extension-enigmail
pkgver=2.1.8
pkgrel=1
pkgdesc="OpenPGP message encryption and authentication for Thunderbird"
arch=('any')
url="https://www.enigmail.net/"
license=('MPL' 'GPL3')
makedepends=('zip' 'python' 'perl')
replaces=('thunderbird-enigmail')
source=("https://www.enigmail.net/download/source/enigmail-${pkgver}.tar.gz"{,.asc}
        "0001-preferences-disable-pEpAutoDownload-by-default.patch"
        "0001-Disable-Thunderbird-78-upgrade-warning-message.patch"
        "0001-genxpi-make-XPI-files-reproducible.patch")
sha512sums=('b21867c08b5e51af4e82a9ecc6e0a7fc056fb165e6cec8fc11d8ed54fd97b30ca5597b5a23243da0803d08aa04a4a3732d56eae9454db89ecd10fcd750b0b1fb'
            'SKIP'
            'baebd963400574db89be747a4419534f945bdc64136d4014656ff98a9615a23984bca724da3f3840670979aab08ce441eee067921e21d0cb216938a20ed785b2'
            '24a25881fe73ac15154df36203a1fb773f556ca58127a4e51681e8dcdf473c42f0abbb5a5750075d1a0dfb685d8e7e56af51598025f57a17e78ca1ca87f2337e'
            '3902e09d801f8a3fd493450a85c23d3cd95c68465df0025599e6c923b9708a6cb0cb09920170ec5055d55a56e287ae468460fca150f7be8af9d83cffa1a40427')
b2sums=('8908705a05277b018d40260ea095d2a2d9dee9300c7c4b06a708846e93a68242f60599fd92a2dc4c6d53026723433b7548b55e9fc97fdbe667af38a526f03f8a'
        'SKIP'
        'c593ed7b094d9feecb2f14624cf0628ab390c96f0fb0212ab0069333508b59057ef4b0518da1bf59eb8aaf0942303c4c45afab76d0b8e77a93763eab975cb4c0'
        'af4a02eba9b605998d6965b6d087cd31c7f24dbe355c46f77aecf060efb8707837060b47abd8648b74ebd59251b245b5c7c1159a7c10371bbfe9b3b7f83c1915'
        '55709a3fd099fab4b11289518a44f2b53e81031606529cec5b4786e796de438faefa52f2a7ab3d29d6b3aca120e279f30d6d7ba3c3e3d02ca2abcb85f1652661')
validpgpkeys=('4F9F89F5505AC1D1A260631CDB1187B9DD5F693B') # Patrick Brunschwig <patrick@enigmail.net>

prepare() {
    cd "${srcdir}"/enigmail

    # Using vendor settings via /usr/lib/thunderbird/defaults/preferences/enigmail.js
    # does not seem to work.
    patch -p1 -i ../0001-preferences-disable-pEpAutoDownload-by-default.patch

    # Disable warning message in favour of a versioned dependency to discourage partial upgrades
    patch -p1 -i ../0001-Disable-Thunderbird-78-upgrade-warning-message.patch

    # Make timestamps in the generated XPI file respect SOURCE_DATE_EPOCH
    # (https://gitlab.com/enigmail/enigmail/-/merge_requests/45 and
    #  https://gitlab.com/enigmail/enigmail/-/merge_requests/46)
    patch -p1 -i ../0001-genxpi-make-XPI-files-reproducible.patch
}

build() {
    cd "${srcdir}"/enigmail

    ./configure
    make -j1 # fails with -j greater than 1
}

package() {
    depends=('thunderbird>=68' 'thunderbird<78' 'gnupg')
    cd "${srcdir}"/enigmail

    if ! _extension_id="$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' build-tb/dist/install.rdf 2>/dev/null)" ||
            [[ -z $_extension_id ]]; then
        _extension_id="$(sed -n 's/.*"id": "\(.*\)".*/\1/p' build-tb/dist/manifest.json)"
    fi
    _extension_dest="${pkgdir}/usr/lib/thunderbird/extensions/${_extension_id}"
    # Should this extension be unpacked or not?
    if grep -q '<em:unpack>true</em:unpack>' build-tb/dist/install.rdf 2>/dev/null; then
        install -dm755 "${_extension_dest}"
        cp -R build-tb/dist/* "${_extension_dest}"
        chmod -R ugo+rX "${_extension_dest}"
    else
        install -Dm644 build-tb/enigmail-${pkgver}.xpi "${_extension_dest}.xpi"
    fi
}
