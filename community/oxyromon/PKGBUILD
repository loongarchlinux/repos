# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=oxyromon
pkgver=0.9.0
pkgrel=1
pkgdesc='Rusty ROM OrgaNizer'
arch=(loongarch64 x86_64)
url=https://github.com/alucryd/oxyromon
license=(GPL3)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  mame-tools
  maxcso
  p7zip
  rust
  yarn
)
optdepends=(
  'mame-tools: CHD support'
  'maxcso: CSO support'
  'p7zip: 7Z and ZIP support'
)
_tag=e9750d16a075834ff612bef338d5f83d1e7da692
source=(git+https://github.com/alucryd/oxyromon.git?signed#tag=${_tag})
validpgpkeys=(9437DD3815A7A9169E3D3946AFF5D95098BC6FF5) # Maxime Gauduin <alucryd@archlinux.org>
b2sums=(SKIP)

prepare() {
  cd oxyromon
  yarn install \
    --frozen-lockfile
  cargo fetch \
    --locked
}

pkgver() {
  cd oxyromon

  git describe --tags
}

build() {
  cd oxyromon
  yarn run build
  cargo build \
    --all-features \
    --release \
    --frozen
}

check() {
  cargo test \
    --all-features \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

package() {
  cargo install \
    --all-features \
    --frozen \
    --offline \
    --no-track \
    --path oxyromon \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
