# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=FanFicFare
pkgname=fanficfare
pkgver=3.22.1
pkgrel=1
pkgdesc="A tool for downloading fanfiction to eBook formats"
arch=('any')
url="https://github.com/JimmXinu/${_pkgname}"
license=('Apache')
changelog=changelog.txt
depends=('python' 'python-beautifulsoup4' 'python-chardet' 'python-html5lib'
         'python-html2text')
optdepends=('python-pillow: support for converting/resizing story images and covers')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('38405aaba622c5f12bf6d342e09c312d9e966de0baf98c18b41770941e33fddd')
b2sums=('66c8d53cad52e115e008c70c1c39c285344c39dffc075ae1f4bf40c5acd2a75ae6f843d8e96363682346c0e69716fcceab2fccb063a9f3aac9b2929d7b0f9dfd')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
