# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cmd2
pkgver=2.4.0
pkgrel=1
pkgdesc="A tool for building interactive command line apps"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
depends=('python-attrs' 'python-colorama' 'python-pyperclip' 'python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-pytest-mock' 'python-pytest-runner' 'python-pytest-xdist'
              'vi')
source=("https://pypi.io/packages/source/c/cmd2/cmd2-$pkgver.tar.gz")
sha512sums=('4ecd4c71294bee86f02b39a588c42a7232309db54459c950f77734926dedc3ec273237b9615bab6ef8a499c65442e3d6f7d2e9f159ccca021db0b450435ffbc0')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cmd2-$pkgver
  python setup.py build
}

check() {
  cd cmd2-$pkgver
  python setup.py pytest
}

package() {
  cd cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
