# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: opqdonut <joel.kaasinen@gmail.com>

pkgname=emacs-lua-mode
pkgver=20151025
pkgrel=3
pkgdesc="Emacs lua-mode"
arch=('any')
url="http://lua-mode.luaforge.net"
license=('GPL')
depends=()
makedepends=('emacs')
install=$pkgname.install
source=(lua-mode-$pkgver.zip::https://github.com/immerrr/lua-mode/zipball/v$pkgver)
sha512sums=('7fc7709405c2e5a9555447095d469947e4ad8a749db4ae2dcae332de713f6d9ec6ab4d38584238c288cdebdb49fe0bccf8e5fa46c44d2d060c0c757dfa640491')

build() {
  cd "$srcdir"/immerrr-lua-mode-*
  make compile EMACS_BATCH="emacs --batch -Q"
}

package() {
  cd "$srcdir"/immerrr-lua-mode-*
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp
  cp *.el{c,} "$pkgdir"/usr/share/emacs/site-lisp/
}
