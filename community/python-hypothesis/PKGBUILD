# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis
pkgver=6.17.1
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
depends=('python-attrs' 'python-sortedcontainers')
optdepends=('python-pytz: for datetime and django module'
            'python-faker: for fakefactory and django module'
            'python-django: for django module'
            'python-numpy: for numpy module'
            'python-pytest: for pytest module'
            'python-lark-parser: for lark module'
            'python-libcst: for codemods module')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect' 'python-coverage'
              'python-typing_extensions' 'python-black' 'python-fakeredis' 'python-libcst')
source=("$pkgname-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('6418f850b6642337d11bee7d78f82d061af1dd3a2e59dcda1e87ef88f99bda23ccbd6e6cb6cce00275380ddf125c77102bf88ae6a3884d754ef9f7cf9b68e4f5')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver
}

build() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py build
}

check() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  python examples/example_hypothesis_entrypoint/setup.py install --root="$PWD/tmp_install" --optimize=1

  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PWD/examples/example_hypothesis_entrypoint" \
  PATH="$PWD/tmp_install/usr/bin:$PATH" \
    pytest --ignore tmp_install || echo "Tests failed"
}

package() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1

  # It's only present when tests are enabled, so adding -f
  rm -rf "$pkgdir"/usr/lib/python3.10/site-packages/tests
}
