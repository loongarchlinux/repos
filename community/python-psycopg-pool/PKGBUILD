# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg-pool
# source is shared with python-psycopg package, hence the weird naming
_pkgname=python-psycopg
pkgver=3.1.3
pkgrel=1
pkgdesc='Connection pool for psycopg'
arch=('any')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL3')
depends=('python' 'python-psycopg')
makedepends=('git' 'python-setuptools')
_commit='080d0a75c0cd40c3f2a8abb4bea537fb1b2413de'
source=("$_pkgname::git+https://github.com/psycopg/psycopg.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  git describe --tags | sed 's/^pool-//'
}

build(){
  cd "$_pkgname/psycopg_pool"

  python setup.py build
}

package(){
  cd "$_pkgname/psycopg_pool"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
