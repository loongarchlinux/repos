# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.4.1
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('2ee1c79894a052ef5ffa306b673f1119e826790e870d74f214bfbcddb4e88f105bba4ea57df220f7a4a71ccc7e41d7e07c90434dab7c0015a135b2462a86652f')

build() {
    cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -ldflags "-linkmode=external -extldflags=$LDFLAGS -X main.version=${pkgver}"

    scdoc < "${pkgname}.1.scd" | gzip > "${pkgname}.1.gz"
}

check() {
    cd "${srcdir}/sh-${pkgver}/"
    go test ./...
}

package() {
    cd "${srcdir}/sh-${pkgver}/"
    install -Dm755 -t "${pkgdir}/usr/bin/" "cmd/${pkgname}/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/man/man1/" "cmd/${pkgname}/${pkgname}.1.gz"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
