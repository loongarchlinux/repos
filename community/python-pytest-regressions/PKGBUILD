# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pytest-regressions
pkgname=python-$_pkgname
pkgver=2.3.1
pkgrel=1
pkgdesc='Pytest plugin for regression testing'
arch=('any')
url='https://github.com/ESSS/pytest-regressions'
license=('MIT')
depends=('python-pytest' 'python-pytest-datadir' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('a10e1b678b817d273a63fc7bfadf373e0673940c4c57021eff58a75c82769f348bc3ab97182f5851ed262b178ac18af8814899454338f123264eaae0739a3525')

build() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
