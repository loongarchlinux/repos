# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-waitress
pkgver=2.1.1
pkgrel=1
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-runner')
source=("https://github.com/Pylons/waitress/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9a65d61a71a587ccea5f3d5feb4b9e810bb1e9d0f0374d5bee582b6e0ca0899f9e4d7ebab1365d1ca11e3ab313df30c35ec86ffbd7e1187adada0a549954c969')

build() {
  cd waitress-$pkgver
  python setup.py build
}

check() {
  cd waitress-$pkgver
  python setup.py pytest
}

package_python-waitress() {
  cd waitress-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
