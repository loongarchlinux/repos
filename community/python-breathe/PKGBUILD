# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python-breathe
pkgver=4.31.0
pkgrel=3
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output"
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=('python' 'python-sphinx' 'python-docutils' 'python-six')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/michaeljones/breathe/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig"::"https://github.com/michaeljones/breathe/releases/download/v${pkgver}/breathe-${pkgver}.tar.gz.sig")
sha512sums=('f6fd7db50ca68cad64d150fcb76227deb282c08109c38b343749f8ce9ee607ccddf767e3020328a61a7570e7a0c1159607148b773bc0b695e87d6b17a6068eac'
            'SKIP')
validpgpkeys=('8AED58021FEACDD5F27BA0E6A72F627716EA9D96') # Melvin Vermeeren <vermeeren@vermwa.re>

build() {
  cd "breathe-${pkgver}"
  python setup.py build
}

check() {
  cd "breathe-${pkgver}"
  PYTHONPATH="$PWD" make test
}

package_python-breathe() {
  cd "breathe-${pkgver}"
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
