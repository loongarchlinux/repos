# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

_name=vobject
pkgname=python-vobject
pkgver=0.9.6.1
pkgrel=9
pkgdesc='Module for parsing and generating vCard and vCalendar files'
url="https://eventable.github.io/vobject/"
license=('Apache')
arch=('any')
depends=('python-dateutil')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('96512aec74b90abb71f6b53898dd7fe47300cc940104c4f79148f0671f790101')
b2sums=('74f9a7dc8058bb792492b1ac44a0310f58c603e677aedda35be0d8e94743e6e1e001a4ae888625f314f706891b7a31db77efa4c268482bbaf44ca37fff236cd6')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python tests.py
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {ACKNOWLEDGEMENTS.txt,README.md} \
    -t "${pkgdir}/usr/share/${pkgname}"
}
