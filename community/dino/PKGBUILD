# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=dino
pkgver=0.1.0
pkgrel=4
pkgdesc="Modern XMPP (Jabber) chat client written in Vala"
arch=(x86_64)
url="https://github.com/dino/dino"
license=(GPL3)
depends=(glib2 glib-networking gtk3 gpgme libgee libgcrypt libsignal-protocol-c
         libsoup sqlite qrencode)
makedepends=(git cmake vala ninja)
source=(${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
	signal-2.3.3.patch)
sha256sums=('100542fcf05781f37f5a871489affe0a82f205cd15e3d0d3cdfd717fa1149835'
            'SKIP'
            '0f46b133b0e11a71d167b66f94e8dfd6f5677344c83f8fda903dd382e83bc673')
validpgpkeys=(A32134DD19952FB12EC43E4501FABCC83FEA225E)

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../signal-2.3.3.patch
}

build() {
    cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
}
