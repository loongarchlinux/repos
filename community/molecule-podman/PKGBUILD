# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-podman
pkgver=2.0.0
pkgrel=1
pkgdesc="Molecule Podman Driver allows use of podman as backend for testing"
arch=(any)
url="https://github.com/ansible-community/molecule-podman"
license=(MIT)
depends=(molecule python-ansible-compat python-selinux)
makedepends=(python-build python-installer python-pip python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible-core podman python-pytest python-pytest-helpers-namespace)
optdepends=('podman: for using a local podman setup')
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('b1e7a458eab5c7d6d5055353fe14e1d9c4c56f7a3579ebf29e3225427d1f4a238a70fcbf71fda1e4de36aa531221c3915314baec1e2bb85782dcf21e35f7d827')
b2sums=('e9ee4cb982afcb8289390a34ac839929cf5bd9640e7e72ae49a1eaf61f536c14f6e1c6d7938f36c48b46b0182ef31403dd636e54bd730fd11dbb131bff0e0b1a')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  pytest -vv -c /dev/null --ignore src/molecule_podman/test/test_func.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
