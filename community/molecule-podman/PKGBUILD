# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-podman
pkgver=2.1.0
pkgrel=1
_commit=1759ab011f6dd40ce24876736ed232a7854cb748  # refs/tags/v2.1.0
pkgdesc="Molecule Podman Driver allows use of podman as backend for testing"
arch=(any)
url="https://github.com/ansible-community/molecule-podman"
license=(MIT)
depends=(molecule python-ansible-compat python-selinux)
makedepends=(git python-build python-installer python-setuptools-scm python-wheel)
checkdepends=(ansible-core podman python-pytest python-pytest-helpers-namespace)
optdepends=(
  'ansible-core: for Dockerfile validation'
  'podman: for using a local podman setup'
)
# switch to git sources as upstream hasn't released on pypi: https://github.com/ansible-community/molecule-plugins/issues/87
source=("git+$url#tag=$_commit")
sha512sums=('SKIP')
b2sums=('SKIP')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  pytest -vv -c /dev/null --ignore src/molecule_podman/test/test_func.py
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"

  # remove tests: https://github.com/ansible-community/molecule-podman/issues/147
  rm -rv "$pkgdir/$site_packages/molecule_podman/test/"
}
