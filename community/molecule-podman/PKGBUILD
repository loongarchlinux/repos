# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-podman
pkgver=2.0.1
pkgrel=1
pkgdesc="Molecule Podman Driver allows use of podman as backend for testing"
arch=(any)
url="https://github.com/ansible-community/molecule-podman"
license=(MIT)
depends=(molecule python-ansible-compat python-selinux)
makedepends=(python-build python-installer python-pip python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible-core podman python-pytest python-pytest-helpers-namespace)
optdepends=('podman: for using a local podman setup')
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('87d4b139a9e4ffe90a51ac27a07a841360ec9e4df9aced8e4316540bf1e31887a53c4924c381839351fc8892c958d91ef8c17e4596858b652a8c9680dcd8cf23')
b2sums=('c58938a14709be83a505ef1ffa12fbc3bff350b14084790909203c23cf88116e197cde66c130fc3090ffabbf68775e68750466c19c7803b10001d079dfd26d27')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  pytest -vv -c /dev/null --ignore src/molecule_podman/test/test_func.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
