# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-podman
pkgver=2.0.3
pkgrel=1
pkgdesc="Molecule Podman Driver allows use of podman as backend for testing"
arch=(any)
url="https://github.com/ansible-community/molecule-podman"
license=(MIT)
depends=(molecule python-ansible-compat python-selinux)
makedepends=(python-build python-installer python-pip python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible-core podman python-pytest python-pytest-helpers-namespace)
optdepends=(
  'ansible-core: for Dockerfile validation'
  'podman: for using a local podman setup'
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('206c8e2b1e97dae0072bc5e42cc95428991269234dc0ff6b1ec30d39d56a9bc180278355a99a47fe0e32ddc80d7ef74f45e8a87cbe2746cbfeb44d126cf2fb4c')
b2sums=('da634e983d22386971218f814b90f3a48bff769b67572dd13e2ff54a4e5ae37a10df1e794ce75bc0ffd9f95b256106e26a858d3adadd41d145244ff39d5be4f2')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  pytest -vv -c /dev/null --ignore src/molecule_podman/test/test_func.py
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"

  rm -rv "$pkgdir/$site_packages/molecule_podman/test/"
}
