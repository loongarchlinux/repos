# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-filelock
pkgver=1.1.1
pkgrel=1
pkgdesc='Heavily tested yet simple filelocking solution using flock'
arch=(any)
url='https://github.com/sheerun/filelock'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-rspec)
options=(!emptydirs)
source=(https://github.com/sheerun/filelock/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('19c7f3b2bdadd962bffc273a1c21f31c1dbb8b4f03374a9dc7cbe81bb02493c6')

prepare() {
  cd filelock-$pkgver
  sed -i 's|git ls-files|find . -type f|' filelock.gemspec
  sed -i '1i require "pathname"' spec/filelock_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd filelock-$pkgver
  gem build filelock.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    filelock-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd filelock-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd filelock-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
