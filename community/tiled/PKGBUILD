# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.7.2
pkgrel=2
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('loongarch64' 'x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt5-declarative' 'qt5-quickcontrols2' 'qt5-svg' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt5-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('6d766adbfcb642279eb0ba27410858c54a29ed8e697445b6fb96ce210d0b1433b1ed4715e1d78af3aea8ec71a87b24eea52216bf6236159e0c8fdf87a2339a59')

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true projects.Tiled.enableZstd:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
