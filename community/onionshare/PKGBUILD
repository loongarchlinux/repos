# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Kevin Azzam <aur at kevin.azz.am>
# Contributor: Ye Shu <shuye02 at outlook.com>

pkgname=onionshare
pkgver=2.4
pkgrel=3
pkgdesc='Share a file over Tor Hidden Services anonymously and securely'
url='https://github.com/onionshare/onionshare'
arch=('any')
license=('GPL3')
depends=('python' 'hicolor-icon-theme' 'tor' 'stem' 'python-requests' 'python-pysocks' 'python-flask' 'python-flask-httpauth' 'python-flask-socketio' 'python-psutil' 'python-pycryptodome' 'python-pyqt5' 'python-qrcode' 'python-unidecode' 'python-colorama' 'pyside2')
makedepends=('python-setuptools')
optdepends=(
  'python-nautilus: enable Nautilus right-click extension'
)
checkdepends=('python-pytest')
source=(https://github.com/onionshare/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/onionshare/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc
)
sha512sums=('ce73d5d9a7a1b1c2e70ad302e251199308cb7c9af194254e28e2a5698bd782f16dab227979efb08c2931c5280270f657d64e5ab8a9f93341649047163d5d55fa'
            'SKIP')
b2sums=('aa0aee4b25a421830ee2202c0b73a6a3b358b534048ae24a72e55970d0dc2e9e5254b688d94deed9736e1a43ec6e07b239f2359b496d82dc1f59edbf37e392cc'
        'SKIP')

validpgpkeys=('927F419D7EC82C2F149C1BD1403C2657CD994F73')

build() {
  cd ${pkgname}/cli
  python setup.py build
  cd ../desktop/src
  python setup.py build
}

check() {
  cd ${pkgname}
  pushd cli
  PYTHONPATH=".:${PYTHONPATH}" pytest tests/
  popd
  pushd desktop
  #PYTHONPATH="src:../cli:${PYTHONPATH}" pytest tests/
}

package() {
  cd ${pkgname}/cli
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  cd ../desktop/src
  python setup.py install --skip-build -O1 --root="${pkgdir}"

  install -Dm 644 org.onionshare.OnionShare.desktop -t "${pkgdir}/usr/share/applications/"
  install -Dm 644 org.onionshare.OnionShare.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/"
  install -Dm 644 org.onionshare.OnionShare.appdata.xml -t "${pkgdir}/usr/share/metainfo/"
}

# vim: ts=2 sw=2 et:
