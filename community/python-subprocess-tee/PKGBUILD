# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.3.2
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools_scm' 'python-toml')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-xdist' 'python-enrich')
optdepends=('python-enrich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e681abc27623a3e05031bc05dc15e34f82c9cb544cd16742855411a9edb11fe8e38f9d11f8e651681d04081f121a47c38ebd172d70d282c8110a62699ef687d6')
b2sums=('3198832f4c8bbf90e83038e39017cff1efc61ede84aded8220214e388a088777bdb861c044cddbf4b07c7b3172f8164e8bbf82b33de68d1fefea0f81bbacdfc9')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
