# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.4.0
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=(any)
url="https://github.com/pycontribs/subprocess-tee"
license=(MIT)
depends=(python)
makedepends=(git python-build python-installer python-setuptools python-setuptools-scm python-wheel)
checkdepends=(ansible-core molecule python-enrich python-pytest python-pytest-xdist)
optdepends=('python-enrich: for rich text rendering')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('944f978c87d612a7846709106b7857761288ef9e6923bf0b69e20cc493b7dbcf51c16867ed7b3038a1d2f180108972c35e8d9fe380542b56c7c68320caf9321b')
b2sums=('0a536daca56dd441848eaaeb3d53384349debdfe7b83458a89ff9e9d44cf63d4cae9eb6436705494cbb15282594a3ad0ce1c8dd51bfb6f79128c6855ab3b2c82')

prepare() {
  cd $_name-$pkgver
  sed -e '/pip/d;/setuptools_scm_git_archive/d' -i pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  # disable broken test: https://github.com/pycontribs/subprocess-tee/issues/58
  pytest -vv -c /dev/null -k "not test_rich_console_ex"
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  # remove tests: https://github.com/pycontribs/subprocess-tee/issues/61
  rm -frv "$pkgdir/$_site_packages/subprocess_tee/test/"
}
