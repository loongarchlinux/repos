# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.4.1
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests' 'python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('cfd7c0cb53a7e2815252e2f5dfb68fbbbe858d185894dd2c7844360be681bb1dd220c2679e03df5a4ab9f952d1e409819528df41bb3bc17286830d1bc4744796')

build() {
  cd python-sdk-$pkgver
  python setup.py build
}

check() {
  # Extracted from .travis.yml
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd python-sdk-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
