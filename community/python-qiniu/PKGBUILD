# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.9.0
pkgrel=2
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('ddf0e2506c66b86822e05566a985961f5f22a7c0d4d8faeedbff2b1c96430c74fad3329a6dfb614cfcf68bd6c9ca308382d6a10b1cc9e0b82d277b1a89501d08')

build() {
  cd python-sdk-$pkgver
  python setup.py build
}

check() {
  cd python-sdk-$pkgver
  pytest || echo "Tests failed"
}

package() {
  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
