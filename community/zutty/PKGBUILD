# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=zutty
pkgver=0.9
pkgrel=1
pkgdesc='Terminal emulator'
arch=(loongarch64 x86_64)
url='https://tomscii.sig7.se/zutty'
license=(GPL3)
depends=(freetype2 libglvnd libxmu)
makedepends=(git python)
source=('git+https://github.com/tomszilagyi/zutty#commit=9f30b58cff891475c3bcb56cb8c91db24b71a617') # tag: 0.9
sha256sums=('SKIP')

prepare() {
  # FS#70372
  sed -i 's,-march=native,,g;s,-mtune=native,,g' $pkgname/wscript
}

build() {
  cd $pkgname
  python ./waf configure --prefix=/usr
  python ./waf -v build
}

package() {
  cd $pkgname
  python ./waf --destdir="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -v doc/* "$pkgdir/usr/share/doc/$pkgname"
}
