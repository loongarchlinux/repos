# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=3.3.0
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-filelock')
makedepends=('python-setuptools-scm' 'python-pip')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses'
              'python-isort')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('530e8592c2813c297355cdea591a772fad3e4cdcfccf72844c49ad68ab1acd57e6050828f78fe3bbfb9d54d62f33c63fec70dfb81d9955abf971703f53daa326')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  # tldextract/_version.py is generated by setuptools-scm, and will fail pylint here.
  python -m pytest --ignore build --ignore tldextract/_version.py
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set sw=2 et:
