# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=zict
pkgname=python-${_pkg}
pkgver=2.1.0
pkgrel=1
pkgdesc="Useful Mutable Mappings"
arch=(any)
url="https://github.com/dask/zict/"
license=(BSD)
makedepends=(python-setuptools)
depends=(python python-heapdict)
checkdepends=(python-pytest python-lmdb python-psutil)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
#source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('15b2cc15f95a476fbe0623fd8f771e1e771310bf7a01f95412a0b605b6e47510')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -vv --color=yes zict
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
