# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=2.0.0
pkgrel=2
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('loongarch64' 'x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi')
makedepends=('rust' 'cmake' 'llvm11')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/${pkgver}.tar.gz")
sha512sums=('c02b97075212a496a4c30a9cfcd6b8ef89cb08bf3e5f9f20bf5f83c63bedb5744e4976ee3641310f6a44d911d670285c8b67c3cb9725acdd632ef5f8880ba4b9')
options=('staticlibs')

prepare() {
  cd "$pkgname-$pkgver"

  export WASMER_INSTALL_PREFIX=/usr
  export ENABLE_LLVM=1
}

build() {
  cd "$pkgname-$pkgver"
  make all
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/usr make install
}

# vim:set ts=2 sw=2 et:
