# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Birks <david@birks.dev>
# Contributor: Mike Williamson <mike at korora dot ca>

pkgname=eksctl
pkgver=0.76.0
pkgrel=1
pkgdesc='Command line tool for creating clusters on Amazon EKS'
url='https://github.com/weaveworks/eksctl'
arch=('loongarch64' 'x86_64')
license=('Apache')
depends=('kubectl' 'glibc')
makedepends=('go')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('08cd84d766ab9aa9b97127a193d6d8994df0f446b6fc8f5204d78f9c76cd51a4')
b2sums=('13cb243c0aebc836d33aefeeff2a4e4a0ecfd186c9b83835554a0120189d323696bfc75ec1c5dbf9c31e04b6eaa97146ca4eb1d42f3aff561ba6f1f9e2ce9b48')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/weaveworks/eksctl/pkg/version.gitTag=${pkgver}" \
    ./cmd/eksctl
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "$pkgdir/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  # completions
  "${pkgdir}/usr/bin/${pkgname}" completion bash | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/fish/completions/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
