# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-treq
pkgver=21.5.0
pkgrel=1
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
         'python-idna' 'python-attrs' 'python-twisted' 'python-hyperlink')
makedepends=('python-setuptools' 'httpbin')
checkdepends=('python-mock')
source=("https://github.com/twisted/treq/archive/release-$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a1bd7b1eac38b5c713406d6731d0873abd400c1e3dcb637f05a669a9909576df48f1e0d5410af3efde591b7047f3e901297dd592561c6d08870debe263822413')

build() {
  cd treq-release-$pkgver
  python setup.py build
}

check() {
  cd treq-release-$pkgver
  PYTHONPATH="$PWD/build/lib" trial treq
}

package() {
  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
