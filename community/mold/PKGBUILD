# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.4.1
pkgrel=1
pkgdesc='A Modern Linker'
arch=('loong64' 'x86_64')
url='https://github.com/rui314/mold'
license=('AGPL3')
# xxhash is bundled
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb')
makedepends=('git' 'python')
checkdepends=('clang' 'libdwarf')
options=('debug')
_commit='19d9255d72520678fd5eccca1ee9933b0f2c7e1b'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use /usr/lib instead of /usr/libexec
  sed -i "s/libexec/lib/" Makefile
}

build() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1
}

check() {
  cd "$pkgname"

  make \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    check
}

package() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    DESTDIR="$pkgdir" \
    install
}
