# Contributor: Raymond Li <arch@raymond.li>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: ibrokemypie <ibrokemypie@bastardi.net>
pkgname=i3lock-color
pkgver=2.12.c.5
pkgrel=1
pkgdesc="Improved i3lock with color customization"
arch=('x86_64')
url=https://github.com/Raymo111/i3lock-color/
license=('MIT')
depends=('xcb-util-image' 'pam' 'libev' 'cairo' 'libxkbcommon-x11'
         'libjpeg-turbo' 'xcb-util-xrm')
provides=('i3lock')
conflicts=('i3lock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Raymo111/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('c59e6561a1d1ffd0abd5ff56c295d71b6cc8439c7d5697cfed4d2f06a87aa0aa102cee5d55363de1d91c501eee102547d0ebef4bb4bd316ef31370eec062ddbb')

prepare() {
    cd "$pkgname-$pkgver"
    autoreconf -fiv
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
