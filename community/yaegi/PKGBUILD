# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.14.2
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('loong64' 'x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('32971a6f10947dce1c8fd6139390694ab0c36333e0a74334c072c6cece50e8385b3f502d8b59b597bcc289584fc7ccf3e4bc80c4808a0eb0ddbf7fafd171abd4')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v ./cmd/yaegi
}

# disabling tests...
#check() {
#  cd "${pkgname}-${pkgver}"
#  sed -i 's|"-race", ||' ./cmd/yaegi/yaegi_test.go
#  GOROOT="/usr/lib/go" go test -v ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
