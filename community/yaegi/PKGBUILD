# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.8.10
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go-pie')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('f80354b377e934582e5300c6fe97cd5c7ae285c57d47e34084948004b7c16d92fc9a600754e31ff53eb617b09d0953a84b0fcd33ea6692640780e52d02a0ea5a')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build ./cmd/yaegi
}

# These checks fail right now :(
#check() {
#  cd "${pkgname}-${pkgver}"
#  go test ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
