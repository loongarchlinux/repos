# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=solidity
pkgver=0.8.18
pkgrel=1
pkgdesc='Contract-Oriented Programming Language'
arch=('loong64' 'x86_64')
url='https://github.com/ethereum/solidity'
license=('GPL3')
depends=('cvc4' 'z3')
makedepends=('boost' 'cmake')
source=("https://github.com/ethereum/solidity/releases/download/v$pkgver/solidity_$pkgver.tar.gz")
sha512sums=('b91980fa6ed436117ef5df8bd8a8b942971d7d2fd6a3473f6ddc49fa9fa903a9d97dcf724c7bf91a8407ffd3a995c982a13e51ead8875d2f8691e2b49deae2d4')

build() {
    cmake -B build -S ${pkgname}_$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTESTS=OFF \
        -DSTRICT_Z3_VERSION=OFF
    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
}
