# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.9.9
pkgrel=1
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(loongarch64 x86_64)
url='https://igraph.org/c/'
license=(GPL2)
depends=(glpk arpack libxml2 suitesparse plfit)
makedepends=(cmake)
source=(https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ee358d9bd13e3b491533e3eda5cda934bc5238aa756a68c2defaab4e33ee57ab')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
