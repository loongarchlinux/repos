# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsboat
pkgver=2.21
pkgrel=1
pkgdesc="An RSS/Atom feed reader for text terminals"
arch=('x86_64')
url="https://newsboat.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext' 'asciidoc' 'rust' 'asciidoctor')
replaces=('newsbeuter')
changelog=$pkgname.changelog
options=('!emptydirs' '!makeflags')
optdepends=('perl: pinboard.pl script')
source=(https://newsboat.org/releases/$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('B8B1756A0DDBF0760CE67CCF4ED6CD61932B9EBE') #Newsboat project <newsboat@googlegroups.com>
sha256sums=('0c46b3dd46bb578dd6dd4915db4cfdffb4352ab258f251080ad14655c75a9c31'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr
  make doc
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
