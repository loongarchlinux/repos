# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

_pyname=pcapy
pkgbase=python-pcapy
pkgname=('python-pcapy' 'python2-pcapy')
pkgver=0.11.5
pkgrel=2
pkgdesc='Extension module that interfaces with the libpcap packet capture library'
url='https://github.com/CoreSecurity/pcapy'
arch=('x86_64')
license=('Apache')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools' 'libpcap')
source=(https://github.com/CoreSecurity/pcapy/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('2ff96eb65caee8a45a3ae3edb78d5b4d8debcace9dc2921aefa3be83bb0af9c6')
sha512sums=('b7b5e81ed94e828501de01758292e18888497e77acc9864421b555b59de26f233c4365093e48213b36e27a51ad887e7f4d5ce2e4fac3f2f1a3006b5b75297977')

prepare() {
  sed '/self.assertEqual(refNone, sys.getrefcount(None))/d' -i ${_pyname}-${pkgver}/tests/pcapytests.py
  cp -ra ${_pyname}-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  msg2 'Building python2...'
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 'Checking python...'
  (cd ${_pyname}-${pkgver}/tests
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python pcapytests.py
  )
  msg2 'Checking python2...'
  (cd ${_pyname}-${pkgver}-py2/tests
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 pcapytests.py
  )
}

package_python-pcapy() {
  depends=('python' 'libpcap')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/pcapy"
}

package_python2-pcapy() {
  depends=('python2' 'libpcap')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/pcapy"
}

# vim: ts=2 sw=2 et:
