# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Konrad <konrad AT knauber DOT name>

pkgname='pinta'
pkgver=1.7.1
pkgrel=1
pkgdesc="Drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="https://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'mono-addins')
makedepends=('intltool')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PintaProject/Pinta/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/PintaProject/Pinta/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('133a02ad2df11eb0922a039e6ce81ee15308deee2d5925a0598c0bfef77ac984353ffd75759166ea467d197cdf5418f66126092c681a97e4389bfdcc623d1d7a'
            'SKIP')
validpgpkeys=('5BDA30D0C2E985E598089E444CF80E80E77C5C35') # Cameron White

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" license-*.txt readme.md
}
