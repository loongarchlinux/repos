# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=matterbridge
pkgver=1.22.3
pkgrel=1
pkgdesc="Multi-protocols (IRC/XMPP/Mattermost/Slack/Matrix/etc) bridge"
arch=(loongarch64 x86_64)
url="https://github.com/42wim/matterbridge"
license=(Apache)
depends=(glibc)
makedepends=(go git)
backup=(etc/matterbridge.toml)
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz.asc::"${url}/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc"
        matterbridge.service)
sha512sums=('d924f788d0cae500e67c87f4071da57f556c5eb3ecfd8a6b9d284e2e836dc88bf6f9efc25dfb72891736a91ed012737291f8374ddfaa841ea796ff1fd5378ca5'
            'SKIP'
            '82db20928e6ff3d259725371caf9af67550c8a7b8f5b597103cdd308772eabcfe6738442a3da711a7e54003668c4b518b246af60dacd0dcf5294790aa3e226cf')
validpgpkeys=(CC7D978417C1AEA1E4CDD7240E41AB4BF4C610B4) # wim <wim@42.be>

prepare() {
  cd ${pkgname}-${pkgver}
  go mod vendor
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v -o ${pkgname} .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 matterbridge.toml.sample "${pkgdir}"/etc/matterbridge.toml
  install -Dm644 ../${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
