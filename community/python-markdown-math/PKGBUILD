# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Marcus Scheunemann

pkgname=python-markdown-math
pkgver=0.7
pkgrel=1
pkgdesc="Math extension for Python-Markdown"
arch=('any')
url="https://github.com/mitya57/python-markdown-math"
license=('BSD')
depends=('python-markdown')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgname:0:1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('17a12175e8b2052a1c3402fca410841c551c678046293b1f7c280ccfe7b302a0'
            'SKIP')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>


build() {
    cd "python-markdown-math-${pkgver}"

    python setup.py build
}

check() {
    cd "python-markdown-math-${pkgver}"

    python setup.py test
}

package() {
    cd "python-markdown-math-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
