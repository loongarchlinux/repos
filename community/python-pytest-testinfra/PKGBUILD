# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=pytest-testinfra
pkgname=python-pytest-testinfra
pkgver=6.5.0
pkgrel=2
pkgdesc='Testinfra test your infrastructures'
arch=(any)
url="https://github.com/pytest-dev/pytest-testinfra"
license=(Apache)
depends=(python-pytest)
makedepends=(python-setuptools-scm python-sphinx)
checkdepends=(ansible python-mock python-paramiko python-pytest-cov
python-pytest-xdist python-pywinrm python-tornado salt)
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts'
            'salt: for tests using saltstack')
conflicts=(python-testinfra)
provides=(python-testinfra)
replaces=(python-testinfra)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f7fcf8facafc0d99b06f55f4241c39b610ff7648b0049c584a75b46944b522b957c98c353f147d74bc8a72f677a179380c456b0868f9dae9af514a9c95d55b13')
b2sums=('98a8f10f7cf62f204c117d295496a7dfa4de476f5cde457af6f5bc71b22e12e9f02b4893adc6b0d1be11915ed652f628c2574fd44750c82c690e3d8326e29c4c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  # man page
  install -Dm 644 doc/build/man/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
