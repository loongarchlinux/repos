# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=pytest-testinfra
pkgname=python-pytest-testinfra
pkgver=6.7.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
arch=(any)
url="https://github.com/pytest-dev/pytest-testinfra"
license=(Apache)
depends=(python-pytest)
makedepends=(python-build python-installer python-setuptools-scm python-sphinx python-wheel)
checkdepends=(ansible python-mock python-paramiko python-pytest-cov
python-pytest-xdist python-pywinrm python-tornado salt)
optdepends=(
  'ansible: for tests using ansible inventories'
  'docker: for test running docker containers'
  'kubectl: for test running containers in kubernetes'
  'lxd: for test running LXC or LXD containers'
  'podman: for test running podman containers'
  'python-paramiko: for testing remote hosts'
  'python-pywinrm: for testing on Windows hosts'
  'salt: for tests using saltstack'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d11f30da0eae9f215173d3f84e6ba7965d926b8ef6c49619c2ccae3d3ff215f351b950eea019e6516a56de0aa7bc533658af978d827a3ad3d1c50e37902b6f91')
b2sums=('08678f381392fe8a1fe335adc90ac6dc67c5bfa33c87f4e9d81b439eaabd29c393b8dc80cc79625784e76e5b0e264cdfc272cddb531acefbac9a8327be46f7be')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  make -C doc man
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver

  # install to temporary location
  python -m installer --destdir="$_test_dir" dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  # man page
  install -Dm 644 doc/build/man/*.1 -t "$pkgdir/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
