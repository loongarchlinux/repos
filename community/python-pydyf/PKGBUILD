# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pydyf
pkgver=0.5.0
pkgrel=1
pkgdesc='A low-level PDF generator'
arch=('any')
url='https://www.courtbouillon.org/pydyf'
license=('BSD')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-flit-core'
  'python-installer'
)
checkdepends=(
  'python-pytest'
  'python-pillow'
  'ghostscript'
)
_commit='4694659d31e3c844d2c0d89f230b709f62d752b5'
source=("$pkgname::git+https://github.com/CourtBouillon/pydyf.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
