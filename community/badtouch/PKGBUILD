# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=badtouch
pkgver=0.7.1
pkgrel=1
pkgdesc='Scriptable network authentication cracker'
url='https://github.com/kpcyrd/badtouch'
depends=('openssl' 'libcap' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('GPL3')
install=badtouch.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}.tar.gz ${pkgname}-${pkgver}.tar.gz.asc::https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc)
sha256sums=('c188bb1df106761a436fd25d3530323a47633c4c937d186e82c00981ffc94b5f'
            'SKIP')
sha512sums=('3bfc58d50b366a1d3d6f6474b1446639e3e6144873228a1e153465977ffc66be060baa00c919e8accbd66eaf226a12f1a81d45160bbba75c702c8564e34ba0b5'
            'SKIP')
validpgpkeys=('64B13F7117D6E07D661BBCE0FE763A64F5E54FD6')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 docs/badtouch.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 scripts/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
