# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=kiwix-tools
pkgver=3.3.0
pkgrel=1
pkgdesc="kiwix command line tools"
license=('GPL3')
arch=('armv7h' 'i686' 'loong64' 'x86_64')
depends=('libkiwix')
makedepends=('meson')
url='https://github.com/kiwix/kiwix-tools'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('15b318080d72373eeb584f2d1b1e4d932d1300191eb8ef919f2536a2e685b413')

build() {
	cd "${pkgname}-${pkgver}"
	arch-meson build
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}/build"
	ninja meson-test
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
