# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-cross-cluster-replication-plugin
pkgver=2.0.1.0
pkgrel=1
_opensearchver=2.0.1
_jdkver=11
pkgdesc="OpenSearch Cross-Cluster Replication Plugin"
arch=('loongarch64' 'x86_64')
url="https://opensearch.org/docs/latest/replication-plugin"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/cross-cluster-replication/archive/${pkgver}.tar.gz"
)
sha256sums=('fe53e85d7f8aaebe97486ac3d18161864bcdc0547d0957fa6c2edd0f8ab581e9')

build() {
  cd "cross-cluster-replication-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  unzip "${srcdir}/cross-cluster-replication-${pkgver}/build/distributions/opensearch-cross-cluster-replication-${pkgver}.zip"

  install -Dm644 "${srcdir}/cross-cluster-replication-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
