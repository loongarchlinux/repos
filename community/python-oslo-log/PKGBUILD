# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=5.0.0
pkgrel=1
pkgdesc="Oslo Logging Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("https://github.com/openstack/oslo.log/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('aa381095efbbb6fc4919d21baa41985efacc119ceeb345502a6b65ded6ebf6b95652788f0e45ec80461f2e23b8622627327c64d511e62775f0c70008b9b595d7')

export PBR_VERSION=$pkgver

build() {
  cd oslo.log-$pkgver
  python setup.py build
}

check() {
  cd oslo.log-$pkgver
  stestr run
}

package() {
  cd oslo.log-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
