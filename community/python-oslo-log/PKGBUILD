# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=4.5.0
pkgrel=2
pkgdesc="Oslo Logging Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("https://github.com/openstack/oslo.log/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a1e1b78b96ea60e0531d00009f0e9d54b69b611c6f62ec0b777061ffa01d814d72da068f1a6b56735d135a149015e9ab56132ab729e90f748eb57f28b5fc906e')

export PBR_VERSION=$pkgver

build() {
  cd oslo.log-$pkgver
  python setup.py build
}

check() {
  cd oslo.log-$pkgver
  stestr run
}

package() {
  cd oslo.log-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
