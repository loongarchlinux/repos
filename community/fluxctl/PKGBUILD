# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fluxctl
_pkgname=flux
pkgver=1.24.3
pkgrel=1
pkgdesc="Controller for the GitOps Kubernetes operator flux"
arch=('loongarch64' 'x86_64')
url="https://github.com/fluxcd/flux"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fluxcd/flux/archive/${pkgver}.tar.gz")
sha512sums=('96c72a58ad600e2db8bf8571790df4565d7ea4a3bb5d78bfdafe65f2fc64b868ca61d3eec8e879c4b2eef17195b3168523b0059600f9a55581d1f949ce2ad578')
b2sums=('1c69cb0c99fb8d3b3b8a82fe9159a9f213133337010b33db0d267e07186b240e730a7c89eec1b718f52135d7369ccbf6178f70e86f98d692249ff634dfd71e57')

build() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-w -X main.version="${pkgver}" -linkmode=external" .
  chmod +x ./fluxctl
}

check() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  go test ./...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  install -Dsm755 ./fluxctl "${pkgdir}/usr/bin/fluxctl"
}
