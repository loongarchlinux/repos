# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-osc
pkgver=1.7.7
pkgrel=1
pkgdesc='Open Sound Control server and client implementations in pure Python'
arch=('any')
url="https://github.com/attwad/python-osc"
license=('Unlicense')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3b0f137ae354df4eff02b23582ac2023037576f6c95fdcfc86e039ed40d549433a87e1c8385798f3005acbd3160da6344df9ec9374d004678498dce4c23b9059')
b2sums=('b140576334074935afbf6855c70e549d62d6accfe1b09fac3f136ed177f98e75479a00b8c0f0337672f9331f71ff347caf1d765bff2294b0fc0a6d682a4c5ef5')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
