# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.13
_commit="25c2850b597ee9a89bda8920e7f3d65ac3ac7e01"
# 2017-03-06 gcc6
pkgrel=22
pkgdesc="An advanced and well-established feature-rich text mode web browser."
arch=("x86_64")
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua51' 'libidn'
	 'gc' 'tre' 'zlib')
makedepends=('git')
source=("git+https://repo.or.cz/elinks.git#commit=$_commit"
	"0001-enable-SSL-verification-by-default.patch")
md5sums=('SKIP'
         '0bdeaeed54d24415db9b59d9fdcf03f9')

# todo: make it work with a modern 'js' package
# (upstream has no interest in writing that)

prepare() {
  cd elinks
  patch -p1 < "$srcdir"/0001-enable-SSL-verification-by-default.patch
}
 
build() {
  cd elinks
  sed -i 's/Terminal=1/Terminal=true/' "contrib/debian/$pkgname.desktop"
  [ -x configure ] || sh autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --disable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --with-zlib
  make
}

package() {
  cd elinks
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/share/locale/locale.alias"

  install -D -m644 "contrib/debian/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
