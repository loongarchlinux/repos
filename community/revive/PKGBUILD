# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=revive
pkgver=1.1.4
pkgrel=2
pkgdesc="faster, stricter, configurable, extensible, and beautiful drop-in replacement for golint"
arch=('loongarch64' 'x86_64')
url='https://revive.run'
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mgechev/revive/archive/v${pkgver}.tar.gz")
sha256sums=('afe0d11a614d5ca706485b7f6ca1e53b374b45017ea75f14149a4434d3ef74b5')

build() {
  cd revive-${pkgver}
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
  go build
}

check() {
  cd revive-${pkgver}/test
  go test || true
}

package() {
  cd revive-${pkgver}
  install -Dm755 revive "${pkgdir}/usr/bin/revive"
  install -Dm644 defaults.toml "${pkgdir}/etc/revive/revive.toml"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
