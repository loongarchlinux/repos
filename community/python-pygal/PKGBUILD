# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Warrick <aur@chriswarrick.com>

pkgname=python-pygal
epoch=1
pkgver=2.4.0
pkgrel=6
pkgdesc='A Python SVG graph plotting library'
arch=('any')
url='https://www.pygal.org/en/stable/'
license=('LGPL3')
depends=('python-setuptools')
optdepends=('python-lxml: for XML output through lxml instead of xml.etree'
            'python-cairosvg: for PNG output')
checkdepends=('python-pytest' 'python-lxml' 'python-pyquery' 'python-cairosvg' 'python-flask')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/pygal/archive/$pkgver.tar.gz")
md5sums=('5c688d30f8e75dced194bfa612f45589')

prepare() {
  sed -i 's/\[pytest\]/[tool:pytest]/' pygal-$pkgver/setup.cfg
}

build() {
  cd pygal-$pkgver
  python setup.py build
}

check() {
  cd pygal-$pkgver
  pytest pygal/
}

package() {
  cd pygal-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
