# Maintainer: David Runge <dvzrv@archlinux.org>

_name=coverage-conditional-plugin
pkgname=python-coverage-conditional-plugin
pkgver=0.7.0
pkgrel=1
pkgdesc="Conditional coverage based on any rules you define"
arch=(any)
url="https://github.com/wemake-services/coverage-conditional-plugin"
license=(MIT)
depends=(python-coverage python-packaging)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-cov python-pytest-randomly)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('c67baa7377e1ea9fbaa6c00c26f6aefbd2e1bb055ec8d6b5527e48f6dd18163e')
b2sums=('1ef41fdeede7ffa59f3f2eb53d122eed8e25185376187e809a3794524c2853a1703ba4c283ae81ea14e237305e454689149a4faeb93be0aa39e2171abc530b01')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # ignore integration and code style checks
  # ignore unwanted pulling in of python-pytest-pythonpath: https://github.com/wemake-services/coverage-conditional-plugin/issues/154
  # ignore broken integration test
  pytest -vv -c /dev/null --ignore tests/test_integration.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
