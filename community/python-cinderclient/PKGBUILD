# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cinderclient
pkgver=8.2.0
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="https://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
         'python-requests' 'python-oslo-utils')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-cinderclient/archive/$pkgver.tar.gz")
sha512sums=('ed311f3b390fd912c294f0fa4e38f8c6d3623f055e46407f7975e69761b347c93b4f2701a9514eddbbfb9d53ba8a96ba1cd8a7ba2c446c4bd987a2a3d7178101')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-cinderclient-$pkgver/requirements.txt
}

build() {
  cd python-cinderclient-$pkgver
  python setup.py build
}

check() {
  cd python-cinderclient-$pkgver
  stestr run
}

package() {
  cd python-cinderclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
