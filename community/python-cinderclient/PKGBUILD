# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cinderclient
pkgver=9.1.0
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="https://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
         'python-requests' 'python-oslo-utils')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-cinderclient/archive/$pkgver.tar.gz")
sha512sums=('0fff4ee9793dec22d34e38c33b11cb974ce0c2e753993ba38ccdb26816f16ee4fd5f9b6763dd261782a51e1f6a30cfcf89d4b9c1be60030550344b581c06b8ec')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-cinderclient-$pkgver/requirements.txt
}

build() {
  cd python-cinderclient-$pkgver
  python setup.py build
}

check() {
  cd python-cinderclient-$pkgver
  stestr run
}

package() {
  cd python-cinderclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
