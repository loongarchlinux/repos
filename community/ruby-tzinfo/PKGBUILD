# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tzinfo
pkgname=ruby-tzinfo
pkgver=2.0.2
pkgrel=1
pkgdesc='Daylight savings aware transformations between times in different time zones'
arch=('any')
url="https://github.com/tzinfo/tzinfo"
license=('MIT')
depends=('ruby' 'ruby-concurrent')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-simplecov
# checkdepends=('ruby-minitest' 'ruby-simplecov')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('a0c71dd9299e84bd71a91858ba8c12a0ba00c625219da1172d5cd1dfd35318ce8a83e59f45489c2ed5161761e1c39834ba63dbfa189acb100ba37ceae842668d')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

# check(){
#   cd "${pkgname}-${pkgver}"
#   rake test
# }

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
