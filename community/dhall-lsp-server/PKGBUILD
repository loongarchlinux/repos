# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-lsp-server
pkgver=1.0.15
pkgrel=4
pkgdesc="Language Server Protocol (LSP) server for Dhall"
url="https://github.com/dhall-lang/dhall-haskell/tree/master/dhall-lsp-server#readme"
license=("custom:MIT")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-data-default' 'dhall'
         'dhall-json' 'haskell-lsp0' 'haskell-hslogger' 'haskell-lens' 'haskell-lens-family-core'
         'haskell-megaparsec' 'haskell-network-uri' 'haskell-optparse-applicative'
         'haskell-prettyprinter' 'haskell-rope-utf16-splay' 'haskell-unordered-containers'
         'haskell-uri-encode')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-doctest' 'haskell-hspec' 'haskell-lsp0-types'
             'haskell-lsp0-test' 'haskell-tasty' 'haskell-tasty-hspec')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7988ecea64b4e309fd7a10143ad1097c65280ec22ff342943f2b18f108073af4f05e66f024d21398f8b96cc535f8805b9534b620b7423ac92140b3e19a041875')

build() {
  cd $pkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  LD_LIBRARY_PATH="$PWD/dist/build" PATH="$PWD/dist/build/dhall-lsp-server:$PATH" runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
