# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=typing_inspect
pkgname=python-$_pkgname
pkgver=0.6.0
pkgrel=1
pkgdesc='Runtime inspection utilities for Python typing module'
arch=('any')
url='https://github.com/ilevkivskyi/typing_inspect'
license=('MIT')
depends=('python-mypy_extensions' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('c08654d2d3887710094fa7374c2ce5fa6b79176543d478d15113d3904bb3c68cdeb08d8331b6eeb271858e96ee859fdaad18dd17237a0618072f630f34758177')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
