# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.3
pkgrel=1
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('cython' 'python')
makedepends=('python-pip' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/explosion/cymem/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e80f1af0be0399c93e9dbddb9c132513ca3e17b14337662097cb36edf606fb7fd0a490d348d44d3f905aa274a9c8f6a5a8008099d0814c0a462775d357bdae26')

build() {
  cd cymem-$pkgver
  python setup.py build
}

check() {
  cd cymem-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8" pytest
}

package() {
  cd cymem-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
