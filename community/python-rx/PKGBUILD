# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ilya Elenskiy <elenskiy.ilya@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=python-rx
pkgver=3.2.0
pkgrel=1
pkgdesc='Reactive Extensions for Python'
arch=(any)
url=http://reactivex.io
license=(APACHE)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-coverage
  python-nose
  python-pytest
  python-pytest-asyncio
)
_tag=5054de8874f275ed0de55007b87cff4817b1d9f7
source=(git+https://github.com/ReactiveX/RxPY.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd RxPY
  git describe --tags | sed 's/^v//'
}

build() {
  cd RxPY
  python setup.py build
}

check() {
  cd RxPY
  python -m pytest
}

package() {
  cd RxPY
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
