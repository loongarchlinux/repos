# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-shims
pkgver=0.7.0
pkgrel=1
pkgdesc="Compatibility shims for pip versions 8 thru current"
url="https://github.com/sarugaku/pip-shims"
license=('ISC')
arch=('any')
depends=('python-pip' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest-runner' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/pip-shims/archive/$pkgver.tar.gz")
sha512sums=('d9a14cd4dab9112a4cdea7e7eaeb8731a4f78b6176200cb08bfc54caad80d020cd8a8c0a4b38ffb12fae9bf7a7841e99cd2fd956c5f85cf35fef6c01687444c1')

build() {
  cd pip-shims-$pkgver
  python setup.py build
}

check() {
  cd pip-shims-$pkgver
  python setup.py pytest
}

package() {
  cd pip-shims-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
