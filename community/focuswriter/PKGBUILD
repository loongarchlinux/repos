# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.8.2
pkgrel=1
pkgdesc='Simple, distraction-free word processor'
arch=('loong64' 'x86_64')
url='https://gottcode.org/focuswriter/'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt6-multimedia' 'qt6-5compat')
makedepends=('cmake' 'qt6-tools')
source=("https://gottcode.org/$pkgname/$pkgname-$pkgver.tar.bz2"{,.asc})
sha256sums=('9fd6384009a33840218c653bb8d3ab69af5e80de884d7be3cd778500484cb583'
            'SKIP')
validpgpkeys=('57E4D43C10CD7EDB8360C84774FFC7F02C4F6B37')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
