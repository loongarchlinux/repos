# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-screensaver
pkgver=3.0.0
pkgrel=1
pkgdesc="Screensaver for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-screensaver"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'libxtst' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-screensaver/archive/v$pkgver.tar.gz"
        $pkgname-qt-5.15.patch::https://github.com/ukui/ukui-screensaver/pull/84.patch)
sha512sums=('10f4bb5afea04d6ac3a936361237c0563fb0e8ed25414a20e06715af92124e52679913efb01275adf6cbd94e41e2d1b2f0646e0f3b5d0ba8cb3729e43827f396'
            '3e167998e310411dec91324fa589b100c2a8f516cd00a63b8ba7feb40bbbe90e7cc9ab663d66aa64731223d4fec2a1fe1d0e03367423f00195ff4dea29c7a2a9')

prepare() {
  mkdir -p build
  patch -d $pkgname-$pkgver -p1 < $pkgname-qt-5.15.patch
  # https://github.com/ukui/ukui-screensaver/issues/65
  cp /etc/pam.d/login $pkgname-$pkgver/data/ukui-screensaver-qt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
