# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=2.1.0
pkgrel=1
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/w3lib"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('a511711e8be7669e80e82bd3ccafa507a57a6f7755af4fab95bd27d3987507cc6ec73497af76ac316831e6f43d103aa98b71388452a83fc01ddbba1f40414bf3')

build() {
  cd w3lib-$pkgver
  python setup.py build
}

check() {
  cd w3lib-$pkgver
  pytest
}

package() {
  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
