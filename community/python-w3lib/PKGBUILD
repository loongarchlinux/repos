# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=2.0.1
pkgrel=1
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/w3lib"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('c4c1a00ad6d0c8f9a3a3a6581fb513328f89bfd9f797bf6289133580581c2579b04da8a2c815d14214bc0dbc87839de3d9f7949ad9398402312d3c522361414e')

build() {
  cd w3lib-$pkgver
  python setup.py build
}

check() {
  cd w3lib-$pkgver
  pytest
}

package() {
  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
