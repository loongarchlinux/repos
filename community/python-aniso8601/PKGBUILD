# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-aniso8601
pkgname=(python-aniso8601 python2-aniso8601)
pkgver=8.0.0
pkgrel=3
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-dateutil' 'python2-dateutil'
              'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('111bf07955795cf418971d4c9d776b91e3e4aa276d6dda78581f1d42b49e30ed67bb0f4fdbc2efeebaf4dbdfed2afe78084dc350dcda0d41afd92ccb821a2e1d')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
  cp -a aniso8601-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build

  cd "$srcdir"/aniso8601-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest

  cd "$srcdir"/aniso8601-$pkgver-py2
  python2 setup.py pytest
}

package_python-aniso8601() {
  depends=('python')

  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-aniso8601() {
  depends=('python2')

  cd aniso8601-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
