# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=PyVirtualDisplay
pkgname=python-${_pkg,,}
pkgver=2.1
pkgrel=1
pkgdesc="Python wrapper for Xvfb, Xephyr and Xvnc"
arch=(any)
url="https://github.com/ponty/PyVirtualDisplay"
license=(BSD)
depends=(python-easyprocess)
makedepends=(python-setuptools)
checkdepends=(
    python-pytest python-pytest-xdist
    python-pyscreenshot python-pillow
    python-entrypoint2 python-path.py python-psutil
    xorg-server-xvfb xorg-server-xephyr
    xorg-xmessage xorg-xdpyinfo scrot vncdotool
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
#source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9d4c6957ec2c4753b5293fb6a60a90d7c27fc01bc5de9b5aa863f7c1e3fb4efc')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}/build/lib" pytest tests
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
