# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-contrib
pkgname=haskell-hspec-contrib
pkgver=0.5.1
pkgrel=200
pkgdesc="Contributed functionality for Hspec"
url="https://hspec.github.io/"
license=('MIT')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-hspec-core' 'haskell-hunit')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        hspec-2.10.patch hspec-2.10-2.patch)
sha512sums=('cc6ee847d7c99b2607c36b2200bf0bc05ce30437110f216f82464069ffdf80a76d836ad7aace3d69a00cf9da4b28d39491a251c6f60401009a6f9c85d20043e0'
            'ded33d9cf5612520909222609c40083baf0af52a2f0c9c82848eaecdca9e7ee8fde272594840576db385161baf2732f73d61cc768730052a5e538d33106780bd'
            '9dfdcd8e108ab8fdb2c27a8cbabd841c70b8b8a4633b683a2aad8829f44014ae9b36de157402db521ad3c4bcda47ca86b0dc521355a189faa56da716b7d3415a')

prepare() {
    patch -d $_hkgname-$pkgver -p2 < hspec-2.10-2.patch
    patch -d $_hkgname-$pkgver -p2 < hspec-2.10.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
