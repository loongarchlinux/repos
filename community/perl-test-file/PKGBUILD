# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Dupuis <lisael@lisael.org>
# Contributor: Rafael Reggiani Manzo <rr.manzo@gmail.com>

pkgname=perl-test-file
pkgver=1.992
pkgrel=2
pkgdesc="This modules provides a collection of test utilities for file attributes."
arch=('any')
url='https://github.com/briandfoy/test-file'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
source=("https://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/Test-File-$pkgver.tar.gz")
sha512sums=('16ba17d3d83374d21d272f5c5ab4dbcb99d09d569025ba415e90963cad3ebe65fedc72a8a6ba57b6ee1fae657aaca7e51999038efd83b90e0b5694e8222c398d')

build() {
  cd Test-File-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-File-$pkgver
  make test
}

package() {
  cd Test-File-$pkgver
  make DESTDIR="$pkgdir" install
}
