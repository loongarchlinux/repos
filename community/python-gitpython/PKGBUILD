# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: brent s. <bts[at]square-r00t[dot]net>

_name=gitpython
pkgdesc="A python library used to interact with Git repositories"
pkgname=python-gitpython
pkgver=3.1.24
pkgrel=3
url="https://github.com/gitpython-developers/gitpython"
license=('BSD')
arch=('any')
depends=('git' 'python-gitdb')
makedepends=('python-setuptools')
checkdepends=('python-ddt' 'python-nose' 'python-pytest' 'python-virtualenv')
source=("git+https://github.com/gitpython-developers/gitpython.git#tag=${pkgver}?signed")
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('27C50E7F590947D7273A741E85194C08421980C9') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  # create empty dummy pytest config so that we can deselect all implicit settings
  touch pytest.ini
  cd "${_name}"
}

build() {
  cd "${_name}"
  python setup.py build
}

check() {
  cd "${_name}"
  export TRAVIS="VERY CONVENIENT"
  git config --global user.name "Test User"
  git config --global user.email "test@user.org"
  ./init-tests-after-clone.sh
  PYTHONDONTWRITEBYTECODE=1 pytest -v -c ../pytest.ini \
    -k "not test_base and not test_reading and not test_writing"
}

package() {
  cd "${_name}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGES,{CONTRIBUTING,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
