# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: brent s. <bts[at]square-r00t[dot]net>

_name=gitpython
pkgdesc="A python library used to interact with Git repositories"
pkgname=python-gitpython
_commit=d438e088278f2df10b3c38bd635d7207cb7548a6  # refs/tags/3.1.27^{}
pkgver=3.1.27
pkgrel=1
url="https://github.com/gitpython-developers/gitpython"
license=(BSD)
arch=(any)
depends=(git python-gitdb)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-ddt python-nose python-pytest python-virtualenv)
source=(git+$url#commit=$_commit?signed)
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('27C50E7F590947D7273A741E85194C08421980C9') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  cd $_name
  export TRAVIS="VERY CONVENIENT"
  git config --global user.name "Test User"
  git config --global user.email "test@user.org"
  ./init-tests-after-clone.sh
  PYTHONDONTWRITEBYTECODE=1 pytest -v -c /dev/null -k "not test_base and not test_reading and not test_writing"
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,CHANGES,{CONTRIBUTING,README}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
