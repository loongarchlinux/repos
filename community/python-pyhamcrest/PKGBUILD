# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyhamcrest
pkgver=1.10.0
pkgrel=1
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://github.com/hamcrest/PyHamcrest/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f218b973911aada45c7abd55e5a2db8c1d1516d608d3edab65c49d5e0155a16aa990c9f0bcce055381542c631d2337f1c3ff4da21f646edb3ea83faaf4977d95')

build() {
  cd PyHamcrest-$pkgver
  python setup.py build
}

check() {
  cd PyHamcrest-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd PyHamcrest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
