# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyhamcrest
pkgver=2.0.0
pkgrel=1
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/hamcrest/PyHamcrest/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('32683422c92f27ffbdb081cfeb588a81bd299fd965e3fdd67a5aca2900e0a89da0fa217b22f4333511831921953519d75861b18fcb164d1ec6cacf13994df5e3')

build() {
  cd PyHamcrest-$pkgver
  python setup.py build
}

check() {
  cd PyHamcrest-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd PyHamcrest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
