# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgname=emacs-php-mode
pkgver=1.24.2
pkgrel=1
license=("GPL")
pkgdesc="PHP mode for emacs"
url="https://github.com/ejmr/php-mode"
arch=('any')
depends=('emacs')
options=(docs zipman)
install=emacs-php-mode.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/ejmr/php-mode/archive/v$pkgver.tar.gz")
sha256sums=('bb468c5ec243ebd6c74be65b09aca42d4f70e55f8f1a8b3b71a0ab807c33f1c9')

build() {
  cd "$srcdir"/php-mode-$pkgver
  make
}

package() {
  cd "$srcdir"/php-mode-$pkgver/lisp
  for i in *.el{,c}; do
    install -Dm0644 $i "$pkgdir"/usr/share/emacs/site-lisp/php-mode/$i
  done
}
