# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgname=emacs-php-mode
pkgver=1.24.0
pkgrel=2
license=("GPL")
pkgdesc="PHP mode for emacs"
url="https://github.com/ejmr/php-mode"
arch=('any')
depends=('emacs')
options=(docs zipman)
install=emacs-php-mode.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/ejmr/php-mode/archive/v$pkgver.tar.gz")
sha256sums=('e8b59a4f34755c18ef7cbcae089f2fad03d4763e3d7e179b18ad2af596af5e64')

build() {
  cd "$srcdir"/php-mode-$pkgver
  make
}

package() {
  cd "$srcdir"/php-mode-$pkgver/lisp
  for i in *.el{,c}; do
    install -Dm0644 $i "$pkgdir"/usr/share/emacs/site-lisp/php-mode/$i
  done
}
