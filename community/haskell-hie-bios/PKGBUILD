# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hie-bios
pkgname=haskell-hie-bios
pkgver=0.7.6
pkgrel=100
pkgdesc="Set up a GHC API session"
url="https://github.com/mpickering/hie-bios"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base16-bytestring' 'haskell-conduit'
         'haskell-conduit-extra' 'haskell-cryptohash-sha1' 'haskell-exceptions' 'haskell-extra'
         'haskell-file-embed' 'haskell-ghc-api-compat' 'haskell-hslogger'
         'haskell-optparse-applicative' 'haskell-temporary' 'haskell-unix-compat'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-yaml')
makedepends=('ghc' 'uusi' 'haskell-hspec-expectations' 'haskell-tasty' 'haskell-tasty-expected-failure'
             'haskell-tasty-hunit')
checkdepends=('cabal-install')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $pkgname-ghc-9.0.2.patch::https://github.com/haskell/hie-bios/commit/da0cb23384cc6e9b393792f8f25a3c174a4edafa.patch)
sha512sums=('33ffb37f5f39ebd9e4000de298d2645c15426818704f1d806b5f186d0d0d82bfd22df70aed181a9eab9d3b2581055a1e9c0925051fa05d97f38f4c8c7c7d0d14'
            '5fa94f58c47d8597a8b797ea526422463569983332e1acd1769e6d67fd85dcbaea1b8ad2ba72f11f9755aaa49a101f4ad601ce5121756638df7ee6e478619885')

prepare() {
  cd $_hkgname-$pkgver

  uusi -u unix-compat $_hkgname.cabal

  patch -Np1 -i ../$pkgname-ghc-9.0.2.patch

  # It is broken only in the hackage tarball
  sed -i 's/FilePah/FilePath/' tests/ParserTests.hs

  # Additional fixes for dynamic ghc
  sed -i 's/updateWays \.//' src/HIE/Bios/Environment.hs

  # https://github.com/mpickering/hie-bios/issues/290
  chmod +x tests/projects/*/*.sh
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  cabal update  # Generate cabal config for tests
  # Test failures are related to process-1.6.13
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
