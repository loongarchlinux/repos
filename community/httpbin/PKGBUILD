# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=httpbin
pkgver=0.7.0
pkgrel=8
pkgdesc="HTTP Request and Response Service"
arch=('any')
url="https://github.com/requests/httpbin"
license=('MIT')
depends=('python-flask' 'python-markupsafe' 'python-itsdangerous' 'python-six' 'python-decorator'
         'python-brotli' 'python-raven' 'python-werkzeug' 'python-gevent' 'python-flasgger'
         'python-blinker')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/requests/httpbin/archive/v$pkgver.tar.gz"
        httpbin-werkzeug-0.15.1.patch
        httpbin-werkzeug-2.0.0.patch)
sha512sums=('faec48a0a2ac8800293b10281966a28195884ad2f69f0f28f1b8c8e1a7bfd8933ebbc9b541c80cdc19e1031a8ba9383afe8e372b9044436cb307dd1e8eddaae7'
            '3c058ca5f685e281f7d60216de844e58727e7677766660df410ec57d8c985485cf611ec64eb71a234bdd49b4fdf66be6138d4bb7258d9e6d0346d6c6ee9f3cdf'
            '25aced8dc34ed517ba7304d86d7e32fc49282262512962edf743265c78f3c5a2e3324902ec76ea121c11a9af0219d8c4e506ce301ad625d9e72f07491b8cbe49')

prepare() {
  cd httpbin-$pkgver
  patch -p1 -i ../httpbin-werkzeug-0.15.1.patch
  patch -p1 -i ../httpbin-werkzeug-2.0.0.patch
  sed -i 's/brotlipy/Brotli/g' setup.py
}

build() {
  cd httpbin-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py build
}

check() {
  cd httpbin-$pkgver
  python test_httpbin.py
}

package() {
  cd httpbin-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
