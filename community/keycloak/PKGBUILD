# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Nikita Volodin <volodin.n at gmail dot com>

pkgname=keycloak
pkgver=16.1.0
_postgresql_jdbc=42.3.1
_java=11
pkgrel=1
pkgdesc="Open Source Identity and Access Management For Modern Applications and Services"
arch=('any')
url="https://www.keycloak.org/"
license=('Apache')
depends=("java-runtime=${_java}" 'grep' 'bash' 'coreutils' 'util-linux')
makedepends=('maven' 'git' "java-environment=${_java}")
backup=(
  opt/keycloak/standalone/configuration/application-roles.properties
  opt/keycloak/standalone/configuration/application-users.properties
  opt/keycloak/standalone/configuration/logging.properties
  opt/keycloak/standalone/configuration/mgmt-groups.properties
  opt/keycloak/standalone/configuration/mgmt-users.properties
  opt/keycloak/standalone/configuration/standalone-ha.xml
  opt/keycloak/standalone/configuration/standalone.xml
)
options=(emptydirs)
source=(https://github.com/keycloak/keycloak/archive/$pkgver/$pkgname-$pkgver.tar.gz
        keycloak.service
        keycloak.sysusers
        keycloak.tmpfiles
        layers.conf
        postgresql-module.xml
        https://jdbc.postgresql.org/download/postgresql-${_postgresql_jdbc}.jar
        common.sh
        pin-java-version.patch)
sha512sums=('024ceb65e58d378e784519f44a34dc2ad09ee42a27bbf86f3b1b46f9562416fdac590482e0e5a00d3e3e34baf883c7410c9be77c8873b2dc755832e24a97179f'
            'dd10a38e4c8867de6d1fcbb4939c6c5946684842462eba3d5355bf1de13fa059edfd5eba37adce587c824ddd618b6bb88d4bbe1ee568ca5abd0f72296da3ddfe'
            'c53b734598ccf675930754514bd2af628355025197e77541e5acb8718629a8ece763c814e8373278758a30475e6a716febeff10bdebf0847d914bc193e732c79'
            '9382b42f09040e34fe1a27d9ff4fb87cabd41525ca26f8ea9629df0169cf93d95cc4bb62bcc5c2ca30efdfd7eeeb13a6f090df9a05823baf701ba729a47f6fd1'
            'd54a04c120edb933c4f7168460bd813ff92a4e3771cedcabfeeb76c5d3e057b3cd0d0486dc11ddbd91e8d87ca90193e4c74cc3414e45106dd556d455ac7e60b2'
            '5eece4f3eb57372969f0e5319804198b546455828e938ff7f37bbcfb58be08efde43a5b0376c98f359c4bc5eb069e0cd9410daefe19da1c03e9e9c57d3c1b75b'
            '51cf3d0de9d9f0d41591aa32d5fabbc711991daf4b11531f5d1bd9d288a089861624c9f58e195c869e6a11f0651046ed0f183bffce977566181582c9ab8c3aab'
            '4ae3f9fc42bfee602480c1c8cc2d65b44305622b426b74070758fe1c92a06ff12901ffebacbfe2ba34cbf783a8787f6073f74db3674c96e7a6109ed5b45d3a07'
            'cb7d95aef66303c7935714c58c51aa1714419c455a6f46c32a79e80dbe4b6523d0dd77bb5f606c5cfdf0380f01220af720f93285658fc19a38b860344a2b9198')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir"/pin-java-version.patch
}

build() {
  cd $pkgname-$pkgver

  export PATH="/usr/lib/jvm/java-${_java}-openjdk/bin:$PATH"
  mvn -Pdistribution \
    -s maven-settings.xml \
    -pl distribution/server-dist \
    -am \
    -Dmaven.test.skip \
    clean install
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/opt"
  cp -r "distribution/server-dist/target/keycloak-$pkgver" "$pkgdir/opt/keycloak"
  install -Dm 644 "$srcdir"/layers.conf -t "$pkgdir/opt/keycloak/modules"
  install -d "$pkgdir"/opt/keycloak/standalone/{data,log}

  install -Dm644 "$srcdir"/postgresql-${_postgresql_jdbc}.jar "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/postgresql-${_postgresql_jdbc}.jar
  sed "s/@JDBC_POSTGRESQL_JAR@/postgresql-${_postgresql_jdbc}.jar/g" "$srcdir"/postgresql-module.xml > "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/module.xml

  install -d "$pkgdir/"{etc,var/log,var/lib/keycloak}
  ln -s /opt/keycloak/standalone/configuration "$pkgdir/etc/keycloak"
  ln -s /opt/keycloak/standalone/log "$pkgdir/var/log/keycloak"
  ln -s /opt/keycloak/standalone/deployments "$pkgdir/var/lib/keycloak/deployments"

  install -Dm644 "$srcdir"/keycloak.service "$pkgdir"/usr/lib/systemd/system/keycloak.service
  install -Dm644 "$srcdir"/keycloak.sysusers "$pkgdir"/usr/lib/sysusers.d/keycloak.conf
  install -Dm644 "$srcdir"/keycloak.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/keycloak.conf

  mkdir -p "$pkgdir"/usr/bin
  ln -s /opt/keycloak/bin/kcadm.sh "$pkgdir"/usr/bin/kcadm
  ln -s /opt/keycloak/bin/kcreg.sh "$pkgdir"/usr/bin/kcreg

  install -Dm644 "$srcdir"/common.sh "$pkgdir"/opt/keycloak/bin/common.sh
}

# vim: ts=2 sw=2 et:
