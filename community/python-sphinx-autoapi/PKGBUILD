# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=2.0.0
pkgrel=1
pkgdesc="A new approach to API documentation in Sphinx."
arch=(any)
url="https://github.com/readthedocs/sphinx-autoapi"
license=(MIT)
depends=(python-astroid python-jinja python-sphinx python-unidecode
python-pyyaml)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
# TODO: package python-go and python-dotnet
# optdepends=(python-go python-dotnet)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('97dcf1b5b54cd0d8efef867594e4a4f3e2d3a2c0ec1e5a891e0a61bc77046006')
b2sums=('40f2d62a06817cd28b8d35510af045b84a314ab638f9a6dc408f64685cab24a72ed93a5c1197e3988154a65f6f060b6ed35e0bc889253d6b1f723bbd8398e298')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/sphinx_autoapi-$pkgver.dist-info/LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
