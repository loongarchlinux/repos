# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=1.9.0
pkgrel=2
pkgdesc="A new approach to API documentation in Sphinx."
arch=('any')
url="https://github.com/readthedocs/sphinx-autoapi"
license=('MIT')
depends=('python-astroid' 'python-jinja' 'python-sphinx' 'python-unidecode'
'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-mock' 'python-pytest')
# TODO: package python-go and python-dotnet
# optdepends=('python-go' 'python-dotnet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('c897ea337df16ad0cde307cbdfe2bece207788dde1587fa4fc8b857d1fc5dcba')
b2sums=('0c9cae319dec6169cdc9498139295994bb24f2d55ac00ffb7a582c04b80199cd2f5e5d6137dbf03dedcc56dd2c4ead9c0d4668c79f5e441d4b358c4f31ce9202')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/sphinx_autoapi-$pkgver.dist-info/LICENSE.rst \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
