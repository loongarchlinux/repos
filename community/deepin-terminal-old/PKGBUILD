# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal-old
pkgver=5.0.4.3
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin, the old version'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal-old"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect')
makedepends=('cmake' 'ninja' 'vala')
optdepends=('deepin-menu: for native menu integration under DDE'
            'zssh: for remote management support')
conflicts=('deepin-terminal')
provides=('deepin-terminal')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal-old/archive/$pkgver.tar.gz")
sha512sums=('1b8a580ff0a2b216a1849651d33fcc6d31c7683bc908a79d7b9253c0fe39f94534d4e9c919c9dd3b1a83844615bc46facd91c693397a580fc9313287606216f1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTEST_BUILD=OFF \
    -DUSE_VENDOR_LIB=OFF \
    -DVERSION=$pkgver \
    ../deepin-terminal-old-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
