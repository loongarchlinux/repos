# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=13.0.5
pkgrel=1
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=(any)
url="https://apps.nextcloud.com/apps/spreed"
license=(AGPL3)
groups=(nextcloud-apps)
makedepends=(nextcloud)
checkdepends=(ripgrep yq)
source=(https://github.com/nextcloud-releases/$_name/releases/download/v$pkgver/$_name-v$pkgver.tar.gz)
sha512sums=('1b9e856b38e5e44d355baa5404e3ef56a0eeb40e31ead83d1624592d17331fbc57fbe06d80a4beb1456366cfd13c8b3edf6865ad1bed7e822f1a751d619715b9')
b2sums=('90bd1fd9df91692831a6b8f1db34d44e554eb6da4f4f68cee1f55de2d92b66433290ab780b86c73a855629b55d6019ee87576b78b3797273c963df3ab92e0bb9')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

_get_php_versions() {
  local _phps=(php7 php)

  _app_min_php="$(xq '.info.dependencies.php["@min-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _app_max_php="$(xq '.info.dependencies.php["@max-version"]' "$_name/appinfo/info.xml"| sed 's/"//g')"
  _system_php=""
  for _php in "${_phps[@]}"; do
    if command -v "$_php" > /dev/null; then
      if [[ -z "$_system_php" ]]; then
        _system_php="$_php"
      fi
    fi
  done
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  _get_php_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "$_nextcloud_major_version" "$_app_min_major_version")"<0)) || (("$(vercmp "$_nextcloud_major_version" "$_app_max_major_version")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "$_app_min_major_version" "$_app_max_major_version" "$_nextcloud_major_version"
    exit 1
  fi

  local _php_version="$($_system_php --version |head -n1 |cut -d ' ' -f2 |sed 's/.[0-9]*$//g')"
  if [[ "$(vercmp "$_php_version" "$_app_min_php" )" -lt 0 ]]; then
    printf "%s requires php >= %s, but %s is provided\n" $pkgname $_app_min_php $_php_version
    exit 1
  fi
  if [[ $_app_max_php != 'null' ]]; then
    if [[ "$(vercmp "$_php_version" "$_app_min_php" )" -lt 0 ]]; then
      printf "%s requires php <= %s, but %s is provided\n" $pkgname $_app_min_php $_php_version
      exit 1
    fi
  fi
}

package() {
  _get_nextcloud_versions
  _get_php_versions

  depends=(
    "nextcloud>=$_app_min_major_version"
    "nextcloud<$_app_max_major_version"
  )
  if [[ "$_app_min_php" != 'null' ]]; then
    depends+=(
      "php>=$_app_min_php"
    )
  fi
  if [[ "$_app_max_php" != 'null' ]]; then
    depends+=(
      "php<=$_app_max_php"
    )
  fi

  install -vdm 755 "$pkgdir/usr/share/webapps/nextcloud/apps/"
  cp -av $_name "$pkgdir/usr/share/webapps/nextcloud/apps/"
}
