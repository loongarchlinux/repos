# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=php-imagick
_name=imagick
pkgver=3.4.4
pkgrel=7
pkgdesc="PHP extension to create and modify images using the ImageMagick library"
arch=('loongarch64' 'x86_64')
url="https://github.com/mkoppanen/imagick"
license=('PHP')
depends=('php' 'imagemagick' 'ttf-font')
checkdepends=('librsvg')
backup=("etc/php/conf.d/${_name}.ini")
source=("$pkgname-$pkgver.tar.gz::https://github.com/mkoppanen/${_name}/archive/${pkgver}.tar.gz"
        "${pkgname}-3.4.4-imagemagick_threading.patch::https://github.com/Imagick/imagick/pull/296.patch")
sha512sums=('f3d3c74b4d0bb5c2dd986a8b960096ff200daa82e60fdd1467a54944be06810923b4e68a4f70194e25c8176afd9a609b9f2545054520ec759202e5fc3f1e827b'
            'd11a08b6a6a4a5e6d9b9cf9e87a6c0bb29ba632d6318ac237fe59910d70b07ef8df5af775451c89c5a81d45e609b9aa69611ecb562bfcbda832d5f0ae1207d55')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # fix imagemagick threading issues when building against php >= 7.4
  patch -Np1 -i "../${pkgname}-3.4.4-imagemagick_threading.patch"
  # setting package version: https://bugs.archlinux.org/task/64185
  sed -e "s/@PACKAGE_VERSION@/${pkgver}/" \
      -i php_imagick.h package.xml
  echo ";extension=${_name}" > "${_name}.ini"
  phpize
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  export NO_INTERACTION="true"
  make -k test
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php/conf.d/"
  install -vDm 644 {ChangeLog,CREDITS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 examples/*.php \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
