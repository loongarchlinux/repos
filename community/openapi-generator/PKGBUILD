# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=5.4.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache)
depends=(bash java-runtime=8)
makedepends=(maven 'java-environment=8' strip-nondeterminism)
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('88dd9d5f2350e0ac044379cc96fb04f0dc58924d35fbe5d6285f4cdfa17bb7ade189c97c0564c036f9502d304f26777f69a548d395a6f068b3accd77963d29b4'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('9f15d3ee7b4e4e59fada9f0839b3906d418056fd5ffd006b510e916113b8db1b1313ebed7641e542c79b8e4dd87c7f2e86cd915710a840e4d9e4a9c9028e312d'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd "$pkgname-$pkgver"
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644  modules/openapi-generator-cli/target/openapi-generator-cli.jar -t "${pkgdir}/usr/share/java/${pkgname}/"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -svf "${pkgname}" "${pkgdir}/usr/bin/${pkgname}-cli"
  install -vDm 644 scripts/openapi-generator-cli-completion.bash "${pkgdir}/usr/share/bash/bash-completion/completions/openapi-generator"
}
