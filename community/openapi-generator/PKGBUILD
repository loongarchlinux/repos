# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=6.0.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache)
depends=(bash java-runtime=8)
makedepends=(maven 'java-environment=8' strip-nondeterminism)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/$pkgname/archive/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('747343847d04a29c28a9bd3ccab5aa66ebcfd8925998821b3c8f5d8fb5327aefd642ead49f47fe0925a193a92f89bf23f18b1eedbcc0c1a71c3756e4e3a42f2d'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('91af591ee3abf6914f11b0f9d1076b7520cfc5412ed0e85dc3053885065d2aefa0d94a701af98bed669a376845801b020f38bcea86ff069f114de92d295d8437'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd $pkgname-$pkgver
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  install -vDm 644 $pkgname-$pkgver/modules/openapi-generator-cli/target/openapi-generator-cli.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/scripts/openapi-generator-cli-completion.bash "$pkgdir/usr/share/bash/bash-completion/completions/openapi-generator"
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf "$pkgname" "$pkgdir/usr/bin/$pkgname-cli"
}
