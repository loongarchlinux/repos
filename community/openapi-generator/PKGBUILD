# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=4.3.1
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=('any')
url="https://github.com/openapitools/openapi-generator/"
license=('Apache')
depends=('bash' 'java-runtime=8')
makedepends=('maven' 'java-environment=8' 'strip-nondeterminism')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('7437a189c11f07b1b83ba232434cc45544641c65bd2cc320f94b5eff563548f934012aab2f4e2f1378ed3609cff2b9989faceefba8a9e97756f4a495e2fc9bb8'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')

build() {
  cd "$pkgname-$pkgver"
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" \
    modules/openapi-generator/target/openapi-generator-${pkgver}.jar \
    modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    modules/openapi-generator-core/target/openapi-generator-core-${pkgver}.jar \
    modules/openapi-generator-maven-plugin/target/openapi-generator-maven-plugin-${pkgver}.jar \
    modules/openapi-generator-online/target/openapi-generator-online.jar

}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644  modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    -t "${pkgdir}/usr/share/java/${pkgname}/"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -svf "${pkgname}" "${pkgdir}/usr/bin/${pkgname}-cli"
  install -vDm 644 scripts/openapi-generator-cli-completion.bash \
    "${pkgdir}/usr/share/bash/bash-completion/completions/openapi-generator"
}
