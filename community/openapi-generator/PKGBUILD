# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=6.2.1
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=(any)
url="https://github.com/openapitools/openapi-generator/"
license=(Apache)
depends=(bash java-runtime=8)
makedepends=(maven 'java-environment=8' strip-nondeterminism)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/$pkgname/archive/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('457ffade7f188f829e7dc72fa0f9b7be68b9619b048ac2d912151cf80ed94f2f3c205fb4b0549b546fba40a835874c7e67d335a5c7ec3c75ada969f50518cc6f'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('dfce8985cd4bf0d7120dee50cf84ec9bd6f3fab35f8741d0faa680f62e0b1dd80e2dc937234c0105813a79708c7053f3dd422cbb2dd24c0d4df4c4155564569a'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd $pkgname-$pkgver
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  install -vDm 644 $pkgname-$pkgver/modules/openapi-generator-cli/target/openapi-generator-cli.jar -t "$pkgdir/usr/share/java/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/scripts/openapi-generator-cli-completion.bash "$pkgdir/usr/share/bash/bash-completion/completions/openapi-generator"
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  ln -svf "$pkgname" "$pkgdir/usr/bin/$pkgname-cli"
}
