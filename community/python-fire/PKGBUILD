# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fire
pkgname=python-fire
pkgver=0.4.0
pkgrel=3
pkgdesc="A library for automatically generating command line interfaces"
arch=('any')
url="https://github.com/google/python-fire"
license=('Apache')
depends=('python-six' 'python-termcolor')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-levenshtein' 'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('c5e2b8763699d1142393a46d0e3e790c5eb2f0706082df8f647878842c216a62')
b2sums=('954de570e593b262468c6f9dabd25c2056a19c50037b91392a3305d455b0bc224a09b31071b86fbc490be5df5b42715412d62c624e3e3efe8d21e6db6c6c67f8')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
