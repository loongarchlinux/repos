# Maintainer: David Runge <dvzrv@archlinux.org>

_name=toolz
pkgname=python-toolz
pkgver=0.11.2
pkgrel=3
pkgdesc="A functional standard library for Python"
arch=('any')
url="https://github.com/pytoolz/toolz/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('67e8aaea3f9fb8811027f19aa9cf56ab980e4f9cea43978ed9515662c78a7bea12314054b2c26da483b055382d4b080125c011fc43bc48b0965a6ff8415f7168')
b2sums=('3a2baddae437272e8f3efb665b63df127b3ec6bc2a68f3381651a1a3a3b45e4de6e575a328306fac95e9261228ee91193effd4aeca8d115f1608cb2561d50f13')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
