# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-guessit
pkgver=3.4.3
pkgrel=2
pkgdesc='A Python module that extracts as much information as possible from a video filename'
arch=('any')
url='https://github.com/guessit-io/guessit'
license=('LGPL3')
depends=(
  'python'
  'python-dateutil'
  'python-babelfish'
  'python-rebulk'
)
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pyyaml')
_commit='1f35e44b0d14b1b007c157d852995c5f940514cf'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'skip-failing-tests.patch'
)
b2sums=('SKIP'
        '332ae9a6e65ec49295a0a42c4a3cdf11011c6e2cb014d7eecc511acfddfb440c007a678e3bb1af7adb36821e90b8e555ae26e4135a7aedb8d477297bd72d8c38')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # disable failing tests
  patch -p1 -i "$srcdir/skip-failing-tests.patch"
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD:$PYTHONPATH" pytest
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  # delete tests from package
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir$site_packages/guessit/test"

}
