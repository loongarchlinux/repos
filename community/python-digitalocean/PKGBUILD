# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-digitalocean
pkgver=1.15.0
pkgrel=1
pkgdesc='digitalocean.com API to manage Droplets and Images'
arch=('any')
license=('LGPL')
url='https://github.com/koalalorenzo/python-digitalocean'
depends=('python-requests' 'python-jsonpickle')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-responses')
source=("$pkgname-$pkgver.tar.gz::https://github.com/koalalorenzo/python-digitalocean/archive/v$pkgver.tar.gz")
sha512sums=('9965fee4be6f73809397bf40270ed6ab4f13393d83617be7aa85910c3e2ea25871eb17a8a1c9f5f6f49c49808090a92bd5cadf0e31ac2a9438f730c03c13dade')

build() {
  cd python-digitalocean-$pkgver
  python setup.py build
}

check() {
  cd python-digitalocean-$pkgver
  python setup.py pytest || warning "https://github.com/koalalorenzo/python-digitalocean/issues/258"
}

package() {
  cd python-digitalocean-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
