# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-py3c
pkgver=1.2
pkgrel=1
pkgdesc="A Python 2/3 compatibility layer for C extensions"
url="https://github.com/encukou/py3c"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/encukou/py3c/archive/v$pkgver.tar.gz")
sha512sums=('69896ea93589a98d061299cab1bd1e4681a2550a9fd4a7aa23f85740bdb3b85e3fe4fa8ee12a5657ebe57f3fe6decd9df506fa67add018fab466ad4c88dd29b6')

prepare() {
  cd py3c-$pkgver
  # Don't regenerate with broken path in "install" target
  sed -i 's/install: py3c.pc/install:/' Makefile
}

build() {
  cd py3c-$pkgver
  python setup.py build
  make prefix=/usr py3c.pc
}

check() {
  cd py3c-$pkgver
  make test-python
}

package() {
  cd py3c-$pkgver
  make prefix="$pkgdir"/usr install
  python setup.py install --root="$pkgdir" --optimize=1

  # Remove headers in broken location
  rm -r "$pkgdir"/usr/include/python*

  install -Dm644 LICENSE.MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
