# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-material-color
pkgver=0.0.0.20200729
_commit=321f214bcb6efa61cd45d8c3e07a1d0c50f13c8e
pkgrel=1
pkgdesc="Material color theme for fcitx5"
arch=('any')
url="https://github.com/hosxy/Fcitx5-Material-Color"
license=('GPL')
source=("https://github.com/hosxy/Fcitx5-Material-Color/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('a155b147f2cdd5e33800d1b443443f8c6a0e776710444c0fde1e4b3c62ff2284f253f2a84622d56bf2cd86ee8d772bcee6525d2b8b6cab5c87c9468db817208a')

package() {
  cd Fcitx5-Material-Color-$_commit
  install -Dm644 arrow.png radio.png -t "$pkgdir"/usr/share/$pkgname/
  for _variant in blue brown deepPurple indigo pink red teal; do
    _variant_name=Material-Color-${_variant^}
    install -Dm644 panel-$_variant.png -t "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/arrow.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/radio.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    install -Dm644 theme-$_variant.conf "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
    sed -i "s/^Name=.*/Name=$_variant_name/" "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
  done
}
