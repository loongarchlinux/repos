# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackdocstheme
pkgver=3.0.1
pkgrel=1
pkgdesc="Open Infrastructure Projects Sphinx Themes"
arch=('any')
url="https://opendev.org/openstack/openstackdocstheme"
license=('Apache')
depends=('python-pbr' 'python-dulwich' 'python-sphinx')
makedepends=('python-setuptools')
options=('!emptydirs')
source=("https://github.com/openstack/openstackdocstheme/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fb73e0cf334e977babe4c3faad387d11c9f3ba290af86b7e7e1c81d6025e95ac73180dc1da7833d35602d2324d7e693f69904bd99b9dff3605e25b280476e056')

export PBR_VERSION=$pkgver

build() {
  cd openstackdocstheme-$pkgver
  python setup.py build
  PYTHONPATH="$PWD/build/lib" sphinx-build -W -b man doc/source doc/build/man
}

package() {
  cd openstackdocstheme-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 doc/build/man/*.1 -t "$pkgdir"/usr/share/man/man1/
}
