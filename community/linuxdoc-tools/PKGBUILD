# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Ell <david at bytesizebits dot net>

pkgname=linuxdoc-tools
pkgver=0.9.78
pkgrel=1
pkgdesc="Convert LinuxDoc SGML source into other formats"
arch=('x86_64')
url="https://packages.debian.org/sid/linuxdoc-tools"
license=('GPL')
depends=('jade')
makedepends=('texlive-bin' 'texlive-core')
optdepends=('groff' 'texlive-bin')
source=(https://ftp.de.debian.org/debian/pool/main/l/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('362f031aa13ce29018a395179e7d6d9fac490b1b0e5a2ce97a5ab4dfe7effa56')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/extern int yyleng;/d' rtf-fix/rtf2rtf.l
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-texdir=/usr/share/texmf-dist/tex/linuxdoc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
