# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: loqs
# Contributor: Paulo Matos <paulo@matos-sorge.com>
# Contributor: b4283 <unknown@unknowndomain.com>

pkgname=spike
pkgver=1.1.0
pkgrel=1
pkgdesc='Spike, a RISC-V ISA Simulator'
arch=(loongarch64 x86_64)
url='https://github.com/riscv/riscv-isa-sim'
license=(BSD)
groups=(risc-v)
depends=(dtc)
replaces=(fesvr)
conflicts=(fesvr)
makedepends=(git)
source=("git+https://github.com/riscv/riscv-isa-sim#commit=530af85d83781a3dae31a4ace84a573ec255fefa") # tag: v1.1.0
b2sums=(SKIP)

build() {
  cd riscv-isa-sim
  ./configure --prefix=/usr
  make LDFLAGS="$LDFLAGS -s -w"
}

package() {
  cd riscv-isa-sim
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
