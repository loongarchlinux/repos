# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pypandoc
pkgver=1.6.4
pkgrel=3
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('pandoc')
makedepends=('python-setuptools')
checkdepends=('texlive-core' 'texlive-latexextra')
source=("https://github.com/bebraw/pypandoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f01a999040f48b858452d7de29fd0c44f7c247ff088fddfcd84ea47826327ce4572119e23a26bd929b6ff282d67974e23a61cdb5e4956ffd35af36cb991c199a')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd pypandoc-$pkgver
  python setup.py build
}

check() {
  cd pypandoc-$pkgver
  python setup.py test
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
