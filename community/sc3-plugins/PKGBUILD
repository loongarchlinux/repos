# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sc3-plugins
pkgver=3.11.0
pkgrel=1
pkgdesc="Extension plugins for the SuperCollider3 audio synthesis server."
arch=('x86_64')
url="https://github.com/supercollider/sc3-plugins"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'supercollider')
makedepends=('cmake' 'fftw' 'stk')
source=("https://github.com/supercollider/${pkgname}/releases/download/Version-${pkgver}/${pkgname}-${pkgver}-Source.tar.bz2"{,.asc})
sha512sums=('f90ee3da189d66df76176ab4a4c7fd722f022119bcc381daaaf4322f6874d84f274977ee8d63b877aba5b89c32177e471ecd971250680a7595f87cef48452f49'
            'SKIP')
b2sums=('ece0c9d88042114fef7e67dcab6baa0ef2345146e9865f78e7d9aab2b3006a0e4075a7c47652b474dae672ffea2c19e842ea2c7d91579d0b927599d3dd09bad4'
        'SKIP')
validpgpkeys=('0B7D1EA4E32B5C3C4275D3C70796CC538BEFF177') # Brian Heim <self@brianlheim.com>

prepare() {
  mv -v "$pkgname-$pkgver-Source" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DSC_PATH=/usr/include/SuperCollider/ \
        -DSYSTEM_STK=ON \
        -DSUPERNOVA=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libfftw3f.so' 'libstk-4.6.1.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {{DEVELOPING,README}.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
