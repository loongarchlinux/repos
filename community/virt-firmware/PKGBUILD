# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=1.7
pkgrel=1
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL2)
# NOTE: setuptools is an undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/3
# NOTE: crc32c is an undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
depends=(python python-crc32c python-cryptography python-pefile python-setuptools)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest)
# no tests in sdist tarball on pypi.org: https://gitlab.com/kraxel/virt-firmware/-/issues/2
# source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('362b1f002ee8823b3124eef3ce94855a8a953c7251d7117fffb472e2e4c18ed513254c65cd29b87702e87b963439df489484ad068fc150c0b47257a28dd47d98')
b2sums=('af8821ee3bc55ec824ed6345efc5fd982b8c68c7fa7c463fd110c00066b7ade51a9bc0191ab4de0b38ea46c19e28dd9ffbf493586b903b9dc1030aa862025e16')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
