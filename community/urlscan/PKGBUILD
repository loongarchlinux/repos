# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=urlscan
pkgver=0.9.6
pkgrel=1
pkgdesc="Mutt and terminal url selector"
url="https://github.com/firecat53/urlscan"
arch=('any')
license=('GPL2')
depends=('python-urwid')
makedepends=('python-setuptools')
optdepends=(
    'xclip: Clipboard support for X11 environments'
    'wl-clipboard: Clipboard support for Wayland'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/firecat53/urlscan/archive/$pkgver.tar.gz")
sha256sums=('e8e406e3f87060f8765aab5a4aaecb3602a0ebe7c0936c22e1ecfc65589420e9')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
