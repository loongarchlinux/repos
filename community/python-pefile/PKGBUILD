# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Contributor: Sonic-Y3k <sonic.y3k@googlemail.com>

pkgname=python-pefile
pkgver=2021.5.24
pkgrel=1
pkgdesc='Python PE parsing module'
url='https://github.com/erocarrera/pefile'
arch=('any')
license=('MIT')
depends=('python' 'python-future')
source=(https://github.com/erocarrera/pefile/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('86e94f339e195216d99ba48f904d196f7f1ef91cbf748adbb785e5a0f2c78ea804949abc2f1ada60aa76fb263abbaf394a23e5b9a354c01eeb0637ea90c3c04b')
b2sums=('00c1a639f0dc948dcc6e72d76a1776276c1060eaf3fdf950f3f77bf30735b12559b4012e49debc66abed5113e13794935df4e843424ad2dcd643d8170822fdb4')

build() {
  cd pefile-${pkgver}
  python setup.py build
}

package_python-pefile() {
  cd pefile-${pkgver}
  python setup.py install --root "${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
