# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-latex2mathml
pkgver=3.62.0
pkgrel=1
pkgdesc="Pure Python library for LaTeX to MathML conversion"
url="https://github.com/roniemartinez/latex2mathml"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-multidict' 'python-xmljson')
source=("https://github.com/roniemartinez/latex2mathml/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0493f2b57b139430fb5551d47f5b89db62f0b371e09f778736230f1d8ac929bc4be938c691566185681bd8f9a6124991038bf2bde0f83889dced46dddbc9f758')

build() {
  cd latex2mathml-$pkgver
  python setup.py build
}

check() {
  cd latex2mathml-$pkgver
  pytest
}

package() {
  cd latex2mathml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
