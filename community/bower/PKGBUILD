# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker@gmail.com> 
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Tom Vincent <http://tlvince.com/contact>

pkgname=bower
pkgver=1.8.8
pkgrel=2
pkgdesc='A package manager for the web'
arch=('any')
url='https://bower.io/'
license=('MIT')
depends=('nodejs' 'semver' 'uglify-js')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('d52ac99d79e43fdb282131e9b523be6a1c7740aa77715ce7f29a08eae8ea73949e3a48398aa335a4af47f8349cd8e43c4a73b48c2fcd1b852bfd4230cbbe7be0'
            'a932f56639679947f1ce67ca7c27855ae08257876880864079077db825fa402ac7d544a192e02683d38de60ed9ac708a366e05c81bd350ace1440c1588086bc1')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules
  for dep in semver uglify-js; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/package-json/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/semver-diff/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}
