# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-log
pkgname=python-click-log
pkgver=0.4.0
pkgrel=1
pkgdesc="Logging integration for Click"
arch=(any)
url="https://github.com/click-contrib/click-log"
license=(MIT)
depends=(python-click)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('95d3cb2fa9821e4f58b25072f39d34f21fe4a7600923f59f4b57b92dbcd9fa2584d710de1d4a1aa2a757c048398342aabbb1b5ab32cf4128ccecee10f9f15a5a')
b2sums=('8a7ce0c2a7617253f08d011958bce158e987faa0008065c9dac382d46b905cc88cd5cb19d0d0859bf522da6448f7c9a20c0a8efe53b3feb5efc9ca3f64cfbc27')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python setup.py install --optimize=1 --root="$pkgdir"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
