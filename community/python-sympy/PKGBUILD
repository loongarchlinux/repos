# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgname=python-sympy
pkgver=1.9
pkgrel=3
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='https://sympy.org/en/index.html'
license=('BSD')
depends=('python-mpmath')
optdepends=('ipython: user friendly interface for isympy'
            'python-symengine: optimized backend, set USE_SYMENGINE=1 to use')
makedepends=('python-setuptools')
source=("https://github.com/sympy/sympy/archive/sympy-$pkgver/sympy-$pkgver.tar.gz"
         sympy-python-3.10.patch)
sha512sums=('139712219ba6bf420ba89b3f8d086ab8883c5ab965e9308ba9ee665ac3ac69a9b16fa30c6f5dfbc77dd34873ff3ff95c0d058954d7f9d853f5e9537efc24dd3c'
            'fbda6f7d293337f08909684179efd9497fea7093dfedf2c0bf2fc67ee1168ce50268f6580091b3e1db78eef59e19c4780e3537b077d156ea7b4680191ad43468')

prepare() {
  patch -d sympy-sympy-$pkgver -p1 < sympy-python-3.10.patch # Fix tests with python 3.10 (Fedora)
}

build() {
  cd sympy-sympy-$pkgver
  python setup.py build
}

check() {
  cd sympy-sympy-$pkgver
  python bin/test
}

package() {
  cd sympy-sympy-$pkgver

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
