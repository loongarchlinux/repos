# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_gemname=yard
pkgname=ruby-${_gemname}
pkgver=0.9.26
pkgrel=2
pkgdesc='YARD is a Ruby Documentation tool. The Y stands for "Yay!"'
url='https://yardoc.org/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
makedepends=('ruby-rake' 'ruby-samus')
options=('!emptydirs')
source=(https://github.com/lsegal/yard/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0f70a089d5e5ba33e326b0ae0cc84a2cacfc5a3f3080545d5fc5bb324dedbbbf65116cc9412b3496c09b2b6c15514e854a741a1fd7e2ae782804d6e4ac28fcd9')
b2sums=('19baaefc54269bbc7eee053b42f2be85644a062cbf55b0fafb63748876aad3a3143767855f4ee7b10a1ae814110a6cf63c01e4055e054aeb31bf8512e793d80d')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  rake gem
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
