# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=python-boto3
pkgver=1.20.24
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
makedepends=('python-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('96a2d6dabaca0aa0d85efcbc2f98f8e41b568e2190352c00101c3df85623867036f64ad9935681ad2a104abd9f6aa9e79de8f7136a846e256185d065afed23a1')

build() {
  cd boto3-$pkgver
  python setup.py build
}

check() {
  cd boto3-$pkgver
  # scripts/ci/run-tests
}

package() {
  cd boto3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
