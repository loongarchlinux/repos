# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-py7zr
_pyname=${pkgname#python-}
pkgver=0.19.0
pkgrel=1
pkgdesc='Pure python 7-zip library'
arch=(any)
url="https://github.com/miurahr/${_pyname}"
license=(LGPL)
_pydeps=(brotli
         brotlicffi
         psutil
         multivolumefile
         pybcj
         pycryptodomex
         pyppmd
         pyzstd
         texttable
         inflate64)
depends=(python
         "${_pydeps[@]/#/python-}")
optdepends=('python-zstandard: zstd compression')
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
checkdepends=(p7zip
              pyannotate
              python-py-cpuinfo
              python-pytest-benchmark
              python-pytest-timeout)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('cca9179c72430cbf8575ea22686344c351a14bf335c12c90e5db3628d93281a1')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m pytest -rsx -m 'not benchmark and not remote_data'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
