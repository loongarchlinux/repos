# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-py7zr
_pyname=${pkgname#python-}
pkgver=0.18.5
pkgrel=1
pkgdesc='Pure python 7-zip library'
arch=(any)
url="https://github.com/miurahr/${_pyname}"
license=(LGPL)
_pydeps=(brotlicffi
         psutil
         multivolumefile
         pycryptodomex
         pyppmd
         pyzstd
         texttable
         zipfile-deflate64)
depends=(python
         "${_pydeps[@]/#/python-}")
optdepends=('python-zstandard: zstd compression')
makedepends=(python-{build,installer}
             python-setuptools-scm
             python-wheel)
checkdepends=(python-pytest-benchmark
              python-pytest-timeout
              pyannotate
              p7zip
              python-py-cpuinfo)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('81ebb10619739764c2d80ef222e5845373ce84523183371ae9cd88e0df908929')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m pytest -rsx -m 'not benchmark and not remote_data'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
