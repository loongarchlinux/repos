# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net

_pipname=debugpy
pkgname=python-$_pipname
pkgver=1.6.2
pkgrel=1
pkgdesc='An implementation of the Debug Adapter Protocol for Python'
arch=(loong64 x86_64)
url='https://aka.ms/debugpy'
license=(EPL MIT)
depends=(python)
makedepends=(python-setuptools)
source=(https://github.com/microsoft/debugpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('61451ed568da9b8f381d89ea93a5bbec212e593cdd682d37f0ec46973678b969'
            'a86e1e1a5a4d7a585c8185721863f563233b680c36fa35d65bfbff11abd519bc')

prepare() {
  cd $_pipname-$pkgver
# remove precompiled libs
  rm src/debugpy/_vendored/pydevd/pydevd_attach_to_process/*.{so,dll,dylib,exe,pdb}

  patch -p1 -i ../fix-build.patch
}

build() {
  cd $_pipname-$pkgver
  python setup.py build
# Compile attach libraries
  cd build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process
  g++ ${CXXFLAGS} -m64 -shared -o attach_linux_amd64.so -fPIC -nostartfiles linux_and_mac/attach.cpp ${LDFLAGS}
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
