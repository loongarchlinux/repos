# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=python-dateparser
pkgver=0.7.6
pkgrel=1
pkgdesc='Python parser for human readable dates'
url=https://github.com/scrapinghub/dateparser
arch=(any)
license=(BSD)
depends=(
  python-dateutil
  python-regex
  python-tzlocal
  python-ruamel-yaml
)
makedepends=(
  git
  python-setuptools
)
_tag=89b0091759f3a9d1dde0e6146bd1f431eb06941d
source=(git+https://github.com/scrapinghub/dateparser.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd dateparser

  git describe --tags | sed 's/^v//'
}

build() {
  cd dateparser

  python setup.py build
}

package() {
  cd dateparser

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-dateparser/
}

# vim: ts=2 sw=2 et:
