# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=python-dateparser
pkgver=1.1.1
pkgrel=1
pkgdesc='Python parser for human readable dates'
url=https://github.com/scrapinghub/dateparser
arch=(any)
license=(BSD)
depends=(
  python-dateutil
  python-pytz
  python-regex
  python-tzlocal
  python-ruamel-yaml
)
makedepends=(
  git
  python-setuptools
)
_tag=89b7fda98c6093648a15db5511e48503ea6a43b7
source=(git+https://github.com/scrapinghub/dateparser.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd dateparser

  git describe --tags | sed 's/^v//'
}

build() {
  cd dateparser

  python setup.py build
}

package() {
  cd dateparser

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-dateparser/
}

# vim: ts=2 sw=2 et:
