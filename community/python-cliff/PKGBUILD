# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cliff
pkgver=4.0.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="https://docs.openstack.org/cliff/latest/"
license=('Apache')
depends=('python-six' 'python-cmd2' 'python-prettytable' 'python-importlib-metadata'
         'python-stevedore' 'python-yaml' 'python-autopage')
makedepends=('git')
checkdepends=('python-stestr' 'python-mock' 'python-sphinx' 'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('a9eb314ad5d8220389618f3052deadfa6eb7443fec965eed71da57fe67a840e2080f01bcc6116563339bac4c44178c4702f180848a268ee85176490c33b17f25')

export PBR_VERSION=$pkgver

build() {
  cd cliff-$pkgver
  python setup.py build
}

check() {
  cd cliff-$pkgver
  stestr run
}

package() {
  cd cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
