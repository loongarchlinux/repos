# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-freezegun
pkgver=1.2.1
pkgrel=1
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
depends=('python-dateutil')
makedepends=('python-setuptools' 'python-dateutil')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/spulec/freezegun/archive/$pkgver.tar.gz")
sha512sums=('18342fe2f2dcebcd16e909a5d10e2fdad7d43831d395883de9c0773bfff8b902616ef95516c6876176bdc3ae868fd07d9c2085b27d65b7ea9c9033bcf256c0f6')

build() {
  cd "$srcdir"/freezegun-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/freezegun-$pkgver
  # https://github.com/spulec/freezegun/issues/250
  pytest || echo "Tests failed"
}

package() {
  cd freezegun-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
