# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=7.9
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(loongarch64 x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primesieve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c567f2a1a9d46a70020f920eb2c794142528a31a055995500e7fcb19642b7c91')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
