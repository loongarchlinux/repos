# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: solsTiCe d'Hiver <solstice.dhiver@gmail.com>

pkgname=lesspipe
pkgver=1.91
pkgrel=3
pkgdesc='an input filter for the pager less'
depends=('less' 'perl')
arch=('any')
license=('GPL')
optdepends=('rpmextract: support for rpm files'
	    'fastjar: support for jar files'
	    'unzip: support for zip files'
	    'unrar: support for rar files'
	    'p7zip: support for 7za files'
	    'cabextract: support for cab files'
	    'cdrtools: support for iso files'
	    'html2text: support for html files'
	    'ghostscript: support for ps files'
	    'djvulibre: support for DjVu files'
	    'odt2txt: support for OpenDocument files'
	    'antiword: support for word file'
	    'catdoc: support for word file'
	    'pandoc: support for word file'
	    'libreoffice-fresh: support for MS Office files'
	    'unrtf: support for rtf file'
	    'mediainfo: support for mp4 file'
	    'imagemagick: support for some image file')
url="https://www-zeuthen.desy.de/~friebel/unix/lesspipe.html"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/wofr06/lesspipe/archive/refs/tags/${pkgver}.tar.gz"
        lesspipe.sh
        'html2text.patch')
sha256sums=('6192e7e451c5db26841c6dc3cbcc23c1ef396c1cc4588288effa699dbc665cdb'
            '522a4893b640ed95e39fd4ca83a5d5ec79207542fec03710fa5e73fe201ed057'
            'a095ccc303fd4da8cdc2d586f478f48c66f8a86743685d94da332f22c1eadfe6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/html2text.patch" -d "${srcdir}/${pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --yes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}"/usr/share/man/man1
  make install PREFIX="${pkgdir}"/usr
  install -D "${srcdir}"/lesspipe.sh "${pkgdir}"/etc/profile.d/lesspipe.sh
}
