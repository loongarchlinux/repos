# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Konstantin Gizdov (kgizdov) <arch@kge.pw>

pkgname=python-tensorflow-estimator
pkgver=2.9.0
pkgrel=1
pkgdesc="A high-level TensorFlow API that greatly simplifies machine learning programming"
url="https://github.com/tensorflow/estimator"
license=('APACHE')
arch=('any')
depends=('python-wrapt')
makedepends=('bazel' 'python-wheel' 'python-setuptools' 'python-pip' 'python-tensorflow')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tensorflow/estimator/archive/v${pkgver}.tar.gz")
sha512sums=('277e398104ac92a48e481e0ee4d3bbc607ae79d0a03c6685723a2430117461c1f17090f56ab88b2ae1c86c8dfc6ec1b44a478a1b606b4432bd68d79ac0720475')

build() {
  cd estimator-${pkgver}

  bazel build //tensorflow_estimator/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_estimator/tools/pip_package/build_pip_package ${srcdir}/estimator_pip
}

check() {
  cd estimator-${pkgver}

  # Of course, upstream doesn't care about working tests...
  # Revisit this at some point.
  # bazel test //tensorflow_estimator/...
}

package() {
  cd estimator-${pkgver}

  WHEEL_PACKAGE=$(find "$srcdir"/estimator_pip -name "tensor*.whl")
  pip install --ignore-installed --upgrade --root "$pkgdir"/ $WHEEL_PACKAGE --no-dependencies
}
