# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-aaf2
pkgver=1.5.0
pkgrel=1
pkgdesc='Read and write Advanced Authoring Format (AAF) files'
url='http://pyaaf.readthedocs.io/'
license=(MIT)
arch=(any)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://pypi.python.org/packages/source/p/pyaaf2/pyaaf2-$pkgver.tar.gz)
sha256sums=('d90c4ffff506df207b8daad2c757698a8538e4a55a0ca5774bad35f24a2b962b')

build() {
  cd pyaaf2-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd pyaaf2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
