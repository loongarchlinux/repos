# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.9.2
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=('any')
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('2f3696d3a4b1ff1a203ae4f73ec42e9862bfcb709267f3d5788c4bdbccbd8f56070a2e51948557344ac23fd0fb157457a80b898b4573676282ac7a6a061a32be')
b2sums=('777f215d020c6ebb344a40de86e1770d1efefa92409bd65ed2db9735ad70f855395f6f3c3be9d139949dcee90d6bc16ea5e8e7ae09af8d45279e8c69b45351b1')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
