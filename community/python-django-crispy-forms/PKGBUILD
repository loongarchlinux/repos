# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.14.0
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=(any)
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=(MIT)
depends=(python-django)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-django)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('02c5f7e6241b57a844fa8b4a5afbc1b77d3c1fd2eae4209763fb826a3da90db4ed0e66de4b7b1c04d5caf52bc101c7979c27b9109288d0ac84706d2124ae179d')
b2sums=('baa605c12bb40291ff4f36b4af63f07f7492a88dfb31da29b7500b19bdec0943adec76ace50ac8b29cbf5c6570619eba3eed596c7d3296922ef592acbc6a2efc')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
