# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-run
pkgver=8.8.2
pkgrel=1
pkgdesc="Install packages and run Python with them"
url="https://github.com/jaraco/pip-run"
license=('MIT')
arch=('any')
depends=('python-autocommand' 'python-packaging' 'python-path' 'python-pip')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'jupyter-nbformat' 'python-pytest-enabler')
source=("https://github.com/jaraco/pip-run/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0250f55b49bb3eec10c78b217285df522bc00475dd9bdc710c107a8569e66b2ae643137da59a07c0f6b5e1254a9a920de3212f6befff245cd797947d3fdf95dc')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pip-run-$pkgver
  python -m build -wn
}

check() {
  cd pip-run-$pkgver
  python -m pytest
}

package() {
  cd pip-run-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
