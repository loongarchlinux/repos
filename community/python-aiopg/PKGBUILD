# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=aiopg
pkgname=python-$_pkgname
pkgver=1.3.4
pkgrel=1
pkgdesc='library for accessing a PostgreSQL database from the asyncio'
arch=('any')
url='https://github.com/aio-libs/aiopg'
license=('BSD')
depends=('python' 'python-psycopg2' 'python-async-timeout')
makedepends=('python-build' 'python-installer' 'python-wheel'
             'python-setuptools' 'python-setuptools-scm' 'python-setuptools-scm-git-archive')
optdepends=('python-sqlalchemy: SQLAlchemy support')
#checkdepends=('python-pytest-runner' 'python-sqlalchemy' 'python-docker')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('1de1172fd207345cd21fe648417abea8e891124b75dd955b7dd92a76932420f148acff87df12d464b99c45fa4a4e461963e9cae3a84c0d447aa69d0bbeceebf0')

# TODO: fix tests (need docker)

prepare() {
  sed -i 's|install_requires = \["psycopg2-binary|install_requires = \["psycopg2|' $_pkgname-$pkgver/setup.py
}

build() {
  cd $_pkgname-$pkgver

  python -m build
}

#check() {
#  cd $_pkgname-$pkgver
#
#  python setup.py pytest
#}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
