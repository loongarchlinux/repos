# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-return-multilevel
pkgver=0.05
pkgrel=4
pkgdesc="Return across multiple call levels"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Return-MultiLevel"
depends=('perl-data-munge')
checkdepends=('perl-test-fatal')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MA/MAUKE/Return-MultiLevel-$pkgver.tar.gz"
        Return-MultiLevel-0.05-Test-Fatal-0.016.patch)
sha512sums=('ca8d5c2067c8e47e49988363a5f5816e51f13eb07a83daccb55208654f23b9dd6a67128d38cf0e61891cb0867d42a3dc0653795907ad743a72691d5da9f8be0d'
            'f6dba2803ff05a0b14186b86d390c41ba8eb61e98e59604f4dbb935cbab1f815bf2cd3cdad9544961ee4d1f97cabf4a54644f186302f7025b7366b65ff3c8ec5')

prepare() {
  cd Return-MultiLevel-$pkgver
  # https://github.com/mauke/Return-MultiLevel/pull/1
  patch -Np1 -i ../Return-MultiLevel-0.05-Test-Fatal-0.016.patch
}

build() {
  cd Return-MultiLevel-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Return-MultiLevel-$pkgver
  make test
}

package() {
  cd Return-MultiLevel-$pkgver
  make DESTDIR="$pkgdir" install
}
