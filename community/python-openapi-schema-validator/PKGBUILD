# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.1.5
pkgrel=3
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-isodate' 'python-jsonschema' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-flake8' 'python-rfc3339-validator'
              'python-strict-rfc3339')
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b564c83d3a5c6ce01a39b2e3920fa2b9f8263ca8dc21954c5f96f6648b3a191ba66488bafba2ef4e18ba5a717293465a86a3d76ea831de7b4755b375fe4f1ddf')

prepare() {
  cd openapi-schema-validator-$pkgver
  sed -i 's/import mock/from unittest import mock/' tests/integration/test_validators.py
}

build() {
  cd openapi-schema-validator-$pkgver
  python setup.py build
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
