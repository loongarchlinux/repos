# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=7.0.3
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("https://github.com/Simplistix/testfixtures/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9206de71820798d99cfa94f58dfabccb0e429254db46eaacfd67e13b170df84ba324811da689925f8c2f678d3f65eb688b4c473f01e638a2f7d815e94d237c2c')

build() {
  cd  testfixtures-$pkgver
  python setup.py build
}

check() {
  cd  testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
