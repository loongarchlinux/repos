# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=7.0.0
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("https://github.com/Simplistix/testfixtures/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a2f6866ac1a173b8371d401a82db1d0a2de1901a0f727222269d10ac87f660ad77365a795b170755c0744086f9041a8cc296dc5e8a81827767439ec724c4e9f9')

build() {
  cd  testfixtures-$pkgver
  python setup.py build
}

check() {
  cd  testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
