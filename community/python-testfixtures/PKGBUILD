# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=7.0.1
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("https://github.com/Simplistix/testfixtures/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('448ac744dd1818ff5bd7788b70ce2ac5b671757c0ddf380433873bc8fbfba430894bd42df83e261d9831e1c0f7368780e8cf4a49af188102fb257705d42eb694')

build() {
  cd  testfixtures-$pkgver
  python setup.py build
}

check() {
  cd  testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
