# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=6.18.3
pkgrel=3
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz"
        'tests-fix-with-python-3-10.patch::https://github.com/simplistix/testfixtures/commit/8fb2122eea0f1d0de1ccca7a3a0f5426bc6d4964.patch')
sha512sums=('1fdd3e8a9dc39be00ebb707c4febb30f4975e7c2c83d6edead14f180bb6171b8cd2625acf0f6285c1ecf5ea56ee3c1e40317a9c47fa0b01fd525303816ced69c'
            'd91b46005525cbefa2100eee25720b1229c6ca8213aaf36578635ffc82edc50aeef278d8e009d13bdb14ab791d7969b5fd39ae70fb2becce8f255edee1256792')

prepare() {
  cd "$srcdir"/testfixtures-$pkgver
  patch -p1 <"$srcdir"/tests-fix-with-python-3-10.patch
}

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
