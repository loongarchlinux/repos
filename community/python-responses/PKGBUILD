# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.16.0
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-localserver')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d414e4cf281f64a953acdaf66b132fbd5e26edc978efdf545a3f8eecc5afdd6473c33a51f6086020c86c20f4de7aa94cf676c81d88a80ce5c7b84f7a741477ad')

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
