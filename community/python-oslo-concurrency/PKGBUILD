# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-concurrency
pkgver=4.5.0
pkgrel=3
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
         'python-fasteners')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-eventlet')
source=("https://github.com/openstack/oslo.concurrency/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('17322fc21d98f0821be20015abd6d1dc1d9af48c9669bc1c3b4f22e529fa49dfb741baae7282ec9f7312040b2227d1657fea4c76089e64fb83d66c9f89d9e78a')

export PBR_VERSION=$pkgver

build() {
  cd oslo.concurrency-$pkgver
  python setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package() {
  cd oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
