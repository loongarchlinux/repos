# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

_pkgname=antlr4
pkgname=python-${_pkgname}
pkgver=4.9.2
pkgrel=1
pkgdesc="ANTLR4 runtime for Python"
arch=('any')
url="https://www.antlr.org/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/antlr/${_pkgname}/archive/${pkgver}.tar.gz"
        'fix-tests.patch')
sha256sums=('6c86ebe2f3583ac19b199e704bdff9d70379f12347f7f2f1efa38051cd9a18cf'
            '4e33e19f83a09997914e5650c5c19aa11ce2c79ad00b41c0759770cdfafd9a80')

prepare() {
  patch -Np1 -i "${srcdir}"/fix-tests.patch -d "${srcdir}/${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python setup.py test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/runtime/Python3"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}"/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
