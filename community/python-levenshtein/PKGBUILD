# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=python-levenshtein
pkgver=0.20.3
pkgrel=1
pkgdesc="Python extension for computing string edit distances and similarities"
url="https://pypi.python.org/pypi/python-Levenshtein/"
license=('GPL')
arch=('loong64' 'x86_64')
depends=('python-rapidfuzz')
makedepends=('python-scikit-build' 'cython' 'rapidfuzz-cpp')
checkdepends=('python-pytest')
#source=("https://files.pythonhosted.org/packages/source/L/Levenshtein/Levenshtein-$pkgver.tar.gz")
source=("https://github.com/maxbachmann/Levenshtein/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3adde94e7f195aaae3361296d4ec2e7694c6afb92abcde866d659a827bfdfc2ab1fb28d92332966abd7da8392590cd96415b7f593bde8375b35bbe845e9ddfe3')

build() {
  cd Levenshtein-$pkgver
  python setup.py build
}

check() {
  cd Levenshtein-$pkgver
  PYTHONPATH=`echo "$PWD"/_skbuild/linux-x86_64-*/setuptools/lib.linux-x86_64-*` \
  pytest
}

package() {
  cd Levenshtein-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
