# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: getzze <getzze at gmail dot com>
pkgname=python-tensorflow-serving-api
_name=tensorflow_serving_api
pkgver=2.9.0
pkgrel=1
pkgdesc="Serving system for machine learning models, designed for production environments"
arch=(any)
url="https://www.tensorflow.org/serving/"
license=('Apache')
depends=('python' 'python-grpcio' 'python-tensorflow' 'python-protobuf')
makedepends=('bazel' 'git' 'python-wheel' 'python-pip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tensorflow/serving/archive/${pkgver}.tar.gz")
sha256sums=('cf741c6b25cc92ef71b7b7ff95930d75b62635bb444e5a1b74779461d961cd80')

build() {
  cd serving-*
  bazel build -c opt tensorflow_serving/tools/pip_package:build_pip_package
  sed -i "s|bazel-genfiles/|bazel-out/k8-opt/bin/|g" tensorflow_serving/tools/pip_package/build_pip_package.sh
  bazel-bin/tensorflow_serving/tools/pip_package/build_pip_package /tmp/pip
}

package() {
  cd serving-*
  pip install --ignore-installed --upgrade --root "${pkgdir}"/ /tmp/pip/tensorflow_serving*.whl --no-dependencies
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
