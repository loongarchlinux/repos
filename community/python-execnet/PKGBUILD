# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-execnet
pkgname=('python-execnet' 'python2-execnet')
pkgver=1.9.0
pkgrel=1
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="https://codespeak.net/execnet"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-pytest-timeout' 'python2-pytest-timeout' 'procps-ng' 'openssh')
source=("https://pypi.io/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('d425e7b6f3708efecb5dfa2c36a837cb55f2c32cf2ec0c1dc11cd1ca6ea614d119d04908b84dd5a3593c87db35e71fee202f843665c853ac3538479f60c83d60')

prepare() {
  cp -a execnet-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/execnet-$pkgver
  python setup.py build

  cd "$srcdir"/execnet-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/execnet-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" pytest testing

  cd "$srcdir"/execnet-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" pytest2 testing
}

package_python-execnet() {
  depends=('python')

  cd execnet-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python2-execnet() {
  depends=('python2')

  cd execnet-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
