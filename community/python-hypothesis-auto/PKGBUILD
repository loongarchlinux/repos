# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis-auto
pkgver=1.1.4
_commit=4ed588ab631d6c44c8959334a3425a8c0d207eff
pkgrel=6
pkgdesc="Extends Hypothesis to add fully automatic testing of type annotated functions"
url="https://github.com/timothycrosley/hypothesis-auto"
license=('MIT')
arch=('any')
depends=('python-pydantic' 'python-hypothesis')
makedepends=('python-pyproject2setuppy')
checkdepends=('python-pytest')
source=("https://github.com/timothycrosley/hypothesis-auto/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('96a9b541dedcbe20eae242201ad2d57366f61ab60aa388ad67102a18bd1fdd916f3a45cee67ff7021dc4358707457f6f607e6a0666b85005150d09e398bfda58')

build() {
  cd hypothesis-auto-$_commit
  python -m pyproject2setuppy build
}

check() {
  cd hypothesis-auto-$_commit
  python -m pytest
}

package() {
  cd hypothesis-auto-$_commit
  python -m pyproject2setuppy install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
