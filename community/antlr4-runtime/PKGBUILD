# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=antlr4-runtime
pkgver=4.9.3
pkgrel=3
pkgdesc='Antlr C++ runtime'
url='https://www.antlr.org/'
arch=('loongarch64' 'x86_64')
depends=('gcc-libs')
makedepends=('git' 'cmake')
license=('BSD')
source=("https://www.antlr.org/download/antlr4-cpp-runtime-${pkgver}-source.zip")
sha256sums=('5f0af6efd81f476c3e775c486eb0a71c25d6bbc14373e88a64690e2738d68e03')
# seems like there is no simple way of dropping the static lib
options=(staticlibs)

prepare() {
  # mysql-workbench is built with C++17, so we need it here as well
  sed -i '/CMAKE_CXX_STANDARD/s/11/17/' CMakeLists.txt
}

build() {
  export CFLAGS+=' -ffat-lto-objects'
  export CXXFLAGS+=' -ffat-lto-objects'

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DANTLR4_INSTALL=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
