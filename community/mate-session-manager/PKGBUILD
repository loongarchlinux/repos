# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.24.3
pkgrel=1
pkgdesc="The MATE Session Handler"
url="https://mate-desktop.org"
arch=('loongarch64' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gettext' 'gtk3' 'libsm' 'mate-desktop')
makedepends=('xtrans' 'systemd' 'python')
optdepends=('gnome-keyring: keyring support'
            'xdg-user-dirs-gtk: manage user directories')
groups=('mate')
conflicts=('mate-session-manager-gtk3')
replaces=('mate-session-manager-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('90a0aec5b59b6287b4d2c4d452b0b6410f9d12490ca1f890e81ba2801bdab0a2')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--disable-upower
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
