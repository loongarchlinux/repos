# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=textdistance
pkgname=python-${_pkg}
pkgver=4.2.2
pkgrel=1
pkgdesc="Compute distance between sequences"
arch=(any)
url="https://github.com/life4/textdistance"
license=(MIT)
depends=(
    python-abydos
    python-jellyfish
    python-numpy
    python-levenshtein
    python-pyxdameraulevenshtein
)
makedepends=(python-setuptools)
checkdepends=(
    python-pytest
    python-hypothesis
    python-isort
    python-pylev
    python-py_stringmatching
    python-tabulate
)
# PyPi tarballs do not have the tests
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('f123ee4f9ec760609b528997bc9055e1333dc5f34de7b22ddf1af5d7aa8613c7')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # Failing tests, reported by email
  pytest --deselect tests/test_external.py
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
