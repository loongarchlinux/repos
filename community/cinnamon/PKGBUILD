# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=5.0.7
pkgrel=5
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=(loongarch64 x86_64)
url="https://github.com/linuxmint/cinnamon"
license=(GPL2)
depends=(accountsservice caribou cinnamon-control-center cinnamon-menus cinnamon-screensaver
         cinnamon-session cinnamon-settings-daemon cjs gnome-backgrounds
         gnome-themes-extra gstreamer libcroco libgnomekbd libkeybinder3 librsvg muffin
         network-manager-applet nemo polkit-gnome python-cairo python-dbus
         python-gobject python-pam python-pexpect python-pillow python-pyinotify
         python-pytz python-tinycss2 python-xapp timezonemap xapps)
optdepends=('blueberry: Bluetooth support'
            'cinnamon-translations: i18n'
            'gnome-panel: fallback mode'
            'metacity: fallback mode'
            'system-config-printer: printer settings'
            'wget: cover download support in audio applet')
makedepends=(meson samurai gobject-introspection intltool)
options=('!emptydirs')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        $pkgname-meson-0.60.2.patch::https://github.com/linuxmint/cinnamon/commit/8fc2df08b40a.patch
        $pkgname-python310-menu-editor.patch::https://github.com/linuxmint/cinnamon/commit/749a4952b7d5.patch
        0001-cinnamon-settings-don-t-rely-on-the-presence-of-cinn.patch
        set_wheel.diff
        default-theme.patch)
sha512sums=('edaa01d245bda906c32af57eb28e5958847da34ec8b4e051e951a42a8b69c50acb318a9a4e63e79b0d8372833c42be488db04c099a82b2b89dc33a720897719a'
            '0c79334171a759943ad4e51f7bc241c9bb07e3f4603034b1131c97667ce04a29444697de9b291093a40906e8e29f734f8ed0804088c277184d32ef1e07020e67'
            '2b3234046286a7373eb31f89d5d10387c1fad299ccf278a5346956a24fc1d82b5d95d54ae337cd1c27b4388edb187819f62a58305ef29cdfbe3f9c239c2f880f'
            '77c2229d350bb1445375d206af769f1dde9329ce3575e40e9c1d5fea3ec0f49c0a7ef36d78caf895d9224147947bd102bab924b5579dc8dc5f8d3269e0c76374'
            'fd7e117054996ed1c3dfd0f968c2bf98ca4fcee9a100221f8839a232147745ec0140e1f68eeffba58a3c44f66f26e05d433648a7a28858ec669524f7266ba04c'
            'ee7dedd59ea370cf81d75def49060f9a29b22e7b025ca7d5db87a0102d50f138c79aa562b0d36a748c4b1c59a37f600ba1f60ff6caf303cf5b6fc4d110d051b4')
b2sums=('f6f0ee9b2432fb3069f4e6062a500b629e8737f2d5a6bdd5819aeeee24bc6ffe8db3094f9ecb4dd7af5f0e3f70a6f6ced7c13f1811c42eab9ff953111e0fe91e'
        '52cc75a00ba6b04d01ea47aeb51adbfe01645fed6cf59107207ffd7cfcaf61fcb74f42808b662549f428ebf50ced015c7253353fbf94385d99adaaac86c9e01f'
        'd5a91dc2ae7c3cf85af97c917737cf42b319de86e2142f00444f4cab8aeacb750d37a2a398298cf9fe1fc7b5cdb5899fa09af85133c0d65b9d7be1730c29b143'
        '1d873efa0d17e358f834c4374f39bbf3f74340849f9c28a0950c0a064772083798aba3435e564acdf43a94665389917788e295073d4a65cc18600d71b7a9f008'
        '3becf1f40068fc629109e6e7d464c3c484296afacc9ab6328b2ccbb3c9735bcbfa9550f9f73b430ede178ae668e37c660ce322b5b4d1873526de3d3d41185160'
        '86c3a29acd132ca321f08fd81dd5a45707accdc035b2aeec95bf358b29072ff1eedb77b2566cf48d7253d1d791599f0f44938c4600761d711cb18b59019f1c62')

prepare() {
  cd ${pkgname}-${pkgver}

  # https://github.com/linuxmint/cinnamon/pull/10489
  patch -Np1 -i ../$pkgname-meson-0.60.2.patch

  # https://github.com/linuxmint/cinnamon/pull/10370
  patch -Np1 -i ../$pkgname-python310-menu-editor.patch

  # Check for the cc-panel module path, not for the irrelevant binary
  # https://github.com/linuxmint/cinnamon/pull/7382
  patch --no-backup-if-mismatch -p1 -i ../0001-cinnamon-settings-don-t-rely-on-the-presence-of-cinn.patch

  # Use wheel group instread of sudo (taken from Fedora)
  patch -p1 < ../set_wheel.diff

  # Set default theme to 'cinnamon'
  patch -p1 < ../default-theme.patch

  # Replace MintInstall with GNOME Software
  sed -i 's/mintinstall.desktop/org.gnome.Software.desktop/' data/org.cinnamon.gschema.xml

  # Add polkit agent to required components
  sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
      cinnamon*.session.in

  # https://github.com/linuxmint/cinnamon/issues/3575#issuecomment-374887122
  # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
  sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
      files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py
}

build() {
  mkdir -p build
  cd build
  arch-meson --libexecdir=lib/cinnamon ../${pkgname}-${pkgver}
  samu
}

package() {
  cd build
  DESTDIR="${pkgdir}" samu install
}
