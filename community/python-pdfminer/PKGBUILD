# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
pkgname=python-${_pkg}
pkgver=20201018
pkgrel=5
pkgdesc="Python PDF Parser"
arch=(any)
url="https://github.com/pdfminer/pdfminer.six"
license=(MIT)
depends=(python-chardet python-cryptography)
makedepends=(python-setuptools cython)
checkdepends=(python-nose)
# Provided tarballs do not include tests
#source=(${url}/releases/download/${pkgver}/${_pkg}.six-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${_pkg}-${pkgver}.tar.gz
        ${pkgname}-drop-sortedcontainers.patch::https://github.com/pdfminer/pdfminer.six/commit/61300eef70bf2b32d300005f011bdf9b48660740.patch)
sha256sums=('0e9c8b0a9c40c2aee044c3091ad6b18b5e6279722e64b6b975f8aedc26bf8e38'
            'ffe04581c39be2535ef813a48673c9bfe8ca3c333e3381fd26446154e2fe4007')

prepare() {
  cd ${_pkg}.six-${pkgver}
  patch -p1 < ../${pkgname}-drop-sortedcontainers.patch
}

build() {
  cd ${_pkg}.six-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}.six-${pkgver}
  nosetests
}

package() {
  cd ${_pkg}.six-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
