# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
pkgname=python-${_pkg}
pkgver=20200726
pkgrel=1
pkgdesc="Python PDF Parser"
arch=(any)
url="https://github.com/pdfminer/pdfminer.six"
license=(MIT)
depends=(python-chardet python-cryptography python-sortedcontainers)
makedepends=(python-setuptools cython)
checkdepends=(python-nose)
# Provided tarballs do not include tests
#source=(${url}/releases/download/${pkgver}/${_pkg}.six-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('77f0b6953f36aeeeb45ab959fabd8dfc964b7926676d5df3ac2f949cd4d524a3')

build() {
  cd ${_pkg}.six-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}.six-${pkgver}
  nosetests
}

package() {
  cd ${_pkg}.six-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
