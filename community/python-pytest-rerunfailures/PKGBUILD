# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=10.2
pkgrel=3
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('8830610276e2cc172ee372ae4d8376bc6329138751ea2737a4def6af32d05c112c872a7a180ff554018058c05ed0d109927dbac61f748586257e40b0243f03c8')
b2sums=('9f317f3a39ac234e145a14d532d2727bb9fb5fead65c05372cf95268e0f589f9de994cd3c47e75dcb354c996595698c43fccdea5134d2e1921429c8ea7b71451')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}
