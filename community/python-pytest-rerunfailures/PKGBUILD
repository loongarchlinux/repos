# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=10.3
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=(any)
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=(MPL2)
depends=(python-pytest)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('30ebafdb221d7ff8c46d50795b1a342b55293eaf60f5a012c631e2333dcf3114bfc4ed5210705edba4ad33a8f215ce7b5988362750149fc5e9463e4c3fa93860')
b2sums=('1063f55f4145eff4db22f121620cb3a53ad8cd0c44580578bd5027c4bd8005af6d33e0da8d67fceb7b8abe91f22c591e7b42b7a810cb5c7d8b5668b0a48eb3f2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES.rst,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
