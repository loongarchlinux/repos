# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=10.0
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('83270e18cc3f825c0e0184950a149899b99a2b46cfaa6ca1c82b7cf388144265831fc2b8b82ccebb5df57463219cb7e0b41f5beb049feedcba90ae6d48d1e05f')
b2sums=('fb445acd6324a377258ebcd32255bd725dc78f2209990e5807a0553da259ffc101969b7572799efa5041ae68bcc264ab80581cf9d9943ab81d9b158a15dd71d5')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}
