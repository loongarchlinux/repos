# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

pkgname=tensorboard
pkgver=2.11.0
pkgrel=2
pkgdesc="TensorFlow's Visualization Toolkit"
url='https://github.com/tensorflow/tensorboard'
arch=('loong64' 'x86_64')
license=('APACHE')
depends=('python' 'python-werkzeug' 'python-numpy' 'python-bleach' 'python-wheel' 'python-markdown'
         'python-html5lib' 'python-protobuf' 'python-grpcio' 'python-google-auth-oauthlib'
         'python-tensorboard_plugin_wit' 'absl-py')
makedepends=('bazel' 'python-setuptools' 'tree' 'rust' 'python-build' 'python-installer' 'python-wheel')
optdepends=('python-tensorflow: advanced features for TensorBoard')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tensorflow/tensorboard/archive/${pkgver}.tar.gz")
options=('!lto')
sha512sums=('b9ffa57080f33032b31aeaf9abfe9914463db03c49530750328c7d6b1f12801322d2591e4f1bd6565514eb6c78ec5a1dc65620105e20bc2978e488a868a2b4bd')

prepare() {
  cd "$pkgname-$pkgver"

  # Remove fixed version requirements as we usually ship more up-to-date
  # versions than are specified and it tends to work fine anyhow.
  sed -i "s/[<>=].*//" tensorboard/pip_package/requirements.txt
}

build() {
  cd "$pkgname-$pkgver"

  PYTHONWARNINGS=ignore bazel build //tensorboard
  bazel build //tensorboard/pip_package:build_pip_package

  cd tensorboard/data/server
  cargo build --release
  python pip_package/build.py --server-binary target/release/rustboard --out-dir /tmp
  cd "$srcdir/$pkgname-$pkgver"

  mkdir pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/external pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/tensorboard pypackage
  cp tensorboard/pip_package/{README.rst,MANIFEST.in,setup.py,requirements.txt} pypackage
  cp README.md pypackage
  cd pypackage
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python -m installer --destdir="$pkgdir" /tmp/*.whl

  cd pypackage
  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -r "$pkgdir"/usr/lib/python3.10/site-packages/external/
}

# vim:set ts=2 sw=2 et:
