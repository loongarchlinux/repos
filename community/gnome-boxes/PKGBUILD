# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.38.1
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Boxes"
license=(LGPL)
groups=(gnome)
depends=(cdrtools gtk-vnc libarchive libgudev libosinfo libsecret libvirt-glib
         mtools qemu spice-gtk tracker webkit2gtk freerdp gtksourceview4)
makedepends=(git gobject-introspection yelp-tools meson spice-protocol vala)
_commit=3761f2b5dab67d2a374badb57e4859dbcef59ed8  # tags/v3.38.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gtk-frdp.git"
        "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git"
        "git+https://gitlab.gnome.org/GNOME/libhandy.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git submodule init
  git submodule set-url subprojects/gtk-frdp "$srcdir/gtk-frdp"
  git submodule set-url subprojects/libovf-glib "$srcdir/libovf-glib"
  git submodule set-url subprojects/libhandy "$srcdir/libhandy"
  git submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
