# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vistir
pkgver=0.7.4
pkgrel=1
pkgdesc="Miscellaneous utilities for dealing with filesystems, paths, projects, subprocesses, and more"
url="https://github.com/sarugaku/vistir"
license=('BSD')
arch=('any')
depends=('python-colorama')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-hypothesis-fspaths' 'python-pytest-timeout' 'python-requests'
              'python-yaspin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/vistir/archive/v$pkgver.tar.gz")
sha512sums=('89a98106999df839a0a06a4847bb940f3f4afeefe2105403892a1f5cab7cfe1223d4a7d7e75c9a3db266b51cabbf9f8cdc409d60e8c45384a79c2ef29d32fa79')

build() {
  cd vistir-$pkgver
  python setup.py build
}

check() {
  cd vistir-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd vistir-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
