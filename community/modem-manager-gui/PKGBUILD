# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ilya Medvedev <medved55rus [at] gmail [dot] com>

pkgname=modem-manager-gui
pkgver=0.0.19.1
pkgrel=5
pkgdesc="Frontend for ModemManager daemon able to control specific modem functions"
arch=('x86_64')
url="https://linuxonly.ru/page/modem-manager-gui"
license=('GPL3')
depends=('gdbm' 'gtk3' 'gtkspell3' 'libnotify' 'modemmanager')
makedepends=('po4a' 'itstool')
optdepends=('networkmanager: monitor network traffic')
options=(!emptydirs)
source=("https://download.tuxfamily.org/gsf/source/$pkgname-$pkgver.tar.gz"
        fix-crash.patch
        no-indicator.patch)
sha256sums=('0b9bae29f3cd3d99b3a61d5530097d576a5fa67d3215d54f3570cb20c6ca0bc2'
            'bcc451c1db096ea1b76907616fecfd14f73baf88b774827b2d1dc6065798ed03'
            '04f85c3d6e7dbf89f5767446a1884f1f45aaea4023a8d07981b819be181849b3')

prepare() {
  cd $pkgname-$pkgver

  # Fix memory corruption because of wrong strsep() usage (FS#61622, FS#61823)
  # Fix segfault in strftime_l() because of timestamps from future (FS#62584)
  patch -Np1 -i ../fix-crash.patch

  # Revert to GTK+ status icon
  patch -Np1 -i ../no-indicator.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
