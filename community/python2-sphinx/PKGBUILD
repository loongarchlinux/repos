# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Fabio Volpe <volpefabio@gmail.com>

pkgname=python2-sphinx
pkgver=1.8.5
pkgrel=5
pkgdesc='Python2 documentation generator'
arch=('any')
url='https://www.sphinx-doc.org/'
license=('BSD')
depends=('python2-babel'
         'python2-docutils'
         'python2-imagesize'
         'python2-jinja'
         'python2-pygments'
         'python2-requests'
         'python2-setuptools'
         'python2-six'
         'python2-snowballstemmer'
         'python2-sphinx-alabaster-theme'
         'python2-sphinxcontrib-websupport'
         'python2-typing')
makedepends=(python-setuptools)
optdepends=('imagemagick: for ext.imgconverter'
            'texlive-latexextra: for generation of PDF documentation')
source=("https://pypi.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz"{,.asc}
         python2-lexer.patch)
sha512sums=('d7e6a7a6186d1d4e4de0233fb6859da1ef3e02f628512ff9096ef4775b9fb8589edb9052d752925d7a2f1cb7901291af96b5036a5d00f5f58966609bd42872f4'
            'SKIP'
            '2a94562e26a4fd52f327ba7e45a3c97d8ab8cc0d85a6739b5546fb7005ff9f5e574685644837f6b12bed01fe8bedc698286fc55dbd0132a61ba5943d0f6f4d5c')
validpgpkeys=('8A11B79A5D0D749A66F0A030102C2C17498D6B9E'  # Takeshi KOMIYA
              'E9BEABB07E7B9CC3F56E62C91425F8CE5EBA0E07') # Takayuki Shimizukawa

prepare() {
  cd Sphinx-$pkgver
  patch -p1 -i ../python2-lexer.patch # Use proper python lexer with new pygments (NixOS)

  # change python2 interpreter
  find . -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  # change sphinx-binaries name in source code
  find . -type f -name '*.py' -exec \
    sed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\12,' {} \;
}

build() {
  cd Sphinx-$pkgver
  make PYTHON=python2 build
}

package() {
  cd Sphinx-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
