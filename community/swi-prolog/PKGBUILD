# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=8.2.4
pkgrel=1
pkgdesc='Prolog environment'
arch=(loongarch64 x86_64)
url='https://www.swi-prolog.org/'
license=(GPL LGPL)
depends=(libarchive libxcrypt libyaml qt5-base)
makedepends=(cmake jdk-openjdk junit libjpeg libxft libxinerama libxpm ninja unixodbc)
optdepends=('java-runtime: for using Prolog from Java'
            'jdk-openjdk: for using Prolog from Java'
            'libjpeg: for using the pl2xpce library'
            'libxft: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'unixodbc: for using the odbc4pl library')
source=("$pkgname-$pkgver.tar.gz::https://www.swi-prolog.org/download/stable/src/swipl-$pkgver.tar.gz")
b2sums=('b18f26102a685ff5c66aeaf0c14a08c532f9327f9f995cd46657a71b6779d021eb4f5651b2be77883197c7c5ee31c23259f46ed559aeeb0bb53ad10aea0081ed')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/swipl-$pkgver" \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -ffile-prefix-map=$PWD= -w" \
    -D LIBEDIT_LIBRARIES=/usr/lib/libedit.so.0 \
    -D LIBEDIT_INCLUDE_DIR=/usr/include \
    -G Ninja
  "$srcdir/swipl-$pkgver/scripts/pgo-compile.sh"
  ninja
}

check() {
  cd build
  ctest -j 12
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: www.swi-prolog.org/download/stable
