# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=3.12.4
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn python2) # node-sass depends on node-gyp 3.8.0 which is still python2 https://github.com/sass/node-sass/issues/2877
source=("$pkgname-$pkgver.tar.gz::https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz")
sha256sums=('2c7c70cc2d47842d8dd7a4ea41adc8e1857958fb9fe1b934453c874fee193041')

build() {
	cd "$pkgname-$pkgver"
	yarn install --pure-lockfile
	yarn upgrade node-sass
	yarn build
}

check() {
	cd "$pkgname-$pkgver"
	yarn test
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r . "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
