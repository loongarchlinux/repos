# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgname=python-networkx
pkgver=2.5.1
pkgrel=2
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
depends=('python-decorator')
optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
            'python-scipy: Provides flexible drawing of graphs.'
            'python-pyparsing: Required for pydot, GML file reading.'
            'python-yaml: Required for YAML format reading and writing.')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://github.com/networkx/networkx/archive/networkx-$pkgver.tar.gz"
        "0001-Updated-decorator-requirement-for-4718.patch")
sha512sums=('0eb43aa9cedacb3e6dfa44703d300591ce5214e983d01e526f34105e0a565d49ef424c4c4232340b01b3e396a09ed44aee658bab0aa613edcba4d7b9b1aebe6a'
            '007e3dceb06f20ec7e2e492839e5d21d51966c6b6ae65d92140caebe97745b386ceebc8db6bdd4a8e27f807a7d158f42ddbaaeb2320e533567c19910ae1dd6e4')

prepare() {
  cd networkx-networkx-$pkgver

  # https://github.com/networkx/networkx/pull/4773
  patch -p1 -i ../0001-Updated-decorator-requirement-for-4718.patch
}

build() {
  cd networkx-networkx-$pkgver
  python setup.py build
}

check() {
  cd networkx-networkx-$pkgver
  python setup.py pytest
}

package() {
  cd networkx-networkx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}
