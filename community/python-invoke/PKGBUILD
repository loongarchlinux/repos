# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.6.0
pkgrel=3
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python-fluidity' 'python-lexicon' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz"
        invoke-devendor.patch)
sha512sums=('640719d576b6c655d2a1b8e2f0556b49621c846fbce26120abc818c20dc6f9445c7663b9aafe41e7e4f7f6160bd17d1678bb6912cbe13dcbaded278cc78eb824'
            'c5e414990f7a36455ff28288f90cccd62e570775523ea307f4ed2b7cdc7b9b0c1ee2d2eb046a048843fd2f016aed0a3cf270355297a1d73119f83edf8bc37be9')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
  rm -r invoke-$pkgver/invoke/vendor

  patch -d invoke-$pkgver -p1 < invoke-devendor.patch
}

build() {
  cd invoke-$pkgver
  python setup.py build
}

check() {
  cd invoke-$pkgver
  python setup.py pytest
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
