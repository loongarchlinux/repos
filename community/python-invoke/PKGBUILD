# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.7.2
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python-fluidity' 'python-lexicon' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz"
        invoke-devendor.patch)
sha512sums=('5f187a6aee19dfab5d8f22efe4980063a0cd2bae235dbafe17e0714871223e79eb4ace5a909b0d67b3b1c7b76bfcb84a3bb51b344faac880fc40c2cb06ed94cc'
            'c5e414990f7a36455ff28288f90cccd62e570775523ea307f4ed2b7cdc7b9b0c1ee2d2eb046a048843fd2f016aed0a3cf270355297a1d73119f83edf8bc37be9')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
  rm -r invoke-$pkgver/invoke/vendor

  patch -d invoke-$pkgver -p1 < invoke-devendor.patch
}

build() {
  cd invoke-$pkgver
  python setup.py build
}

check() {
  cd invoke-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest tests
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
