# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.4.1
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('9076a1e643e3496b180d486de48efe599eddafc3e8ac32ab5cfcb27c5aabfd4ea360c0e2e59f96aceb65f3453b5ff2c228bf7d76635fe62854fe092b1ebdf3b2')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
