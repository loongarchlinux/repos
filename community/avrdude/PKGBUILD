# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=avrdude
pkgver=7.0
pkgrel=2
epoch=1
pkgdesc="Download/upload/manipulate the ROM and EEPROM contents of AVR microcontrollers"
arch=('loong64' 'x86_64')
url="https://www.nongnu.org/avrdude/"
license=('GPL')
depends=('readline' 'libftdi' 'libusb-compat' 'elfutils')
source=(https://download.savannah.gnu.org/releases/avrdude/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('EF497ABE47ED91B3FC3D7EA54D902FF7723BDEE9' '5E84F980C3CAFD4BB5841070F48CA81B69A85873' '56628323218C669FF578705C7E9EADC3030D34EB')
md5sums=('7927cfbccfe2f016eaef1bdeff399e96'
         'SKIP')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --mandir=/usr/share/man \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-linuxgpio \
    --enable-linuxspi
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
