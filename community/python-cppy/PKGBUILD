# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cppy
pkgver=1.2.1
pkgrel=1
pkgdesc="A collection of C++ headers which make it easier to write Python C extension modules"
url="https://github.com/nucleic/cppy"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://github.com/nucleic/cppy/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f6493830a8a66f52fff7c592db9d255fd7178ba320faee6a96258f29b6f299ffeca83f001650390b05c1037eb1350b0bd4da701d77a7bcb1a1babc108b7b8f97')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cppy-$pkgver
  python setup.py build
}

check() {
  cd cppy-$pkgver
  python -m pytest
}

package() {
  cd cppy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
