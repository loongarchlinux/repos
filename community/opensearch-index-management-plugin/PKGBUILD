# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-index-management-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Index Management Plugin"
arch=('loongarch64' 'x86_64')
url="https://opensearch.org/docs/latest/im-plugin"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/index-management/archive/${pkgver}.tar.gz"
)
sha256sums=('ceead2ec42ce92c7c3ec280ab5b0336c1fed326bf142e52482e2f70b7b7faf2c')

build() {
  cd "index-management-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  unzip "${srcdir}/index-management-${pkgver}/build/distributions/opensearch-index-management-${pkgver}.zip"

  install -Dm644 "${srcdir}/index-management-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
