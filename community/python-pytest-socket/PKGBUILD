# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-socket
pkgver=0.4.1
pkgrel=3
pkgdesc='Pytest Plugin to disable socket calls during tests'
arch=('any')
license=('MIT')
url='https://github.com/miketheman/pytest-socket'
depends=('python-pytest')
makedepends=('python-dephell')
checkdepends=('python-asynctest' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-httpbin'
              'python-starlette')
source=("https://github.com/miketheman/pytest-socket/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3a9cda0dd91e43d83c2a28c6e4e068f6c152c129d6f558de1aa6764d508cf430406ab3943a4b76ebdfb017c59f1ac7b55c570d38938f1b94ccf1de09d087924e')

prepare() {
  cd pytest-socket-$pkgver
  dephell deps convert --from pyproject.toml --to setup.py

  # dephell does not generate py_modules into setup.py, so the resulting installation is missing
  # the module source code itself. Patching it here as a workaround until we fix it properly.
  sed -i '/license=/a \    py_modules=["pytest_socket"],' setup.py
}

build() {
  cd pytest-socket-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-socket-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest --deselect setup.py
  }

package() {
  cd pytest-socket-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
