# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-socket
pkgver=0.5.1
pkgrel=1
pkgdesc='Pytest Plugin to disable socket calls during tests'
arch=('any')
license=('MIT')
url='https://github.com/miketheman/pytest-socket'
depends=('python-pytest')
makedepends=('python-pyproject2setuppy')
checkdepends=('python-asynctest' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-httpbin'
              'python-httpx' 'python-requests' 'python-starlette')
source=("https://github.com/miketheman/pytest-socket/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f43a99419056086aaee52206bc2816452919da617e54535285ce9e6c59b8bc39edc3326a5c629cf25f1677bd2152d300a6191464671298888c7683d3c2181eff')

build() {
  cd pytest-socket-$pkgver
  python -m pyproject2setuppy build
}

check() {
  # Hack entry points by installing it

  cd pytest-socket-$pkgver
  python -m pyproject2setuppy install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-socket-$pkgver
  python -m pyproject2setuppy install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
