# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=6.0.2
pkgrel=2
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=1dc306b86dafeb5801d993926af171b86336f5d2
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/usr/share/cups/data/default-testpage.pdf
}

# vim: ts=2 sw=2 et:
