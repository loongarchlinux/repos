# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: sxe <sxxe@gmx.de>

pkgname=flowblade
pkgver=2.8.0.3
pkgrel=3
pkgdesc='Multitrack non-linear video editor'
arch=('any')
url='https://jliljebl.github.io/flowblade/'
license=('GPL3')
depends=('dbus-python' 'ffmpeg' 'frei0r-plugins' 'gmic' 'gtk3' 'mlt6' 'python-cairo'
         'python-gobject' 'python-numpy' 'python-pillow' 'sdl_image' 'swh-plugins')
source=("https://github.com/jliljebl/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('3753363d15caa6f1901118132a87f92d3b5e843c325775dbd83b30ee4d2a9bbf')

prepare() {
  cd $pkgname-$pkgver/$pkgname-trunk
  _python_site_packages=`python -c 'import site; print(site.getsitepackages()[0])'`
  sed -i "s|/usr/share/pyshared/Flowblade|$_python_site_packages/Flowblade|" flowblade
}

build() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python setup.py build
}

package() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
