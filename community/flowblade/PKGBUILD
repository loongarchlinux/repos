# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: sxe <sxxe@gmx.de>

pkgname=flowblade
pkgver=2.6
pkgrel=1
pkgdesc="Multitrack non-linear video editor"
arch=(any)
url="https://jliljebl.github.io/flowblade/"
license=(GPL3)
depends=(ffmpeg frei0r-plugins gmic gtk3 mlt python-cairo python-dbus python-gobject
         python-numpy python-pillow sdl_image swh-plugins)
source=("https://github.com/jliljebl/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('1caef13b0a21cd0720a4ae9bb8eec414706fd5a5c8562dbb6bb70164cbdfc141')

prepare() {
  cd $pkgname-$pkgver/$pkgname-trunk
  _python_site_packages=`echo /usr/lib/python3*/site-packages`
  sed -i "s|/usr/share/pyshared/Flowblade|$_python_site_packages/Flowblade|" flowblade
  sed -i "s|,'res/help/\*'|,'res/help/*','res/help/*/*'|
          s|share/appdata|share/metainfo|" setup.py
}

build() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver/$pkgname-trunk
  python3 setup.py install --root="$pkgdir" --optimize=1
}
