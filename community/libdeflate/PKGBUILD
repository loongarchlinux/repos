# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>

pkgname=libdeflate
pkgver=1.8
pkgrel=3
pkgdesc='Heavily optimized library for DEFLATE/zlib/gzip compression and decompression'
arch=(loongarch64 x86_64)
url=https://github.com/ebiggers/libdeflate
license=(MIT)
depends=(glibc)
makedepends=(git)
provides=(libdeflate.so)
_tag=047aa84e01b38d82f3612810e357bd40f14a3d39
source=(git+https://github.com/ebiggers/libdeflate.git#tag=${_tag})
b2sums=(SKIP)

build() {
  make PREFIX=/usr -C libdeflate
}

pkgver() {
  cd libdeflate
  git describe --tags | sed 's/^v//'
}

check() {
  make PREFIX=/usr check -C libdeflate
}

package() {
  make PREFIX=/usr DESTDIR="${pkgdir}" install -C libdeflate
  install -Dm 644 libdeflate/COPYING -t "${pkgdir}"/usr/share/licenses/libdeflate/
}

# vim ts=2 sw=2 et:
