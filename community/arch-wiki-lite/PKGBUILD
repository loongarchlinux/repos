# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=arch-wiki-lite
pkgver=20220525
_codever=20181022
pkgrel=1
pkgdesc='The wiki without html. 1/9 as big, easily searched and viewable on console.'
arch=('any')
url='http://kmkeen.com/arch-wiki-lite/'
_watch="https://www.archlinux.org/packages/community/any/arch-wiki-docs/"
license=('GPL' 'FDL')
depends=('bash' 'gzip')
makedepends=('python' 'arch-wiki-docs')
optdepends=('dialog: for interactive menu'
            'arch-wiki-docs: for wiki-search-html'
            'xdg-utils: for wiki-search-html')
source=("http://kmkeen.com/$pkgname/$pkgname-$_codever.tar.gz")
sha512sums=('1bc127c964ddf858a60e1be672dbe11bb9015e0a690a7552da5ac0c007b75fa3bc53bdf64aea199a39c2e91c787bbd01879f66b7739a3f367ee59fe5f33cf50b')
b2sums=('837da2d5832f16e449845e918fc8f24c6c34c4addce78b3356a0627ae54b164b98a79196a839c6de7c9756323cbce31a3eb50873c8cf0e9c41c986a75c852dcb')

build() {
  cd "$pkgname"
  LC_ALL=en_US.UTF-8 python3 wiki_lite.py
}

package() {
  cd "$pkgname"
  install -d "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -m 0644 wiki/* "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -Dm 0755 wiki-search      "$pkgdir/usr/bin/wiki-search"
  install -Dm 0755 wiki-search-html "$pkgdir/usr/bin/wiki-search-html"
  install -Dm 0644 wiki-search.dialog.rc "$pkgdir/etc/dialog.d/wiki-search.dialog.rc"
}

