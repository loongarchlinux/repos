# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-dephell-setuptools
_pyname=dephell_setuptools
pkgver=0.2.4
pkgrel=4
pkgdesc='Extract meta information from setup.py'
arch=(any)
url="https://github.com/dephell/$_pyname"
license=(MIT)
depends=(python-setuptools)
checkdepends=(python-pytest)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname:0:1}/$_pyname/$_archive.tar.gz")
sha256sums=('663629e1ebf7b20bf7e372ee2a2e7ebf1a15aeb3bc6d46ad32e1bcb21044ca29')

prepare() {
    cd "$_archive"
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "$_archive"
    python setup.py build
}

check() {
    cd "$_archive"
    python -m pytest
}

package() {
    cd "$_archive"
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
