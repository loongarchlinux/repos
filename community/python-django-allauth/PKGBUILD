# Maintainer: David Runge <dave@sleepmap.de>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.43.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=('any')
url="https://github.com/pennersr/django-allauth"
license=('MIT')
depends=('python-django' 'python-openid' 'python-requests'
'python-requests-oauthlib' 'python-pyjwt')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest')
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('803e276f0c21053d328ceada18f82f3e89df0442316e7fae0b02ce2e0ba1a62d98ac923e8031fcf33b11da125200795199ab65fc1f576b218048ae57d211dfad')
b2sums=('da95a7b131eb12650ec1d5b5813ce70d40508f79761dd1d67ad4811b0341a1359405ffa3b79bdc1f354a154a828182781d2e27f989a03a845970fe942fb96a50')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  coverage run manage.py test allauth
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
