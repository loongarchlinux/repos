# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.51.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=(any)
url="https://github.com/pennersr/django-allauth"
license=(MIT)
depends=(python-django python-openid python-requests python-requests-oauthlib
python-pyjwt)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-mock python-pytest)
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-$pkgver.tar.gz::https://github.com/pennersr/$_name/archive/$pkgver.tar.gz)
sha512sums=('df62c686cbbe2595136436fcaa354f1d9fd0128ec27572a853f90e8f1d47fb0e36b6821b535a7f5265e849a78fca74cc061dfe049da006ea7021222375f86c82')
b2sums=('6f128f9126ac5d9e7f2e820a9807d8e1a707ae5b4ae9be238a04033f9ea97f2b55aa67da2eabc1dd839c605c6ad399ea94396c379d3b3b9a6989dc0b1e0d895b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python manage.py test allauth
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
