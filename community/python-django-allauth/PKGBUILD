# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.47.0
pkgrel=2
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=(any)
url="https://github.com/pennersr/django-allauth"
license=(MIT)
depends=(python-django python-openid python-requests python-requests-oauthlib
python-pyjwt)
makedepends=(python-setuptools)
checkdepends=(python-mock python-pytest)
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=(
  "${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz"
)
sha512sums=('535f333523f2f72a0c33aecaa974817fb2bd0bc1ac6cd0d8c77ead4b71d2521f3822886ae394202a67c023e645e3ac1923345232691205afa725f04077ea0377')
b2sums=('9cfdb92c1f35acf1da8721346f3386e1ca770e0c86998373e51e3d6da2b158711f1230cab3959361ce1cbac6bccf17f723a776b01f178e860d604ed53aae2940')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python manage.py test allauth
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
