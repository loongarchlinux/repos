# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libopencm3
_commit=470a1394a838fe308d886f6880b7054bab4aa5eb
pkgver=r2768.470a1394
pkgrel=1
pkgdesc='Open Source firmware library for various ARM Cortex microcontrollers'
arch=('any')
url='http://www.libopencm3.org'
license=('GPL')
depends=('arm-none-eabi-newlib')
makedepends=('git' 'arm-none-eabi-gcc' 'python-yaml')
options=(!strip staticlibs !buildflags)
source=("git://github.com/libopencm3/libopencm3.git#commit=$_commit"
        '0001-Revert-Drop-install-target-and-documentation.patch')
sha512sums=('SKIP'
            '90454277085d38657730970f8cefbd0e9f166486313824e74a144b69917b952dcca5a949699402d90391e4ebeecc6593e644ef3f4088a87999ec2d60840a219e')

pkgver() {
  cd $pkgname

  printf 'r%s.%s' $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -p1 < ../0001-Revert-Drop-install-target-and-documentation.patch
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr/arm-none-eabi install
}

