# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rhinoceros <https://aur.archlinux.org/account/rhinoceros>
# Contributor: Patrice Peterson <runiq at archlinux dot us>

pkgname=vim-tagbar
pkgver=3.0.0
pkgrel=1
pkgdesc='Plugin to browse the tags of the current file and get an overview of its structure'
url='https://majutsushi.github.io/tagbar/'
arch=('any')
license=('custom:vim')
depends=('ctags' 'vim')
groups=('vim-plugins')
source=(https://github.com/majutsushi/tagbar/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('01633e56211ddf753b8b24757e5f67a3ac1d79e36e9c1b39d431db968d592a878d4e19e7631059270069885eea6e608655cb604df3f512588e159ffba1057aa5')

package() {
  cd ${pkgname#vim-}-${pkgver}
  _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
