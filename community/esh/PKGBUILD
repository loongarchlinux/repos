# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>

pkgname=esh
pkgver=0.3.1
pkgrel=1
pkgdesc="Simple templating engine based on shell"
arch=('any')
url="https://github.com/jirutka/esh"
license=('MIT')
depends=('gawk' 'sed')
makedepends=('asciidoctor')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jirutka/esh/archive/v${pkgver}/esh-${pkgver}.tar.gz")
sha512sums=('a29f8b028ceba305c8a37f2df20be95701fa3bdaeefd9853e05cc6423a6c685b33954deabda9af25c31baeae2321084e2a2badee216010c8efd75e58888effa3')
b2sums=('b74ede2dd932c25cd75c16429b09a9767538eb76a248453335bfc0179604f18a959ac0d1f6420ca99d44d505e04af50c76e32353d4dd0147a3453c0685f4f798')

build() {
	cd "$pkgname-$pkgver"
	make man
}

check() {
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make prefix=/usr/ DESTDIR="$pkgdir/" install
	install -vDm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
