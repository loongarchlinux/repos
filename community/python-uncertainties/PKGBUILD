# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-uncertainties
pkgver=3.1.4
pkgrel=1
pkgdesc="Transparent calculations with uncertainties on the quantities involved (aka error propagation); fast calculation of derivatives."
arch=('any')
license=('BSD')
url="https://github.com/lebigot/uncertainties"
depends=('python-future')
optdepends=('python-numpy: additional support for NumPy arrays and matrices')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-tests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lebigot/uncertainties/archive/$pkgver.tar.gz")
sha512sums=('581b083785fbe8a222b6b26a99f2f9b1c82a8f2cbfc77108f3b75524338808497e6669da9eb0793514f1926ee810d7151735ad2e8ae941587b026af5347c29d5')

build() {
  cd uncertainties-$pkgver
  python setup.py build
}

check() {
  cd uncertainties-$pkgver
  python setup.py nosetests
}

package() {
  cd uncertainties-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
