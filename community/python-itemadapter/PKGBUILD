# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.5.0
pkgrel=1
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-attrs' 'python-pydantic' 'python-pytest' 'scrapy')
source=("https://github.com/scrapy/itemadapter/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8ae2c54843c839559cf11ba220d5d8104290c53eabc062436355b857aee22038894fefbe94590f11c4a028a231e4fc916a0d8ccd9a3562018ec798f3bddd3155')

build() {
  cd itemadapter-$pkgver
  python setup.py build
}

check() {
  cd itemadapter-$pkgver
  pytest
}

package() {
  cd itemadapter-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
