# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sphinxcontrib-autoprogram
pkgver=0.1.7
pkgrel=3
pkgdesc="Documenting CLI programs"
url="https://github.com/sphinx-contrib/autoprogram"
license=('BSD')
arch=('any')
depends=('python-sphinx' 'python-six')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sphinx-contrib/autoprogram/archive/$pkgver.tar.gz"
        $pkgname-python310.patch::https://github.com/sphinx-contrib/autoprogram/pull/25.patch
        LICENSE)
sha512sums=('81679a8bed93274193efb5ac784d72280db32f37156aca5a422af6c8e8abd6d4e4a22e070ee73e9b3194eacf22a8228be0df3329f6fd6a0ff2408a3c7973c8a7'
            'c6ee6f19d7c7cff9161f88435766a9671f3932cffd41334f805d138ef37a5620bb6b70ce86ecdcdc02958abeb67a4ee1acff7794bab90690bf5a22f654f72ddd'
            '48e8c1f6e914f9e45281fca9d2cbcdc860751413cc255111f2dc560a15e26d34dce3fbb41b83ad42e717124bc9e43f63eb737ac217295b2fad180214b9f9e498')

prepare() {
  cd autoprogram-$pkgver
  sed "s/'/\"/g" ../$pkgname-python310.patch | patch -Np1
}

build() {
  cd autoprogram-$pkgver
  python setup.py build
}

check() {
  cd autoprogram-$pkgver
  python setup.py test
}

package() {
  cd autoprogram-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
