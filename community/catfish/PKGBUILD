# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>

pkgname=catfish
pkgver=4.16.0
pkgrel=4
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://docs.xfce.org/apps/catfish/start'
license=('GPL')
depends=('dbus-python' 'gtk3' 'python-cairo' 'python-gobject' 'python-pexpect' 'xdg-utils'
         'xfconf')
makedepends=('intltool' 'python-distutils-extra')
optdepends=('mlocate: search in locate index'
            'zeitgeist: search in zeitgeist index')
source=("https://archive.xfce.org/src/apps/catfish/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('1f6facee57a659af560f06024ca6f98aa4d638bf57a8bcfb613b4dc70fcc3b47')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package () {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
