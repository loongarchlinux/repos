# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-compute-runtime
pkgver=20.30.17454
pkgrel=1
pkgdesc="Intel(R) Graphics Compute Runtime for oneAPI Level Zero and OpenCL(TM) Driver"
arch=(x86_64)
url="https://01.org/compute-runtime"
license=(MIT)
depends=(gcc-libs intel-gmmlib intel-graphics-compiler)
makedepends=(cmake libva level-zero-headers)
optdepends=('libva: for cl_intel_va_api_media_sharing'
            'libdrm: for cl_intel_va_api_media_sharing')
provides=(opencl-driver level-zero-driver)
source=(https://github.com/intel/compute-runtime/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('835304d1862970a5aa0ec1080717d292b11e3d942f827371fb8ecd78420069ce')

build() {
    cmake -B build -S compute-runtime-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm755 build/bin/libocloc.so -t "${pkgdir}"/usr/lib/intel-opencl
    install -Dm644 compute-runtime-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
    
    local _lz_sover
    local _lz_major
    _lz_sover=$(find "${pkgdir}"/usr/lib -type f -name 'libze_intel_gpu.so.*.*.*' | sed 's/^.*\.so\.//')
    _lz_major=${_lz_sover%%.*}
    ln -s libze_intel_gpu.so.${_lz_sover} "${pkgdir}"/usr/lib/libze_intel_gpu.so
    ln -s libze_intel_gpu.so.${_lz_sover} "${pkgdir}"/usr/lib/libze_intel_gpu.so.${_lz_major}
}
