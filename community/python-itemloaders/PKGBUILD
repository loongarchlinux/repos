# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemloaders
pkgver=1.0.3
pkgrel=1
pkgdesc="Library to populate items using XPath and CSS with a convenient API"
url="https://github.com/scrapy/itemloaders"
license=('BSD')
arch=('any')
depends=('python-w3lib' 'python-parsel' 'python-jmespath' 'python-itemadapter')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/scrapy/itemloaders/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('94b69a5c6324835ec0a2408976795430313780e04559d21d0df6467cb7b952d2be5db78fe2ece0fc2ba6855b1c11426b75dcdad7440a4b2547bae1ec7c01df85')

build() {
  cd itemloaders-$pkgver
  python setup.py build
}

check() {
  cd itemloaders-$pkgver
  python -m pytest
}

package() {
  cd itemloaders-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
