# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=2.1.0
pkgrel=1
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=(any)
url="https://github.com/ansible-community/ansible-compat"
license=(MIT)
depends=(python-jsonschema python-pyyaml python-subprocess-tee)
makedepends=(python-build python-installer python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible python-flaky python-pytest python-pytest-mock)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('62bae1bb5c20663d5ee1bfec04f073c08be721040af9ef5bddc7904e6678d184')
b2sums=('5ac74cdaef1c932094996f331ed94418836b3351cbd376ae35ef22046b68dad0a8ab68fd293015db9954e0a5e1df4b5c529b3845cee5778daed2bb207733ca4f')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
