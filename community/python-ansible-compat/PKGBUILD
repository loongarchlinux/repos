# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=2.0.4
pkgrel=1
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=(any)
url="https://github.com/ansible-community/ansible-compat"
license=(MIT)
depends=(python-subprocess-tee python-yaml)
makedepends=(python-build python-installer python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible python-flaky python-pytest python-pytest-mock)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('3d5fb21c1ff069e84ac5fc17f6c8d2261c2a7b299986be810d339dfc77c128a9')
b2sums=('53aa93ef3144071c2d9022bce99c6d20a67650fa98a6896db4fd1c866ada292390cc8e6bb6e7bd3b2b05cef8b3cab2803522ca1bbb7dbacb7b9da29b16f1fbb7')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
