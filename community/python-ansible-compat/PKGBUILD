# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=0.5.0
pkgrel=3
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=('any')
url="https://github.com/ansible-community/ansible-compat"
license=('MIT')
depends=('python-subprocess-tee' 'python-yaml')
makedepends=('python-setuptools-scm' 'python-setuptools-scm-git-archive')
checkdepends=('ansible' 'python-flaky' 'python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('0730fbbb32710d19f4244a4cabad9c6b33b4b92ddf72aee353484e17543405f5')
b2sums=('468dbcf20865225c76896dd8aad6f4b9a749b7a45d661484f89e2eaab4416b29f2e0ea0477b86ea52193165cd23180a79ebd5adac571e0b7d659096e84c27bcb')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not care about pytest options
  sed -e '/addopts/d' -i pyproject.toml
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
