# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-neutronclient
pkgver=8.2.0
pkgrel=1
pkgdesc="OpenStack Networking (Neutron) Client"
arch=('any')
url="https://docs.openstack.org/python-neutronclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-debtcollector' 'python-iso8601' 'python-netaddr'
         'python-osc-lib' 'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization'
         'python-oslo-utils' 'python-os-client-config' 'python-keystoneauth1'
         'python-keystoneclient' 'python-requests' 'python-simplejson')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-requests-mock')
source=("https://github.com/openstack/python-neutronclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d076f2c1695dd893860d47dc3f7523e9252baa5f9ee864dfe0e298a93391121d5535f7ca6e30888709e9a6426dccc32b41059e20bf3f1a6889bb722f9deb30b7')

export PBR_VERSION=$pkgver

build() {
  cd python-neutronclient-$pkgver
  python setup.py build
}

check() {
  cd python-neutronclient-$pkgver
  stestr run
}

package() {
  cd python-neutronclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
