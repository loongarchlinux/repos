# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>
pkgname=clojure
pkgver=1.10.3.849
pkgrel=1
pkgdesc='Lisp dialect for the JVM'
url='https://clojure.org/'
arch=(any)
license=(EPL)
depends=(java-environment)
makedepends=(setconf)
optdepends=('leiningen: automate clojure projects'
            'rlwrap: friendly shell with readline support')
install=clojure.install
source=("https://download.clojure.org/install/clojure-tools-$pkgver.tar.gz")
b2sums=('04ec99cc662b27902498d3585b5096cc3ea15eaea147e08dd83bab30c5a19d0711e908673bc1aef57b6b9a0e5883c4227d31eb514bd5fb879af73055ae8349b3')

prepare() {
  cd clojure-tools
  setconf clj bin_dir=/usr/bin
  setconf clojure install_dir=/usr/share/clojure
  echo 'export CLOJURE_HOME=/usr/share/clojure' > clojure.sh
}

package() {
  cd clojure-tools
  install -Dm644 deps.edn "$pkgdir/usr/share/clojure/deps.edn"
  install -Dm644 example-deps.edn "$pkgdir/usr/share/clojure/example-deps.edn"
  install -Dm644 clojure-tools-$pkgver.jar \
    "$pkgdir/usr/share/clojure/libexec/clojure-tools-$pkgver.jar"
  install -Dm755 clojure "$pkgdir/usr/bin/clojure"
  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm755 exec.jar "$pkgdir/usr/share/clojure/libexec/exec.jar"
  install -Dm755 clojure.sh "$pkgdir/etc/profile.d/clojure.sh"
  install -Dm644 clojure.1 "$pkgdir/usr/share/man/man1/clojure.1"
  install -Dm644 clj.1 "$pkgdir/usr/share/man/man1/clj.1"
}

# getver: raw.githubusercontent.com/clojure/brew-install/1.10.3/stable.properties
