# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: katt <magunasu.b97@gmail.com>

pkgname=yt-dlp
pkgver=2021.12.27
pkgrel=1
pkgdesc='A youtube-dl fork with additional features and fixes'
arch=(any)
url='https://github.com/yt-dlp/yt-dlp'
license=(Unlicense)
depends=(python-mutagen python-pycryptodomex python-websockets)
makedepends=(python-setuptools pandoc)
optdepends=('ffmpeg: for video post-processing'
            'rtmpdump: for rtmp streams support'
            'atomicparsley: for embedding thumbnails into m4a files'
            'aria2: for using aria2 as external downloader')
source=($pkgname-$pkgver.tar.gz::https://github.com/yt-dlp/yt-dlp/releases/download/$pkgver/yt-dlp.tar.gz)
sha256sums=('3492c344b1b71ac13c73a53dbb3bc504f7ebaa325c10807ad905428e74a6d630')

build() {
  cd $pkgname
  make pypi-files
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
