# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-sugar
pkgname=haskell-tasty-sugar
pkgver=1.1.1.0
pkgrel=1
pkgdesc="Tests defined by Search Using Golden Answer References"
url="https://github.com/kquick/tasty-sugar"
license=("custom:ISC")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-filemanip' 'haskell-kvitable' 'haskell-logict' 'haskell-microlens'
         'haskell-optparse-applicative' 'haskell-prettyprinter' 'haskell-tagged' 'haskell-tasty')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-pretty-show' 'haskell-raw-strings-qq'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('1b3bc09df225dbd4aaadb21656a0fa6ad1dbbf88c07dd9d0518767310fd106f4')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
