# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgname=python-regex
pkgver=2020.10.11
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('463e770c48da76a8da82b8d4a48a541f314e0df91cbb6d873a341dbe578efafd')
b2sums=('63d620ba264d27147d8f112f9c4621c2194c8ff8c35d54c6691008190f6c7fa73143f496d2fdcd063dcdc66d4f8bd713eeb3fc0c29317540896601c088eae7e3')

build() {
  cd "regex-${pkgver}"

  python setup.py build
}

check() {
  cd "regex-${pkgver}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${pkgver}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  sed -n '1,/^$/p' regex_3/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
