# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>

_pyname=adb-shell
pkgname=python-${_pyname}
pkgver=0.4.2
pkgrel=1
pkgdesc="A Python implementation of ADB with shell and FileSync functionality."
arch=(any)
url="https://github.com/JeffLIrion/adb_shell"
license=('Apache')
depends=('python' 'python-cryptography' 'python-pyasn1' 'python-rsa')
makedepends=('python-setuptools')
optdepends=('python-aiofiles: async API'
            'python-libusb1: To connect to a device via USB')
conflicts=('python-adb_shell')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/JeffLIrion/adb_shell/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('bf81a14a2df57a2109c61106b4597fd9aefab5639b6eb853f2220f6e81497c34b3dcafc2a3fc6dceb836505a5a4fc6b7a73d8a275a4de5065fac6d5625e69b29')

build() {
  cd "${_pyname/-/_}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pyname/-/_}-${pkgver}"
  python setup.py install -O1 --root="$pkgdir" --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
