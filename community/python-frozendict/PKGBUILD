# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-frozendict
pkgver=2.3.3
pkgrel=1
epoch=1
pkgdesc='An immutable dictionary'
url='https://pypi.python.org/pypi/frozendict/'
arch=('any')
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/f/frozendict/frozendict-$pkgver.tar.gz")
sha512sums=('7d29801e2bbf2d0fe377d3e182568d9b50cc548f72d7a3499b6cb5010830fc4610b379c6b5d0f3ad3bf9db9905f747dabde2a5d8c5d5b8deceb2d894e5e3e509')
b2sums=('7ebba5ddd8ee0d89a4c0dd5c841450bd6f551ae46a93549d6018904fda378304345c1300db0a5aaafa21b8db680d4cc7e6cdaa4f76f7d4ad5c7d05e4bdf92bfb')

prepare() {
	cd frozendict-$pkgver
	# remove unneeded tests
	pushd test
	rm -v c_only.py test_frozendict_c.py test_frozendict_c_subclass.py
	popd
}

build() {
	cd frozendict-$pkgver
	python setup.py py build
}

check(){
	cd frozendict-$pkgver
	PYTHONPATH="$PWD/build/lib" pytest
}

package() {
	cd frozendict-$pkgver
	python setup.py py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
