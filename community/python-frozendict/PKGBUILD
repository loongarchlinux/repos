# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-frozendict
pkgver=2.3.2
pkgrel=1
epoch=1
pkgdesc='An immutable dictionary'
url='https://pypi.python.org/pypi/frozendict/'
arch=('any')
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/f/frozendict/frozendict-$pkgver.tar.gz")
sha512sums=('e6674f12d311ba75ea2a7e7dc70c5ffef5b34e5461858c6a0c145437946a938cf7e93ec2e0d3c9321f3e5ca2c6d3bf4707aa57e36803cccc0bf2f26a6f95e57d')
b2sums=('f32874cab519c91a3b006b37c54fd2439527c7cdc796205e84d525d294e27088f702b7fbdc3a9758264be9286c22ef6d6cd324d28edc133b48ad6aa4481b4d06')

prepare() {
	cd frozendict-$pkgver
	# remove unneeded tests
	pushd test
	rm -v c_only.py test_frozendict_c.py test_frozendict_c_subclass.py
	popd
}

build() {
	cd frozendict-$pkgver
	python setup.py py build
}

check(){
	cd frozendict-$pkgver
	PYTHONPATH="$PWD/build/lib" pytest
}

package() {
	cd frozendict-$pkgver
	python setup.py py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
