# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgname=python-furl
pkgver=2.1.0
pkgrel=3
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
depends=('python-orderedmultidict' 'python-six')
makedepends=('python-setuptools')
checkdepends=('flake8' 'python-pycodestyle' 'python-pyflakes')
license=('custom:Unlicense')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c7de9c87e5368a3ccb7c0639b9c679b3c71ac75144f275b2491e88d361bfca8612b3b23e76e538a98b02c1666e2dac000cb9f5add2e97d240cead7f40a1411ca')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {API,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
