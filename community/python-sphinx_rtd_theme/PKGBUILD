# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgname=python-sphinx_rtd_theme
pkgver=1.1.1
pkgrel=1
pkgdesc='Read the Docs theme for Sphinx'
arch=('any')
url=https://github.com/readthedocs/sphinx_rtd_theme
license=('MIT')
depends=('python-sphinx')
makedepends=('nodejs-lts-fermium' 'npm' 'python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel')
source=("$url/archive/$pkgver/sphinx_rtd_theme-$pkgver.tar.gz")
b2sums=('1998051e814fcbee541ffe53ccaa5d1ebdeaa27bb95c2e68c840032660fc105621041f72a12c6f2ec77db400cfef21a57850e351921572a71b52dfa2e0b460fc')

prepare() {
  cd sphinx_rtd_theme-$pkgver
  npm ci
}

build() {
  cd sphinx_rtd_theme-$pkgver
  npm run build
  python -m build --wheel --no-isolation
}

package() {
  cd sphinx_rtd_theme-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
