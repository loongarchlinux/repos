# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Nick Bolten <Shirakawasuna at gmail _dot_ com>

pkgname=gprename
pkgver=20220807
pkgrel=1
pkgdesc="A GTK batch renamer for files and directories"
arch=('any')
url="http://gprename.sourceforge.net/"
license=('GPL3')
depends=('perl-gtk3' 'perl-libintl-perl' 'perl-locale-gettext' 'pango-perl')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        $pkgname-build.patch)
sha256sums=('c51e7021e57a6bfda3c6feab379132793fa4b304a7750a65f4d2e44a97dd661a'
            '8d7207621618e19095167d86efdce64c1a7d19e5fba34baac42c4e393555ef50')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np0 -i "${srcdir}"/$pkgname-build.patch

#29940 fix
  sed 's#/usr/local#/usr#' -i bin/$pkgname
#45250 fix
  sed 's#/usr/share/icons/gprename.png#/usr/share/icons/hicolor/64x64/apps/gprename.png#' -i bin/$pkgname
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make PREFIX=/usr DESTDIR="${pkgdir}"/usr install
}
