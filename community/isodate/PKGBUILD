# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=isodate
pkgname=('python-isodate')
pkgver=0.6.0
pkgrel=9
pkgdesc="An ISO 8601 date/time/duration parser and formatter"
arch=('any')
url="https://github.com/gweis/isodate/"
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/i/isodate/isodate-$pkgver.tar.gz"
        fix-interpret-decimal-object-as-an-integer.patch)
sha1sums=('2c69e08c9894a9cd96c627146463260f5731df62'
          '69f5b1c9478ab5c259924e52380af441055b2452')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -Np1 -i ../fix-interpret-decimal-object-as-an-integer.patch
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py test
}

package_python-isodate() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
