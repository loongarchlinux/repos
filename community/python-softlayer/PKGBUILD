# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-softlayer
pkgver=5.9.7
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools' 'tk')
source=("$pkgname-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz"
         https://github.com/softlayer/softlayer-python/commit/34cad8a2.patch)
sha512sums=('196748b608cf68a01eed145c8e5085974e40ed1b995055e63d349007445b431f730b3d019f337fe28f89ffe219b8a29304994a60c0086f87d97e52579e472cf0'
            '4485deafcc7d4c6c5769d1372c1e2418ee475b39e69e89a82a6bed327223c8b84930b15724a447ef18d5f73b2a04a55b05264ee3ecd4e77cd66f42ae81364821')

prepare() {
  cd softlayer-python-$pkgver
  sed -i 's/==/>=/' setup.py

  patch -p1 < ../34cad8a2.patch # Fix for python 3.10
}

build() {
  cd softlayer-python-$pkgver
  python setup.py build
}

check() {
  cd softlayer-python-$pkgver
  python setup.py pytest --addopts '-k "not test_edit_perms_on_bad"'
}

package() {
  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # It's only present when tests are enabled, so adding -f
  rm -rf "$pkgdir"/usr/lib/python3.10/site-packages/tests
}

# vim:set ts=2 sw=2 et:
