# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Magnus Brevik <janmbrevik@gmail.com>

_pkgname=podcastparser
pkgname=python-podcastparser
pkgver=0.6.6
pkgrel=2
pkgdesc='Simplified, fast RSS parsing library in Python'
arch=('any')
url='https://github.com/gpodder/podcastparser'
license=('ISC')
depends=('python')
checkdepends=('python-pytest-cov')
source=("https://github.com/gpodder/$_pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b36adead23adb138aa12504740d8b875c2eda581bd72cdcb18a579cb5e1ec2fb')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  pytest
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
