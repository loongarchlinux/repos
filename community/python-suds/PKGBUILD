# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-suds
pkgver=0.6
pkgrel=4
pkgdesc="A lightweight SOAP python client"
arch=('any')
url="https://bitbucket.org/jurko/suds"
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/jurko/suds/get/release-$pkgver.tar.gz")
sha512sums=('9930c4e023e937e0f5323bcaae7017af6c4462c240d90cb77ec403da6f4f0ac1304013bdc30d9c0f2f778510b0762f9b771333f75e3dfb5a79dc24961a15c9be')

prepare() {
  mv jurko-suds-* $pkgname-$pkgver
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" -O1
  # https://bugs.archlinux.org/task/57562
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "${pkgdir}/usr/lib/python${_py3_ver%"."*}/site-packages/tests"
}

# vim:set ts=2 sw=2 et:
