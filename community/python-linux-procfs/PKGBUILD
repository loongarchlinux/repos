# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.7.0
pkgrel=1
pkgdesc="Linux /proc abstraction classes for python"
arch=(any)
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git/"
license=(GPL2)
depends=(python-six)
makedepends=(python-setuptools)
source=("https://git.kernel.org/pub/scm/libs/python/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0681ddeddbf415acfe8e53f8067d7a87d4ae515609c47fb7943a651acdf07621a2a0b9ddc378ffede8cdb02e6863c439aec6ab8a5b29d82acd0f3073fc95fc4')
b2sums=('c4e8b9776deed4a53425035873f33070b28e2e76b56dacf3311f8069dd7c1adf5999221934bef7e62702444825d7c25a2964cf716f47b953b45bc27241036bd8')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build/:${PYTHONPATH}"
  python bitmasklist_test.py
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}/"
}
