# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=fastapi
pkgname=python-$_pkgname
pkgver=0.68.1
pkgrel=2
pkgdesc='FastAPI framework, high performance, easy to learn, fast to code, ready for production'
arch=('any')
url='https://github.com/tiangolo/fastapi'
license=('MIT')
depends=('python-starlette' 'python-pydantic')
optdepends=('python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
            'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
            'python-email-validator' 'python-python-multipart')
makedepends=('python-setuptools' 'python-dephell')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-asyncio'
              'python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
              'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
              'python-email-validator'  'python-pyjwt' 'python-sqlalchemy'
              'python-peewee' 'python-flask' 'python-passlib'
              'python-bcrypt' 'python-httpx' 'python-python-multipart'
              'python-databases' 'python-aiosqlite' 'python-jose')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('754f6ede434fcf09fc71753a47b101254350dfda35cf0f500583c531a3dd6439694b61998091a104b79921e6c5855d6696e2736b2b45e44e63b8cbdf66aede79')

prepare() {
  cd $_pkgname-$pkgver

  dephell deps convert --from pyproject.toml --to setup.py

  # don't pin starlette dependency
  sed -i 's|starlette.*"|starlette"|' pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -p no:warnings
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
