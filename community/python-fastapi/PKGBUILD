# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=fastapi
pkgname=python-$_name
pkgver=0.85.1
pkgrel=1
pkgdesc='FastAPI framework, high performance, easy to learn, fast to code, ready for production'
arch=(any)
url="https://github.com/tiangolo/fastapi"
license=(MIT)
depends=(python-pydantic python-starlette)
optdepends=(
  'hypercorn: for Hypercorn as ASGI server'
  'python-email-validator: for email validation'
  'python-itsdangerous: for SessionMiddleware support'
  'python-jinja: for default starlette template configuration'
  'python-orjson: for ORJSONResponse'
  'python-python-multipart: for form parsing support'
  'python-pyyaml: for starlette SchemaGenerator support'
  'python-requests: for TestClient support'
  'python-ujson: for faster JSON parsing and UJSONResponse'
  'uvicorn: for Uvicorn as ASGI server'
)
makedepends=(python-build python-installer python-hatchling python-wheel)
checkdepends=(
  # test dependencies
  python-aiosqlite
  python-anyio
  python-databases
  python-flask
  python-httpx
  python-peewee
  python-pytest
  python-pytest-cov
  python-sqlalchemy
  python-trio

  # optdepends
  python-email-validator
  python-itsdangerous
  python-jinja
  python-orjson
  python-python-multipart
  python-pyyaml
  python-requests
  python-ujson
  uvicorn

  # dev depends
  python-bcrypt
  python-cryptography
  python-jose
  python-passlib
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('608be993d17ed26a61681326d0d39063355548324389f12c2c244c1789b681077056bfc405fcb36a175758141c168e903ee71c0dd80c23a5d9f7e693913fbabe')
b2sums=('2c396f17bceb33335cd5f7e264a643f101af8c14012389071307801e78ac20358f1ceeeba7b65d1cd2e08fc84b03d0eed23b332c3934420ccd9ff6edb20ba0c9')

prepare() {
  cd $_name-$pkgver
  # do not pin starlette dependency
  sed -i 's|starlette.*"|starlette"|' pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _deselected=(
    --deselect docs_src/sql_databases/sql_app_py310/tests/test_sql_app.py::test_create_user
    --deselect docs_src/sql_databases/sql_app_py39/tests/test_sql_app.py::test_create_user
    # disable tests that break due to our python-starlette being too new and using python-httpx instead of python-requests
    --deselect tests/test_enforce_once_required_parameter.py::test_get_invalid
    --deselect tests/test_extra_routes.py::test_delete
    --deselect tests/test_generate_unique_id_function.py::test_warn_duplicate_operation_id
    --deselect tests/test_get_request_body.py::test_get_with_body
    --deselect tests/test_tuples.py::test_tuple_form_valid
    --deselect tests/test_tutorial/test_cookie_params/test_tutorial001.py::test[/items-cookies2-200-expected_response2]
    --deselect tests/test_tutorial/test_cookie_params/test_tutorial001.py::test[/items-cookies3-200-expected_response3]
    --deselect tests/test_tutorial/test_cookie_params/test_tutorial001_py310.py::test[/items-cookies2-200-expected_response2]
    --deselect tests/test_tutorial/test_cookie_params/test_tutorial001_py310.py::test[/items-cookies3-200-expected_response3]
  )

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -p no:warnings "${_deselected[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
