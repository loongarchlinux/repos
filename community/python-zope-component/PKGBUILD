# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=python-zope-component
pkgver=5.0.1
pkgrel=3
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
depends=('python-setuptools' 'python-zope-deferredimport' 'python-zope-deprecation'
         'python-zope-event' 'python-zope-hookable' 'python-zope-interface')
checkdepends=('python-zope-testrunner' 'python-persistent' 'python-zope-location'
              'python-zope-proxy' 'python-zope-security' 'python-zope-configuration' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.component/archive/$pkgver.tar.gz")
sha512sums=('af67167805c0f611c27d6e4b4fcfef2cc707c5b828a3298676e89630abcc70f262edb5e9ab2da15cb424375e29ab8a53a253bff3d91a85f761c1aa86dd0416ef')

build() {
  cd "$srcdir"/zope.component-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.component-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.component-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
