# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libsysstat
pkgver=0.4.3
pkgrel=1
pkgdesc="Library to query system statistics (net, resource usage, ...)"
arch=("x86_64")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/libsysstat/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/libsysstat/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('a34511e4f9fe1ed977ffc65b11948459922760f9a22b225d48d1c7cae9fc6c94b315c1394768f4c7b94ffc28c1267410c21ba55ff89e54976526ea1e5e4d7a56'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
