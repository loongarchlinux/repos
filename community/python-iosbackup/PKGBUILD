# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

_pyname=iOSbackup
pkgname=python-iosbackup
pkgver=0.9.923
pkgrel=2
pkgdesc='Library for reading and extracting files from password-encrypted iOS backups'
arch=(any)
url='https://github.com/avibrazil/iOSbackup'
license=('LGPL')
depends=('python' 'python-nskeyedunarchiver' 'python-pycryptodome')
makedepends=(python-{build,installer,wheel} 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha512sums=('be9a2fb7195cef0b281b59aa191c410480005e588ccd91b474c089e06d93125f5e4fb0c2e52c859c706f6f1cced808ce2d2c65afb2713441df0f68f9816e0315')

build() {
    cd "${_pyname}-${pkgver}"
    python -m build -wn
}

package() {
    cd "${_pyname}-${pkgver}"
    python -m installer -d "$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
