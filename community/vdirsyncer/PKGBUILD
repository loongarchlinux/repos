# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=vdirsyncer
pkgver=0.18.0
pkgrel=3
pkgdesc="Synchronize CalDAV and CardDAV."
arch=(any)
url="https://vdirsyncer.pimutils.org/en/stable/"
license=(BSD)
depends=(python-atomicwrites python-click python-click-log
python-click-threading python-requests python-requests-toolbelt)
makedepends=(git python-setuptools-scm python-sphinx)
checkdepends=(python-hypothesis python-pytest python-pytest-localserver
python-pytest-subtesthack)
optdepends=('python-requests-oauthlib: Google support'
            'python-etesync: Etesync support'
            'python-django: Etesync support')
source=(
  "git+https://github.com/pimutils/vdirsyncer.git#tag=${pkgver}?signed"
  "${pkgname}-0.18.0-remove_broken_fingerprint_test.patch"
)
sha512sums=('SKIP'
            '91cdd6350771025478d44aa260b41357a8142d8bbbf7b63cd821b54d3645b7049a4d44ddb82de37425747c32dfa9a1d3b9ad08c77ec142b30a8281741e0c062d')
validpgpkeys=('951082781CA308E4A529DEFD6F9FFF122B98C0CD') # Hugo Osvaldo Barrera <hugo@barrera.io>

prepare() {
  cd "${pkgname}"
  # remove broken test. upstream will replace it with differing dependencies in an upcoming version
  # https://github.com/pimutils/vdirsyncer/commit/1f6cc6f8beebd644d6570ce98fe56c33a8846daf
  patch -Np1 -i ../"${pkgname}-0.18.0-remove_broken_fingerprint_test.patch"
}

build() {
  cd "${pkgname}"
  python setup.py build
  export PYTHONPATH="build:${PYTHONPATH}"
  sphinx-build -b man docs/ build/
}

check() {
  cd "${pkgname}"
  export DETERMINISTIC_TESTS=true
  pytest --tb=short -c /dev/null
}

package() {
  cd "${pkgname}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # systemd
  install -vDm 644 "contrib/${pkgname}."{service,timer} -t "${pkgdir}/usr/lib/systemd/user/"
  # man page
  install -vDm 644 "build/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
  # docs
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
