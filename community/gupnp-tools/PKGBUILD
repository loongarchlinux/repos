# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gupnp-tools
pkgver=0.10.2
pkgrel=1
pkgdesc="Utilities and demos to work with UPnP"
arch=('loongarch64' 'x86_64')
url="https://wiki.gnome.org/Projects/GUPnP"
license=('GPL')
depends=('gtksourceview4' 'gupnp' 'gupnp-av')
makedepends=('meson')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('6de49ef4b375b8a164f74b766168b1184e0d28196b6b07a4f5341f08dfd85d6c')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
