# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=sphinx-autobuild
pkgname=python-$_pkgname
pkgver=2020.09.01
pkgrel=2
pkgdesc='Rebuild Sphinx documentation on changes, with live-reload in the browser'
arch=('any')
url='https://github.com/executablebooks/sphinx-autobuild'
license=('BSD')
depends=('python' 'python-sphinx' 'python-livereload')
makedepends=('python-setuptools' 'python-dephell')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('fc72ba86749ff186eab52458241d687eaf99f5faf6f33ecd588f988826c5e3f02bd7a592c30d67faa122f146b2c483c2df7ad23937ee3ad46d80ed580063ce12')

prepare() {
  cd $_pkgname-$pkgver

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=src pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
