# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=python-argparse-manpage
pkgver=1.5
pkgrel=3
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=('any')
license=('Apache')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praiskup/argparse-manpage/archive/v${pkgver}.tar.gz")
sha512sums=('bfdea19fa62ac029754d6df26c4fb51c18f7fee4fae681c1117253627e724cf1ff1db5196ad0690fd436cebbe403536905f157cb7eae7be50d321c663b491fd0')

build() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py test
}

package() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
