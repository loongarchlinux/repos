# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=python-argparse-manpage
pkgver=1.1
pkgrel=3
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=('any')
license=('Apache')
makedepends=('python-setuptools')
source=("https://github.com/praiskup/argparse-manpage/archive/v${pkgver}.tar.gz")
sha512sums=('2479d06558390439835f0ca8a8af3c48e5d905b51274add7d4a4047911ad3a01259e1342b540d002e33263a19e590326d62a9167bbf1df6ccb5183fb7825edcc')

build() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py test
}

package() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
