# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aioitertools
_pkgname=aioitertools
pkgver=0.7.0
pkgrel=3
pkgdesc='Implementation of itertools, builtins, and more for AsyncIO and mixed-type iterables'
arch=(any)
url='https://github.com/jreese/aioitertools'
license=(MIT)
depends=(python python-typing_extensions)
makedepends=(python-setuptools)
source=("https://files.pythonhosted.org/packages/source/a/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('341cb05a0903177ef1b73d4cc12c92aee18e81c364e0138f4efc7ec3c47b8177')

prepare() {
  cd $_pkgname-$pkgver
  # setup.py generated by flit uses setuptools-specific install_requires but
  # imports setup() from distutils.core
  # https://github.com/takluyver/flit/blob/2.3.0/flit/sdist.py#L16
  sed -i 's#distutils.core#setuptools#' setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest -v aioitertools.tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
