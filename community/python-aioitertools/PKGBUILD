# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aioitertools
_pkgname=aioitertools
pkgver=0.8.0
pkgrel=3
pkgdesc='Implementation of itertools, builtins, and more for AsyncIO and mixed-type iterables'
arch=(any)
url='https://github.com/omnilib/aioitertools'
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
source=("https://files.pythonhosted.org/packages/source/a/$_pkgname/$_pkgname-$pkgver.tar.gz"
        python310.patch)
sha256sums=('8b02facfbc9b0f1867739949a223f3d3267ed8663691cc95abd94e2c1d8c2b46'
            'c45fcea71f7c551b0452cb4cdc30359b1724d4af74e818ec1793dfd31f940918')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../python310.patch
  # setup.py generated by flit uses setuptools-specific install_requires but
  # imports setup() from distutils.core
  # https://github.com/takluyver/flit/blob/2.3.0/flit/sdist.py#L16
  sed -i 's#distutils.core#setuptools#' setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest -v aioitertools.tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
