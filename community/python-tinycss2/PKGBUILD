# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-tinycss2
pkgver=1.1.1
pkgrel=1
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD')
arch=('any')
depends=('python-webencodings')
makedepends=('python-pyproject2setuppy')
checkdepends=('python-pytest-cov' 'python-pytest-isort' 'python-pytest-runner')
_css_tests_commit=c5749e51dda3868b3e8062e65a36584c2fec8059
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz"
        css-parsing-tests-$_css_tests_commit.tar.gz::https://github.com/SimonSapin/css-parsing-tests/archive/$_css_tests_commit.tar.gz)
sha512sums=('297ac51cf4eb9a063e88d677b8cf2511ff6c459c7a385ee8ba6bb4b9d9b0d8482f5caa1cc6511d3edefbe8e8ee284d72fedc154a5372880cd7d9a9a26aab1a62'
            'ef44eb1144a6484f308fabe4187310b2ef0067f328bf5213969d87c475fccae3d716dc66640a0a554ab2a3d7da0efb3a168a288ac6eef7722f3ae46959943b49')

prepare() {
  cd tinycss2-$pkgver
  sed -i 's/--flake8//' pyproject.toml
  cp ../css-parsing-tests-$_css_tests_commit/* tests/css-parsing-tests/
}

build() {
  cd tinycss2-$pkgver
  python -m pyproject2setuppy build
}

check() {
  cd tinycss2-$pkgver
  PYTHONPATH=build/lib pytest
}

package() {
  cd tinycss2-$pkgver
  python -m pyproject2setuppy install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
