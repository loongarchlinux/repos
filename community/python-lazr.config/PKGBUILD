# Maintainer: David Runge <dvzrv@archlinux.org>

_name=lazr.config
pkgname=python-lazr.config
pkgver=2.2.2
pkgrel=2
pkgdesc="Create configuration schemas, and process and validate configurations"
arch=('any')
url="https://launchpad.net/lazr.config"
license=('LGPL3')
depends=('python-lazr.delegates' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-tox')
provides=('python-lazr-config')
replaces=('python-lazr-config')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('3427d2be4b4763238bd8378ec5936d38d0b944074d56f6ada55d0e3f232fe0846630a0ef3900c169b58092d029297d7c937ea4ad349aaa2745ed25b71df9c7a0'
            'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # TODO: this is ugly, but tests won't run either with pytest nor using nose
  # directly for some reason, due to coverage import errors
  tox -e py38
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {HACKING,NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
