# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=go-ethereum
pkgver=1.10.4
_commit=aa637fd38a379db6da98df0d520fb1c5139a18ce
pkgrel=1
pkgdesc='Official Go implementation of the Ethereum protocol'
arch=('loongarch64' 'x86_64')
url='https://geth.ethereum.org/'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go')
provides=('geth')
conflicts=('geth')
replaces=('geth')
source=("git+https://github.com/ethereum/go-ethereum.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
    cd ${pkgname}

    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie"
    make all
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin build/bin/*
}
