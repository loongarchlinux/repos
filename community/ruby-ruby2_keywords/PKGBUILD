# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Anatoly Bashmakov <anatoly at posteo dot net>

_gemname='ruby2_keywords'
pkgname="ruby-${_gemname}"
pkgver=0.0.4
pkgrel=3
pkgdesc='Shim library for Module#ruby2_keywords'
arch=('any')
url='https://github.com/ruby/ruby2_keywords'
license=('RUBY' 'BSD')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler')
checkdepends=('ruby-test-unit')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('b5e278d26a4a02717e3193b6bb005c66718840da6491d2fb73c3d9f1a9a8654365f5cb1a2200c90f8b93c48c364c81ce3423cb5616bffcaa27e16f67644e9c55')

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
