# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonschema-spec
pkgver=0.1.1
pkgrel=1
pkgdesc="JSONSchema Spec with object-oriented paths"
url="https://github.com/p1c2u/jsonschema-spec"
license=('Apache')
arch=('any')
depends=('python-jsonschema' 'python-pathable' 'python-yaml' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("https://github.com/p1c2u/jsonschema-spec/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b1bdd03c6fc022877d0801d859abe0dd19e066afe2a1eab6c650a8741a50473e74945ddde429508e5dccf379a84b25fbd60c49fdc310aa770bed52b63e60e397')

prepare() {
  cd jsonschema-spec-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd jsonschema-spec-$pkgver
  python -m build -wn
}

check() {
  cd jsonschema-spec-$pkgver
  PYTHONPATH="$PWD" pytest
}

package() {
  cd jsonschema-spec-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
