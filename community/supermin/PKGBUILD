# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor:  Achilleas Pipinellis <axilleas archlinux gr>

pkgname=supermin
pkgver=5.2.1
pkgrel=2
pkgdesc="Tool for creating supermin appliances"
arch=('loongarch64' 'x86_64')
url="http://people.redhat.com/~rjones/supermin/"
license=('GPL')
makedepends=('ocaml' 'ocaml-findlib')
depends=('e2fsprogs' 'pacman' 'pacman-contrib' 'cpio')
conflicts=('febootstrap<=3.21')
source=("https://download.libguestfs.org/${pkgname}/5.2-stable/${pkgname}-${pkgver}.tar.gz"{,.sig}
        "pacman6.patch")
sha512sums=('f10ea404e0b27238ea3c25cb68f44e716aa180a45a420e63c1958768032f0710e8e3e7f1346cda780ea1f916f499499da2c02df4019d91ea3b3a69b75cfda545'
            'SKIP'
            'd1fe3258715171c7fd4be4a4a279d6c2caa75304aa3cf6b95c74a1b720a0ea71df7296f184e8a525273285abdafb2c4bda82145c4c10d398753cbc8dfea21161')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i ../pacman6.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
