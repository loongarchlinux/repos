# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-lumberjack
pkgver=1.2.8
pkgrel=1
pkgdesc='A simple, powerful, and very fast logging utility that can be a drop in replacement for Logger'
arch=(any)
url='https://github.com/bdurand/lumberjack'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-rspec ruby-timecop)
options=(!emptydirs)
source=(https://github.com/bdurand/lumberjack/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8b6fb22bcb2e29b3a076e6f5eda7a2ddd3cf3e2177f139b768b863be85b74187')

prepare() {
  cd lumberjack-$pkgver
  sed -i 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -printf "%P\\\\0"|' lumberjack.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd lumberjack-$pkgver
  gem build lumberjack.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    lumberjack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd lumberjack-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd lumberjack-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT_LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
