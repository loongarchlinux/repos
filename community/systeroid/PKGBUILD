# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=systeroid
pkgver=0.3.0
pkgrel=1
pkgdesc="A more powerful alternative to sysctl"
arch=('loong64' 'x86_64')
url="https://github.com/orhun/systeroid"
license=('MIT' 'Apache')
depends=('libxcb' 'libxkbcommon' 'linux-docs')
makedepends=('cargo' 'python')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0851199a75dd0c8867df826c5ccc5f9c8e5bddcdcc39c22803e45ada8c2b9bf2e5e40497583303b00c1d103ac0150686701e63c5ff1e96760c716aa3c0fa4d97')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  NO_COLOR=1 cargo test --frozen --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-tui" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man8/$pkgname.8" -t "$pkgdir/usr/share/man/man8"
  install -Dm 644 "man8/$pkgname-tui.8" -t "$pkgdir/usr/share/man/man8"
}

# vim: ts=2 sw=2 et:
