# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=pytest-subtesthack
pkgname=python-${_name}
pkgver=0.1.2
pkgrel=3
pkgdesc="A hack to explicitly set up and tear down fixtures"
arch=('any')
url="https://github.com/untitaker/pytest-subtesthack"
license=('custom')
depends=('python-pytest')
makedepends=('python-setuptools')
source=("${url}/archive/${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('ddb7b0c27f912bec31e58ddc54930e229755df8980d146c861f7f1c766ae742b4f1552117d89638bdbd443f22890262320a56565954ebc6deeae6184f459a5a8')
b2sums=('f1f5a052f9418732de4a97ff438d280d177860439e667e29ff0d0503740e009c88046b5ba859884e5fa9e958ea1be7388d7fc6838c19e44075ab0ef757ee41c7')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
  install -vDm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.rst
}
