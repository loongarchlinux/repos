# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-deck
_name=deck
pkgver=1.6.0
pkgrel=1
# upstream doesn't adhere to semver:
# https://github.com/nextcloud/deck/issues/1197
epoch=1
pkgdesc="Kanban style organization tool aimed at personal planning and project organization"
arch=(any)
url="https://github.com/nextcloud/deck"
license=(AGPL3)
groups=(nextcloud-apps)
makedepends=(nextcloud ripgrep yq)
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}.tar.gz")
sha512sums=('522b4d9f5eb347dcc07f53610511e8f8a01fe5a1d584ee87599ab5575217f811855c5acca1913e0fdb285e8c47c6f1654d047bfe907b1a43784b879de20ba44d')
b2sums=('888b963f383bcd8014978c5bcb4fb2b6b9b0fe484cfda702b9123793471777b4ab59f6c61ec09eb4b8a0631a5ae1c56f52948b7f1e2e3ba112437b5673e1963b')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
