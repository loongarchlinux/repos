# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.6.5
pkgrel=1
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('loong64' 'x86_64')
url='https://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess/goaccess.conf')
source=("https://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('3dfde3c1a60041a158ecc53c5d37982f02f8843b2224d4fe28897b99044bdf0ec91b6091b5d1d10cf276ee17f0ea4787c84ada03a0631e0a8a2694c71d537bdd')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
