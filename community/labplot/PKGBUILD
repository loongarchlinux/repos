# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=labplot
pkgver=2.8.0
pkgrel=2
arch=(x86_64)
pkgdesc="KDE Application for interactive graphing and analysis of scientific data"
url="https://labplot.kde.org/"
license=(GPL2)
depends=(cantor netcdf cfitsio fftw gsl qt5-serialport libcerf qt5-mqtt kuserfeedback)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        labplot-cantor-20.08.1.patch::"https://invent.kde.org/education/labplot/-/commit/4255438c.patch")
sha256sums=('8957c708ea572d9b7bfd7bdd1e9627bb1e10717c4b8cb10c7687ca6ca187a705'
            'a3cb9853ca8736753662fba2cea13d8d38398f933659a3f86cfe84c6dc14ad68')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../labplot-cantor-20.08.1.patch # Fix build with cantor 20.08.1
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
