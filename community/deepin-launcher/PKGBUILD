# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=5.5.34
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'xdg-user-dirs')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/dde-launcher/pull/329.patch)
sha512sums=('0c5efd8ca3fb810b1eb81153341dfdc30be83fcdf4bc0e70762d0b51bede99a0789806e362b8649e82446842b992db0cb3baba8149d344fd3153621cf66d96b7'
            '1f27cf6895abdeea688621b37e01972ec8939e6370fd2ac9b1fe5b299dc4b05cd377119150d757cc1609dfc782cde71f3fca1d729ca33f9a7c5c3252d1cd485a')

prepare() {
  cd dde-launcher-$pkgver
  patch -p1 -i ../$pkgname-ldflags.patch
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

check() {
  cd dde-launcher-$pkgver
  # ninja test
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
