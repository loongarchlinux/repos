# Maintainer: David Runge <dvzrv@archlinux.org>

_name=authheaders
pkgname=python-authheaders
pkgver=0.14.1
pkgrel=3
pkgdesc="Library for the generation of email authentication headers"
arch=('any')
url="https://github.com/ValiMail/authentication-headers"
license=('BSD' 'MPL2' 'ZPL')
depends=('publicsuffix-list' 'python-authres' 'python-dkim' 'python-dnspython'
'python-publicsuffix2')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc}
        psddmarc.csv::https://www.psddmarc.org/psddmarc-participants.csv)
sha512sums=('842881a6d2c3d192664960a9d1954f6e2c267cc6b07d7567f019869d07c1e40fc12275c381e90ea94559bd43a9d2e183d35e80d0ee9a861e80519a98daf2a699'
            'SKIP'
            'f3840255c1901111a563ca87ae155853d7e00d82a7e479520b789779c3c0d7273123a52abb419085d0561f25ffdaca70de76167935189a1cc20189d1f2f60b8a')
validpgpkeys=('E7729BFFBE85400FEEEE23B178D7DEFB9AD59AF1') # Donald Scott Kitterman <scott@kitterman.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # embed psddmarc.csv: https://github.com/ValiMail/authentication-headers/issues/4
  cp -av ../psddmarc.csv "${_name}/"
}

build() {
  cd "$pkgname-$pkgver"
  # use the system provided publicsuffix list (PSL)
  python setup.py psllocal --path='/usr/share/publicsuffix/public_suffix_list.dat'
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CHANGES,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
