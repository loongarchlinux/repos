# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

_name=flake8-docstrings
pkgname=python-flake8-docstrings
pkgver=1.6.0
pkgrel=2
pkgdesc='Plugin to Flake8 to include checks provided by pydocstyle'
arch=('any')
url=https://gitlab.com/pycqa/flake8-docstrings
license=('MIT')
depends=('flake8' 'python-pydocstyle')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('9fe7c6a306064af8e62a055c2f61e9eb1da55f84bb39caef2b84ce53708ac34b')
b2sums=('ee147e27f226675a2a59375b9e7812d111a34bef1bc35843096f8e7ca9cd94ac805ca49a8d83d1f6d7a78dbf6338a0a1270d668b51775a2b91ad7c3172ad41da')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m flake8 flake8_docstrings.py
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
