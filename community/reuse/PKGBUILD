# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
_pkgname=reuse-tool
pkgver=0.14.0
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://github.com/fsfe/reuse-tool'
license=('GPL3' 'Apache' 'custom:CC-BY-SA-4.0' 'custom:CC0-1.0')
depends=(
  'python'
  'python-debian'
  'python-requests'
  'python-license-expression'
  'python-boolean.py'
  'python-jinja'
  'python-binaryornot'
)
makedepends=(
  'python-setuptools' 
  'python-setuptools-scm'
)
checkdepends=('python-pytest')
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2bacfd6d2ce79f64afc605ae08ca72f11834ab91aa2e90e330b08335384563ff3caa6150edb237b50c83c2be4deb4f88acfb52ce8efd6e42259f51451c0098ac')
b2sums=('b8dbe73846974ca8725a5c261d3cec90c151d7e076654fd43e258529fd55c4f57779308db903a55f4ebfa24168d4485d42709b4fdda2cb1d1bae01703c9627c5')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

check() {
  cd "$_pkgname-$pkgver"

  pytest
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install \
    --root="$pkgdir" \
    --optimize=1 \
    --skip-build

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
