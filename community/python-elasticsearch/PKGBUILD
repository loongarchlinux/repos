# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elasticsearch
pkgver=7.8.1
pkgrel=5
arch=('any')
pkgdesc='Python client for Elasticsearch'
url='https://github.com/elastic/elasticsearch-py'
license=('Apache')
depends=('python-urllib3')
makedepends=('python-setuptools' 'git')
checkdepends=('python-requests' 'python-mock' 'python-yaml' 'python-pytest' 'python-numpy'
              'python-pandas' 'python-aiohttp' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elastic/elasticsearch-py/archive/$pkgver.tar.gz"
        "$pkgname-python39.patch::https://github.com/elastic/elasticsearch-py/commit/7747f2b25753.patch"
        python310.patch)
#       git+https://github.com/elastic/elasticsearch.git)
sha512sums=('46f4295bca0da9b1fb4e8dea6ef511e4f37c342394e24c36712f63e3c75bc76c60f37a81350b7873465e129a635cbf70dd7039daae13e851cfbcc995f91bd432'
            '053e6cc452278c3b62c47c9168ec42935e441cdb43107ff793bd40bcd7f60d23f093bb4c0e67f05d1200c436dd7194632a5514ec24caf4aab1ad6e6bb8f1c7b2'
            '495694c1d89ad453fd1d2e83092a741a256cbe5627ebcca22770b340a9ac77fabcdea83fa84f1c9c0ffe734517a099f98ee8a95f1384b5f716eb34273a21b39f')

prepare() {
  cd elasticsearch-py-$pkgver
  patch -Np1 -i ../$pkgname-python39.patch
  patch -Np1 -F3 -i ../python310.patch
  sed -i 's/, "certifi"//' setup.py
  sed -i 's|CA_CERTS = None|CA_CERTS = "/etc/ssl/certs/ca-certificates.crt"|' \
    elasticsearch/{connection/http_urllib3.py,_async/http_aiohttp.py}
}

build() {
  cd elasticsearch-py-$pkgver
  python setup.py build
}

check() {
  cd elasticsearch-py-$pkgver
  # Skip tests that requires a running elasticsearch server
  #TEST_ES_REPO="$srcdir"/elasticsearch
  pytest --deselect test_elasticsearch/test_async/test_server
}

package() {
  cd elasticsearch-py-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
