# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=4.9
pkgrel=1
pkgdesc='Personal and small-business financial-accounting application'
arch=(loongarch64 x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking gwenhywfar boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gtest libdbi-drivers mariadb-libs postgresql-libs swig)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('9a551c30c1b712199fc2227a109afb984d7bef18e138170bbc7225c6c1fab72d')

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
