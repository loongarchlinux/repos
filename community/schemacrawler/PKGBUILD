# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=schemacrawler
pkgver=16.15.11
pkgrel=2
pkgdesc="Free database schema discovery and comprehension tool"
arch=('any')
url="https://www.schemacrawler.com"
license=('GPL3')
depends=('java-runtime' 'graphviz')
makedepends=('maven' 'java-environment' 'unzip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/schemacrawler/SchemaCrawler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3fdc404969829e91fce3ea7fb9c40866577c5c091b7a52d183017a258fcebfb6093efc2ec10587a9f1465c9b2d7cea52af7bb7fb27fc2298647e6aadf92951ea')

build() {
  cd SchemaCrawler-$pkgver

  mvn -Ddistrib package
}

package() {
  cd SchemaCrawler-$pkgver/schemacrawler-distrib/target
  unzip schemacrawler-$pkgver-distribution.zip
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r schemacrawler-$pkgver-distribution "$pkgdir"/opt/schemacrawler
  echo -e '#!/bin/bash\ncd /opt/schemacrawler/_schemacrawler\n./schemacrawler.sh $@' > "$pkgdir"/usr/bin/schemacrawler
  chmod -R +r "$pkgdir"/opt/schemacrawler
  chmod +x "$pkgdir"/usr/bin/schemacrawler
}

# vim: ts=2 sw=2 et:
