# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=schemacrawler
pkgver=16.19.1
pkgrel=1
pkgdesc="Free database schema discovery and comprehension tool"
arch=('any')
url="https://www.schemacrawler.com"
license=('GPL3')
depends=('java-runtime' 'graphviz')
makedepends=('maven' 'java-environment=17' 'unzip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/schemacrawler/SchemaCrawler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('55fcc42d15a27052d5484e23cc4479b4d8a1e0ba1100693cf7b274a3530e4c34c868cc32d76e0f5369aa84dbcd42dfb976e9c1714a93a0fc2380edb99089b82a')

build() {
  cd SchemaCrawler-$pkgver

  export JAVA_HOME="/usr/lib/jvm/java-17-openjdk"
  mvn -Ddistrib -DskipTests package
}

package() {
  cd SchemaCrawler-$pkgver/schemacrawler-distrib/target
  unzip schemacrawler-$pkgver-distribution.zip
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r schemacrawler-$pkgver-distribution "$pkgdir"/opt/schemacrawler
  echo -e '#!/bin/bash\ncd /opt/schemacrawler/_schemacrawler/bin\n./schemacrawler.sh $@' > "$pkgdir"/usr/bin/schemacrawler
  chmod -R +r "$pkgdir"/opt/schemacrawler
  chmod +x "$pkgdir"/usr/bin/schemacrawler
}

# vim: ts=2 sw=2 et:
