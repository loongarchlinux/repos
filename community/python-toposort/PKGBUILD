# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-toposort
pkgver=1.5
pkgrel=4
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://bitbucket.org/ericvsmith/toposort'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/ericvsmith/toposort/get/$pkgver.tar.bz2")
sha512sums=('f79ee4a04b157db164e484be054bed6d310023722cd13469454022eb96ccd3ff5bf6dc570384d3ca790c819b67afa4a96c716da2b99c458d8ddfe2e56e5222a0')

prepare() {
  mv ericvsmith-toposort-* toposort-$pkgver
}

build() {
  cd toposort-$pkgver
  python setup.py build
}

check() {
  cd toposort-$pkgver
  python setup.py test
}

package() {
  cd toposort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
