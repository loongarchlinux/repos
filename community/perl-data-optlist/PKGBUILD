# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-data-optlist
pkgver=0.112
pkgrel=1
pkgdesc="Parse and validate simple name/value option pairs"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-params-util' 'perl-scalar-list-utils' 'perl-sub-install')
makedepends=('perl-extutils-makemaker')
url="https://metacpan.org/release/Data-OptList"
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Data-OptList-$pkgver.tar.gz)
sha512sums=('79198ef244b4e5cb5201f938f228f20158ccdde76ff9d200b706104ddac5dc3ac8da3280ba335a60fd66d16202b803447ac7709a7d84e514d3a3dc59327ddc71')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""  \
      PERL_AUTOINSTALL=--skipdeps             \
      PERL_MM_OPT="INSTALLDIRS=vendor"        \
      PERL_MB_OPT="--installdirs vendor"      \
      MODULEBUILDRC=/dev/null
    cd "$srcdir"/Data-OptList-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$srcdir"/Data-OptList-$pkgver

    make test
  )
}

package() {
  cd "$srcdir"/Data-OptList-$pkgver

  make DESTDIR="$pkgdir"/ install
}
