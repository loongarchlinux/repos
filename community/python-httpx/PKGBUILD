# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=httpx
pkgname=python-httpx
pkgver=0.14.0
pkgrel=1
pkgdesc="A next generation HTTP client for Python"
arch=('any')
url="https://github.com/encode/${_pkgname}"
license=('BSD')
depends=('python-chardet' 'python-httpcore' 'python-idna' 'python-rfc3986' 'python-sniffio')
optdepends=('python-brotli: for brotli response decompression')
makedepends=('python-setuptools')
checkdepends=('python-pytest-asyncio' 'python-pytest-trio' 'python-brotli' 'python-h2' 'python-trustme' 'uvicorn')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "0001-Do-not-override-the-system-SSL-certificates-with-the.patch")
sha512sums=('1a49a2f112a324e6a4b4cd76a6c13bf53ab96332356c8fafa71bd37a4e6bc5f888df0c85937ac0287d7cd21f3eadb18b18155e8911550cfe48946b283309c22c'
            'e4b1e33a77cdb5555a4ac3d09b59bc4a0a176a7f5d97099d66a4876abcfd3889058e21beb8bfc375e8e7e61b3808d2a76119033e1a44602d8e6cd461ea3f88de')
b2sums=('93cc96ba3862f4119bcd67dde7539aee6ecffe504f533b38ad698fe9159b18b5333719b25df739d5b3073d7e423e9798016f08b677869d4816357c986657f126'
        'e03d340cef44bfb322219030bbcbe53c1323c8b9d9d7aace07045e80faa185a7594839c9ff0fee9604a7fddf916db462f95a341d51f17072bce8e55d53055c7b')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run coverage in unittests!
    sed -i '/^addopts/d' setup.cfg

    # bad certifi
    patch -p1 -i ../0001-Do-not-override-the-system-SSL-certificates-with-the.patch
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
