# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=4.0
pkgrel=3
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(x86_64)
url="https://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('gensio' 'libyaml')
backup=(etc/ser2net/ser2net.yaml)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
	ser2net.service)
sha256sums=('8673311fe3d2726c7f12094f46cdf13ec4bf3cfdcb6620d121e2c3f7d0a7bcf0'
            'b093e1f5bf7dd826816828623a5c6858f83a9f0dcb1a9785f545b29efd1c320c')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR="$pkgdir" install
  install -Dm0644 ser2net.yaml "$pkgdir"/etc/ser2net/ser2net.yaml
  install -Dm0644 "$srcdir"/ser2net.service "$pkgdir"/usr/lib/systemd/system/ser2net.service
}
