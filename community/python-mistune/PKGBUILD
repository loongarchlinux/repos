# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: shmilee

_pkgname=mistune
pkgname=python-mistune
pkgver=2.0.0
pkgrel=2
pkgdesc="A fast yet powerful Python Markdown parser with renderers and plugins"
arch=(any)
url="https://github.com/lepture/mistune"
license=(BSD)
depends=(python)
makedepends=(python-build python-install python-setuptools python-wheel)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lepture/mistune/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('4d63695f7153e409adcadec2e053987e6f4c7e627a73c0338a163fe04a8eb2f0e0c6f0222063dc49b724f89f6a56eeebf6404aef30146a49760311e5a87c5d7b')
b2sums=('bebebe26ed581095824c86d044d4f4564915c5bdba3e1fab5470a6b6ebb0775d8c1ec995d38a281b31f8d23d221f1d0024375bfd19adec284245ee9a155d6c8e')

build() {
  cd "${_pkgname}-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_pkgname}-$pkgver"
  pytest -vv
}

package() {
  cd "${_pkgname}-$pkgver"
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
