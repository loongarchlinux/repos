# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: shmilee

_pkgname=mistune
pkgname=python-mistune
pkgver=2.0.3
pkgrel=2
pkgdesc="A fast yet powerful Python Markdown parser with renderers and plugins"
arch=(any)
url="https://github.com/lepture/mistune"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lepture/mistune/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('1a88b77201d771f1c67b134363d26fba1becb1306e13d2d4fc9216b0ebf6716c17dd6ecba4cbc201e4cab25216597c652a505b14f6446296624af4a9aee85faa')
b2sums=('987b702969b2fa72945781cadf22d650acb79157c3c9a1181208662cc5d5b8d91718f75249f57bd6c69ff36a7a300e9b96816631894e673621e383be4727741b')

build() {
  cd "${_pkgname}-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_pkgname}-$pkgver"
  pytest -vv
}

package() {
  cd "${_pkgname}-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
