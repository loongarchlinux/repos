# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: shmilee

_pkgname=mistune
pkgname=python-mistune
pkgver=2.0.4
pkgrel=1
pkgdesc="A fast yet powerful Python Markdown parser with renderers and plugins"
arch=(any)
url="https://github.com/lepture/mistune"
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lepture/mistune/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e600e3b6766b1f5d69a5737d3e685a3087e80fbe48e499be47598991922c8b909acdf360e14f784ba0c81a8c7a214da32900dd10ed050f3c2a3e192b17d3dcf2')
b2sums=('e70f39a18fddc800ed0a9ef4394fc6b9799cc5dfe0814760899049bf3f12fb8d5acd035754b7c3ba5a98e18640d9a33c2ece464fb2fe427ab29db4a9a91f3fcd')

build() {
  cd "${_pkgname}-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_pkgname}-$pkgver"
  pytest -vv
}

package() {
  cd "${_pkgname}-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
