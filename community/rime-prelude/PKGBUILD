# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=rime-prelude
pkgname=(rime-prelude librime-data)
pkgver=0.0.0.20200908
_commit=f372f522d095cfde272893b827f0a406fb7b861d
pkgrel=1
pkgdesc="Essential files for building up your Rime configuration"
arch=('any')
url="https://github.com/rime/rime-prelude"
license=('LGPL')
source=("https://github.com/rime/rime-prelude/archive/$_commit/rime-prelude-$_commit.tar.gz")
sha512sums=('e09c75c9bf667b9c2bf571460491f47505cbf3635105ecdc5b4e00166d608b5264d619575973ca76fe9131d086f99dbf9d2a9a647c413ce15457baf10344d8d3')

package_rime-prelude() {
  pkgdesc="Essential files for building up your Rime configuration"

  cd $pkgname-$_commit
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
}

package_librime-data() {
  pkgdesc="Meta package of preset librime data"
  depends=('rime-prelude' 'rime-essay' 'rime-luna-pinyin' 'rime-bopomofo' 'rime-cangjie' 'rime-stroke' 'rime-terra-pinyin')
  replaces=('brise')
  conflicts=('brise')
}
