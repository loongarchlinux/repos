# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

_name=pytesseract
pkgname=python-pytesseract
pkgver=0.3.8
pkgrel=1
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-pillow' 'tesseract')
makedepends=('python-setuptools' )
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("$url/archive/v$pkgver/$_name-v$pkgver.tar.gz")
b2sums=('601610e14c259ab69ca0f4b17e3ea0478c8904c524c573b960859036684cdef5aaf16268e454e4d7f16e5b54d6d4522dd758642c70de3b23822f7e2bf4cfd861')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
