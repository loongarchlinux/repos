# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=fabric
pkgver=2.7.0
pkgrel=1
pkgdesc="Python library and command-line tool designed to streamline deploying applications or performing system administration tasks via the SSH protocol"
url="https://www.fabfile.org/"
license=('BSD')
arch=('any')
depends=('python-paramiko' 'python-invoke')
optdepends=('python-patchwork: Common deployment/sysadmin operations')
makedepends=(python-setuptools python-wheel python-build python-installer)
source=("https://github.com/fabric/fabric/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7e364ffd1e9956397042490daa76bc12db88eea090ce35c3d43408346158ff05948196905cec448cc2480f36d432a45675c6937ca96f81f9d42e226be8f6c8fc')

build() {
  cd "${srcdir}"/fabric-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}"/fabric-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 README.rst "${pkgdir}"/usr/share/doc/${pkgname}/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
