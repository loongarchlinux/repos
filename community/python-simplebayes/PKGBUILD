# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=simplebayes
pkgname=python-simplebayes
pkgver=1.5.8
pkgrel=1
pkgdesc='Memory-based, optional-persistence naïve bayesian text classifier Python library'
arch=('any')
url='https://github.com/hickeroar/simplebayes'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
#source=("https://github.com/hickeroar/$_pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
source=(https://pypi.io/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('363418c0ef185ac2158ebbd6d8afb45aa997254fcb809a73ed20a7d5dccf8b85')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  nosetests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
