# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=python-kubernetes
_pkgbase=kubernetes
pkgver=10.0.1
pkgrel=2
license=('Apache')
pkgdesc='Python client for the kubernetes API'
arch=('any')
url='https://github.com/kubernetes-client/python'
depends=('python' 'python-certifi' 'python-six' 'python-dateutil'
         'python-urllib3' 'python-yaml' 'python-google-auth'
         'python-websocket-client' 'python-requests' 'python-requests-oauthlib'
         'python-adal')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pluggy' 'python-py' 'python-mock' 'python-recommonmark' 'python-isort')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase::1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('3770a496663396ad1def665eeadb947b3f45217a08b64b10c01a57e981ac8592')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  :
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  py.test -vvv -s --ignore=kubernetes/e2e_test
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
