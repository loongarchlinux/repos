# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.10
pkgrel=2
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment java-runtime)
makedepends=(git)
optdepends=('leiningen-completions: completion scripts'
            'rlwrap: for readline support')
noextract=("$pkgname-$pkgver-standalone.jar")
source=("$url/releases/download/$pkgver/$pkgname-$pkgver-standalone.jar"{,.asc}
        "git+$url#commit=27e04f41501e8b9a153e7b654837067881ecd704") # tag: 2.9.10
validpgpkeys=('6A2D483DB59437EBB97D09B1040193357D0606ED') # Phil Hagelberg <phil@hagelb.org>
b2sums=('70e0614a2516e04047fadce10ace583bd84e68e8bff96efcab7a3cea4d687bb91e5317d326a9fabfa7413a1e11d45be6abc9062934a5b09be8010f50d23b2467'
        'SKIP'
        'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 $pkgname-$pkgver-standalone.jar \
                 "$pkgdir/usr/share/java/$pkgname-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
