# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

pkgbase=python-coverage
pkgname=(python-coverage python2-coverage)
pkgver=5.3
pkgrel=1
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('loongarch64' 'x86_64')
url="https://nedbatchelder.com/code/coverage/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-flaky' 'python-hypothesis' 'python-mock'
'python-pycontracts' 'python-pytest' 'python-pytest-xdist' 'python-toml'
'python-unittest-mixins' 'python-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/coverage-$pkgver.tar.gz")
sha512sums=('fb61b583d39d5f055742031e24b8b6509215fd5d4cfdae76391e8f9c03ab4d88c2955c4e751b2d792d841491991872d788f8204fc157e273ce6c254152bb4039')
b2sums=('d84bc7907da7d66e3f767b97116b9ef061652b1090420d0b6271de5b2883d8736fc422410f405b4bc1d056dad655fc9d474b3f95011ba453481eea7e62e2b480')

prepare() {
  mv -v "coveragepy-coverage-$pkgver" "${pkgbase}-$pkgver"
}

build() {
  cd "${pkgbase}-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-$pkgver"
  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package_python-coverage() {
  depends=('python-setuptools')
  optdepends=('python-toml: for pyproject.toml support')
  cd "${pkgbase}-$pkgver"

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
}

package_python2-coverage() {
  depends=('python2-setuptools')
  optdepends=('python2-toml: for pyproject.toml support')
  cd "${pkgbase}-$pkgver"

  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # circumvent file conflicts with python-coverage
  mv -v "$pkgdir/usr/bin/coverage"{,2}
}
