# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.42.1
pkgrel=2
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('loongarch64' 'x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('atkmm' 'bluez-libs' 'cairo' 'cairomm' 'gcc-libs' 'glibc' 'glibmm'
'gtkmm3' 'libsigc++' 'libx11' 'pangomm' 'ttf-roboto')
makedepends=('avahi' 'boost' 'curl' 'gdk-pixbuf2' 'eigen' 'faust' 'fftw'
'glib2' 'gperf' 'gtk3' 'intltool' 'jack' 'ladspa' 'liblo' 'liblrdf' 'lilv'
'lv2' 'pango' 'sassc' 'waf' 'zita-convolver' 'zita-resampler')
optdepends=('new-session-manager: for session management')
provides=('guitarix2' 'libgxw.so' 'libgxwmm.so' 'ladspa-host' 'lv2-host')
conflicts=('guitarix2')
replaces=('guitarix2')
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz"
        gcc11-compat-volatile-atomic-variables.patch)
sha512sums=('66a71b4d66e0cbcff7feeceae6317c016e497626c9fde3ca44e1f06e42d9021b2857151660c1c49832cc8e6f2aa27de68dfea67d4e896c7e89f84865f8ba1eb0'
            '05d66714c72e1c2a647d3d769f4d81f6a48d653f9bd3094a5306343b3838fe34d137d6c34b0955a3889a6feb7f1b2a9ac237ca2ec42190dffff0822b3ac706b2')
b2sums=('06b2a19d68e77c622eea222e3f0658d9f89c6c8377e9b0442d581e14342f82f97948ca0ebb7ff40f4abd6cab0c9ba582fa6f43f74cad5e112d2e762445f2ab43'
        '0281b89eca6dc32051226c09ddb6255e5c8bfd4a278aced69f995954d2dcc5c7d7ad9299d6827afa37cdb857f8b236976663fa63165b94fb5029a473ae07bb09')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../gcc11-compat-volatile-atomic-variables.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=('libavahi-common.so' 'libavahi-gobject.so' 'libboost_iostreams.so'
  'libcurl.so' 'libfftw3f.so' 'libgdk-3.so' 'libgdk_pixbuf-2.0.so'
  'libgio-2.0.so' 'libglib-2.0.so' 'libgobject-2.0.so' 'libjack.so'
  'liblilv-0.so' 'liblo.so' 'liblrdf.so' 'libpangocairo-1.0.so'
  'libpango-1.0.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
