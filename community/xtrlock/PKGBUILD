# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.13
pkgrel=2
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libx11' 'libxcrypt' 'libcrypt.so')
source=(http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz)
sha512sums=('656e1e77194b816b04260a88c8046b8ef25d6df0405de6fe1ee1dfb2ebfa528a329c1b67be70eaac5ce4ee6a4159d728d363bf8a59232941b76b1e6d14aa91aa')
b2sums=('879c27936cf8058707cbdb2247e1044c8c5eadf74dcb2bcfd5ec8f4b2bab80da6859118e48943de97f7696e41456771bf274b03728a61f5ac6bef8b471192dce')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD" \
    LDLIBS="-lX11 -lcrypt" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
