# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=SolidPython
pkgname=python-solidpython
pkgver=1.1.1
pkgrel=3
pkgdesc='Python frontend for solid modelling that compiles to OpenSCAD'
arch=('any')
url='https://github.com/SolidCode/SolidPython'
license=('LGPL')
depends=('python-euclid3' 'python-pypng' 'python-prettytable' 'python-regex')
makedepends=('python-setuptools' 'python-dephell')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('fa8b28e6d0adf9cdfa762a85adcd5f8dea3ea5a4136d51dc836b333dc701475b82802af226edbef785185a3e124d778d02f1fecee931475e96e88facbfb4e79e')

prepare() {
  cd $_pkgname-$pkgver

  sed -i -e '/tox/d' -e 's|regex = "^2019.4"|regex = ">=2019.4"|' pyproject.toml

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=$(pwd) solid/test/run_all_tests.sh
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
