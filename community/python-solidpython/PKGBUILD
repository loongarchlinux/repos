# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=SolidPython
pkgname=python-solidpython
pkgver=1.1.3
pkgrel=1
_commit=0a4f539c31a25df52a42bab2ceeffafd45596f73 # missing tag for 1.1.3
pkgdesc='Python frontend for solid modelling that compiles to OpenSCAD'
arch=('any')
url='https://github.com/SolidCode/SolidPython'
license=('LGPL')
depends=('python-euclid3' 'python-pypng' 'python-prettytable')
makedepends=('python-build' 'python-installer' 'python-poetry' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-runner')
source=("https://github.com/SolidCode/SolidPython/archive/$_commit.tar.gz")
sha512sums=('d0f701e9508721e3f7f5a2b65dfe5692cac425374f3b0136a5792c5fa93ba7c3295288149cc07ad9c902df7b973e1c80f1023d44ec6919c33f52590a15573f6d')

prepare() {
  cd $_pkgname-*

  sed -i -e '/tox/d' -e 's|setuptools>=.*|setuptools\",|' pyproject.toml
}

build() {
  cd $_pkgname-*

  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-*

  PYTHONPATH=$(pwd) solid/test/run_all_tests.sh
}

package() {
  cd $_pkgname-*

  python -m installer --destdir="$pkgdir" dist/*.whl
}
