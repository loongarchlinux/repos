# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2020.04.12.2
_tag=2020.04.12-2
pkgrel=2
epoch=2
pkgdesc='Base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
groups=('deepin')
source=("$pkgname-$_tag.tar.gz::https://github.com/linuxdeepin/deepin-desktop-base/archive/$_tag.tar.gz"
        distribution.info)
sha512sums=('7b37a48936d653c22353e9a8779c3644df9a675150217a06c16a6d40a9d6ddbfc90bd18f1d60c58d947dc641a69a699219202b77644299d0f3b5dc1426db6615'
            '27625e6d0786b8adacdb7c52806d4faa28d2ab6b319a593b3ea9bcb69f0cc18ea19b258d629e3a0069ef9a69503589b0285289caef39a1e85bbd99e915c7cd7d')

build() {
  cd $pkgname-$_tag
  make
}

package() {
  cd $pkgname-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/distribution.info -t "$pkgdir"/usr/share/deepin/

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version

  # Remove UOS logo
  rm "$pkgdir"/usr/share/deepin/uos_logo.svg

  # Remove apt-specific templates
  rm -r "$pkgdir"/usr/share/python-apt
}
