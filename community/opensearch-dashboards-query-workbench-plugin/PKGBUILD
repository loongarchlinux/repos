# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-query-workbench-plugin'
_pluginname='query-workbench-dashboards-plugin'
pkgver=1.2.2.0
_dashboardsver=1.2.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Query Workbench Plugin'
url='https://opensearch.org/docs/latest/query-workbench/index/'
arch=('loongarch64' 'x86_64')
license=('Apache')
depends=('opensearch-dashboards')
makedepends=('yarn' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/sql.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=(
  'SKIP'
  'SKIP'
)

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "OpenSearch-Dashboards/package.json" \
    "sql/workbench/package.json"
}

build() {
  mv "sql/workbench" "OpenSearch-Dashboards/plugins/${_pluginname}"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn build --skip-archive
}

# TODO: Fix tests
#check() {
#  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
#  NODE_PATH=../../node_modules yarn test:jest
#}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
}

# vim: ts=2 sw=2 et:
