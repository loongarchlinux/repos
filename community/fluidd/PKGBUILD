# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd
pkgver=1.18.2
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL3)
makedepends=(
  git
  nodejs-lts-gallium
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=e3b763e715ae96df54c758a448293f97eadc990a
source=(git+https://github.com/fluidd-core/fluidd.git?signed#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('SKIP')

build() {
  cd fluidd
  npm install --frozen-lockfile
  npx vue-cli-service build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
