# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wand
pkgver=0.6.6
pkgrel=1
pkgdesc="Ctypes-based simple MagickWand API binding for Python"
url="https://github.com/emcconville/wand"
license=('MIT')
arch=('any')
depends=('imagemagick' 'libxml2' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-memory-profiler' 'python-psutil'
              'ghostscript' 'librsvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/emcconville/wand/archive/$pkgver.tar.gz")
sha512sums=('8135088da9d26b444e809a8a99f7b07d39e283a3167acd7ed7943b0944dc0486598de40896c1db1aa984dfb9b598b6e030a14223b8367eef697e9373697e39c5')

build() {
  cd wand-$pkgver
  python setup.py build
}

check() {
  cd wand-$pkgver
  python setup.py pytest || :
}

package() {
  cd wand-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
