# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pynitrokey
pkgname=python-pynitrokey
pkgver=0.4.3.1
pkgrel=1
pkgdesc="A command line interface for the Nitrokey FIDO2 and Nitrokey Start"
arch=('any')
url="https://github.com/Nitrokey/pynitrokey/releases"
license=('Apache')
depends=('python-cbor' 'python-cffi' 'python-click' 'python-cryptography'
'python-dateutil' 'python-ecdsa' 'python-fido2' 'python-intelhex'
'python-pygments' 'python-pyserial' 'python-pyusb' 'python-requests'
'python-urllib3')
makedepends=('python-dephell' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('14972c367aa938f11ae928e3353688f7fa819b3ef82f0d5c95c9e75a4fa33db0c973009f11ff079c42eee301e7c510556a257ed30b7e41df44cee43d72d9f449')
b2sums=('aa4c9cf81e6965b9662ed82d5fc369cda3e261460bb66a1ed1ef7b9820942c7d91dd5fdaa5a8b1293943d6bf4599f43fcce759e83a030486a7d27f6a10dd052c')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # flit creates a weird setup.py, so we use dephell to create one:
  # https://github.com/Nitrokey/pynitrokey/issues/88
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # when using dephell to create the setup.py, it doesn't install the VERSION file, because reasons
  install -vDm 644 "${_name}/VERSION" "${pkgdir}/${_site_packages}/${_name}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
