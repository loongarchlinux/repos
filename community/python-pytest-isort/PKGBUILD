# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-isort
pkgver=1.2.0
pkgrel=1
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='https://github.com/moccu/pytest-isort'
depends=('python-pytest-cache' 'python-isort')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/moccu/pytest-isort/archive/$pkgver.tar.gz")
sha512sums=('521ce288479a253d5b405b27f72e6777b9f5581e00ab7a93870f0df30c98d3d3694ef2b55a5b9eb9a4501c7389194bfa59692c5ff3fb53621f811e0f99a49e4c')

build() {
  cd pytest-isort-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
