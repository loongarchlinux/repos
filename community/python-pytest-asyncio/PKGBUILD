# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.18.3
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-flaky' 'python-hypothesis' 'python-pytest-trio')
source=("https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('04d2046c039131598cb593afc1908be0dff2b24a91c89f36fca75d2d65916571e48fe149c1ba90244f299586277320cf6f130a02824fde9ee0a4d2429bf58a54')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
