# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>

pkgname=python-matrix-common
pkgver=1.1.0
pkgrel=1
pkgdesc="Common code for Synapse, Sydent and Sygnal"
arch=(any)
url="https://github.com/matrix-org/matrix-python-common"
license=('Apache')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-twisted' 'python-service-identity' 'python-aiounittest')
source=($pkgname-$pkgver.tar.gz::"https://github.com/matrix-org/matrix-python-common/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('22cb3844f4e442e92c1a1dc1e5d0559525c0e375063a5adb0fe7c7b5051d94cbdeb114ee12acf84ffc97e4fd9b351417a6a624fd156c98acee5dc1bb650eb2a7')
b2sums=('90fe64187dadcabae90753f2bc5486bc9e958820334d718734d69172f4d8920d0d86dd9e6211fae7e893140d67d88e8f21d4cebb03ff6e8ef6c220d713d2de1c')

build() {
	cd "matrix-python-common-$pkgver"
	python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
	cd "matrix-python-common-$pkgver"
	PYTHONPATH=. trial tests
}

package() {
	cd "matrix-python-common-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
