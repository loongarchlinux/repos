# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>

pkgname=python-matrix-common
pkgver=1.2.1
pkgrel=1
pkgdesc="Common code for Synapse, Sydent and Sygnal"
arch=(any)
url="https://github.com/matrix-org/matrix-python-common"
license=('Apache')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-twisted' 'python-service-identity' 'python-aiounittest' 'python-pip')
source=($pkgname-$pkgver.tar.gz::"https://github.com/matrix-org/matrix-python-common/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('9ab05d6aeddf4915970916d34e625b7a059823e56ba8868f955cf8971a51ac31891304a7fb00170c60c6b107f7526565f67bd78416a466c50092954c5b422114')
b2sums=('94f25d90c9df948054739cee39033c1f261e61300aa74b7ce606cf8a3c82e716687aa57ddb0f72b8617ae56c21486b50a68f9e6c8611e2f8b19c33b0d2a557e7')

build() {
	cd "matrix-python-common-$pkgver"
	python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
	cd "matrix-python-common-$pkgver"
	pip install dist/*.whl
	PYTHONPATH=. trial tests
}

package() {
	cd "matrix-python-common-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
}
