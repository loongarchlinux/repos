# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=1.7.1
pkgrel=2
pkgdesc='The Pantheon Screenshot Tool'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=57731e24610912038e279b03e4fbeae01378e7df
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-screenshot

  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
