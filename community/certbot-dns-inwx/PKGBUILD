# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=certbot-dns-inwx
pkgver=2.1.3
pkgrel=3
pkgdesc="INWX DNS authenticator plugin for certbot"
arch=(any)
url="https://github.com/oGGy990/certbot-dns-inwx"
license=(Apache)
makedepends=(python-setuptools)
depends=(certbot python)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6abbb0dd8a54406bd2c2c58db09d93a7a98a1eb8507170cc7db58c1955d52b3d2a3cdb054bba237f92a5485eefdbc9dc2ab53a618f907271c337d61441e2c3b6')
backup=(
  "etc/letsencrypt/inwx.cfg"
)

build() {
  cd "${pkgname}-${pkgver}"

  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm 644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"

  chmod 0600 "${pkgdir}/etc/letsencrypt/inwx.cfg"
}
