# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Leewings Ng <osideal@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-table-chinese
pkgver=1.8.10
pkgrel=1
pkgdesc="Some table-based input method of tables engines for IBus"
arch=('any')
url="https://github.com/mike-fabian/ibus-table-chinese"
license=('GPL3')
depends=('ibus-table')
makedepends=('cmake-fedora')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mike-fabian/ibus-table-chinese/archive/$pkgver.tar.gz")
sha512sums=('2a42d4d2bffb99d23ff8d6ae32a9e1acc3769433bdd829dd7bd53bcb4a3b0bae2c06d35fca544821d70f6b989265e0b74e5397e028fcf62bc41d2328af9b1a08')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_FEDORA_TMP_DIR=. -DDATA_DIR=/usr/share -DPRJ_DOC_DIR=/usr/share/doc/$pkgname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
