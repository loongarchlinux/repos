# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Leewings Ng <osideal@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-table-chinese
pkgver=1.8.4
pkgrel=1
pkgdesc="Some table-based input method of tables engines for IBus"
arch=('any')
url="https://github.com/mike-fabian/ibus-table-chinese"
license=('GPL3')
depends=('ibus-table')
makedepends=('cmake-fedora')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mike-fabian/ibus-table-chinese/archive/$pkgver.tar.gz")
sha512sums=('a6b3b2f821715042693ccb9230f125308dba01877091e28c386a6f755cca9710b0fe95e53ad3db9c51a8718150d40555b737ec9a5cf9eb9085ca3ec5f2b75de3')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_FEDORA_TMP_DIR=. -DDATA_DIR=/usr/share -DPRJ_DOC_DIR=/usr/share/doc/$pkgname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
