# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Leewings Ng <osideal@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-table-chinese
pkgver=1.8.5
pkgrel=1
pkgdesc="Some table-based input method of tables engines for IBus"
arch=('any')
url="https://github.com/mike-fabian/ibus-table-chinese"
license=('GPL3')
depends=('ibus-table')
makedepends=('cmake-fedora')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mike-fabian/ibus-table-chinese/archive/$pkgver.tar.gz")
sha512sums=('5af18274cd972930f6b44a33478e149f0d4127654f8cb97a8739b3b05102bae45f3631587050001ec6399feaed123918a6c665305d626acc89f111667ae2e614')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_FEDORA_TMP_DIR=. -DDATA_DIR=/usr/share -DPRJ_DOC_DIR=/usr/share/doc/$pkgname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
