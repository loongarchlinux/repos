# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-validators
pkgver=0.19.0
pkgrel=1
pkgdesc="Python Data Validation for Humans"
url="https://github.com/kvesteri/validators"
license=('MIT')
arch=('any')
depends=('python-decorator' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/kvesteri/validators/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bc643e3b77521af4fa19e9a08e809d67196b4c05918e8286ae0071de84d1f544dbd725645095b9db679e28fcde5c67fba68e314254efcdb587e836c1d05d8fd2')

build() {
  cd validators-$pkgver
  python setup.py build
}

check() {
  cd validators-$pkgver
  python -m pytest
}

package() {
  cd validators-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
