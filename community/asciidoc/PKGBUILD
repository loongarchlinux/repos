# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=9.0.2
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('any')
url='https://asciidoc.org/'
license=('GPL')
depends=('python' 'libxslt' 'docbook-xsl')
checkdepends=('dblatex' 'graphviz' 'lilypond' 'source-highlight')
optdepends=('graphviz: graphviz-filter'
            'lilypond: music-filter'
            'imagemagick: music-filter'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'w3m: text generation'
            'lynx: alternative text generation')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asciidoc/asciidoc-py3/archive/${pkgver}.tar.gz")
sha256sums=('ea73425151f56f278433e442f8b5085599765fa120574db65e6d053eb52927e2')
b2sums=('5f5deca6b8c15d4b86e2bd6fdb131908eecd862a5fdbbd8418937050d335a25ad012a3b1bc716379f5623db676176fc6da1a09691c2b42a213a6230281626b69')

prepare() {
  cd ${pkgname}-py3-${pkgver}

  autoconf
}

build() {
  cd ${pkgname}-py3-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make manpages
}

check() {
  cd ${pkgname}-py3-${pkgver}

  # this overrides the mocked time in the test
  # https://github.com/asciidoc/asciidoc-py3/issues/131
  env -u SOURCE_DATE_EPOCH make test
}

package() {
  cd ${pkgname}-py3-${pkgver}

  make install DESTDIR="${pkgdir}"
  make docs DESTDIR="${pkgdir}"

  # junk files: https://github.com/asciidoc/asciidoc-py3/issues/133
  rm -v "${pkgdir}"/usr/share/doc/asciidoc/doc/*.{md5,svg,png}
  install -Dm644 asciidocapi.py \
    "${pkgdir}/$(python -c 'import sysconfig; print(sysconfig.get_paths()["platlib"])')"/asciidocapi.py
}
