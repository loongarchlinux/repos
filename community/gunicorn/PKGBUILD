# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgname=gunicorn
pkgver=20.1.0
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='https://gunicorn.org/'
license=('MIT')
depends=('python-setuptools')
optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
            'python-gevent: For asynchronous request handling with gevent.')
checkdepends=('python-aiohttp' 'python-coverage' 'python-pytest' 'python-pytest-cov'
              'python-eventlet' 'python-gevent')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benoitc/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('4fd905f62adc30e044cf2a56a1a77e14bc633258267d6bfbd4f6a68494f93f377e9fb9ed94fab7f11f9d7813857a680974a88c4b6bf97d4f1b74792a81810111')

prepare() {
  sed -e 's/==/>=/' -e 's/,<.*$//' -i gunicorn-$pkgver/requirements_test.txt
}

build() {
  cd gunicorn-$pkgver
  python setup.py build
}

check() {
  cd gunicorn-$pkgver
  python -m pytest
}

package() {
  cd gunicorn-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
