# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Muflone http://www.muflone.com/contacts/english/

_pkgname=qtpy
pkgname=python-qtpy
pkgver=1.11.2
pkgrel=3
pkgdesc="Provides an uniform layer to support PyQt5 and PySide2 with a single codebase"
arch=(any)
url="https://github.com/spyder-ide/qtpy/"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
optdepends=('python-pyqt5: Qt5 Python bindings'
            'python-pyqt5-webengine: Qt5-WebEngine python bindings'
            'pyside2: PySide Qt5 Python bindings')
checkdepends=(
    python-pytest
    python-mock
    python-pyqt5
    python-pyqt5-webengine
    pyside2
    qt5-serialport
    qt5-svg
    qt5-tools
    qt5-websockets
    qt5-xmlpatterns
    xorg-server-xvfb
)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0c93485cff68c02449dcf2b8db413fffd7467da16fa106b18b96d4eff2e68d73')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  xvfb-run --auto-servernum pytest -x qtpy -v -rw
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
