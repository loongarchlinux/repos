# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

pkgname=curaengine
pkgver=4.7.1
pkgrel=2
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus' 'polyclipping')
checkdepends=('cppunit')
makedepends=('cmake' 'git' 'gmock' 'gtest' 'rapidjson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('e5006290f6f0c2a1816e93344094c46b4861f7b953948edd1812a2a06869d77d66d6145d40b1c72b31ada028a102fd8ca463aa112ec9983e13dd13d5ff4ac9b3')


build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=ON \
     -DUSE_SYSTEM_LIBS=ON \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
