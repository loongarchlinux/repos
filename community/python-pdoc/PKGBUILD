# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pdoc
pkgver=7.1.1
pkgrel=1
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://pdoc.dev/'
license=('Unlicense')
depends=('python-jinja' 'python-pygments' 'python-markupsafe')
checkdepends=('python-pytest' 'python-pytest-timeout' 'python-hypothesis')
source=("https://github.com/mitmproxy/pdoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0a4ad2c028a6632dcfb43bff89b9a5bfcd0250613349487ca5d910dd1eac9eea9124034cb0039f42f6ab0093a85759214375ba13c2bbeac7dac286908c43d77e')

build() {
  cd pdoc-$pkgver
  python setup.py build
}

check() {
  cd pdoc-$pkgver

  # test #11 of test_snapshot.py currently fails
  pytest --deselect test/test_snapshot.py::test_snapshots[html-snapshot11]
}

package() {
  cd pdoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
