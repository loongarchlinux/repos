# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pdoc
pkgver=12.0.2
pkgrel=1
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://pdoc.dev/'
license=('Unlicense')
depends=('python-jinja' 'python-pygments' 'python-markupsafe')
checkdepends=('python-pytest' 'python-pytest-timeout' 'python-hypothesis' 'nodejs')
source=("https://github.com/mitmproxy/pdoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7fa7628dafd527156d98c772b1b460e663926c85ea2eebde4b2d09f1df7f5344b06ac23a1b994675551e7a686ee5561c19d8a1fcc7a003ce2c8937402118a92b')

build() {
  cd pdoc-$pkgver
  python setup.py build
}

check() {
  cd pdoc-$pkgver

  PYTHONPATH="$PWD" python test/test_snapshot.py
  # test #10 of test_snapshot.py currently fails
  pytest --deselect test/test_snapshot.py::test_snapshots[html-snapshot10]
}

package() {
  cd pdoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
