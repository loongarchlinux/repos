# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pdoc
pkgver=12.1.0
pkgrel=1
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://pdoc.dev/'
license=('Unlicense')
depends=('python-jinja' 'python-pygments' 'python-markupsafe')
checkdepends=('python-pytest' 'python-pytest-timeout' 'python-hypothesis' 'nodejs')
makedepends=('python-setuptools')
source=("https://github.com/mitmproxy/pdoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('12d85165d4a66b2e6770c5acef1743182600215e38dca8ae16686ae78e472edf9990229d0dc6f31989695ce5069156d15b1406ccdcd6e46a7d1524c4ecc75c15')

build() {
  cd pdoc-$pkgver
  python setup.py build
}

check() {
  cd pdoc-$pkgver

#  PYTHONPATH="$PWD" python test/test_snapshot.py
  pytest -v
}

package() {
  cd pdoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
