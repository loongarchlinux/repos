# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filter-rspamd
pkgname=opensmtpd-${_pkg}
pkgver=0.1.6
pkgrel=2
pkgdesc="OpenSMTPD filter integration for Rspamd"
arch=(x86_64)
url=https://github.com/poolpOrg/filter-rspamd
license=(ISC)
depends=(glibc rspamd)
makedepends=(go)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d452143902c713f2d277651e8322b6da00d8531f32b8c78f7c945b4e9302083df2ad419614ed2a9d4d31270d8edd0c4e9ac8a11c9f470fcf8364347e101ac456')

build() {
  cd ${_pkg}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -v -o ${_pkg}
}

package() {
  cd ${_pkg}-$pkgver
  install -D filter-rspamd -t "${pkgdir}"/usr/lib/smtpd/opensmtpd
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}/
}
