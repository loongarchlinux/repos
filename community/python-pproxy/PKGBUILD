# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-pproxy
pkgver=2.3.7
# The commit that bumps version. Upstream does not use git tags [1], and the
# source tarball on PyPI does not include LICENSE
# https://github.com/qwj/python-proxy/issues/91
_commit=7fccf8dd62204f34b0aa3a70fc568fd6ddff7728
pkgrel=1
pkgdesc="Versatile TCP/UDP asynchronous tunnel proxy implemented in Python 3 asyncio"
arch=('any')
url="https://github.com/qwj/python-proxy"
license=('MIT')
depends=(python)
makedepends=(python-setuptools)
optdepends=(
  'python-pycryptodome: accelerated cipher operations'
  'python-uvloop: faster asynchronous operations'
  'python-daemon: run the server as a daemon'
)
source=(https://github.com/qwj/python-proxy/archive/$_commit/pproxy-$_commit.tar.gz)
sha256sums=('c3ab3d36e18611ff0f4eb50ac60de9f1e86124829e970ec4109594521f8d425e')

build() {
  cd python-proxy-$_commit
  python setup.py build
}

package() {
  cd python-proxy-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
