# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtracefs
pkgname=(libtracefs libtracefs-docs)
pkgver=1.4.0
pkgrel=1
pkgdesc="Linux kernel trace file system library"
arch=(loongarch64 x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/about/"
license=(GPL2 LGPL2.1)
makedepends=(asciidoc libtraceevent xmlto)
provides=(libtracefs.so)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.2.0-docs_makefile.patch
)
sha512sums=('d25d666a1be9fc6c9d04afc8701891c34faa5877c6af571c4942c883bdc7d8504d732f937f0182ecdc2bae6c88737880b1ad563e6d27e32a48492ba1542adcff'
            'bdfffba9957d889c60baea9c197709bf1a62d30ac4b8fd47d930b8d5b67fa040817b9a835e226dd1201f2ccdb7fcb43a12093c2b559fb6c0edc3e9ba167062c2')
b2sums=('c8f9a538da2a1ca489d4affdb34312a7dc846ab2a489ec70f8aa3caed1c3139b9b3e9d5a965da7d050dc793bd01867cae8c0baa2880c7e62fea703f867de32dd'
        '1340af2c65dc344912715c69827d5849fda83e4bcf136da3ccdbee4e3b508e9b557af0828914dff8d27abebbdbdab975d8800432d5252b6b1eb0da08b0cb4b02')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.2.0-docs_makefile.patch
}

build() {
  make LDFLAGS="$LDFLAGS" -C $pkgname-$pkgver
  make doc -C $pkgname-$pkgver
}

package_libtracefs() {
  depends+=(glibc libtraceevent.so)
  optdepends=('libtracefs-docs: for documentation')

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation

  (
    cd "$pkgdir"
    _pick libtracefs-docs usr/share/doc
  )
}

package_libtracefs-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
