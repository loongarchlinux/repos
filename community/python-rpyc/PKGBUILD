# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-rpyc
_name=rpyc
pkgver=5.0.1
pkgrel=3
pkgdesc='Remote Python Call - A transparent and symmetric RPC library for python'
url='https://github.com/tomerfiliba-org/rpyc'
arch=('any')
license=('MIT')
depends=('python' 'python-plumbum')
makedepends=('python-setuptools')
source=(https://github.com/tomerfiliba-org/${_name}/archive/${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('57b0d6b2dba33c7f6c9d204b4deae8ab08ed56e5799aa2c7ae4bcb8ab98486055090e3079d8b0d615fcd517111a6e02893dff85607aaca5497ae76429abe3148')
b2sums=('84d0bee67004928c26a1765a26267e3e8eb5dc09a8fd53c1d75ca691cf313a7563398626b7e89a8893415294721623206204784da01444708edb56aec8101719')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
