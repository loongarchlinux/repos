# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-rpyc
_name=rpyc
pkgver=5.3.0
pkgrel=1
pkgdesc='Remote Python Call - A transparent and symmetric RPC library for python'
url='https://github.com/tomerfiliba-org/rpyc'
arch=('any')
license=('MIT')
depends=('python' 'python-plumbum')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
source=(https://github.com/tomerfiliba-org/${_name}/archive/${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('371c60dedf3fba3232c5c1d7616a04b8cf2a8295e31b945ab51d0dee5ba5d8769062aafa860db85b951f0283b8800b61972a7f2f7fa3cc69929c7563e74be1ee')
b2sums=('c7b0c06ea7151d80d84e057b7991cbc7bcb047f24544b7669d80218f9eeb9342ba58020f608bb615dea932a1380b0afb36ba9f8e216f139ba4aadd4a7efa344d')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
