# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-rpyc
_name=rpyc
pkgver=5.1.0
pkgrel=1
pkgdesc='Remote Python Call - A transparent and symmetric RPC library for python'
url='https://github.com/tomerfiliba-org/rpyc'
arch=('any')
license=('MIT')
depends=('python' 'python-plumbum')
makedepends=('python-setuptools')
source=(https://github.com/tomerfiliba-org/${_name}/archive/${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('e9783392f423abca6b488dff5be36dae066980004bd221ca5a1c451f0f9dc7e1daf749650106802f9159de619a8d5e2cd6dc90e9a5f49d16f01bbedd59b504cf')
b2sums=('4be17e5de4b0475cd63b99bf20e55e3221149f974b9efec638e0387d71815a5a2e4ea782578152690e019e7b8a4ece17405eed6626578a9c9e379d0f4b589008')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
