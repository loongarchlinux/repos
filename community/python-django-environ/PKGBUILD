# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-environ
pkgname=python-django-environ
pkgver=0.8.1
pkgrel=3
pkgdesc="Utilize 12factor inspired environment variables to configure your Django application"
arch=('any')
url="https://github.com/joke2k/django-environ"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('20f30c2479fe50b4185c8cd252bfdc8d4c9e7c169742b45d616698de018d457144b8ecd7d552aee65d1573db7b39fd578f5c9d101115a16d6b5e330429a7b6c6')
b2sums=('df2db4f76673504dfe0f89b04e98abe43b230fc6cb8f5b869ea888f907bc6179963532e3b794df6e4e54346fe3b0238edf54725853384d0b4488d1a771e381c2')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -vv
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
