# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.path
pkgver=3.3.1
pkgrel=2
pkgdesc="Miscellaneous path functions"
url="https://github.com/jaraco/jaraco.path"
license=('MIT')
arch=('any')
depends=('python-jaraco')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/jaraco.path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1bdf0fb2ec2070285b4760fc752f7c30317e9076e7eb4bf3abd5dcdd35cbba45f86b8b3c523555afd77c0bf5a47f35146ca22e48e6677c80a62fd9b00affaffe')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jaraco.path-$pkgver
  sed -i '/singledispatch/d' setup.cfg
  sed -i 's/from singledispatch import singledispatch/from functools import singledispatch/' jaraco/path.py
}

build() {
  cd jaraco.path-$pkgver
  python setup.py build
}

check() {
  cd jaraco.path-$pkgver
  python -m pytest
}

package() {
  cd jaraco.path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
