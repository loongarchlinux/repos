# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=sha3sum
pkgver=1.1.5
pkgrel=3
pkgdesc="Keccak-family checksum calculator, including SHA-3"
arch=('x86_64')
url='https://github.com/maandree/sha3sum'
license=('custom:ISC')
depends=('libkeccak>=1.2')
source=("https://github.com/maandree/sha3sum/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('e7820ee2a4c6f6cde4417e21dac7ae128a5041a8d76b70a99eaf02c86d2d115af52b2b65f1222c9be431a90c3d7044e5911c228ec80789f493749920b51427fe')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' config.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
