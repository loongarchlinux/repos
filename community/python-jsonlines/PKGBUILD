# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonlines
pkgver=3.0.0
pkgrel=1
pkgdesc="Library with helpers for the jsonlines file format"
url="https://github.com/wbolster/jsonlines"
license=('BSD')
arch=('any')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/wbolster/jsonlines/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0939e9654ec2ddf930fb1b3ef9dbdf675a481400519753a9f751f2b8874ca06c0bcfc76c256e7fc152b5ea265d9c895a9ef695d69cbab170a4e73d684c703505')

build() {
  cd jsonlines-$pkgver
  python setup.py build
}

check() {
  cd jsonlines-$pkgver
  python -m pytest
}

package() {
  cd jsonlines-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
