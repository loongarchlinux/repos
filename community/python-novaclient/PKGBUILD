# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-novaclient
pkgver=17.5.0
pkgrel=1
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-novaclient/archive/$pkgver.tar.gz")
sha512sums=('9912cb7f4449d96eeead174fad358550f85922b8887649304f775f03916d3959416ea5a4fe4cce5144f2d73ee4177bb65fc334e4d903c2f6bce92af4135d9991')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-novaclient-$pkgver/requirements.txt
}

build() {
  cd python-novaclient-$pkgver
  python setup.py build
}

check() {
  cd python-novaclient-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/python-novaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
