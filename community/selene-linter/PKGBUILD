# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=selene
pkgname=selene-linter
pkgver=0.20.0
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('loong64' 'x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('69f885db88acc85d34fe28afc37157b074c268c6ed279e5edf77769976d88aa17c2fe12a627d94805f746438469eb40ea2db421b134abddfba01854a15138e2f')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
}

# vim:set ts=2 sw=2 et:
