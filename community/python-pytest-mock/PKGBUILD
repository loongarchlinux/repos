# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mock
pkgver=3.7.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-asyncio')
source=("https://github.com/pytest-dev/pytest-mock/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('567e6f4144304b32d395a67e5762006202889b6b078a8a5064495684caaecce1205d37982c116ccc2f43cac4de5695cdc38c3197c40598424e41a2967053f520')

build() {
  cd pytest-mock-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python setup.py build
}

check() {
  cd pytest-mock-$pkgver
  python setup.py egg_info
  export PYTHONPATH="src:${PYTHONPATH}"
  # disable all tests that require the plugin to be installed
  pytest -v --assert=plain \
    -k "not test_used_with_session_scope \
        and not test_used_with_package_scope \
        and not test_used_with_module_scope \
        and not test_used_with_class_scope \
        and not test_monkeypatch_ini \
        and not test_monkeypatch_native \
        and not test_standalone_mock \
        and not test_plain_stopall"
}

package() {
  cd pytest-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
