# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='jwt'
pkgname="ruby-${_gemname}"
pkgver=2.4.1
pkgrel=1
pkgdesc='A pure ruby implementation of the RFC 7519 OAuth JSON Web Token (JWT) standard'
arch=('any')
url="https://github.com/jwt/${pkgname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz"
        'drop-ci-dependencies.patch')
sha512sums=('871ffe9dc659877a77de0c2a51b1f78b45614cf8265bd07820a9b4b08e38160861b4c9f98391138bfaaf64261ce2eb58eba26c40f29c41b02b8fa16cd260997a'
            'f053410646562d664b4e5401ab86cf8b049393f68c1c232c94482509ed33c5dd796afc1b43ead5078d99dcbeb65ae8011901101e76282fb34ae05f17b3738d14')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -N -i "${srcdir}/drop-ci-dependencies.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gem build "${pkgname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake test --trace
}

package() {
cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md AUTHORS CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
