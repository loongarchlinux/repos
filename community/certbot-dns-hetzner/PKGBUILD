# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=certbot-dns-hetzner
pkgdesc="Certbot plugin enabling dns-01 challenge on the Hetzner DNS API"
pkgver=1.0.5
pkgrel=4
arch=("any")
url="https://github.com/ctrlaltcoop/certbot-dns-hetzner"
license=("APACHE")
depends=("certbot" "python-requests" "python-zope-interface" "python-requests-mock" "python-mock")
makedepends=("python-setuptools")
checkdepends=("python-pytest")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        $pkgname-certbot-1.16.patch::https://github.com/ctrlaltcoop/certbot-dns-hetzner/pull/16.patch)
sha256sums=('c2c2398bec560ee5607ee849c89f22fb38c0874c1620bc60c8aa20cf7148afbd'
            'd7b934e3228c6533e42a5a47fead618085bf6289fb0ff8e20d22a5de50922861')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ../$pkgname-certbot-1.16.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python setup.py build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHONPATH=. pytest
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	python setup.py install --root="${pkgdir}" -O1 --skip-build
}
