# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=hoe
pkgname=ruby-${_gemname}
_gitcommit=c5bb62f78193e8dcb734d412c38791ce6f74773c
pkgver=3.20.0
pkgrel=4
pkgdesc='Hoe is a rake/rubygems helper for project Rakefiles'
url='https://www.zenspider.com/projects/hoe.html'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('git' 'ruby-rdoc' 'ruby-rake')
checkdepends=('ruby-minitest')
options=('!emptydirs')
source=("git+https://github.com/seattlerb/hoe#commit=${_gitcommit}"
        ruby27.patch)
sha512sums=('SKIP'
            'ec5e34b53fafc72fb6d9af3aa79dc1509632c390668a97db807d370759a6f0bdfe328933b648d35dedcc9662f94d6f1f99a2b982a037b49d354592a86f782a57')

pkgver() {
  cd ${_gemname}
  grep 'VERSION = "' lib/hoe.rb|sed -E 's|.*"(.+)"|\1|'
}

prepare() {
  cd ${_gemname}
  patch -p1 < ../ruby27.patch
}

build() {
  cd ${_gemname}
  rake gem
}

check() {
  cd ${_gemname}
  rake test
}

package() {
  cd ${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
