# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-sys
pkgver=0.0.20220829
_commit=d48e67d0026122f55f22f0df396e7a51d2b20fb9
pkgrel=1
pkgdesc='Go packages for low-level interaction with the operating system'
arch=('any')
url="https://github.com/golang/sys"
license=('BSD')
depends=('go')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/sys/archive/$_commit.tar.gz")
sha512sums=('b200744282640d2453f63dbed37c45c3b3d2f16a75ab783fa56f077d4dec3879187bc54dec810e823f35f753f6f91b876e350f3a01888f7ea09e95684fc826fa')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$srcdir"/build/src/golang.org/x/sys
  cd "$srcdir"/build/src/golang.org/x/sys
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/sys

  install -Dm644 sys-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
