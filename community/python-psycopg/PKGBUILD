# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg
pkgver=3.0.16
pkgrel=1
pkgdesc='PostgreSQL database adapter for Python'
arch=('loong64' 'x86_64')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=(
  'git'
  'cython'
  'python-setuptools'
  'python-wheel'
  'python-pip'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
_commit='6c118f09d57510fd8299c852f7390bbc28f1d819'
source=("$pkgname::git+https://github.com/psycopg/psycopg.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"
}

build(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c
  do
    python "$subpackage/setup.py" build
  done
}

package(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c
  do
    python "$subpackage/setup.py" install --root="$pkgdir" --optimize=1 --skip-build
  done
}
