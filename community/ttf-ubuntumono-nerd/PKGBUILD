# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Agustin Carrasco <asermax at gmail dot com>

pkgname=ttf-ubuntumono-nerd
pkgver=2.2.2
pkgrel=3
pkgdesc="Patched font Ubuntu Mono from nerd-fonts library"
arch=("any")
url="https://github.com/ryanoasis/nerd-fonts"
license=("MIT")
provides=(ttf-font-nerd)
conflicts=(nerd-fonts-ubuntu-mono)
replaces=(nerd-fonts-ubuntu-mono)
source=("ubuntumono-${pkgver}.zip::${url}/releases/download/v${pkgver}/UbuntuMono.zip"
        "LICENSE-${pkgver}::${url}/raw/v${pkgver}/LICENSE")
sha256sums=('6a5b0cf6ec7c20c93d3854d0ffa1f0944ad18d11b6574d057b401ece63f64c56'
            '245b522abb5aba0d8e84898bf6d5e95102620637626106bfd0061189a2370360')

prepare() {
  find "${srcdir}" -name '*Windows*' -delete
}

package() {
  install -Dm644 *.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 LICENSE-${pkgver} "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
