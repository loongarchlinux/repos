# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=recursion-schemes
pkgname=haskell-recursion-schemes
pkgver=5.2
pkgrel=17
pkgdesc="Representing common recursion patterns as higher-order functions"
url="https://github.com/ekmett/recursion-schemes"
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-comonad' 'haskell-data-fix' 'haskell-free'
         'haskell-th-abstraction')
makedepends=('ghc' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('eb30ec4bef3e38ef2d33d7be85e90f193da3d4b5024a20d5b965bb43e03c4d9a95fc3f1cf56c1686f88b295d3d6459f0c421d57e0eb205454aad9f0ee12dda37')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
