# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-httpretty
pkgver=1.0.2
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
depends=('python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-eventlet' 'python-freezegun' 'python-httplib2'
              'python-redis' 'python-rednose' 'python-requests' 'python-sure' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gabrielfalcao/HTTPretty/archive/$pkgver.tar.gz")
sha512sums=('f9f7001e9d65e0f0613e5e59c61f8330e445887b9552afbcfe8f5367a536dbabefb8a080330e06552313c3758becb39cf59d1ef7e835a3198adc9dfeff0074bd')

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests
}

package_python-httpretty() {
  cd HTTPretty-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
