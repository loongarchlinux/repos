# Maintainer: Anatol Pomozov

_gemname=reline
pkgname=ruby-$_gemname
pkgver=0.2.5
pkgrel=1
pkgdesc="Alternative GNU Readline or Editline implementation by pure Ruby."
arch=('any')
url='https://github.com/ruby/reline'
license=('BSD-2')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('85b770e995deea7e2d3f86523750461ff78a70f11b06a0826b979b35b2b9412d415611edd8503a480e4f1303fa4af5e543c9e85b1f76499a2fe67b0e31cf6e33')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  HOME="/tmp" GEM_HOME="$_gemdir" GEM_PATH="$_gemdir" gem install -N --no-user-install --ignore-dependencies \
     -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
