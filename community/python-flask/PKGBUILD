# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python-flask
pkgver=2.0.1
pkgrel=1
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
makedepends=('python-setuptools'  'python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('55d76e65aefb6f430050f1ef7e0378ece673a18b8316974d7b8bf468fe09c4b89022a3dd8bb41dabf2a6b7566bf7a23cb53246be4b5bd9a8ddcf0e7b2f114dc8')

build() {
  cd "flask-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py build
}

check() {
  cd "flask-$pkgver"
  python setup.py test
}

package() {
  cd "flask-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
