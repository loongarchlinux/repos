# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=python-lsp-black
pkgver=1.0.1
pkgrel=1
pkgdesc="python-lsp-server plugin that adds support to black autoformatter, forked from pyls-black"
arch=(any)
url="https://github.com/python-lsp/python-lsp-black"
license=(MIT)
depends=(python-black python-lsp-server python-toml)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('432666772a642e45536af72da39f9437d46662ebca924247ab3776090edc7e22')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --skip-build
  test-env/bin/python -m pytest -v --color=yes tests
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
