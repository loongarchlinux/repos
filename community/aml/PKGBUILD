# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=aml
pkgver=0.2.0
pkgrel=2
pkgdesc='Another Main Loop'
arch=(loongarch64 x86_64)
url=https://github.com/any1/aml
license=(custom:ISC)
depends=(glibc)
makedepends=(
  git
  meson
  ninja
)
provides=(libaml.so)
_tag=1d8185ec15c68074cec1fd252c01c5ecad877b73
source=(git+https://github.com/any1/aml.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd aml
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson aml build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 aml/COPYING -t "${pkgdir}"/usr/share/licenses/aml
}

# vim: ts=2 sw=2 et:
