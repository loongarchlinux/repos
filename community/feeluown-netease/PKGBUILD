# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=0.9.1
pkgrel=1
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-netease/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9de12935688b10284819307b69572e4deb1504ef248d1a53eba472111e621379c0c54554a2ef86a733f3461e6e730cedb72a93266194c420381423ebed046edc')

build() {
  cd feeluown-netease-$pkgver
  python setup.py build
}

check() {
  cd feeluown-netease-$pkgver
  pytest
}

package() {
  cd feeluown-netease-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
