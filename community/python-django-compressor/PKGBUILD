# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django_compressor
pkgname=python-django-compressor
pkgver=2.4
pkgrel=1
pkgdesc="Compresses linked and inline javascript or CSS into a single cached file"
arch=('any')
url="https://github.com/django-compressor/django-compressor"
license=('Apache' 'BSD' 'MIT')
depends=('python-django-appconf' 'python-rcssmin' 'python-rjsmin')
makedepends=('python-setuptools')
checkdepends=('python-beautifulsoup4' 'python-brotli' 'python-calmjs'
'python-coverage' 'python-csscompressor' 'python-django-classy-tags'
'python-django-sekizai' 'python-html5lib' 'python-jinja' 'python-lxml'
'python-mock' 'slimit')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b4dd43ab4ad13872138c9b9be3012e2675db2131b384350c5aa13ac7859b1aa93760cc3b9c91393f51bac404eb66ae04f35b187e0b0ebacf5f0da457dd42e727')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin.py test --settings=compressor.test_settings compressor
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
