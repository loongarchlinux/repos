# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django_compressor
pkgname=python-django-compressor
pkgver=3.1
pkgrel=1
pkgdesc="Compresses linked and inline javascript or CSS into a single cached file"
arch=(any)
url="https://github.com/django-compressor/django-compressor"
license=(Apache BSD MIT)
depends=(python-django-appconf python-rcssmin python-rjsmin)
makedepends=(python-setuptools)
checkdepends=(python-beautifulsoup4 python-brotli python-calmjs python-coverage
python-csscompressor python-django-sekizai python-html5lib python-jinja
python-lxml python-mock slimit)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a0026278d6de64984039d91ad195514f2aa6def8a7a3c88a6b75e03974d7e19026fdbd4b30ab4b33d441a13465ef03ac536e6a5b36aeffcbfaa76b68ef36fbaa')
b2sums=('35469deb786718c4c053b11a5dbda18ee8b2e9bda2cc3325932ab36a2fae60458f801ff755ef82ef9aad2c641a81ac31a56014f3c0b520a14bfab6467be77316')

prepare() {
  cd "${_name}-$pkgver"
  sed -e 's/rcssmin ==/rcssmin >=/' \
      -e 's/rjsmin ==/rjsmin >=/' \
      -i setup.py
}

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin test --settings=compressor.test_settings compressor
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
