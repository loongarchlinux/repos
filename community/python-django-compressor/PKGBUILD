# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django_compressor
pkgname=python-django-compressor
pkgver=4.0
pkgrel=1
pkgdesc="Compresses linked and inline javascript or CSS into a single cached file"
arch=(any)
url="https://github.com/django-compressor/django-compressor"
license=(Apache BSD MIT)
depends=(python-django-appconf python-rcssmin python-rjsmin)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-beautifulsoup4 python-brotli python-calmjs python-coverage
python-csscompressor python-django-sekizai python-html5lib python-jinja
python-lxml python-mock slimit)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d93750f8eda0957c1dacccc157b6a4a3db4966d8aa36d2d025ca9fd91695ad79ffb2598d1c65a547c7d97adaf03e7520316b96100b4646800b5c87bee2ef6cc5')
b2sums=('0a23282051de239ecbbe55f0ccf61dad68735048eb1a20b763c0037a11250e98fae7a34a671e4dc71b424ea5910bcc0892189d7b826a97802178b613dbadfd41')

prepare() {
  cd $_name-$pkgver
  # version pinning is just bad...
  sed -e 's/rcssmin ==/rcssmin >=/;s/rjsmin ==/rjsmin >=/' -i setup.py
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  django-admin test --settings=compressor.test_settings compressor
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
