# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-randomly
pkgver=3.10.1
pkgrel=1
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-randomly'
depends=('python-importlib-metadata' 'python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-factory-boy' 'python-faker' 'python-numpy' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('d93e017df706d7021ff3f4682b4ed65d474177a60e303455685266d47a0a214e4f30b2f7dd150768de711d602ca1d22e2cfeca2703a7139cfaab97e3424afa1c')

build() {
  cd pytest-randomly-$pkgver
  python setup.py build
}

check() {
  cd pytest-randomly-$pkgver
  # TODO: Understand why...
  PYTHONPATH="$PWD/src" pytest --deselect tests/test_pytest_randomly.py::test_entrypoint_injection
}

package() {
  cd pytest-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
