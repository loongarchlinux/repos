# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi_intercept
pkgname=python-wsgi-intercept
pkgver=1.10.0
pkgrel=1
pkgdesc="Intercept socket connection to wsgi applications for testing"
arch=(any)
url="https://github.com/cdent/wsgi-intercept"
license=(MIT)
depends=(python-six)
makedepends=(python-build python-installer python-setuptools python-sphinx python-wheel)
checkdepends=(python-httplib2 python-pytest python-requests python-urllib3)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('27848e52e77f1d7ca7a079eb24025bb38d31f4c6b0b82c2717c543a5ffa83a95fe0dd893c326135b29c465a89ffe149da6d5d9c8bfe9f181546862e4f42a3832')
b2sums=('43c2e9dcd98616132801721392b0150b49fe5e851f343f53d8eb42c2f7f8b84f5eb9bb41edec7b830908f122a80458752e1c97f6a68006088221bb3233e90951')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  cd docs
  make man
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package_python-wsgi-intercept() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README -t "$pkgdir/usr/share/$pkgname/"
  install -vDm 644 docs/_build/man/${_name}.1 -t "$pkgdir/usr/share/man/man1/"
}
