# Maintainer: BlackEagle < ike DOT devolder AT gmail DOT com >
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Martin Striz < ms AT martinstriz DOT cz >
# Contributor: Jarek Sedlacek <jareksedlacek@gmail.com>

pkgbase=php-mongodb
pkgname=(php-mongodb php7-mongodb)
pkgver=1.14.1
pkgrel=1
pkgdesc="MongoDB driver for PHP"
arch=("loong64" "x86_64")
url="http://mongodb.github.io/mongo-php-driver"
license=("Apache")
makedepends=('php7' 'php' 're2c')
source=(
	"https://pecl.php.net/get/mongodb-$pkgver.tgz"
	"mongodb.ini"
)
sha512sums=('246082926d3e172a2dba63e39e90ead04a09eee440120a95460866e496b9b5db34d7e64b4bcd8b9032982523ce9cb9523dfba0fccb05de85d610a904d932ae29'
            '47b167ab4d761f4cf8351fef14e46c714dacae6d9a1b6d281bb87129499ee520f55f35f47c16b55ea2ecf41c67aad12439a99d3363d381b2511659b01f0b6ef0')

prepare() {
    cp -a "$srcdir/mongodb-$pkgver" "$srcdir/mongodb7-$pkgver"
}

build() {
	cd "$srcdir/mongodb-$pkgver"
	phpize
	./configure --prefix=/usr

	cd "$srcdir/mongodb7-$pkgver"
	phpize7
	./configure --prefix=/usr
}

package_php-mongodb() {
    pkgdesc="MongoDB driver for PHP"
    depends=('php')
    backup=("etc/php/conf.d/mongodb.ini")
	cd mongodb-$pkgver
	make INSTALL_ROOT="$pkgdir" install
	install -Dm644 "$srcdir/mongodb.ini" "$pkgdir/etc/php/conf.d/mongodb.ini"
}

package_php7-mongodb() {
    pkgdesc="MongoDB driver for PHP7"
    depends=('php7')
    backup=("etc/php7/conf.d/mongodb.ini")
	cd mongodb7-$pkgver
	make INSTALL_ROOT="$pkgdir" install
	install -Dm644 "$srcdir/mongodb.ini" "$pkgdir/etc/php7/conf.d/mongodb.ini"
}
