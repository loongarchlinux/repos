# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname='python-et-xmlfile'
pkgver=1.1
pkgrel=1
pkgdesc="A low memory library for creating large XML files."
arch=('any')
url="https://et-xmlfile.readthedocs.io/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-lxml')
source=("https://foss.heptapod.net/openpyxl/et_xmlfile/-/archive/${pkgver}/et_xmlfile-${pkgver}.tar.gz")
sha256sums=('3d3486a2ef43dc099d7b64cb247f1a9595edd39225e4398b6538d55ce1f8a36b')

prepare() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  # fix deprecation warning
  sed -i "s/strict/strict-markers/" pytest.ini
}

build() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  python setup.py build
}

check() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  python setup.py pytest
}

package() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENCE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
