# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-sugar
pkgver=0.9.5
_commit=eb5c26cb7f07cc125a72d154b743a6276585a996
pkgrel=1
pkgdesc='A plugin for py.test that changes the default look and feel of py.test'
arch=('any')
license=('BSD')
url='https://pivotfinland.com/pytest-sugar/'
depends=('python-pytest' 'python-termcolor')
makedepends=('python-setuptools')
source=("https://github.com/Frozenball/pytest-sugar/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('0f0a4e1b165f18a23badf9eb9b3780906287f77ea1e8fc6a36d5730a0cdd853cf3bdf911b9e3ef9a675416cdd1c095f812b47b54c140c9831e2d23a14dcbb972')

prepare() {
  mv pytest-sugar-{$_commit,$pkgver}
}

build() {
  cd pytest-sugar-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-sugar-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-sugar-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
