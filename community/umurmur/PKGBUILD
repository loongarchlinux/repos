# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.17
pkgrel=18
pkgdesc='Minimalistic Mumble server'
url="https://github.com/umurmur/umurmur"
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'libconfig' 'protobuf-c' 'mbedtls')
backup=('etc/umurmur/umurmur.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/umurmur/umurmur/archive/${pkgver}.tar.gz
        umurmur.sysusers
        umurmur.service
        umurmur.tmpfiles)
sha256sums=('e77b7b6616768f4a1c07442afe49a772692f667b00c23cc85909d4dd0ce206d2'
            '0fc68df464ee51a431d934d068aed0be5f8c5e64d0bd29848f97532d39f8c310'
            'b8b22b6299777fbd1d12e3105280c8585ceca9b6caf7b8d3ab0642c5a56b031f'
            '287068f47fc035a70e2ae0c8434e8013f176d185bf7688216c36976982fe4491')
sha512sums=('a496a51fd7815ad117f5aee17bb78cbd319c584ad60ab8aebbfd8ddf7b1760f443f2337bc74be1e0d5af17d3c3df2ae6c9060eca576cf1e6ed4c6cb0825e9c15'
            'd84950a32ab8a2e84f5fe333cd2894e52aba624531644d106c982aa4ff04271d318543398fa7f48c719f26338679fa971bb5332472e9040ac9aa8a9b4a1f2832'
            '746a3e2d9e8c5154bdfb2cef6cbe39cccf0356bc1dde0434b92ec1a6b224a5bfa51fd15483c3ac5a75292eae7a6d4b0431ecb2a586bdd9fcc3fe9b2a7bff64a1'
            '825b50448231b5d791e87d7c4c471fdfe2e9a1560dad6fc90c2f4f8d0c5ed682291bf20b147a6a8c7ae361aeb8b1a11c24c6d41ffc17f06fb0f5ccd8208a899a')

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DSSL=mbedtls \
        -Wno-dev \
        -B build \
        -S .
  make -C build
}

package() {
  depends+=('libconfig.so' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so' 'libprotobuf-c.so')
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -vDm 640 "${pkgdir}/usr/etc/umurmur.conf" -t "${pkgdir}/etc/umurmur"
  rm -r "${pkgdir}/usr/etc"
  install -vDm 644 "${srcdir}/umurmur.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -vDm 644 "${srcdir}/umurmur.sysusers" "${pkgdir}/usr/lib/sysusers.d/umurmur.conf"
  install -vDm 644 "${srcdir}/umurmur.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/umurmur.conf"
}

# vim: ts=2 sw=2 et:
