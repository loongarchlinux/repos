# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=firefox-adblock-plus
pkgver=3.13
pkgrel=1
_file=3948047
pkgdesc="Extension for firefox which block ads and banners"
arch=('any')
url="https://adblockplus.org/"
license=('GPL3')
makedepends=('unzip' 'jq')
groups=('firefox-addons')
# https://addons.mozilla.org/en-US/firefox/addon/adblock-plus/versions/
source=("https://addons.mozilla.org/firefox/downloads/file/${_file}/adblock-plus-${pkgver}.xpi")
noextract=("adblock-plus-$pkgver.xpi")
sha256sums=('f6a04b895c3a227f8a4f0a768269755ad52fe5a12ccbeb59805cd9e173df8d2a')

check() {
  unzip adblock-plus-$pkgver.xpi manifest.json
  jq '.version' manifest.json | grep -E '^"'$pkgver'"$'
}

package() {
  depends=("firefox")

  _extension_id="{d10d0bf8-f5b5-c8b4-a8b2-2b9879e08c5d}"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  install -Dm644 "${srcdir}"/adblock-plus-${pkgver}.xpi "${_extension_dest}.xpi"
}
