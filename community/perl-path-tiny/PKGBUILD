# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-path-tiny
pkgver=0.122
pkgrel=2
pkgdesc='File path utility'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/Path-Tiny
source=("https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/Path-Tiny-$pkgver.tar.gz")
md5sums=(f746d8da5d049a5523b1880666bb0992)
sha512sums=(cca49e56208f767469d37a081817b386eaf2ce2164cc97fd652c2faccc99f483805425e1bf346466871c156f9fe36f5a3f65e96c5b219af33e3e2016144f2c6d)
_ddir="Path-Tiny-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
