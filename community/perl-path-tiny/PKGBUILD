# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-path-tiny
pkgver=0.114
pkgrel=1
pkgdesc='File path utility'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/Path-Tiny
source=("https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/Path-Tiny-$pkgver.tar.gz")
md5sums=(dd6092293f8766fb87d003dceebe4263)
sha512sums=(932d9d4658af1e578142ff3bb4e541e6fb7754397c55f869d17f9989a181c10b528779aeedcaafc2cd879a31a6febe63d220cbf843f187db92e6ae1087c39e56)
_ddir="Path-Tiny-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
