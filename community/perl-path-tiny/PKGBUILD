# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-path-tiny
pkgver=0.120
pkgrel=1
pkgdesc='File path utility'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/Path-Tiny
source=("https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/Path-Tiny-$pkgver.tar.gz")
md5sums=(046d14dde6e3568aee5085487dd4769e)
sha512sums=(3900fed77950154de60f0596b31f16c653597135d944a54a7e67d77254e0fe77f870defa76ab0c11f895c6d2de4cf0e340726074cf58aa83297f6b3aa30d7b12)
_ddir="Path-Tiny-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
