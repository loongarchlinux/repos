# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: said <atvordhosbn@gmail.com>
# Contributor: clinx <clinx.hacker@googlemail.com>

pkgname=rbutil
pkgver=1.4.1
pkgrel=1
pkgdesc='Rockbox Utility'
arch=(loongarch64 x86_64)
url=https://www.rockbox.org/twiki/bin/view/Main/RockboxUtility
license=(GPL)
depends=(
  crypto++
  qt5-base
  libusbx
  libxkbcommon-x11
)
makedepends=(
  gendesk
  qt5-tools
)
optdepends=('speex: voice files support')
source=(https://download.rockbox.org/${pkgname}/source/RockboxUtility-v${pkgver}-src.tar.bz2)
sha256sums=('3e1949f9f358e3f423a1cef6cd5f563be90da85e5b650bb03a1e04a40270a97e')

prepare() {
  gendesk -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --name="Rockbox Utility" --exec="RockboxUtility" --categories="Application;Utility;" --comment="Rockbox automated installer"
}

build() {
  cd RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

  lrelease-qt5 rbutilqt.pro
  qmake-qt5
  make
}

package(){
  cd RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 {,"${pkgdir}"/usr/bin/}RockboxUtility
  install -m 644 icons/rockbox-256.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
  install -m 644 {"${srcdir}","${pkgdir}"/usr/share/applications}/rbutil.desktop
}

# vim: ts=2 sw=2 et:
