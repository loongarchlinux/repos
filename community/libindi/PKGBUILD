# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.9.3
pkgrel=2
pkgdesc='A distributed control protocol designed to operate astronomical instrumentation'
url='https://www.indilib.org/index.php?title=Main_Page'
license=(GPL2)
arch=(loongarch64 x86_64)
depends=(libnova cfitsio libusb libjpeg gsl libtheora fftw)
makedepends=(cmake boost qt5-base)
source=(https://github.com/indilib/indi/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('0ebcae9a5b7a1a94fdaf41ff8c743310a1597c773ec50e92ad10c9a406c239ad')

prepare() {
  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/drivers/*/*.rules
}

build() {
  cmake -B build -S indi-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
