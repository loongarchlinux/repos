# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Joey Dumont <joey.dumont@gmail.com>

_pkg=h5py
_mpi=openmpi
pkgname=python-${_pkg}-${_mpi}
pkgver=2.10.0
pkgrel=2
pkgdesc="General-purpose Python bindings for the HDF5 library (${_mpi} version)"
arch=(x86_64)
url="https://www.h5py.org/"
license=(BSD)
depends=(hdf5-${_mpi} python-numpy python-six python-mpi4py)
makedepends=(cython python-pkgconfig)
checkdepends=(inetutils python-pytest)
conflicts=(python-h5py)
provides=(python-h5py)
source=(https://files.pythonhosted.org/packages/source/h/${_pkg}/${_pkg}-${pkgver}.tar.gz
        python-h5py-hd5-1.12.patch
        h5py-2.10.0-hdf5-v1.12-h5i.patch)
sha256sums=('84412798925dc870ffd7107f045d7659e60f5d46d1c70c700375248bf6bf512d'
            '0533a13f65de73ea9b61220cdb367038b441a61ac7468ebcda549a15c47f879b'
            'b8686225d69b3bb103b5153a4783569eec98ca49be136623bcad18d140427e64')
validpgpkeys=(AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7 # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>
              96B7334D7610EE3E68AFFE589E027116943D6A8B) # Thomas A Caswell <tcaswell@bnl.gov> (new key)
# See https://github.com/h5py/h5py/issues/1299 about lack of GPG sigs for recent releases

prepare() {
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" ${_pkg}-${pkgver}/setup_build.py
  cd ${_pkg}-${pkgver}
  patch -p1 -i ../h5py-2.10.0-hdf5-v1.12-h5i.patch
  patch -p1 -i ../python-h5py-hd5-1.12.patch
}

build() {
  export CC=mpicc
  cd ${_pkg}-${pkgver}
  python setup.py configure --mpi 
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # Ongoing issues with hdf5 1.12 https://github.com/h5py/h5py/pull/1536
  python setup.py test || warning "Tests failed"
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -Dm644 licenses/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
