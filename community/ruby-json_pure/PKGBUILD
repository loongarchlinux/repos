# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-json_pure
pkgver=2.6.2
pkgrel=1
pkgdesc='A JSON implementation in pure Ruby'
arch=(any)
url='http://flori.github.io/json/'
license=(RUBY)
depends=(ruby)
makedepends=(ruby-rake)
options=(!emptydirs)
source=(https://github.com/flori/json/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a69d05680e8c8a0d038f546fd62a6e261a67469f8d5b4272702c855fa5e038b9')

build() {
  local _gemdir="$(gem env gemdir)"
  cd json-$pkgver
  gem build json_pure.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    json_pure-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd json-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake test_pure
}

package() {
  cd json-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
