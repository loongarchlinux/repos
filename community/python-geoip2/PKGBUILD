# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-geoip2
pkgver=4.2.0
_testdatacommit=cbaa463dc6950ababbf678ca85fb3833b81c76d3
pkgrel=1
pkgdesc="MaxMind GeoIP2 API"
url="https://github.com/maxmind/GeoIP2-python"
license=('Apache')
arch=('any')
depends=('python-aiohttp' 'python-maxminddb' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-mocket')
source=("https://github.com/maxmind/GeoIP2-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        MaxMind-DB-$_testdatacommit.tar.gz::https://github.com/maxmind/MaxMind-DB/archive/$_testdatacommit.tar.gz)
sha512sums=('e58320152c36a1bdeb509330a31b852ecdefa3ec65127e75be38d494378754af923ab0e078038674a8ecaa8e6a30c174255d5f430daabaaad33fbd7a9650df3a'
            '337552c1d6524ba31c1d3df55c0b6137ca2d5115529b8c1a619c82baa198886c274bac4228e456273851bddc46ca983ad2be6329b8f19d2769b745fb2c0f96b1')

prepare() {
  cd GeoIP2-python-$pkgver
  rmdir tests/data
  ln -s "$srcdir"/MaxMind-DB-$_testdatacommit tests/data
}

build() {
  cd GeoIP2-python-$pkgver
  python setup.py build
}

check() {
  cd GeoIP2-python-$pkgver
  python setup.py test
}

package() {
  cd GeoIP2-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
