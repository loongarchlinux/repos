# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: gardar <aur@gardar.net>
pkgname=python-pynetbox
pkgver=6.1.3
pkgrel=2
pkgdesc="NetBox API client library"
arch=('any')
url="https://github.com/digitalocean/pynetbox"
license=('Apache')
depends=('python' 'python-requests' 'python-six')
makedepends=('python-setuptools-scm')
source=("https://files.pythonhosted.org/packages/source/p/pynetbox/pynetbox-${pkgver}.tar.gz")
sha256sums=('e579c5a74ff98dffd2cecb0451b2fffc1924731b56876d7d97b9aaa2b6ef7aa6')

build() {
	cd "pynetbox-${pkgver}"
	python setup.py build
}

package() {
	cd "pynetbox-${pkgver}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
