# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=gnucash-docs
pkgver=4.10.1
pkgrel=1
pkgdesc='Documentation for GnuCash'
arch=('any')
url='https://gnucash.org/docs.phtml'
license=('FDL')
depends=('yelp')
makedepends=('cmake')
source=(https://downloads.sourceforge.net/gnucash/$pkgname-$pkgver.tar.gz)
sha256sums=('8f81dfbf13a49ec0ab676a82f8dde5b364658528df2ac41372d1e0fb68a8c231')

prepare() {
  cd $pkgname-$pkgver
  mkdir build
}


build() {
  cd $pkgname-$pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="${pkgdir}" install
}
