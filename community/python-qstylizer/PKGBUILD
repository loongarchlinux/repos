# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=qstylizer
pkgname=python-${_pkg}
pkgver=0.1.10
pkgrel=1
pkgdesc="Qt stylesheet generation utility for PyQt/PySide"
arch=(any)
url="https://github.com/blambright/qstylizer"
license=(MIT)
depends=(python-inflection python-tinycss)
makedepends=(python-pbr python-setuptools)
checkdepends=(
    python-pytest
    python-pytest-runner
    python-pytest-mock
    python-pytest-xdist
)
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a5d561ef757d1751766674fba977e339cb4028b057a350969224403f286c4751')

build() {
  cd ${_pkg}-${pkgver}
  export PBR_VERSION=${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib/ pytest
}

package() {
  cd ${_pkg}-${pkgver}
  export PBR_VERSION=${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
