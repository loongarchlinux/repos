# Maintainer:
# Contributor: Florian Klink <flokli at flokli dot de>

pkgname=python-pystache
_pkgname=pystache
pkgver=0.6.0
pkgrel=10
pkgdesc='The mustache template engine written in python'
arch=(any)
url='https://github.com/sarnold/pystache'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/sarnold/$_pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b8aa98c6c7cb1f7bbdf0bc859bce5dc259ff28290b0f9bc68d9a9e38e83dd3947a6130c370d4def0811827fd09c6e1bbffe5af098c6233819c7ef0b38bcab9fd')

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$_pkgname-$pkgver"
  # This package uses 2to3 to convert itself to Python 3 on the fly
  # So we need to jump through some hoops here
  test_dir="../test_dir"
  rm -rf "$test_dir"
  mkdir "$test_dir"
  python setup.py install --root="$test_dir"
  PYTHONPATH=$(readlink -e "$test_dir"/usr/lib/python*/site-packages/) \
  	"$test_dir"/usr/bin/pystache-test .
}

package() {
  cd "$_pkgname-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
