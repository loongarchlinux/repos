# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.6.2
pkgrel=2
pkgdesc='Unit conversion tool and library written in rust'
arch=('loongarch64' 'x86_64')
url=https://rinkcalc.app
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("rink-$pkgver.tar.gz::https://static.crates.io/crates/rink/rink-$pkgver.crate")
b2sums=('a05c1605d6da18bd6d82413c1ce989ef41a154e0ceaddaca747ca705d2623f36c9d2110692a43d0c479354f308ab2152255c8f13027edbc2941750854f79b547')

prepare() {
  cd rink-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd rink-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd rink-$pkgver
  cargo test --locked --offline
}

package() {
  cd rink-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rink
}

# vim:set ts=2 sw=2 et:
