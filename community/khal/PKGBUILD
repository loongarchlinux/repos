# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

_name=khal
pkgname=khal
pkgver=0.10.3
pkgrel=1
pkgdesc='CLI calendar application build around CalDAV'
arch=('any')
url="https://lostpackets.de/khal/"
license=('MIT')
makedepends=('python-setuptools-scm' 'python-sphinxcontrib-newsfeed')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-configobj' 'python-dateutil' 'python-icalendar' 'python-pytz'
'python-tzlocal' 'python-urwid' 'python-xdg')
optdepends=('python-setproctitle: Set process name'
            'vdirsyncer: Synchronize CalDAV calendars')
checkdepends=('python-pytest' 'python-freezegun' 'vdirsyncer')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('2fdd8fc14fe597e5a7d6e9c63c7868d960b4ed021b563c684a71f07090eda432')
b2sums=('290bc799f09401d47f2f48596813ec568859be090a204482a7c2cdd967ec3ccf4c935f5ab35c8ad1cc2e3c2340b4a0e4753debe14ff6114b1b7f0afdf5deaecc')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man PYTHONPATH="${PWD}"
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v \
    || echo "Tests from hell: https://github.com/pimutils/khal/issues/860"
}

package() {
  cd "${pkgname}-${pkgver}"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # executables
  install -vDm 755 bin/{i,}"${pkgname}" -t "$pkgdir/usr/bin/"
  # configuration
  install -vDm 644 "${pkgname}.conf.sample" \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/${pkgname}.conf"
  # man page
  install -vDm 644 doc/build/man/${pkgname}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # zsh completion
  install -vDm 644 "misc/__${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS.txt,{CHANGELOG,CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
