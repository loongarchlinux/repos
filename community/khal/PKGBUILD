# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

_name=khal
pkgname=khal
pkgver=0.10.4
pkgrel=2
pkgdesc='CLI calendar application build around CalDAV'
arch=('any')
url="https://lostpackets.de/khal/"
license=('MIT')
makedepends=('python-setuptools-scm' 'python-sphinxcontrib-newsfeed')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-configobj' 'python-dateutil' 'python-icalendar' 'python-pytz'
'python-pyxdg' 'python-urwid' 'python-tzlocal')
optdepends=('python-setproctitle: Set process name'
            'vdirsyncer: Synchronize CalDAV calendars')
checkdepends=('python-pytest' 'python-freezegun' 'vdirsyncer')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('3fdb980a9a61c0206d7a82b16f77b408a4f341a2b866b9c9fcf6a641850d129f')
b2sums=('0cb9deb793f577bbcedcbba4788988973ee9d0ab71fdd30a01142a54434767d74b1d10e5bfa5f6a2feb9f18a8a9fe22717d4a38a07ab5e30b331c87b79a79f39')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man PYTHONPATH="${PWD}"
}

check() {
  cd "${pkgname}-${pkgver}"
  env
  export PYTHONPATH="build:${PYTHONPATH}"
  export LC_ALL="C"
  # the tests are very fragile
  # https://github.com/pimutils/khal/issues/860"
  pytest -v -k "not test_only_update_old_event"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # executables
  install -vDm 755 bin/{i,}"${pkgname}" -t "$pkgdir/usr/bin/"
  # configuration
  install -vDm 644 "${pkgname}.conf.sample" \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/${pkgname}.conf"
  # man page
  install -vDm 644 doc/build/man/${pkgname}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # zsh completion
  install -vDm 644 "misc/__${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS.txt,{CHANGELOG,CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
