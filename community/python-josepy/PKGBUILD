# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=1.11.0
pkgrel=1
pkgdesc="JOSE protocol implementation in Python"
url="https://github.com/certbot/josepy"
license=('PerlArtistic')
arch=('any')
depends=('python-cryptography' 'python-pyopenssl' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/j/josepy/josepy-$pkgver.tar.gz")
sha512sums=('06aed9534434a94f7b922270221aaa85feb4ba9d836460303ecb7fea1c5679d557a6dad6d32bbd1a05ccc4855318fb56752ebea91498bb8c36884cf7b3d01859')

prepare() {
  # thou shalt not enforce coverage in distro packaging
  # https://github.com/certbot/josepy/issues/34
  rm josepy-$pkgver/pytest.ini
}

build() {
  cd josepy-$pkgver
  python setup.py build
}

check() {
  cd josepy-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd josepy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
