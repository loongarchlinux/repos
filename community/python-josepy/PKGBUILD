# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=1.8.0
pkgrel=2
pkgdesc="JOSE protocol implementation in Python"
url="https://github.com/certbot/josepy"
license=('PerlArtistic')
arch=('any')
depends=('python-cryptography' 'python-pyopenssl' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/j/josepy/josepy-$pkgver.tar.gz")
sha512sums=('50cdedfa4cf67055ae65dfeef2328ba7bb369fb8b763b425e4ca73108555c9cd04f12a644309c2f2e1db50e1d227be3bdbd00037e509d2b2d82565c213b81e4e')

prepare() {
  # thou shalt not enforce coverage in distro packaging
  # https://github.com/certbot/josepy/issues/34
  rm josepy-$pkgver/pytest.ini
}

build() {
  cd josepy-$pkgver
  python setup.py build
}

check() {
  cd josepy-$pkgver
  python -m pytest
}

package() {
  cd josepy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
