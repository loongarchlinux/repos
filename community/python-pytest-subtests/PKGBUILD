# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-subtests
pkgver=0.7.0
pkgrel=1
pkgdesc='unittest subTest() support and subtests fixture'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-subtests'
depends=('python-pytest')
makedepends=('python-setuptools-scm')
source=("https://github.com/pytest-dev/pytest-subtests/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('96531a42c06a20cdf3f3665993d3e80d198a60d536246f4dc46df20b56e47f3f6fdce9478351a8922e370435f974e63d565550213fbbeed8487eedc131f46404')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-subtests-$pkgver
  python setup.py build
}

check() {
  cd pytest-subtests-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-subtests-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
