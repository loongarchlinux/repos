# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=libilbc
pkgver=2.0.2
pkgrel=5
pkgdesc='A friendly copy of the iLBC codec from the WebRTC project'
arch=('x86_64')
url='https://github.com/TimothyGu/libilbc/'
license=('BSD')
depends=('glibc')
source=("https://github.com/TimothyGu/libilbc/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('88e2dc14e1fccd7b7a0d7f84e96ac22a33c3c86820cee4b0a05e3dcc08c2b096')

build() {
    cd "${pkgname}-${pkgver}"
    
    ./configure \
        --prefix='/usr' \
        --enable-static='no' \
        --enable-shared='yes'
        
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
    
    install -D -m644 "${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
