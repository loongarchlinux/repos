# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geopy
pkgname=python-geopy
pkgver=2.2.0
pkgrel=3
pkgdesc="Geocoding library for Python"
arch=('any')
url="https://github.com/geopy/geopy"
license=('MIT')
depends=('python-geographiclib')
makedepends=('python-setuptools')
checkdepends=('python-aiohttp' 'python-async_generator' 'python-mock'
'python-pytest-aiohttp' 'python-pytest' 'python-pytz' 'python-requests')
optdepends=('python-aiohttp: for geopy.adapters.AioHTTPAdapter'
            'python-requests: for geopy.adapters.RequestsAdapter')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('26f33680d23445aecfa9cb1546007c74332adbf6d7510575941ef948391b8530c45eba28088d9ae781a8af63de8fa57f6f20e46be9d87a9621a242df83a51ff9'
            'SKIP')
b2sums=('6c4e9571012e3f55ef03759527f5171e0c631951ae923b9c88968d64dca1fc10fe0b584bf7eb825a4ff21357fbc15fbc10be4d63acace675dc60afaa9f42b9a8'
        'SKIP')
validpgpkeys=('A18FE9F6F570D5B4E1E1853FAA7B5406547AF062') # Kostya Esmukov <kostya@esmukov.ru>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # geocoders tests require internet access and are flaky
  pytest -v --ignore='test/geocoders'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
