# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.7.2
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=(any)
url="https://www.laborejo.org/fluajho/"
license=(GPL3)
groups=(pro-audio)
depends=(hicolor-icon-theme libcalfbox-lss python python-pyqt5 ttf-dejavu)
optdepends=('new-session-manager: Session management')
provides=(soundfont-synthesizer)
source=(https://www.laborejo.org/downloads/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('3b74edb734710781723df2b182a2dbab2a52af3167ae79fffdec9ccbae4ed8046012513a507e6ebc4852ff7ad57f3b3df231128b498f186d5128600fc26cc6b6'
            'SKIP')
b2sums=('d20d31d9db743aa56c24422ab0a8d593118be4138d13caffba1e978d37583ac32b6c5871210a39752751cfef4616f4f6f3510c3c9038332787b97852d908823a'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
