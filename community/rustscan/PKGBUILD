# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Gobidev <adrian[dot]groh[at]t-online[dot]de>
# Contributor: Hao Long <aur@esd.cc>

pkgname=rustscan
pkgver=2.1.0
pkgrel=2
pkgdesc='A modern port scanner'
arch=('loongarch64' 'x86_64')
url='https://github.com/rustscan/RustScan'
license=('GPL3')
depends=('gcc-libs' 'nmap')
makedepends=('git' 'rust')
checkdepends=('python')
options=('!lto')
_commit='075025113d849303af8366ada88da2edcd8a24c0'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'update-lockfile.patch'
)
b2sums=('SKIP'
        'bede006174ed64bb1f2dcfb1ea4fc9e8b676af176c25299415a99e107581103c04f1ec08363bdd8106129ad694fb8f0b3be9d0e216222b57e44de5166f519b83')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # update out-of-sync lockfile
  patch -p1 -i ../update-lockfile.patch

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rustscan
}
