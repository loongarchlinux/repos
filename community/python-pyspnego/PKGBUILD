# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyspnego
pkgname=python-${_pkg}
pkgver=0.2.0
pkgrel=5
pkgdesc="SPNEGO Authentication library"
arch=(any)
url="https://github.com/jborean93/pyspnego"
license=(MIT)
depends=(python-cryptography)
makedepends=(python-setuptools)
checkdepends=(cython python-pytest python-pytest-mock python-gssapi python-krb5 python-ruamel-yaml)
optdepends=('python-gssapi: Kerberos support'
            'python-krb5: Kerberos support'
            'python-ruamel-yaml: YAML configuration')
# No tests in pypi tarball
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-credssp.patch::https://github.com/jborean93/pyspnego/pull/25.patch)
sha256sums=('3d201f33c73d5455d3e6db1892585c6962d6181d0fe3fe0549483ff1febf3451'
            '74d16ada063c1204b73953b5c9614e0a32d080886f5467a6ab888a53d57a1275')

prepare() {
  cd ${_pkg}-${pkgver}
  patch -p1 < ../${pkgname}-fix-credssp.patch
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}/build/lib" pytest -v --color=yes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
