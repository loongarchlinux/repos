# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-spell
pkgver=1.23
pkgrel=1
pkgdesc="A formatter for spellchecking Pod"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Spell"
depends=('perl-class-tiny' 'perl-file-sharedir' 'perl-lingua-en-inflect' 'perl-path-tiny'
         'perl-pod-parser')
makedepends=('perl-file-sharedir-install' 'perl-test-deep')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/Pod-Spell-$pkgver.tar.gz")
sha512sums=('005ed29f2bcba22cf2bf102ca79f62b0342e58340f3ad3c517713c3c7ee5df19b7afc0163e00fc090065c31741c4ffb4de4da2d0db5012718b64148614baa660')

build() {
  cd Pod-Spell-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Spell-$pkgver
  make test
}

package() {
  cd Pod-Spell-$pkgver
  make DESTDIR="$pkgdir" install
}
