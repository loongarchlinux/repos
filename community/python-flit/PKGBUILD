# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=flit
pkgbase=python-$_pkgname
pkgname=($pkgbase{,-core})
pkgver=3.2.0
pkgrel=2
pkgdesc='Simplified packaging of Python modules'
arch=('any')
url='https://github.com/takluyver/flit'
license=('BSD')
depends=('python' 'python-toml')
makedepends=('python-setuptools' 'python-dephell'
             'python-requests' 'python-docutils')
             #'python-build' 'python-install' 'python-pyproject2setuppy'
checkdepends=('python-pytest' 'python-testpath' 'python-responses')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('b40768fb4b0b2a2e3116ac790dfa629da5a57cda34b10abb8ce6a8febcc3436795d5122dcfa3db2c6d07d493f00391d2ca7300112561c8ff2a0b04d77c879662')

prepare() {
  cd $_pkgname-$pkgver

  dephell deps convert --from pyproject.toml --to setup.py

  cd flit_core

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build

  cd flit_core

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=flit_core python -m pytest
}

package_python-flit() {
  depends+=('python-flit-core' 'python-requests' 'python-docutils')
  provides=($_pkgname)
  conflicts=($_pkgname)

  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  cp -a flit/license_templates "$pkgdir"/usr/lib/python3.9/site-packages/flit/

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-flit-core() {
  pkgdesc+=' (core backend)'

  cd $_pkgname-$pkgver/flit_core

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
