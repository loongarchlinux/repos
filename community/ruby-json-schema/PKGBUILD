# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Roberto Valentini <valantin89 [at] gmail [dot] com>

_gemname='json-schema'
pkgname="ruby-${_gemname}"
pkgver=2.8.1
pkgrel=4
pkgdesc='Interface for validating JSON objects against a JSON schema conforming to JSON Schema Draft 4.'
arch=('any')
url='https://github.com/ruby-json-schema/json-schema'
license=('MIT')
makedepends=('ruby-rdoc' 'ruby-bundler')
checkdepends=('ruby-rake' 'ruby-minitest' 'ruby-test-unit' 'ruby-webmock')
depends=('ruby' 'ruby-addressable')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
        'fix-broken-ruby26-and-newer-tests.patch'
        'disable_tests_with_external_fixtures.patch')
sha512sums=('4634fb46e0cf7cbe74def73bcdd1f8bbdf50430477cd5a2bba47c79a17f63b582d68c9064b5aec70c17b0e73db5032ddffb0a036d2f6e34ff571880b325f297b'
            'cd2e2fc621edb7e49855eebeaddb098b0c3fbea817201e36b9f2d43df38306f790c2ebc2cc19092bc3084b8a3b1a69e2271f6497845f6defc410082a91971dcc'
            '3fd3d48fcc3e5a8356ac6b5839ec5db3495468071a19902550280ebecbb0e3bdd205fe2aa5ba41841dd08999a9bac5364b153c15f60e12a6af422a802816de4f')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # remove simplecov. It's listed as a dependency, but not actually used/required
  # sed --in-place '/simplecov/d' Gemfile

  # replace upper version boundaries for ruby gems
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  # the output of the ipaddr lib changed.
  # This patch adjusts the expected output to match ruby2.6 and newer
  # this is based on https://github.com/ruby-json-schema/json-schema/pull/437
  patch --forward --verbose --strip=1 --input=../fix-broken-ruby26-and-newer-tests.patch

  # disable tests that try to download fixtures from dead upstream websites
  patch --forward --verbose --strip=1 --input=../disable_tests_with_external_fixtures.patch

  # tests from this file fail because they try to download fixtures from http://json-schema.org/address
  # but that source is dead
  #rm test/schema_reader_test.rb
  #rm test/validator_schema_reader_test.rb
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 CHANGELOG.md CONTRIBUTING.md CONTRIBUTORS.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
