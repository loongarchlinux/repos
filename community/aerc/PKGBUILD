# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: Gökberk Yaltıraklı <aur at gkbrk dot com>
# Contributor: Drew DeVault <sir@cmpwn.com>

pkgname=aerc
pkgver=0.10.0
pkgrel=1
pkgdesc='Email Client for your Terminal'
arch=('loongarch64' 'x86_64')
url='https://git.sr.ht/~rjarry/aerc'
license=('MIT')
depends=('notmuch-runtime' 'gawk')
makedepends=('go' 'scdoc')
optdepends=('w3m: for showing HTML messages'
            'dante: for showing HTML messages')
provides=('aerc')
conflicts=('aerc')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~rjarry/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('016182847e1fb8198973aa8522d9a205a8589760d40b0177cb41a9b236277fb4aa11d2c0f14bd836be5a53f4b4d5ef65b407407e09399a2ad025fbf22d53bfbb')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw -tags=notmuch"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR=$pkgdir install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/aerc/"
}
