# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.test
pkgver=5.1.0
pkgrel=2
pkgdesc="Testing support by jaraco"
url="https://github.com/jaraco/jaraco.test"
license=('MIT')
arch=('any')
depends=('python-jaraco.collections' 'python-jaraco.context' 'python-jaraco.functools' 'python-toml'
         'python-more-itertools')
makedepends=('python-setuptools-scm' 'python-wheel' 'python-build' 'python-installer')
checkdepends=('python-pytest-enabler' 'python-pytest-mypy')
source=("https://github.com/jaraco/jaraco.test/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6cd810b8075e622b7a631b3bcca953c6210f2f427ea35f9958f98110fcfb5ef9bd7046ad2909355451208983aa2a25abee29fec829cc18dc94ad491d319e1361')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.test-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jaraco.test-$pkgver
  python -m pytest
}

package() {
  cd jaraco.test-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
