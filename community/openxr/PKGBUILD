# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openxr
pkgver=1.0.24
pkgrel=1
pkgdesc='An open standard for virtual reality and augmented reality platforms and devices'
arch=('loong64' 'x86_64')
url='https://www.khronos.org/openxr/'
depends=('jsoncpp' 'libx11' 'vulkan-icd-loader' 'libglvnd' 'libxrandr' 'libxxf86vm')
makedepends=('cmake' 'vulkan-headers' 'python' 'ninja' 'wayland' 'mesa')
license=('APACHE')
source=("https://github.com/KhronosGroup/OpenXR-SDK-Source/releases/download/release-${pkgver}/OpenXR-SDK-Source-release-${pkgver}.tar.gz"{,.asc}
        fix-gcc11.patch)
validpgpkeys=(F029455EAB70F520FF4A03BD7F534CD56F00321E)
sha512sums=('84f344f08224953b7e9b05268b114cb3fc4acdc345ad4f83d90a25bee8012621faa13cfd5af31f2302803eb79f684278c4d7d2ce65552653073e56cebc9acba5'
            'SKIP'
            '783726fb48dc4551dfec0da2f28049d73afe66d237db54f0914b5156b8a45c9d5edf2f9a4b97a8adbbbfa1157c07cd418dbd4d53252f2feb49e9420fde0b0840')

prepare() {
  cd OpenXR-SDK-Source-release-${pkgver}

  patch -Np1 -i "${srcdir}"/fix-gcc11.patch
}

build() {
  cd OpenXR-SDK-Source-release-${pkgver}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DPRESENTATION_BACKEND=xlib \
    -DDYNAMIC_LOADER=ON
  ninja -C build
}

package() {
  cd OpenXR-SDK-Source-release-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
