# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.9.6
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python-setuptools' 'python-mypy_extensions' 'python-typing_extensions')
checkdepends=('python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha512sums=('a53cc4396d329bf7d6c8fe1367e09803bc2a7bd1efe9f9a3ce679ca82fb70d2b7922e64e9a62cd15a7724a879e56729636130d337571e743825d678180251316')

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
