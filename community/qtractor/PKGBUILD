# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.29
pkgrel=3
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(loong64 x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(glibc gcc-libs hicolor-icon-theme libmad libxcb qt6-base qt6-svg zlib)
makedepends=(alsa-lib aubio cmake dssi jack ladspa liblo libogg libsamplerate
libsndfile libvorbis lilv lv2 qt6-tools rubberband suil vst3sdk)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(clap-host dssi-host ladspa-host lv2-host vst-host vst3-host)
options=(debug)
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('bd432e3eb2d804aacc45943c36160c938778827a1c832f30045b2456d363843ac2f77e4b2cb897b905aa026b7d9238eb2963ca1ebbd64d127d890857c5426c41')
b2sums=('d7f17211e885c8458c7d74d1cd0cde69d316b1765ab79bdfb3ff1abcfb35ad008f017dd12372a2fafb309a7bead0c3d31f8f1cc322da123d39760377c1883e82')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libaubio.so libjack.so liblilv-0.so liblo.so libogg.so
  librubberband.so libsamplerate.so libsndfile.so libvorbisenc.so
  libvorbisfile.so libvorbis.so )

  make DESTDIR="$pkgdir" install -C build
  # docs
  install -vDm 644 $pkgname-$pkgver/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
