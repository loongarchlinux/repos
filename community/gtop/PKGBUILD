# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Peter Cai <peter at typeblog dot net>

pkgname=gtop
pkgver=1.1.1
pkgrel=1
pkgdesc='System monitoring dashboard for terminal'
arch=('any')
url='https://github.com/aksakalli/gtop'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("$pkgname-$pkgver.tar.gz::https://github.com/aksakalli/gtop/archive/v${pkgver}.tar.gz")
sha512sums=('cd1516bd4e9c6fde4803420df66436ad4c8265b6cb35f2a261f05e501785bcdccd55348dba0064452a84ac5c95a6e3d6a222d25d94950569286d475a9ae88fe3')

build() {
  cd $pkgname-$pkgver
  npm install --production
}

package() {
  local _npmdir="$pkgdir/usr/lib/node_modules"
  mkdir -p "$_npmdir"/gtop
  cp -R $pkgname-$pkgver/* "$_npmdir/gtop"
  local _bindir="$pkgdir/usr/bin"
  mkdir -p "$_bindir"
  ln -s /usr/lib/node_modules/gtop/bin/gtop "$_bindir/gtop"

  install -Dm644 $pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
