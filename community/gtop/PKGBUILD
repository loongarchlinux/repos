# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Peter Cai <peter at typeblog dot net>

pkgname=gtop
pkgver=1.1.2
pkgrel=1
pkgdesc='System monitoring dashboard for terminal'
arch=('any')
url='https://github.com/aksakalli/gtop'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("$pkgname-$pkgver.tar.gz::https://github.com/aksakalli/gtop/archive/v${pkgver}.tar.gz")
sha512sums=('0cdec073ceedad2eed28806ad07886c71cbebb8a3bd30ce23b7c82e22c633040fc1bbd4c8628f55cc3156c3515e1feb3a7b662d3a4806435bdabf8c89e073ec3')

build() {
  cd $pkgname-$pkgver
  npm install --production
}

package() {
  local _npmdir="$pkgdir/usr/lib/node_modules"
  mkdir -p "$_npmdir"/gtop
  cp -R $pkgname-$pkgver/* "$_npmdir/gtop"
  local _bindir="$pkgdir/usr/bin"
  mkdir -p "$_bindir"
  ln -s /usr/lib/node_modules/gtop/bin/gtop "$_bindir/gtop"

  install -Dm644 $pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
