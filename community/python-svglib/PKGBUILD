# Maintainer: David Runge <dvzrv@archlinux.org>

_name=svglib
pkgname=python-svglib
pkgver=1.3.0
pkgrel=1
pkgdesc="Read SVG files and convert them to other formats"
arch=(any)
url="https://github.com/deeplook/svglib"
license=(LGPL3)
depends=(python-lxml python-reportlab python-tinycss2 python-cssselect2)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pillow python-pytest)
# sdist tarballs do not include all necessary files: https://github.com/deeplook/svglib/issues/341
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-v$pkgver.tar.gz::https://github.com/deeplook/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('457fb69ba1c298079888f200502c7099cf5ee674857ff8682948011bf8e1544715e6196544212f5c0ddef1202d04f3f05d01edb4351e673ceae16a85ca0d66e5')
b2sums=('f53bda781479ddc5d4b04fd122b5141620db81756308973cb283957615711e62623baa74b69af954936be24970a4e4a6f026667c4069d62bf1705a839ecc52f3')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  # disable tests that need remote resources
  pytest -vv -k "not TestWikipediaSymbols and not TestWikipediaFlags"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
