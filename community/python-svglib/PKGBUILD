# Maintainer: David Runge <dvzrv@archlinux.org>

_name=svglib
pkgname=python-svglib
pkgver=1.4.1
pkgrel=1
pkgdesc="Read SVG files and convert them to other formats"
arch=(any)
url="https://github.com/deeplook/svglib"
license=(LGPL3)
depends=(python-lxml python-reportlab python-tinycss2 python-cssselect2)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pillow python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('c639de1f83b9fad743fc1dc84fb5aa404e0c385ae315a24b37ffecc37e4e18ba95a48a3781faac1a233b69afcb9bfa0fd896cce5499fa7af428cf5344ec93582')
b2sums=('84bc511419fb08582cf63ff0be242772d4c18bf8c9e72cbe23a649350355d2df992bf3087c84767008a5fdf256aa6909f3c8a0974a20e70ad8e50aef464e8cdb')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  # disable tests that need remote resources
  pytest -vv -k "not TestWikipediaSymbols and not TestWikipediaFlags"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
