# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-hpack
pkgver=1.4.2
pkgrel=1
pkgdesc='A compresssor and decompressor for HTTP 2.0 HPACK'
arch=(any)
url='https://github.com/socketry/protocol-hpack'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-hpack/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('507caed6431b66e5f558cd68964d09a3850d39e07569be1932543b42e7760ed7')

prepare() {
  cd protocol-hpack-$pkgver
  sed -r -e 's|~>|>=|g' -e '/covered/d' -e 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -printf "%P\\\\0"|' -i http-hpack.gemspec spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-hpack-$pkgver
  rake build
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/protocol-hpack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-hpack-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd protocol-hpack-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
