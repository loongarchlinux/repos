# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream
pkgname=python-${_pyname}
pkgver=2.1.0
pkgrel=1
pkgdesc='Simple streaming JSON parser'
arch=(any)
url="https://github.com/daggaz/json-stream"
license=('MIT')
depends=('python')
makedepends=(python-build python-installer python-setuptools python-wheel)
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
b2sums=('1e30c94a934a7c7afa08ebc9bd161ad8b1801ffa93d799c3a56fb024bcb828a01057bdcc4f3eecfe5384806ce965801032b846ef678d7081f1592e093fb4b3fc')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
