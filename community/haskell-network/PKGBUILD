# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=network
pkgname=haskell-network
pkgver=3.1.2.0
pkgrel=35
pkgdesc="Low-level networking interface"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-doctest' 'haskell-hspec' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ccab6a31a5bfe5a30ad6d0e757d523d03eb7560c47bee0abd79205f2aaedab985bc6381df50fcdb8ffb48a081198891daa46e0ec6602a99bf5878c762b89c6b7')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/8080/8003/' tests/Network/SocketSpec.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
