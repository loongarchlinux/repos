# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=tox-current-env
pkgname=python-$_pkgname
pkgver=0.0.3
pkgrel=1
pkgdesc='tox plugin to run tests in current Python environment'
arch=('any')
url='https://github.com/fedora-python/tox-current-env'
license=('MIT')
depends=('python' 'python-tox')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        '1.patch::https://github.com/FFY00/tox-current-env/commit/8ac0f789508d7266a7755478f9880f27642eb97c.patch')
sha512sums=('909e4e246ba7e09979ac91222bddf52d0088a56f02225324b1fe11a267d0fd8454d440a8cffebeaa917e8114233a8c65d685634bfc50c69fe91464dcf551a0b6'
            '1aad2b2f9ad9f586baf22aaf6d8682f7b13c0e5ba5cc7fc60c1dee5e227db0711ea3c9d48f4e680ff1d04014940855f83a064a75cfe8b8b55435c70187776263')

build() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../*.patch

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
