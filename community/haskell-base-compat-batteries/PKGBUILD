# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=base-compat-batteries
pkgname=haskell-base-compat-batteries
pkgver=0.12.1
pkgrel=23
pkgdesc="base-compat with extra batteries"
url="https://hackage.haskell.org/package/base-compat-batteries"
license=('MIT')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-base-compat')
makedepends=('ghc')
checkdepends=('haskell-hspec' 'haskell-hspec-discover' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('346ad1e53e4712017e610d1883e91371c1bbdefdce35b2830b8807e207f6fbd2233e2be153d85b6632059f58d3d9c74435537719823533ddddd47d96b231b0cf')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
