# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fixtures
pkgver=3.0.0
pkgrel=8
pkgdesc="Fixtures, reusable state for writing clean tests and more"
arch=('any')
license=('Apache')
url="https://launchpad.net/python-fixtures"
depends=('python-pbr' 'python-six' 'python-testtools')
checkdepends=('python-subunit' 'python-mock')
source=("https://pypi.io/packages/source/f/fixtures/fixtures-$pkgver.tar.gz")
sha512sums=('e52ba864c42d15a8baa60583761006b2dd78e3cd5f3f043f5702346aef428b933d62e8fa5889b7cb4196e4072acc5df3b7ffa39702a1b782887bdc109299b695')

build() {
  cd fixtures-$pkgver
  python setup.py build
}

check() {
  cd fixtures-$pkgver
  python -m testtools.run fixtures.test_suite
}

package() {
  cd fixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
