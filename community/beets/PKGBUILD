# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.4.9
pkgrel=4
pkgdesc='Flexible music library manager and tagger'
arch=(any)
url=https://beets.io/
license=(MIT)
depends=(
  python-jellyfish
  python-munkres
  python-musicbrainzngs
  python-mutagen
  python-setuptools
  python-six
  python-unidecode
  python-yaml
)
makedepends=(
  git
  python-sphinx
)
optdepends=(
  'bash-completion: Bash completion'
  'bs1770gain: ReplayGain plugin'
  'ffmpeg: Convert plugin'
  'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'imagemagick: Thumbnails plugin'
  'python-beautifulsoup4: Lyrics plugin'
  'python-flask: Web plugin'
  'python-gobject: ReplayGain plugin'
  'python-mpd2: MPDStats plugin'
  'python-pyacoustid: Chromaprint/Acoustid plugin'
  'python-pylast: LastGenre plugin'
  'python-requests: Chromaprint/Acoustid, BPD, FetchArt plugins'
  'python-xdg: Thumbnails plugin'
)
_tag=2b8a2eb96bcaf418cdfcc0cfe762a18e31cff138
source=(
  git+https://github.com/beetbox/beets.git#tag=${_tag}
  beet
)
b2sums=('SKIP'
        '1b36ba07d85f0fbcae0a3d8253ce2e5d7ecf8a17c6f0bb946579663de546d53cd4fe5da84ee8d880d7f95f48e66ef6010232a995d86d0c52b52218cd991a7782')

pkgver() {
  cd beets

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd beets

  git show dab0c1f9abda5b17cc7488f89a6fe08be7bc56a0 --  beets/util/functemplate.py | git apply - # Python 3.8.4 compatibility
}

build() {
  cd beets

  python setup.py build sdist
}

package() {
  cd beets

  python setup.py install --root="${pkgdir}" --optimize=1

  install -dm 755 "${pkgdir}"/usr/share/bash-completion/completions/
  install -m 644 ../beet "${pkgdir}"/usr/share/bash-completion/completions/
  install -dm 755 "${pkgdir}"/usr/share/zsh/site-functions
  install -m 644 extra/_beet "${pkgdir}"/usr/share/zsh/site-functions/

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/beetsconfig.5 "${pkgdir}"/usr/share/man/man5/

  install -dm 755 "${pkgdir}"/usr/share/licenses/beets
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
