# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: berberman <hatsue@typed.icu>

_hkgname=tomland
pkgname=haskell-tomland
pkgver=1.3.3.0
pkgrel=22
pkgdesc="Implementation of bidirectional TOML serialization."
url="https://github.com/kowainik/tomland"
license=('MPL2')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-megaparsec' 'haskell-parser-combinators'
         'haskell-unordered-containers' 'haskell-validation-selective')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-hspec' 'haskell-hspec-hedgehog'
             'haskell-hspec-megaparsec' 'haskell-markdown-unlit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('3bf4e6382993c07f776b2dd7326e2eeecde581fe145a60caaa3b0decb8d81404e772a12594442afa6bce289c3df6548dc46df6c82822e9c7e4f0be0b7d3af1c8')

prepare(){
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u hspec $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver    
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/{,tomland-}readme
}
