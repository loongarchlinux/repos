# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=j2cli
pkgname=python-${_pkgname}
pkgver=0.3.12b
pkgrel=5
pkgdesc="Command-line interface to Jinja2 for templating in shell scripts."
url="https://github.com/kolypto/j2cli"
license=("BSD")
arch=('any')
depends=('python' 'python-jinja' 'python-pyaml')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kolypto/j2cli/archive/v${pkgver}.tar.gz")
sha512sums=('6c64aee49091969650214c7e581435d61a32211da14be3b80504af192e30d0de545e698426c3b68deb71e4ac8e422036a8202f0ea4d29f1cc3598872c4299b90')
b2sums=('08b434596021576dcecbaa8e700479c103f5d93c3d42880f8f5062fb0db78d4bd0bcc43c068e56b950a654b383a27b5aaff3b58ef22382c6fd6da8d9135ede98')

build () {
	cd ${_pkgname}-${pkgver}
	python setup.py build
}

check () {
	cd ${_pkgname}-${pkgver}
	python setup.py test
}

package () {
	cd ${_pkgname}-${pkgver}
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
