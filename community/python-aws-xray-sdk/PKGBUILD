# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.10.0
pkgrel=1
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
# Not including python-future as it's needed only on Python 2
# https://github.com/aws/aws-xray-sdk-python/pull/343
depends=(python python-botocore python-wrapt)
# See extensions in https://github.com/aws/aws-xray-sdk-python/tree/master/aws_xray_sdk/ext
optdepends=(python-aiobotocore python-aiohttp python-bottle python-django
            python-flask python-flask-sqlalchemy python-mysql-connector
            python-pg8000 python-psycopg2 python-pymongo python-pymysql
            python-pynamodb python-requests python-sqlalchemy)
makedepends=(python-setuptools ${optdepends[@]})
checkdepends=(python-pytest python-pytest-aiohttp python-testing.postgresql
              python-webtest python-django-fake-model python-mock python-pytest-benchmark)
# Not using PyPI source tarball as it does not include tests
source=("https://github.com/aws/aws-xray-sdk-python/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "python310.diff")
sha256sums=('ac44b578718a6d975e980354194e1a4e300f9713bf60b74dbe1ce62efe6dd1ed'
            '11784ed0d9b9f89bf1cc43a6d97d8488672b74e3419e121be1a50f09e409ad71')

prepare() {
  cd $_pkgname-$pkgver
  # The 'database_version' field relies on a private member in pg8000 [1], which is removed in pg8000 1.21 [2]
  # https://github.com/aws/aws-xray-sdk-python/blob/2.8.0/aws_xray_sdk/ext/pg8000/patch.py#L27
  # https://github.com/tlocke/pg8000/commit/fa382a2da52c004fa9b98b242e855132d3fd6d23#diff-2d43fca8e06901904747dfab40d9a7a698ff03fab4c9ba1c01a8ce04105d537eL799
  sed -i "/sql\['database_version'\]/d" tests/ext/pg8000/test_pg8000.py

  # Django 4.x compatibility; see https://docs.djangoproject.com/en/3.2/ref/urls/#url
  sed -i 's#from django.conf.urls import url#from django.urls import re_path as url#' tests/ext/django/app/views.py

  # https://github.com/aws/aws-xray-sdk-python/issues/321
  patch -Np1 -i ../python310.diff
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # See setenv= in upstream tox.ini
  export DJANGO_SETTINGS_MODULE=tests.ext.django.app.settings
  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # the test suite for pymysql uses testing.mysqld, which is not
  # compatible with MariaDB
  # https://github.com/tk0miya/testing.mysqld/issues/3
  pytest -v tests --ignore tests/ext/pymysql
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
