# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx_lv2_theme
pkgname=python-sphinx-lv2-theme
pkgver=1.2.0
pkgrel=1
pkgdesc="A minimal static theme for Sphinx"
arch=(any)
url="https://gitlab.com/lv2/sphinx_lv2_theme"
license=(ISC)
depends=(python-sphinx)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('c6a8e0d8a31821bcfd63ed2bcf188c3180d452885b1ec3ea07efdbcd5a6dfac1')
b2sums=('e68ca67ae57e46fc2277d7644d5c013bad109a7ce363f85ae19efe44a021b2a3a13c19335a534e04bdd835d91276f24b831b3885253b974c5f642fe224b3ebf2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
