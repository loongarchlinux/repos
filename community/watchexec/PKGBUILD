# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.19.0
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
arch=('loongarch64' 'x86_64')
license=('Apache')
source=("https://github.com/watchexec/watchexec/archive/refs/tags/cli-v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('6837dff4d14802e1e3714a2f3ef4330f8faadd10449508b128cff7f93d921101')

build() {
  cd "watchexec-cli-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-cli-v$pkgver"
  cargo test --locked
}

package() {
  cd "watchexec-cli-v$pkgver"
  install -Dm644 "doc/watchexec.1" "$pkgdir/usr/share/man/man1/watchexec.1"
  gzip -9 "$pkgdir/usr/share/man/man1/watchexec.1"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
