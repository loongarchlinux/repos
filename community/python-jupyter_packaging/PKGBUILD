# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter_packaging
pkgname=python-jupyter_packaging
pkgver=0.12.1
pkgrel=1
pkgdesc='Jupyter Packaging Utilities'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-packaging python-deprecation python-wheel python-tomlkit)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-mock python-pytest-timeout python-setuptools)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('8ca939a805dbe0c073968e8d5603aba0e7e4130d2e956207a37d98f8bc940d34')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -vv
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
