# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-contracts
pkgver=0.17
pkgrel=1
pkgdesc='Contracts for Ruby'
arch=(any)
url='https://github.com/egonSchiele/contracts.ruby'
license=(BSD)
depends=(ruby)
checkdepends=(ruby-rspec)
options=(!emptydirs)
source=(https://github.com/egonSchiele/contracts.ruby/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1b4e5dd290512639d95d5a8517cac466c13228ddba58ebb3a6843ed2ffe4c3dd')

prepare() {
  cd contracts.ruby-$pkgver
  sed -r -e 's|git ls-files|find -type f|' -i contracts.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby-$pkgver
  gem build contracts.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    contracts-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd contracts.ruby-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
