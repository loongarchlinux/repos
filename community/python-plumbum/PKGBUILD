# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-plumbum
_name=plumbum
pkgver=1.7.0
pkgrel=3
pkgdesc='A small yet feature-rich Python library for shell script-like programs, and more'
url='https://github.com/tomerfiliba/plumbum'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('0a12891278c12bb98b8bbb825448375240ee347eb3ca166b9cbb4da3e083979d947a7bf0a843fd9a0b7c9a13d09f24e988ee877f29420a3f29f40e387eec8826')
b2sums=('19ae8a37dff38ac3d263d9400e44ac174c46ff8cef92cf45b9ea31d6d344543fba66de3fd0fcd15acc205af5939f11be6c56e6cd9ca39a666f92f18dcd29b9f7')

build() {
  cd "${_name}-${pkgver}"
  export PYTHONHASHSEED=0
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH=. python -c 'import plumbum'
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
