# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-plumbum
_name=plumbum
pkgver=1.8.0
pkgrel=1
pkgdesc='A small yet feature-rich Python library for shell script-like programs, and more'
url='https://github.com/tomerfiliba/plumbum'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-setuptools-scm')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('47874c42081d385fad85b9073fbeb711112b8fa847fc79035be1277a1fdabcfb06e7822a4dbba271d86502b9b297a70742f30b1158c363b668a47e783c9990cd')
b2sums=('30006fa3826bbdd9482592d825931fd7dcb05b2aca6d762093c4f722f0426aaa6f07fde55754f115e4fba4a45997d2995a657c5a1cfa34c417bb4f5fb2e5834d')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH=. python -c 'import plumbum'
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
