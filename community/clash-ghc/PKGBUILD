# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=clash-ghc
pkgver=1.4.2
pkgrel=11
pkgdesc="CAES Language for Synchronous Hardware"
url="https://clash-lang.org/"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-clash-lib' 'haskell-clash-prelude'
         'haskell-concurrent-supply' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-typelits-extra'
         'haskell-ghc-typelits-knownnat' 'haskell-ghc-typelits-natnormalise' 'haskell-hashable'
         'haskell-lens' 'haskell-primitive' 'haskell-reflection' 'haskell-split' 'haskell-uniplate'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc')
conflicts=('clash')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3d50d7379626af851207e8a838734f6714f816d6e21f3f4c90c3b1062480ce8968bf185a121bc4ebb02058ea428b006e9d7c1ce8a7383c3ec679dad6dd594c28')

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
