# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Nikola
pkgname=nikola
pkgver=8.2.0
pkgrel=1
pkgdesc="A static site and blog generator"
arch=(any)
url="https://getnikola.com/"
license=(MIT)
# still requires python-setuptools due to the use of pkg_resources
depends=(python-babel python-blinker python-doit python-dateutil
python-docutils python-logbook python-lxml python-mako python-markdown
python-natsort python-piexif python-pillow python-pygments python-pyrss2gen
python-requests python-setuptools python-unidecode python-yapsy)
makedepends=(jupyter-notebook python-aiohttp python-ghp-import python-husl
python-ipykernel python-jinja python-micawber python-phpserialize python-pygal
python-pyphen python-ruamel-yaml python-toml python-typogrify python-watchdog
zeromq)
checkdepends=(python-freezegun python-pytest)
optdepends=('jupyter-notebook: for .ipynb support'
            'python-aiohttp: for `nikola auto`'
            'python-ghp-import: for upload to Github pages'
            'python-hsluv: for section color mixing'
            'python-html5lib: for HTML5 minify and format support'
            'python-husl: for optional color support'
            'python-ipykernel: for .ipynb support'
            'python-jinja: for jinja2 based themes'
            'python-micawber: for embedding media in posts'
            'python-phpserialize: for Wordpress import'
            'python-pygal: for SVG graph plotting'
            'python-pyphen: for hyphenation support'
            'python-ruamel-yaml: for YAML support'
            'python-toml: for TOML support'
            'python-typogrify: for typographical enhancements'
            'python-watchdog: for `nikola auto`')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('c55450791da2e71bb57f44816aae866d6fc5d20c632fb93f9224ab9c7e630b9a1ba8cd2fb569b0f0c416d3246aaeada8bd5571fed8ee3bdce9eb3865c89402bf'
            'SKIP')
b2sums=('04ff867295caed134558dc81e9c7cf3a7e607792cc0bc6aec670a45e443c15a1f89b1a8062fbc0a1af3e41951e843eac5106daf53b6121dd95aae0984967df75'
        'SKIP')
validpgpkeys=('DF32C99CFF84282CF9D4CF7171023DBD5EAAEA16') # Chris Warrick <kwpolska@gmail.com>

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove options to pytest so that we don't require python-coverage et. al.
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  # shell completions
  PYTHONPATH="build" \
  python -m "${pkgname}" tabcompletion \
    --shell bash \
    --hardcode-tasks \
    > "${pkgname}_bash"
  PYTHONPATH="build" \
  python -m "${pkgname}" tabcompletion \
    --shell zsh \
    --hardcode-tasks \
    > "${pkgname}_zsh"
  # ensure that the shell completion is assigned to the correct executable
  sed -e "s/__main__.py/${pkgname}/g" -i "${pkgname}_"{bash,zsh}
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k "not test_compiling_markdown"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "${pkgname}_bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -vDm 644 "${pkgname}_zsh" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
