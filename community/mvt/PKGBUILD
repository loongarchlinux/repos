# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=1.4.1
pkgrel=2
pkgdesc="Mobile Verification Toolkit helps conducting forensics of mobile devices in order to find signs of a potential compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-click' 'python-iosbackup' 'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests' 'python-simplejson')
makedepends=('python-setuptools')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('07d09e9aaa82668f377c2fa6d46bcfb41e536ac9e2b2dd090f492c4892f616b6895effa606cc7e813a58c34e0768cde2fde68b98b1e75eec4e6837a739dd00ea')
b2sums=('b32d1284e42948b700512d497f45603a0fd0fdb4d3a19ef4c268004537c5bb6e195490b21a9cfeea9534244440e25721cd3b366de62f8c9f0c856cae771eda65')

prepare() {
  cd "mvt-${pkgver}"
  sed -i 's/packaging>=21.0/packaging>=20.9/' setup.py
}

build() {
  cd "mvt-${pkgver}"
  python setup.py build
}

package() {
  cd "mvt-${pkgver}"
  python setup.py install -O1 --root="$pkgdir" --skip-build
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
