# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.1.6
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-appdirs' 'python-click' 'python-cryptography' 'python-iosbackup'
         'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests'
         'python-simplejson' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('94bbd52220ce11edd46f11c40ed4bfe4dcab3875696cbe2484b67bd18a425392acfebdc7239512f89797ba65434753a53bf9868adaafcf54e59f05b060b18828')
b2sums=('450293db6e433ede1232fa729e2946f725fc58e5900b7b4a86169dffa7cc2d94d1662b70064e97dec7b2d870d6ed19d2569347bbfa43817881f56d59fa54ba48')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
