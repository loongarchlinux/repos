# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.2.1
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-appdirs' 'python-click' 'python-cryptography' 'python-iosbackup'
         'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests'
         'python-simplejson' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('f459b816cfe4a6517326f0048a8c558e1430f6bde152ad90c18b944f2bdaf1fd0e758d25a7aabefa0ac4bc80c03f08c3a91df32a97deb7c8d9f51b62770a3a60')
b2sums=('aed1a44df6567eaede9ddd4067ff504bebbaebecaad252cbaaa48922fb5948e0676b16fa4e490b5f9c6cb1caf257b4588ead0defa9a745745664feda7166e05f')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
