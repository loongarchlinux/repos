# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=1.5.5
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-click' 'python-iosbackup' 'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests' 'python-simplejson')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('2120b96fc530848453d2abaf459dbfa0cf6ebf5f47ba368019739343326b3a3b9f71cb2eedef317e8a1736d6989fb52f170f7ad8783e75563dca1d8dff66c6a4')
b2sums=('e159fc3e62b6b2c900706bba697c8ff41c27e5ab96e4bfb08830521ac3706ad00676b966c0a124ba105be2dee60b0a2042f6089dd188adf1624bcca8d2918d44')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
