# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.1
pkgrel=4
pkgdesc='The Pantheon Top Panel'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
_tag=8fce5684de93e3dcb42dfa1c86db973a839598b1
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd wingpanel

  git describe --tags
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
