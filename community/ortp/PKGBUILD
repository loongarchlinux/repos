# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=4.5.22
pkgrel=1
pkgdesc='A Real-time Transport Protocol (RTP) library'
arch=(loongarch64 x86_64)
url='https://github.com/BelledonneCommunications/ortp'
license=(GPL3)
depends=(bctoolbox)
makedepends=(cmake doxygen)
source=(https://github.com/BelledonneCommunications/ortp/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d47795431e7faa4002e6dcdaa128e2c7b5a270f7a8e059daf749ca30a0f709bd')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
