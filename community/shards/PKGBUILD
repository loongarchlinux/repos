# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.16.0
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(loongarch64 x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libevent gc)
makedepends=(crystal)
checkdepends=(git mercurial)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('e23a51fdcb9747e86b3c58d733a1c7556a78002b46482ca0fdacfe63924dc454')

build() {
  cd shards-$pkgver
  CRYSTAL_OPTS="--release" make
}

check() {
  cd shards-$pkgver

  # remove git config settings once https://github.com/crystal-lang/shards/issues/430 is fixed
  git config --global user.email "you@example.com"
  git config --global user.name "Your Name"
  git config --global column.ui always

  make test -j1 # -j1 is due to this bug https://github.com/crystal-lang/shards/issues/347
}

package() {
  cd shards-$pkgver
  PREFIX=/usr DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
