# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pycuda
pkgver=2021.1
pkgrel=9
pkgdesc="Python wrapper for Nvidia CUDA"
arch=('loongarch64' 'x86_64')
url="https://mathema.tician.de/software/pycuda"
license=('MIT' 'Apache')
depends=('boost-libs' 'cuda' 'nvidia-utils' 'python' 'python-numpy' 'python-pytools')
provides=('pycuda-headers')
conflicts=('pycuda-headers')
replaces=('pycuda-headers')
source=("https://pypi.io/packages/source/p/pycuda/pycuda-$pkgver.tar.gz")
makedepends=('ctags' 'python-setuptools' 'python-numpy' 'mesa' 'cuda' 'boost' 'nvidia-utils')
sha512sums=('1df09f59e254f7e39b630ff41d22237ab0c81c0e6e7b7611c57eb98ade8531356b9cb3e381e6fa7e8495a46c465458febaf8ad56ceb8d570b5b25defb6dd55a9')

build() {
   _arch=''
   [[ "$CARCH" = "x86_64" ]] && _arch='64'

   cd pycuda-$pkgver
   python ./configure.py \
                  --cuda-root=/opt/cuda \
                  --cuda-inc-dir=/opt/cuda/include \
                  --cudadrv-lib-dir=/opt/cuda/lib${_arch} \
                  --cudart-lib-dir=/opt/cuda/lib${_arch} \
                  --no-use-shipped-boost \
                  --boost-python-libname=boost_python3
   make
}

package() {
   cd pycuda-$pkgver
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

   install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
