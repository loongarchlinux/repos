# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=hunspell-fr
pkgver=6.4.1
pkgrel=2
pkgdesc='French (modern) hunspell dictionaries'
arch=(any)
url='https://grammalecte.net/download.php?prj=fr'
license=('MPL2')
makedepends=('qt5-webengine')
optdepends=('hunspell: the spell checking libraries and apps')
source=("https://grammalecte.net/download/fr/${pkgname/-fr/}-french-dictionaries-v${pkgver}.zip")
sha256sums=('744e77a3bd115144fa3131485231638d41c21e65ed757bfc576ccec1dcc9c6cc')

package() {
  install -dm755 "${pkgdir}"/usr/share/hunspell
  install -m644 fr-moderne.dic "${pkgdir}"/usr/share/hunspell/fr_FR.dic
  install -m644 fr-moderne.aff "${pkgdir}"/usr/share/hunspell/fr_FR.aff

  pushd "${pkgdir}"/usr/share/hunspell/
  fr_FR_aliases="fr_BE fr_CA fr_CH fr_LU"
  for lang in ${fr_FR_aliases}; do
    ln -s fr_FR.aff ${lang}.aff
    ln -s fr_FR.dic ${lang}.dic
  done

  popd

  # the symlinks
  install -dm755 "${pkgdir}"/usr/share/myspell/dicts
  pushd "${pkgdir}"/usr/share/myspell/dicts
    for file in "${pkgdir}"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd

  # docs
  install -Dm644 README_dict_fr.txt "${pkgdir}"/usr/share/doc/${pkgname}/README_dict_fr.txt

# Install webengine dictionaries
  install -d "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  done
}
