# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pastedeploy
pkgver=3.0
pkgrel=1
pkgdesc="Load, configure, and compose WSGI applications and servers"
arch=('any')
license=('MIT')
url="http://pythonpaste.org/deploy/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Pylons/pastedeploy/archive/$pkgver.tar.gz")
sha512sums=('86c39d517f2bc794a022ecd37d78ca017e4872336ebd492edd762f02e245944d533f19caad024b62cbdd328e766a13b635be21a35032c0359eb0fb02cf6964ab')

prepare() {
  cd pastedeploy-$pkgver
  sed -i 's/--cov//' pytest.ini
}

build() {
  cd pastedeploy-$pkgver
  python setup.py build
}

check() {
  cd pastedeploy-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd pastedeploy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
