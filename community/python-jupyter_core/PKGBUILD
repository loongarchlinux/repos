# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jupyter_core
pkgver=4.9.1
pkgrel=3
pkgdesc="Jupyter core package. A base package on which Jupyter projects rely."
arch=('any')
url="https://pypi.python.org/pypi/jupyter_core"
license=('BSD')
depends=('python-traitlets')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-nose')
#source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/j/jupyter_core/jupyter_core-$pkgver.tar.gz")
sha256sums=('dce8a7499da5a53ae3afd5a9f4b02e5df1d57250cf48f3ad79da23b4778cd6fa')

prepare() {
  cd "$srcdir/jupyter_core-$pkgver"
  sed -e '/\/usr\/local\/share\/jupyter/d' -i jupyter_core/paths.py
}

build() {
  cd "$srcdir/jupyter_core-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/jupyter_core-$pkgver"
  pytest \
    --deselect jupyter_core/tests/test_command.py::test_not_on_path \
    --deselect jupyter_core/tests/test_command.py::test_path_priority \
    --deselect jupyter_core/tests/test_paths.py::test_jupyter_path_prefer_env # https://github.com/jupyter/jupyter_core/issues/208
}

package() {
  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 examples/jupyter-completion.bash "$pkgdir/usr/share/bash-completion/completions/jupyter"
  install -Dm644 examples/completions-zsh "$pkgdir/usr/share/zsh/site-functions/_jupyter"
}
