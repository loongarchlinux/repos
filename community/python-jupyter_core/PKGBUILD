# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jupyter_core
pkgver=4.10.0
pkgrel=1
pkgdesc="Jupyter core package. A base package on which Jupyter projects rely."
arch=('any')
url="https://pypi.python.org/pypi/jupyter_core"
license=('BSD')
depends=('python-traitlets')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-nose')
source=("https://files.pythonhosted.org/packages/source/j/jupyter_core/jupyter_core-$pkgver.tar.gz")
sha256sums=('a6de44b16b7b31d7271130c71a6792c4040f077011961138afed5e5e73181aec')

prepare() {
  cd jupyter_core-$pkgver
  sed -e '/\/usr\/local\/share\/jupyter/d' -i jupyter_core/paths.py
  sed -e 's|setuptools>=60.0|setuptools|' -i pyproject.toml
}

build() {
  cd jupyter_core-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jupyter_core-$pkgver
  pytest -v \
    --deselect jupyter_core/tests/test_command.py::test_not_on_path \
    --deselect jupyter_core/tests/test_command.py::test_path_priority \
    --deselect jupyter_core/tests/test_command.py::test_argv0 \
    --deselect jupyter_core/tests/test_paths.py::test_jupyter_path_prefer_env # https://github.com/jupyter/jupyter_core/issues/208
}

package() {
  cd jupyter_core-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 examples/jupyter-completion.bash "$pkgdir/usr/share/bash-completion/completions/jupyter"
  install -Dm644 examples/completions-zsh "$pkgdir/usr/share/zsh/site-functions/_jupyter"
}
