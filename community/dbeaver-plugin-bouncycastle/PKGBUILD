# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-bouncycastle
pkgver=1.64.0.v20191109.0815
pkgrel=1
pkgdesc='Bouncy Castle library for DBeaver plugin for SSHv2 tunnels'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5')
source=("https://dbeaver.io/update/ce/latest/plugins/org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar"
        "https://dbeaver.io/update/ce/latest/plugins/org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar"
        "https://dbeaver.io/update/ce/latest/plugins/org.bouncycastle.bcprov_${pkgver%.*}-${pkgver##*.}.jar"
        "${pkgname}.info")
sha256sums=('8c4f68ba9ccff40ad874a68a25102924975b39ddbaf13bf0bb9c9e9344896958'
            '1f4c49a04da9c15632d6d9c4a6f11ea349de6bd0ec921f7f69ea7648605e71a5'
            'b47650241df91583d1afac3eafa52eed8db6bb509e0b0f1692e0cdca8d1ec34a'
            '571e6aab0c8e9789fc1f8ddc4222eb67b0b6d74dcce30299a08b7240b7c7b2de')
noextract=("org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar"
           "org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar"
           "org.bouncycastle.bcprov_${pkgver}.jar")

package() {
  # Copy bundles
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins/" \
    org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar \
    org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar \
    org.bouncycastle.bcprov_${pkgver%.*}-${pkgver##*.}.jar
  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
