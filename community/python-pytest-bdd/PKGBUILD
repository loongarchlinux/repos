# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-bdd
pkgver=6.0.1
pkgrel=1
pkgdesc='BDD library for the pytest runner'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-bdd'
depends=('python-glob2' 'python-mako' 'python-parse' 'python-parse-type' 'python-py' 'python-pytest')
makedepends=('python-setuptools')
source=("https://github.com/pytest-dev/pytest-bdd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('30bc697ae177e3f311af0c781e99bde5b491dfef28109359c0d80f33feb8b0b170ffe15c288fe73a624144a1d1c3223b220d591b4acc30d1ad53600482bef87d')

build() {
  cd pytest-bdd-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-bdd-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-bdd-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
