# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-bdd
pkgver=4.1.0
pkgrel=4
pkgdesc='BDD library for the pytest runner'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-bdd'
depends=('python-glob2' 'python-mako' 'python-parse' 'python-parse-type' 'python-py' 'python-pytest')
makedepends=('python-setuptools')
source=("https://github.com/pytest-dev/pytest-bdd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('54ce5ae2f7b3deff7d436d42f80fc47892707eb1e6e76469dda9753ff0b2d727bb47b5be300eadccadd87838aee43130fec5dff6441353eb07c0787c84d55afa')

build() {
  cd pytest-bdd-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-bdd-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-bdd-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
