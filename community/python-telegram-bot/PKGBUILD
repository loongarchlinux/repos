# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-telegram-bot
pkgver=12.2.0
pkgrel=1
pkgdesc="A pure Python interface for the Telegram Bot AP"
url="https://github.com/python-telegram-bot/python-telegram-bot"
license=('GPL')
arch=('any')
depends=('python-cryptography' 'python-future' 'python-tornado' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-flaky'
              'python-pytest-timeout')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-telegram-bot/python-telegram-bot/archive/v$pkgver.tar.gz"
        fix-tests.patch)
sha512sums=('c29c9750cef5b2baad54b8e9e5aee0ba1459557439ce8961733b00c67d678b0da87e91b973a847da4781ae2ba20c1d869d9dacd713f4d89f428c784b5a818bd8'
            '19a70af0cbfa12e4f3312cb8332dbd3f7964306594df2f46ba276de32636090ce3571a4272f9ce5a14471514f62159bcabba136cf956ce8c5b8a075de29f8185')

prepare() {
  cd python-telegram-bot-$pkgver

  patch -Np1 -i ../fix-tests.patch

  sed -i '/certifi/d' telegram/__main__.py requirements.txt
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i telegram/utils/request.py tests/test_official.py
  sed -e 's/import telegram.vendor.ptb_urllib3.urllib3/import urllib3/' \
      -e 's/from telegram.vendor.ptb_urllib3.urllib3/from urllib3/' \
      -e 's/from telegram.vendor.ptb_urllib3 import urllib3/import urllib3/' \
      -i telegram/utils/request.py tests/test_{bot,official}.py
}

build() {
  cd python-telegram-bot-$pkgver
  python setup.py build
}

check() {
  cd python-telegram-bot-$pkgver
  pytest
}

package() {
  cd python-telegram-bot-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
