# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: robertfoster

pkgname=libpam-google-authenticator
pkgver=1.08
pkgrel=1
pkgdesc='PAM module for google authenticator app'
arch=('x86_64')
url="https://github.com/google/google-authenticator-libpam"
license=('Apache')
depends=('pam')
optdepends=('qrencode: scannable QR codes for google auth phone app')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/google-authenticator-libpam/archive/$pkgver.tar.gz")
sha512sums=('f53d2fc20b5fa0f4621566509a2ef746077e3345de289bd2c9565440eb972e3a80807bf50a2cce8e2cc520df72c2e236629a921e3fce90fd635aff0c0ef36f75')

build() {
  cd google-authenticator-libpam-$pkgver
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd google-authenticator-libpam-$pkgver
  make DESTDIR="$pkgdir" install
  libtool --finish "$pkgdir"/usr/lib/security
}
