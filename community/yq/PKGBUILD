# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.12.2
pkgrel=1
pkgdesc="Command-line YAML/XML processor - jq wrapper for YAML/XML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq' 'python-yaml' 'python-xmltodict' 'python-toml'
         'python-argcomplete')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('2f156d0724b61487ac8752ed4eaa702a5737b804d5afa46fa55866951cd106d2')
b2sums=('956513d466024eadd3ff97d64fc83cd336929cf5ae4ec6e30d4717061a571e837ad4ee2215a4cf1039dcb8061ff5453df98d3ae1623fe05e73776d227aa6a5d0')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/"setuptools",/d' setup.py
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
