# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=debian-ports-archive-keyring
pkgver=2022.02.15
pkgrel=1
pkgdesc="GnuPG archive keys of the debian-ports archive"
arch=('any')
url="https://packages.debian.org/sid/debian-ports-archive-keyring"
license=('GPL')
depends=('gnupg')
source=("https://mirrors.kernel.org/debian/pool/main/d/debian-ports-archive-keyring/debian-ports-archive-keyring_$pkgver.tar.xz")
sha512sums=('e8f4565ec1c0f13c59e9f953d9be1ce02f14098d634be6cf4a334642f789d8d50027db49c274979f14cef38ce2c674fe66607b8058d809ec0c616527e35fcc0f')

build() {
  cd $pkgname-$pkgver
  # Translated from debian/rules

  # Build keyrings
  mkdir -p build/keyrings
  gpg --no-keyring --import-options import-export --import active-keys/* > build/keyrings/debian-ports-archive-keyring.gpg
  gpg --no-keyring --import-options import-export --import removed-keys/* > build/keyrings/debian-ports-archive-keyring-removed.gpg
  
  # Build fragment files
  mkdir -p build/trusted.gpg.d
  for k in $(ls active-keys); do
    gpg --no-keyring --import-options import-export --import active-keys/$k > build/trusted.gpg.d/${k%.key}.gpg
  done
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 build/trusted.gpg.d/*gpg -t "$pkgdir"/etc/apt/trusted.gpg.d/
  install -Dm644 build/keyrings/*gpg -t "$pkgdir"/usr/share/keyrings/
}

# vim:set ts=2 sw=2 et:
