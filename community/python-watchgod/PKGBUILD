# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

_pkgname=watchgod
pkgname=python-$_pkgname
pkgver=0.13
pkgrel=1
pkgdesc='Simple, modern file watching and code reload in python'
arch=('any')
url='https://github.com/samuelcolvin/watchgod'
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-setuptools-rust' 'rust')
_tag=d12bfec6ed873d76e0f1d31f086b2c1a30790676
source=("git+$url#tag=${_tag}")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  _version=$(git describe --tags | sed 's/^v//')
  sed "s/0.0.dev0/${_version}/" -i watchfiles/version.py
  echo ${_version}
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
