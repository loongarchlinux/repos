# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

_pkgname=watchgod
pkgname=python-$_pkgname
pkgver=0.7
pkgrel=2
pkgdesc='Simple, modern file watching and code reload in python'
arch=('any')
url='https://github.com/samuelcolvin/watchgod'
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools')
_tag=2a3462c82b81fc2339cb79ece6bd2eabea2f9937
source=("git+$url#tag=${_tag}")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --tags | sed 's/^v//'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
