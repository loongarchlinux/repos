# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=python-aiohttp-socks
pkgver=0.7.1
pkgrel=4
pkgdesc="SOCKS proxy connector for aiohttp. SOCKS4(a) and SOCKS5 are supported."
arch=('any')
url="https://github.com/romis2012/aiohttp-socks/"
license=('Apache')
depends=('python' 'python-aiohttp' 'python-attrs' 'python-async-timeout' 'python-python-socks')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-flask' 'python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/romis2012/aiohttp-socks?signed#tag=v${pkgver}")
sha256sums=('SKIP')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev (maintainer)

build() {
    cd "${pkgname//python-}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${pkgname//python-}"
    pytest
}

package() {
    cd "${pkgname//python-}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
