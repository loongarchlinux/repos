# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.8.1
pkgrel=3
pkgdesc="Access the Mediathek of many German TV stations"
arch=(any)
url="https://github.com/mediathekview/mediathekview"
license=(GPL3)
depends=(bash hicolor-icon-theme java-runtime)
makedepends=(maven strip-nondeterminism)
conflicts=(mediathek)
provides=(mediathek)
replaces=(mediathek)
optdepends=(
  'libnotify: to use desktop notifications'
  'mplayer: for recording streams'
  'vlc: for stream playback'
)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
  "de.${pkgname}.${_name}.desktop"
  "${pkgname}.sh"
  "${pkgname}-13.8.1-CVE-2021-45046.patch"
  "${pkgname}-13.8.1-CVE-2021-45105.patch"
)
sha512sums=('91acae0a5add48fab5d6fff54519eaac1321ad15f052ecb9c9221811eb4b793cf61a52f46d0f7f3377c89a2efaf81949c29363729a33225fff0cbecfbbdf1c3e'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '24313f9873aef8680eb466d756c0f537c4d2320e51296f354422bcf70f8f42ffff481c7db0cf58024b2953efb1f7442728e3e977c1ad03aaf3b9c47a535cc6a0'
            '0048f32dfc1ef8cc1dc25900a8d233fe9af0aa09fd3593dea4885f95ff9d388533c0656d1b0e4aa46fbecf11225dc60741f25f2b054793402d1f332a4f8c7479'
            '4bca64212708923b9e469f77e52dd7b217a469d3ab3273d988d0ac8e3a309c4b6a8f66b62038601d6aef3815159a6992db1897225cb4ec47ae095da48354b946')
b2sums=('536a7f1d71b2893d5605b2b6a4c4cad2f63e4381b9245e8b4cc892de09f7f7848f408247f6777cade68814d57adbc2f73527698bd70259c574c5e214bf8d59dc'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        'cbf668c6ccfb42b575d40de256ec03bb7863ea7db0bb02586f6727728fb5f1f004169849bfa9082a40b93042dc9c8f330c743e5983847c0a20f5d613748bae60'
        '495476b6377dedf057ebd0172d8f17d402b5c431d2da07505ed6b79d7559215da6c4746922eb59dc611f3dff81aecd9babfd112fdf19080df28c7335ab55f7bb'
        '2c63415989e031a336633fcee6bcc91bed1f47e7ee0a964e554b5157076a86459218cca3ebde73fed47a2959896b6896c07aac6a70d4c37171bc518947f8401f')

prepare() {
  cd "${_name}-${pkgver}"
  patch -Np1 -i ../"${pkgname}-13.8.1-CVE-2021-45046.patch"
}

build() {
  cd "${_name}-${pkgver}"
  ./mvnw clean install -Plinux,install4j-linux
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  local _size

  cd "${_name}-${pkgver}"
  # jar
  install -vDm 644 "target/${_name}.jar" -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
