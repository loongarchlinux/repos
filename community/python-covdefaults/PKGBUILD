# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=covdefaults
pkgname=python-$_pkgname
pkgver=2.0.0
pkgrel=2
pkgdesc='A coverage plugin to provide sensible default settings'
arch=('any')
url='https://github.com/asottile/covdefaults'
license=('MIT')
depends=('python-coverage')
makedepends=('python-setuptools')
checkdepends=('python-tox-current-env' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('61bb9fb82dcf0f3e0794a6794d73b967c62d1444529aaaab686ec5bf87fb06ccc7a25f594f49b1d71ab9aea5c74d617cfe4fc22437f0bf9ccbcb597d260e87fd')

prepare() {
  cd $_pkgname-$pkgver

  # remove coverage usage as it wants the plugin we are building
  sed -i -e's|coverage run|python|' -e '/coverage .*/d' tox.ini
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  tox -e py310 --current-env
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
