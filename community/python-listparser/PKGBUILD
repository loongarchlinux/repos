# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gabriele Musco <gabmus@disroot.org>

_pkgname=listparser
pkgname=python-listparser
pkgver=0.18
pkgrel=5
pkgdesc='Python library that parses OPML, RDF+FOAF, and the iGoogle subscription lists'
arch=('any')
url='https://github.com/kurtmckee/listparser'
license=('GPL3')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/kurtmckee/$_pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('cee2d369b8e494142e045ac72848727543f3391ad8b8489ff8a3e8a53b6b0a48')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  python lptest.py
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
