# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=2.6.2
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
groups=('x-apps')
depends=('gspell' 'gtksourceview3' 'libpeas' 'xapps')
makedepends=('meson' 'samurai' 'gobject-introspection' 'intltool' 'itstool')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('357ef0e07b942878344c141b3494a960e6fbd6d9b12c9dab1e4e49d523d763f6')
b2sums=('58292b35e696c7d3ad522e4a10a67f04e82a20abe3212c6eebd72fb44c890d37387b12bbb0f19ed5a6f59e089c59225155a3a1fbb30be1761fe5d191833c389f')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
