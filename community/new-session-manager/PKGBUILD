# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.4.0
pkgrel=2
pkgdesc="Audio session manager"
arch=('x86_64')
url="https://github.com/linuxaudio/new-session-manager"
license=('GPL3')
depends=('gcc-libs' 'glibc')
makedepends=('fltk' 'jack' 'liblo' 'meson')
optdepends=('fltk: for nsm-legacy-gui')
provides=('non-session-manager')
conflicts=('non-session-manager')
replaces=('non-session-manager')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('0cbcd0e03f774da30b35a60556a8c18621274e9f57a6fd2155e112003fe1ba46b48e831e1b42da2c6b0e4a48716386aa05a150b24c949797438be95dab897af5')
b2sums=('aa5abfb53317cb08c2a34e4d6fa6d254c076fc8e5d4ab378726b02e375771f928fb1191fbc29afe059244ebc81205696fedc65018b39a7c637bd8a2204178382')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

package() {
  depends+=('libjack.so' 'liblo.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
