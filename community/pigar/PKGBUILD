# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pigar
pkgver=1.0.0
pkgrel=1
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
depends=('jupyter-nbformat' 'python-colorama' 'python-requests')
makedepends=('python-setuptools')
source=("https://github.com/Damnever/pigar/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('93632c3de4e06a81747572d36b3b9336dfd59ce6a05bb0e806ce9537a02fcd728252f184aca5f07e1eb3463754555c9b2d4ef2aaae82be7d01e090272a1abf07')

build() {
  cd pigar-$pkgver
  python setup.py build
}

check() {
  cd pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v
}

package() {
  cd pigar-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
