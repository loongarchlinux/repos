# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.11.0
pkgrel=1
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=(any)
url="https://www.sciss.de/mellite/"
license=(AGPL3)
groups=(pro-audio)
depends=('java-environment>=16' supercollider)
makedepends=(gendesk sbt)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Sciss/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('3dab5989dc510f9307af8674fc0de6b7274baf85d80c9a0337ffafe6b04db3f8966ac1c6c2b791885278d17c34ba454130374170417bfc705b377cec714b3429'
            '1a6a572b4c365cdad87930bef805afa229b329cb2ceefb6b9ce42b233ab86f768ae3e3d5fd7cac1129786878c38fd891015aaf6637d9bd7b5ce3b2ef5fe9d30b')
b2sums=('2da3554fd95f553125ff8064d92f3c16d3b642d826e6dd0e931a3879d698f36bfa818b65608f996d2a2afebad6266bb769e6407c53c449eb8168bf93f8ab90ff'
        '4f1fd83d7cf5561e76f746860e5436e7dfab9bd50ad103f499b82de99a67034122f0e8e99e6a58574d6611585d63b2976e5d31979c7233294464683c86cce3ea')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  gendesk -n \
          --pkgname "${_app_name}" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "${pkgname}" \
          --icon "${_app_name}" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 "app/${_name}.jar" -t "${pkgdir}/usr/share/scala/${pkgname}"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 icons/application.png "${pkgdir}/usr/share/icons/hicolor/256x256/de.sciss.mellite.Mellite.png"
  install -vDm 644 icons/application.svg "${pkgdir}/usr/share/icons/hicolor/scalable/de.sciss.mellite.Mellite.svg"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications"
}
