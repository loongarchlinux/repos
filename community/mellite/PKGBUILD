# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.13.2
pkgrel=1
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=(any)
url="https://www.sciss.de/mellite/"
license=(AGPL3)
groups=(pro-audio)
depends=('java-environment>=16' supercollider)
makedepends=(gendesk sbt)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/Sciss/$pkgname/archive/refs/tags/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('ed782255afe595c249e93fea53ea6d1c0676e46e2da6a349f13d2e8dd0f9726b14e02222149eecd57c4dd459627609c570c975e8c15c229cf995a2b690b88168'
            '1a6a572b4c365cdad87930bef805afa229b329cb2ceefb6b9ce42b233ab86f768ae3e3d5fd7cac1129786878c38fd891015aaf6637d9bd7b5ce3b2ef5fe9d30b')
b2sums=('39a47921178aaf987caeb68904b4708fb302f736ed7a39852410723321b0d714a91ed1b8616d6267377d8f2c047b9b28e98a6e5b422876deb6abc6fec7341894'
        '4f1fd83d7cf5561e76f746860e5436e7dfab9bd50ad103f499b82de99a67034122f0e8e99e6a58574d6611585d63b2976e5d31979c7233294464683c86cce3ea')

prepare() {
  gendesk -n \
          --pkgname "$_app_name" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "$pkgname" \
          --icon "$_app_name" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd $_name-$pkgver
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  install -vDm 644 $_name-$pkgver/app/$_name.jar -t "$pkgdir/usr/share/scala/$pkgname"
  install -vDm 644 $_name-$pkgver/icons/application.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/de.sciss.mellite.Mellite.png"
  install -vDm 644 $_name-$pkgver/icons/application.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/de.sciss.mellite.Mellite.svg"

  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
}
