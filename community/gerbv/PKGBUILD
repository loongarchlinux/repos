# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=gerbv
pkgver=2.8.1
pkgrel=1
pkgdesc="An open source Gerber file (RS-274X only) viewer"
#url="http://gerbv.gpleda.org/"
url="http://gerbv.geda-project.org/"
license=('GPL')
arch=('loongarch64' 'x86_64')
depends=('gtk2' 'dconf')
optdepends=('cairo: for better graphics')
makedepends=('cvs' 'gettext')
# cvs for autopoint
changelog=ChangeLog
#source=("https://downloads.sourceforge.net/gerbv/$pkgname-$pkgver.tar.gz")
source=("gerbv-$pkgver.tgz::https://github.com/gerbv/gerbv/archive/refs/tags/v$pkgver.tar.gz")
md5sums=('f81675716efa5040a1bdc8cce26629df')

prepare ()
{
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "21 i VERSION=$pkgver" src/Makefile.am
  sed -i "64 i #define VERSION \"$pkgver\"" src/gerbv.h
}

build ()
{
  cd "$srcdir/$pkgname-$pkgver"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./autogen.sh
  sed -i 's|am_aux_dir=`cd $ac_aux_dir|am_aux_dir=`cd $srcdir|' configure
  ./configure --prefix=/usr \
              --disable-update-desktop-database 
  make
}

package()
{
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install 
}

