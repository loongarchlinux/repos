# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-regexp
pkgver=0.075
pkgrel=1
pkgdesc="Parse regular expressions"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Regexp"
depends=('perl-list-moreutils' 'perl-ppi' 'perl-task-weaken')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz")
sha512sums=('c05844850fa4c138d2c6beb2d9c6dd335a2e60bbbed27850a1e723755983e1fb3f6457a37add300b791e164eabbd923b2b4efc34f898711a895129afb975ef40')

build() {
  cd PPIx-Regexp-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Regexp-$pkgver
  make test
}

package() {
  cd PPIx-Regexp-$pkgver
  make DESTDIR="$pkgdir" install
}
