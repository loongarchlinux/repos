# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-regexp
pkgver=0.085
pkgrel=2
pkgdesc="Parse regular expressions"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Regexp"
depends=('perl-list-moreutils' 'perl-ppi' 'perl-task-weaken')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz")
sha512sums=('d5c29e4127b803267e81f1cf6dde522850a3efb85de8421bfd2c5e144c36c3cfa5ec51e13342c6f2a104325748c5d59891b19de02d69b8e7a3243356e0bcf4ff')

build() {
  cd PPIx-Regexp-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Regexp-$pkgver
  make test
}

package() {
  cd PPIx-Regexp-$pkgver
  make DESTDIR="$pkgdir" install
}
