# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-attr_extras
pkgver=7.0.0
pkgrel=1
pkgdesc='Takes some boilerplate out of Ruby with methods like attr_initialize'
arch=(any)
url='https://github.com/barsoom/attr_extras'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-m ruby-minitest ruby-rake)
options=(!emptydirs)
source=(https://github.com/barsoom/attr_extras/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5e48ad97cdf3ffbc696913117910581cb367f920fc1bcf6ce2aff92e2ccdbadd')

prepare() {
  cd attr_extras-$pkgver
  sed 's|git ls-files|find -type f|' -i attr_extras.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd attr_extras-$pkgver
  gem build attr_extras.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    attr_extras-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd attr_extras-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd attr_extras-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
