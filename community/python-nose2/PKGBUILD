# Maintainer: David Runge <dvzrv@archlinux.org>

_name=nose2
pkgname=python-nose2
pkgver=0.9.2
pkgrel=1
pkgdesc="The next generation of nicer testing for python"
arch=('any')
url="https://github.com/nose-devs/nose2"
license=('BSD')
depends=('python-coverage' 'python-setuptools' 'python-six')
optdepends=('python-coverage: for coverage_plugin'
            'python-sphinx: for doc'
            'python-sphinx_rtd_theme: for doc'
            'python-mock: for doc')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c1d8231decf4ab1712d7b486efcc4835df3c665d401713ac100186a513c68da4e9e1710f4289802fea1cfdbee1c5e3358c30a0e9d14554f408323efd00a1a293')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
