# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=1.27.0
pkgrel=1
pkgdesc='Apache plugin for Certbot'
arch=('any')
license=('Apache')
url='https://pypi.python.org/pypi/certbot-apache'
replaces=('letsencrypt-apache')
depends=(
  'certbot'
  'python-acme'
  'python-augeas'
  'python-setuptools'
)
makedepends=(
  'python-build'
  'python-wheel'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2'
              '86379B4F0AF371B50CD9E5FF3402831161D1D280'
              '20F201346BF8F3F455A73F9A780CC99432A28621'
              'F2871B4152AE13C49519111F447BF683AA3B26C3')
sha512sums=('b46beff5305ac85fb2da558b25276d650425bd3f7d10d006d57ae8496a6b9f174fd05d089683edcda23a9323f224b99bb721793c3bf26af596982cec67466e82'
            'SKIP')
b2sums=('cbef07fcbfd24519c7e9f3135fd370d35189bee7b8086c771c3eef5ee195e94fe942b9a547650a3c53010bb93b9f6661cfb02bd7e79efcf1e57422ee9de0e669'
        'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  # nuke setuptools from orbit ^W install_requires
  sed \
    -e '/setuptools>=/d' \
    -i setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  python -m pytest
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
