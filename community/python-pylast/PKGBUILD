# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgname=python-pylast
pkgver=4.2.1
pkgrel=1
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=(any)
url='https://github.com/pylast/pylast'
license=(Apache)
depends=(python-six)
makedepends=(
  git
  python-setuptools
  python-setuptools-scm
)
_tag=a516a44c329d6921755ebd7cd7e5fda9e851ad95
source=(git+https://github.com/pylast/pylast.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pylast

  git describe --tags
}

build() {
  cd pylast

  python setup.py build
}

package() {
  cd pylast

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
