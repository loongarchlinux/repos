# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Marcus Hoffmann <bubu@bubu1.eu>

pkgname=apksigcopier
pkgver=1.0.2
pkgrel=1
pkgdesc='copy/extract/patch android apk signatures & compare apks'
url='https://github.com/obfusk/apksigcopier'
arch=('any')
license=('GPL3')
depends=('python' 'python-click')
makedepends=('python-setuptools' 'pandoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/obfusk/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('cedd22a78ba05efd866a4017bc620cc8166130c9e312d21f280f9d88bf75c4279f6a6cc2ecb9ad00073b7113fc02f89d7bf57bef0021d20c87ab014fb624cb6e')
b2sums=('b9e5364e02204dc3237d08d52f8c783c817d37db7d4c7df6619c6aecb643ffcc74f5984f8a8ba7c3db5fb5caa289696a0ecd5b1a8b3d043c77e7488781abf3b6')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 apksigcopier.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim:set ts=2 sw=2 et:
