# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Marcus Hoffmann <bubu@bubu1.eu>

pkgname=apksigcopier
pkgver=1.0.1
pkgrel=2
pkgdesc='copy/extract/patch android apk signatures & compare apks'
url='https://github.com/obfusk/apksigcopier'
arch=('any')
license=('GPL3')
depends=('python' 'python-click')
makedepends=('python-setuptools' 'pandoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/obfusk/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3000896b8dd8fee38edb355186c5e810b8dbd708ce8c27d416ac833268940400a95cf19696d509a733b2c328858f99d57dcb67c5b43175260b21878967d624ad')
b2sums=('ea700f690abb90d4a32e0882abaafd484accf83dbe8c4db0a23cdb3aabad738a005189f8a3f82dd78e7a4e9139c90d2299a4d8724ffc6bb73df83ac75c488fa1')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 apksigcopier.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim:set ts=2 sw=2 et:
