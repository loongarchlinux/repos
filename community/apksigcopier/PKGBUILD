# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Marcus Hoffmann <bubu@bubu1.eu>

pkgname=apksigcopier
pkgver=1.1.0
pkgrel=1
pkgdesc='copy/extract/patch android apk signatures & compare apks'
url='https://github.com/obfusk/apksigcopier'
arch=('any')
license=('GPL3')
depends=('python' 'python-click')
makedepends=('python-setuptools' 'pandoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/obfusk/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('285099c66cd3d080deae4e98aa8b3156254289dccaa77a8a8ed3733ea349bd22b4d5bb2c29bba7113aa7e5d13503d378542dd50617c8e180a2f0038cd38c4d3c')
b2sums=('8b11178aae4b7d3b76df27f6251d66239d29f1694ce93f4250544b5278d88478ea1cea6ad9fcb4bf629c489885f6b31113ddf0c358c8d937773289ca140f15b9')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 apksigcopier.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim:set ts=2 sw=2 et:
