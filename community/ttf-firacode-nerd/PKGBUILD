# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Joseph R. Quinn <quinn.josephr@protonmail.com>

pkgname=ttf-firacode-nerd
pkgver=2.2.2
pkgrel=2
pkgdesc="Patched font Fira (Fura) Code from nerd-fonts library"
arch=("any")
url="https://github.com/ryanoasis/nerd-fonts"
license=("MIT")
provides=(ttf-font-nerd)
conflicts=(nerd-fonts-fira-code)
replaces=(nerd-fonts-fira-code)
source=("fira-${pkgver}.zip::${url}/releases/download/v${pkgver}/FiraCode.zip")
sha256sums=('20182e6e7c42cf8ab479d83af3200266261ec9bd4e80cdaceb793ecd56c9a398')

prepare() {
  find "${srcdir}" -name '*Windows*' -delete
}

package() {
  install -Dm644 *.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
