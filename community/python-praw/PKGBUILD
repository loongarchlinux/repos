# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-praw
pkgver=7.3.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
depends=('python-prawcore' 'python-update-checker' 'python-websocket-client')
makedepends=('python-setuptools' 'python-pytest-runner')
checkdepends=('python-mock' 'python-betamax-serializers' 'python-betamax-matchers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('e10a1f427352e1dc62862c8bfaefc815dcf7fe33370088f74077ed0b1b98281eaa55264f770a7bfe699064d292199443ceb94d56bb65569117c839d994d08fc8')

build() {
  cd praw-$pkgver
  python setup.py build
}

check() {
  cd praw-$pkgver
  python setup.py pytest
}

package() {
  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
