# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.13.1
pkgrel=2
pkgdesc='A library and program to extract, create, and modify WIM files'
arch=('x86_64')
url='https://wimlib.net'
license=('custom')
depends=('openssl' 'fuse' 'libxml2' 'ntfs-3g')
checkdepends=('attr')
optdepends=('cdrkit: for making ISO image of Windows PE'
            'mtools: for making disk image of Windows PE' 
            'syslinux: for making disk image of Windows PE'
            'cabextract: for extracting Windows PE from the WAIK')
source=("https://wimlib.net/downloads/$pkgname-$pkgver.tar.gz")
sha256sums=('47f4bc645c1b6ee15068d406a90bb38aec816354e140291ccb01e536f2cdaf5f')

build() {
    cd "$pkgname-$pkgver"
    ./configure --with-libcrypto --with-fuse --with-ntfs-3g --prefix=/usr
    make
}

check() {
    cd "$pkgname-$pkgver"
    make check
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm644 NEWS README -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 COPYING* -t "$pkgdir/usr/share/licenses/$pkgname"
}
