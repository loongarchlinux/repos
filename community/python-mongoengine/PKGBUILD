# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-mongoengine
pkgver=0.23.1
pkgrel=4
pkgdesc="A Python Object-Document-Mapper for working with MongoDB"
url="https://github.com/MongoEngine/mongoengine"
license=('MIT')
arch=('any')
depends=('python-pymongo' 'python-six')
optdepends=('python-dateutil: for DateTimeField'
            'python-pillow: for ImageField / ImageGridFsProxy')
makedepends=('python-pip')
checkdepends=('python-pytest-cov' 'python-coverage' 'python-blinker' 'python-pillow' 'python-zipp'
              'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/MongoEngine/mongoengine/archive/v$pkgver.tar.gz")
sha512sums=('fcd2ff1df3e2b28bc4cba67cad71a1856e9f94d4d2ecb731a5cce2f1c5e9abc163be37b7db518dfaa54a0ef7f664ac49b621a0554961aa7e13686ff608ef132a')

build() {
  cd mongoengine-$pkgver
  python setup.py build
}

check() {
  cd mongoengine-$pkgver
  # No sane way to run it without mongodb
  # python -m pytest
}

package() {
  cd mongoengine-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
