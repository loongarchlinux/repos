# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-mongoengine
pkgver=0.20.0
pkgrel=1
pkgdesc="A Python Object-Document-Mapper for working with MongoDB"
url="https://github.com/MongoEngine/mongoengine"
license=('MIT')
arch=('any')
depends=('python-pymongo' 'python-six')
optdepends=('python-dateutil: for DateTimeField'
            'python-pillow: for ImageField / ImageGridFsProxy')
makedepends=('python-pip')
checkdepends=('python-pytest-cov' 'python-coverage' 'python-blinker' 'python-pillow' 'python-zipp'
              'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/MongoEngine/mongoengine/archive/v$pkgver.tar.gz")
sha512sums=('ef973af9893f6e53811e1a47a131989f5a057af04ab9d512059f968af89fcd23f05d910c7de03330b5f4771db8e11ea2e48060f95ed790f8d238df6c6f7b9306')

build() {
  cd mongoengine-$pkgver
  python setup.py build
}

check() {
  cd mongoengine-$pkgver
  # No sane way to run it without mongodb
  # python -m pytest
}

package() {
  cd mongoengine-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
