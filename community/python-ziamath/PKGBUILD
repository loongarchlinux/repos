# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.5
pkgrel=1
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont')
optdepends=('python-latex2mathml: for latex support')
makedepends=('python-setuptools')
checkdepends=('python-latex2mathml' 'python-nbval' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/ziamath/get/$pkgver.tar.bz2")
sha512sums=('1e23f8018758b0cd21585d2e37d43172a9c39e9c73151042166db9c11fe34a3a869870df0f1e2f591d38b2107e211af2ad4b593b3d53e0d9f647b7b123ddad4e')

prepare() {
  mv cdelker-ziamath-* ziamath-$pkgver
}

build() {
  cd ziamath-$pkgver
  python setup.py build
}

check() {
  cd ziamath-$pkgver
  python setup.py pytest --addopts --nbval || echo "Tests failed"
}

package() {
  cd ziamath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
