# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.3
pkgrel=3
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont')
optdepends=('python-latex2mathml: for latex support')
makedepends=('python-setuptools')
checkdepends=('python-latex2mathml' 'python-nbval' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/ziamath/get/$pkgver.tar.bz2")
sha512sums=('7d9511f1e15b728e2cb7cde82eff12752a60f8dfd6e20766ca58404e1e495bb98ee408cf0253d6f36f675aaab440500631f9e1ee650a8192ab32e6b5774d1e16')

prepare() {
  mv cdelker-ziamath-* ziamath-$pkgver
}

build() {
  cd ziamath-$pkgver
  python setup.py build
}

check() {
  cd ziamath-$pkgver
  python setup.py pytest --addopts --nbval || echo "Tests failed"
}

package() {
  cd ziamath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
