# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.4
pkgrel=1
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont')
optdepends=('python-latex2mathml: for latex support')
makedepends=('python-setuptools')
checkdepends=('python-latex2mathml' 'python-nbval' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/ziamath/get/$pkgver.tar.bz2")
sha512sums=('7abea8ada194b0c5de6897b441bcdc8f2101eeaa58a242360a9454e957b7fc99488d72f2ef3307dd6159b77d5055aa9bf050716542c78ad2b2808ddecd921501')

prepare() {
  mv cdelker-ziamath-* ziamath-$pkgver
}

build() {
  cd ziamath-$pkgver
  python setup.py build
}

check() {
  cd ziamath-$pkgver
  python setup.py pytest --addopts --nbval || echo "Tests failed"
}

package() {
  cd ziamath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
