# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-performance-analyzer-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Performance Analyzer Plugin"
arch=('loongarch64' 'x86_64')
url="https://opensearch.org/docs/latest/monitoring-plugins/pa"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/performance-analyzer/archive/${pkgver}.tar.gz"
)
sha256sums=('ec8cc9a21bf8a5f310feb0e562b4f55adbdb5cde7471974f811525c5682a7302')

build() {
  cd "performance-analyzer-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-performance-analyzer"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-performance-analyzer"
  unzip "${srcdir}/performance-analyzer-${pkgver}/build/distributions/opensearch-performance-analyzer-${pkgver}.zip"

  install -Dm644 "${srcdir}/performance-analyzer-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
