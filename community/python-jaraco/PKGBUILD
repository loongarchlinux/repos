# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jaraco
pkgver=2022.2.21
pkgrel=1
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools' 'python-toml'
         'python-jaraco.context')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
provides=('python-jaraco-text'
          'python-jaraco-functools'
          'python-jaraco-collections'
          'python-jaraco-classes'
          'python-jaraco-stream'
          'python-jaraco-logging'
          'python-jaraco-itertools')

# These should not exist and do not deserve so many packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
# And now cherrypy depends on this too :-(
_text=3.7.0
_functools=3.5.0
_collections=3.5.1
_classes=3.2.1
_stream=3.0.3
_logging=3.1.0
_itertools=6.2.1
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
sha512sums=('559dc5ed0f2c0b46dd0cbbc112ab01bbdb68bc12923cff8f745a5e233696ca9f50cef6dbf60116df5121f4761457574deebea142c2a23ca6c4664d7395b5607e'
            '7257f41506f90ca2fda1bd12fed58fa4d4ff5738547f8cdae8ccad1901e0c818afb701dd8e9050fc77ca1271bc2466d5a542990138bc3db2cf9b04d12850844c'
            '2918be537296ca81e5f78a3966a47451dc9dc2d233d997c3f01461c489b44051c497dab55496f85883ac1b91a28850a9d77ff154a3f5e074da1ed07f3259c246'
            '4c85cfdd1da88de01596867365147be6cadd7e1d230798683236fdfde579747e3309aac95ae4f3ef86c91bb4493f3096c4ecc14e3db764ef308aab41ead88e23'
            'fe50c21b26e734c9c2d0b7cf8a1c8cc8bab80417adf073a4ae213ba4067aa33e2f6c8886aac996971d2f98aacd295d9999aa3ac7dba24dd952cc27d15bef22e2'
            'ce9a9cbad6d6bb448615eac986d361f9c6ff477b1209e85433f12a49055a9e30125cd49bebcf915a08b8efe0732439a8e1d336e63f6454369b3e846a9a1383ba'
            '7b4c27143848d899472a9ce207819062719adbef2cf038754c9236fee7da39f62238b85f8d83bdc41bf5a37e8d102e79dd4afbc6efa46c2d7c7ca04c032c67d3')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/inflect//"    jaraco.itertools.egg-info/requires.txt
  sed -i "s/inflect//"    setup.cfg
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py
}

build() {
  cd "$srcdir/jaraco.text-$_text"
  python -m build --wheel --no-isolation
  cd "$srcdir/jaraco.functools-$_functools"
  python -m build --wheel --no-isolation 
  cd "$srcdir/jaraco.collections-$_collections"
  python -m build --wheel --no-isolation 
  cd "$srcdir/jaraco.classes-$_classes"
  python -m build --wheel --no-isolation 
  cd "$srcdir/jaraco.stream-$_stream"
  python -m build --wheel --no-isolation 
  cd "$srcdir/jaraco.logging-$_logging"
  python -m build --wheel --no-isolation 
  cd "$srcdir/jaraco.itertools-$_itertools"
  python -m build --wheel --no-isolation 
}

package() {
  cd "$srcdir/jaraco.text-$_text"
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.functools-$_functools"   
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.collections-$_collections"
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.classes-$_classes"
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.stream-$_stream"
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.logging-$_logging"
  python -m installer --destdir="$pkgdir" dist/*.whl
  cd "$srcdir/jaraco.itertools-$_itertools"
  python -m installer --destdir="$pkgdir" dist/*.whl
  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
