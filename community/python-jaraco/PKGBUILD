# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jaraco
pkgver=2020.08.23
pkgrel=1
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools')
makedepends=('python-setuptools-scm')

# These should not exist and do not deserve so many packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
# And now cherrypy depends on this too :-(
_text=3.2.0
_functools=3.0.1
_collections=3.0.0
_classes=3.1.0
_stream=3.0.0
_logging=3.0.0
_itertools=5.0.0
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
sha512sums=('4107ef1078b44714b6ee5fb6ebd2bb7c35415debc6eaedd8bc18a70a2936cc7c7757acd7b3fac9587201ef52a25d9b29e49f2385a0a08afffb1d0c3de6ecc377'
            '96696839d3405e571bd0120443c5494f13d737b7e0fd9324a7a003c88ff45bdaa89a717c81b9c5ecb4eded9781ba2c05f4735e5cf24c1d1c162fc3d55d38eedb'
            'c6c5d28fe4b3940c81c68bf877249805db3f508d83a0071947c7a5596d3636631df91cc6e87802b0fa6cd5c8bdd1ef2f39b924471666b44d2147cb64ad1533ad'
            '697d892a01c4e8ad83f57e69dcd744f976cea67fa3efcd55b10eda02f6bf1916162c0fa952d0d711e62f62fdff62740ee55a93be9fc46ebd4ab22416092f0ff6'
            'f2ce7e65d9a3bcd81985f45d03f7cd56b2f2939f13a423b8b65714f19eb249493d89cad774460dd08c1345124d3d10f85ce6875b5f7049705f993c0a3146e5d9'
            'bc0edeef6223d0eb78110d51159a70afd16542e830e3cd37dec7628e582bce44218cd7669c03c41125db9824b6fc774f608dd7f6540224659f4dc2f1927d681c'
            '38ae2cbdd436b277e3d3c252fadeb5239e2d32b2291686b2a3dbaca69328392eb1db4318b2144954b17302e5b15b94ad89f1ea8a1b2e354deec7d4516bd7ec85')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/'inflect',//" setup.py
  sed -i "s/inflect//"    jaraco.itertools.egg-info/requires.txt
  sed -i "s/inflect//"    setup.cfg
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py
}

package() {
  cd "$srcdir/jaraco.text-$_text"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.functools-$_functools"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.collections-$_collections"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.classes-$_classes"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.stream-$_stream"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.logging-$_logging"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.itertools-$_itertools"
  python3 setup.py install --root="$pkgdir/" --optimize=0

  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
