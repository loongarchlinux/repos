# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=6.5.1
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=(any)
url="https://github.com/Bachmann1234/diff_cover"
license=(Apache)
depends=(python-chardet python-jinja python-pluggy python-pygments)
makedepends=(python-build python-installer python-poetry python-wheel)
checkdepends=(python-pytest python-pytest-mock python-tomli)
optdepends=('python-tomli: for TOML support')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('d24e83d841025c540d843a598ac58277fbd750dc7f4008a567d551d0bfa9346c90eba3c5a8c326f782e0237b9401184a93d9b85339641e5974a4a5a5aa77303e'
            'SKIP')
b2sums=('598dba85fbb5bdbdb1aea66aafbf490334e522d85ab8838d31d484e7158206e5773ed60c82b911d534b1b7f1ec4909f472764259575a29a1b4916b438a01b070'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # ignore integration and code style checks
  pytest -vv --ignore 'tests/test_integration.py' --ignore 'tests/test_violations_reporter.py' --ignore 'tests/test_clover_violations_reporter.py'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
