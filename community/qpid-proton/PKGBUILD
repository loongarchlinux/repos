# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=qpid-proton
pkgver=0.36.0
pkgrel=3
pkgdesc='High-performance, lightweight messaging library'
arch=('loongarch64' 'x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
depends=('libjsoncpp.so' 'python')
makedepends=('cmake' 'doxygen' 'go' 'python-setuptools' 'python-sphinx' 'python-wheel' 'swig')
options=(!emptydirs)
source=("https://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('b22d498a5193c966f9c703caa84320eed6491131962475f155eead9246f3c5ce40316b00e43c9868ac564e9ac46ffcdbba335fa6396028d958e69573d38b728e')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        -DENABLE_TOX_TEST=OFF \
        ..
    make
}

check() {
    cd ${pkgname}-${pkgver}/build

    make test || true
}

package() {
    cd ${pkgname}-${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
