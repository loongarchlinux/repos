# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.16.0
pkgrel=3
pkgdesc='Program to create VideoDVDs and CDs'
arch=('any')
url='https://rastersoft.com/programas/devede.html'
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-setuptools' 'ffmpeg' 'cdrtools' 'dvdauthor'
         'vcdimager')
source=("https://gitlab.com/rastersoft/$_pkgname/-/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        '0001-Add-AppStream-metadata-file.patch')
sha256sums=('722d6c72d30b1c2bf3ac958b4671b36c51c1c6702f92eda5e3b8069ec76d5e45'
            'a1a96a46b5cb437bbfaa3ccf343039350f0c553cf853dbe2788345212fcdd951')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../0001-Add-AppStream-metadata-file.patch
  sed -i "/share', 'pixmaps/d" setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
