# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=2.0.0
pkgrel=1
pkgdesc="Molecule Docker Driver"
arch=(any)
url="https://github.com/ansible-community/molecule-docker"
license=(MIT)
depends=(molecule python-ansible-compat python-docker python-requests python-selinux)
makedepends=(python-build python-installer python-pip python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible-core python-filelock python-pytest python-pytest-helpers-namespace)
optdepends=(
  'docker: for using a local docker setup'
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('adc3367b58110da78876522865b268de866b5f0fead6de31c79c61357a83b1dc927190a2016ee7cc608c318c65e5e287ec2edd5278c15e622e06f6a14dac5b60')
b2sums=('854eb8a519bad49f560acc91811dbb4fd0a5c50e727e369e89126ddf4159af3bca50528acc1945c6ea5602cc6169d4676c65c05ee27587523dbf017369a01b8f')

build() {
  cd $pkgname-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  pytest -c /dev/null -vv --ignore "src/molecule_docker/test/test_func.py"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
