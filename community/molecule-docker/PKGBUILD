# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=2.1.0
pkgrel=3
pkgdesc="Molecule Docker Driver"
arch=(any)
url="https://github.com/ansible-community/molecule-docker"
license=(MIT)
depends=(molecule python-docker python-requests python-selinux)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-wheel)
checkdepends=(ansible-core python-filelock python-pytest python-pytest-helpers-namespace)
optdepends=(
  'ansible-core: for Dockerfile validation'
  'docker: for using a local docker setup'
)
source=(
  https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  $pkgname-2.1.0-dependencies.patch
)
sha512sums=('3c3dc493eb183ab3acc01738d8fd2260718a44f2c1a15073dac7b9da0899a3672fc5f2bfae98aa27ea2472170ec83d049bf15943fdbccf6dc1e4efd39d833450'
            '0772a5fe31d0275a3abc2133ef2797e6ceebf6a7d019bf7d680656e8f51cf7b71adb36768b86c9271b4d6cc0d24fd3dd6cda84923a7698d3f4cf9cce09a994bb')
b2sums=('c4c86520f779b52d15f7c5c0985f3b54edc9d19a39afd1afbe2610fbb733b12731c1f3dc16026c02680286904633dce74d5708495dffc03ac54fd59554300415'
        'dcf66b869bbcbc3a916e69109e986397975687f6f62f56090fb5d0afe49c68872140aac7f1edad51d36b4705980c2573d88472fd7f836697f574cb0b8e4b9fe2')

prepare() {
  # remove the use of pip and setuptools-scm-git-archive from makedepends:
  # https://github.com/ansible-community/molecule-docker/pull/195
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.1.0-dependencies.patch
}

build() {
  cd $pkgname-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -c /dev/null -vv --ignore "src/molecule_docker/test/test_func.py"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"

  rm -rv "$pkgdir/$site_packages/molecule_docker/test/"
}
