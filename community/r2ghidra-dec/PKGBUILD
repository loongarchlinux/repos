# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra-dec
_gitcommit=8e576eeadc211de4ac8d8c759cc368fa48cdfa99
_gitcutter=1adc30794b9d6906ee1ac86ceb80f3a612204169
pkgver=4.5.1
pkgrel=1
pkgdesc='Deep ghidra decompiler integration for radare2 and Cutter'
url='https://github.com/radareorg/r2ghidra-dec'
arch=('x86_64')
license=('LGPL3')
depends=('radare2' 'glibc')
makedepends=('git' 'cmake' 'qt5-base')
provides=('radare2-cutter-r2ghidra-dec'
          'radare2-r2ghidra-dec')
source=("git+https://github.com/radareorg/r2ghidra-dec#commit=${_gitcommit}"
        "git+https://github.com/radareorg/cutter#commit=${_gitcutter}"
        thestr4ng3r-ghidra::git+https://github.com/thestr4ng3r/ghidra.git
        git+https://github.com/zeux/pugixml.git)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git config submodule."ghidra/ghidra".url "${srcdir}/thestr4ng3r-ghidra"
  git submodule update --recursive
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_CUTTER_PLUGIN=ON \
    -DBUILD_SLASPECS=ON \
    -DCUTTER_SOURCE_DIR="${srcdir}/cutter"
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
  # move shared library into R2_LIBR_PLUGINS
  r2version=$(r2 -version|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  install -d "${pkgdir}/usr/lib/radare2/${r2version}"
  mv "${pkgdir}"/usr/share/radare2/plugins/*.so -t "${pkgdir}/usr/lib/radare2/${r2version}"
}

# vim: ts=2 sw=2 et:
