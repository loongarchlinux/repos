# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=3.2.1
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-babel' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-debtcollector')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler')
source=("https://github.com/openstack/python-magnumclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6901840f522021212c1535addd4dd5eea4f08b8f72505aa1a1566fce00fab43f814f117d274f173283bebfde3a65b88c994a226ca37582b7f965abf6bcd7da91')

export PBR_VERSION=$pkgver

build() {
  cd python-magnumclient-$pkgver
  python setup.py build
}

check() {
  cd python-magnumclient-$pkgver
  stestr run
}

package() {
  cd python-magnumclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
