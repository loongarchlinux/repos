# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=yadm
pkgver=3.1.1
pkgrel=2
pkgdesc="Yet Another Dotfiles Manager"
arch=('any')
url="https://github.com/TheLocehiliosan/yadm"
license=('GPL3')
depends=('git' 'awk')
optdepends=('gnupg: encrypt/decrypt sensitive files'
            'python-j2cli: use jinja2 templates'
            'esh: use shell templates')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TheLocehiliosan/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('6be40ff6b1130be93649bb533d63ddc2dad626bb3022392863b600b31eef2529df443a9eb24915979bef2d1ea4ef04124668723ffb0ce0f86727f481a3997c74')
b2sums=('c85bfdaf15da8e88aea8e1b41af5396111fe95922f38c9709d09d76b6cb515d4aff99f0caaa225baf36bc408f6efe7f53e7b2febd45c78acf2b5f3c897b87290')

package() {
	cd $pkgname-$pkgver
	install -vDm755 yadm -t "$pkgdir/usr/bin"
	install -vDm644 yadm.1 -t "$pkgdir/usr/share/man/man1"
	install -vDm644 completion/bash/yadm -t "${pkgdir}/usr/share/bash-completion/completions"
	install -vDm644 completion/zsh/_yadm -t "${pkgdir}/usr/share/zsh/site-functions"
	install -vDm644 completion/fish/yadm.fish -t "${pkgdir}/usr/share/fish/vendor_completions.d"
}
