# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=test-framework-leancheck
pkgname=haskell-test-framework-leancheck
pkgver=0.0.2
pkgrel=1
pkgdesc="LeanCheck support for test-framework"
url="https://github.com/rudymatela/test-framework-leancheck"
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-leancheck' 'haskell-test-framework')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0a911b8ed9246e0899e1c97962a39214ddb9178e7adb3695ee987825aca4322803fced271f961aacabde3a15d5ae20fdbe53871cb09a716a9ce5db43d4740515')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
