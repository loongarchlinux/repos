# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=thor
pkgname=ruby-thor
pkgver=1.1.0
pkgrel=2
pkgdesc='Toolkit for building powerful command-line interfaces'
url='https://github.com/erikhuda/thor'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/erikhuda/thor/archive/v${pkgver}.tar.gz)
sha256sums=('ef285b4fedcb7ed48283198678bcc6d633dc26ac77ba01d7609ba81c2f10e130')
sha512sums=('d1872a834478c5a2054dde6f55e93f6765fd628536f99f888bd7495436c88658537d48c44ef1e5ad6d1388edfa294ea2a3f3cbde32c19b7d2253b78bc62bf9d0')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
