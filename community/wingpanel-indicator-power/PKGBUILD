# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-power
pkgver=6.1.0
pkgrel=2
pkgdesc='Wingpanel Power Indicator'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-power
license=(GPL3)
groups=(pantheon)
depends=(
  bamf
  glib2
  gtk3
  libgee
  libgranite.so
  libgtop
  libudev.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  intltool
  meson
  vala
  wingpanel
)
_tag=f99e0caa9c3c32252de519ba8bb2577f2b0a3808
source=(git+https://github.com/elementary/wingpanel-indicator-power.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-power

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
