# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-quart-cors
pkgver=0.5.0
pkgrel=1
pkgdesc='A Quart extension to provide Cross Origin Resource Sharing, access control, support'
url=https://gitlab.com/pgjones/quart-cors
arch=(any)
license=(MIT)
depends=(
  python
  python-quart
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-tox
)
_tag=597b48b6bf0cd6a73327f05063915aca4afc0662
source=(git+https://gitlab.com/pgjones/quart-cors.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd quart-cors

  git describe --tags
}

build() {
  cd quart-cors

  python setup.py build
}

check() {
  cd quart-cors

  tox -e py39
}

package() {
  cd quart-cors

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-quart-cors/
}

# vim: ts=2 sw=2 et:
