# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgname=python-jmespath
pkgver=1.0.0
pkgrel=1
pkgdesc='A query language for JSON'
arch=('any')
url="https://github.com/jmespath/jmespath"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-nose' 'python-pytest')
source=("https://github.com/jmespath/jmespath.py/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d55ea1c4041e1a5043b3e3b7efcd1e561b92a394e3facbe96b069ff6a9a11c20553e62cfbc5fea966670c71376f9f9ccc24d8905f253e0f1d96dd7a588cc3e44')

build() {
  cd jmespath.py-$pkgver
  python setup.py build
}

check() {
  cd jmespath.py-$pkgver
  # Their tox.ini is misleading...
  pytest tests/
  nosetests extra/
}

package() {
  cd jmespath.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
