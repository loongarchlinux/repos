# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-reports-scheduler-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Reports Scheduler Plugin"
arch=('loongarch64' 'x86_64')
url="https://opensearch.org/docs/latest/dashboards/reporting"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/dashboards-reports/archive/${pkgver}.tar.gz"
)
sha256sums=('18ab991b73c32d1509bb54089b3602a00f75a1198cf7b7b539a4c7f7daef9307')

build() {
  cd "dashboards-reports-${pkgver}/reports-scheduler"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  unzip "${srcdir}/dashboards-reports-${pkgver}/reports-scheduler/build/distributions/opensearch-reports-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/dashboards-reports-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
