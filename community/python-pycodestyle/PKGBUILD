# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=python-pycodestyle
pkgver=2.8.0
pkgrel=5
pkgdesc="Python style guide checker"
arch=('any')
url="https://pycodestyle.pycqa.org"
license=('custom:Expat')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/PyCQA/pycodestyle/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6fdf89c3a58df23792a2c1bb09fae781ab59110de4d3ab9d35a2262974bf78d9ce06f023c6f97922b7ee845db2156604a778d687c9eeacdd2eb298621799ce5c')

build() {
  cd pycodestyle-$pkgver
  python setup.py build
}

check() {
  cd pycodestyle-$pkgver
  python -m unittest discover
}

package() {
  cd pycodestyle-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
