# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=python-pycodestyle
pkgver=2.9.1
pkgrel=2
pkgdesc="Python style guide checker"
arch=('any')
url="https://pycodestyle.pycqa.org"
license=('custom:Expat')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/PyCQA/pycodestyle/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('420248fa90b67a625acc7cca0bde4e8a0f69e0de9e2798ddd3b3c6ee8c9917b0c8c970dde2f01881ac446354d75131a84bbe7c207754190343f6df5d3b125ab7')

build() {
  cd pycodestyle-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd pycodestyle-$pkgver
  python -m unittest discover
}

package() {
  cd pycodestyle-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pycodestyle-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
