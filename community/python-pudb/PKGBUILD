# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-pudb
pkgver=2022.1.2
pkgrel=1
pkgdesc="A full-screen, console-based Python debugger"
url="https://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
depends=('python-urwid' 'python-pygments' 'python-jedi' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://github.com/inducer/pudb/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0281d4c36c4a9b1c1ef1b32c117263af351e5f55638601298c39aa37072cbf24940fa7a543465f5a7ac9da16aeda0589e207fd96459dd9e3f8a2596593f21331')

build() {
  cd pudb-$pkgver
  python setup.py build
}

check() {
  cd pudb-$pkgver
  python -m pytest --tb=native -rxs test/
}

package() {
  cd pudb-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim: set ft=sh ts=2 sw=2 et:
