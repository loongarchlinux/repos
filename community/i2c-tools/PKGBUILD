# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=i2c-tools
pkgver=4.1
pkgrel=5
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('loongarch64' 'x86_64')
url="https://i2c.wiki.kernel.org/index.php/I2C_Tools"
license=('GPL')
depends=('perl')
makedepends=('python' 'git')
optdepends=('read-edid: for decode-edid script'
            'python: for smbus module')
source=(
  "https://www.kernel.org/pub/software/utils/i2c-tools/$pkgname-$pkgver.tar.xz"
  "https://www.kernel.org/pub/software/utils/i2c-tools/$pkgname-$pkgver.tar.sign"
)
sha256sums=(
  '57b219efd183795bd545dd5a60d9eabbe9dcb6f8fb92bc7ba2122b87f98527d5'
  'SKIP'
)
validpgpkeys=(
  '7CA69F4460F1BDC41FD2C858A5526B9BB3CD4E6A' # Jean Delvare
)

build() {
  cd ${pkgname}-${pkgver}
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" sbindir="$pkgdir/usr/bin" install
  install -Dm755 eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/bin"
  cd py-smbus
  python setup.py install --prefix "${pkgdir}/usr"
}

# vim:set ts=2 sw=2 et:
