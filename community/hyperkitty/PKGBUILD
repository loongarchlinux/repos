# Maintainer: David Runge <dvzrv@archlinux.org>

_name=HyperKitty
pkgname=hyperkitty
pkgver=1.3.3
pkgrel=1
pkgdesc="A web interface to access GNU Mailman v3 archives"
arch=('any')
url="https://gitlab.com/mailman/hyperkitty"
license=('GPL3')
depends=('python-dateutil' 'python-django' 'python-django-compressor'
'python-django-extensions' 'python-django-gravatar' 'python-django-haystack'
'python-django-mailman3' 'python-django-q' 'python-django-rest-framework'
'python-flufl-lock' 'python-mailmanclient' 'python-networkx' 'python-pytz'
'python-robot-detection' 'sassc')
makedepends=('python-isort' 'python-setuptools')
checkdepends=('python-beautifulsoup4' 'python-elasticsearch' 'python-mock'
'python-pytest' 'python-whoosh')
optdepends=('python-elasticsearch: for using elasticsearch as search backend'
            'python-whoosh: for using whoosh as search backend'
            'python-xapian-haystack: for using xapian as search backend'
            'uwsgi-plugin-python: for running inside uwsgi'
            'ruby-sassc: alternative to sassc')
backup=("etc/uwsgi/${pkgname}.ini"
        "etc/webapps/${pkgname}/settings_local.py")
install="${pkgname}.install"
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}-1.3.2-settings.patch"
        "${pkgname}-qcluster.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.uwsgi")
sha512sums=('4be4630bc0344c3e1633772978880fea996fbf2bb49b728c1d05ae13ab1df8c63b6b7b9b735df32eb2ae31798e76be537748e42c830a63aa01eeff0680f1dc76'
            'SKIP'
            'ec08a05402729bc23f9bff11be85630343f257d2bdb0531941d9f1475a3bb6b80d7083e2a0291517264247076b7639ecc427bdc5d5d37d9b8f094f6f484f6f96'
            'a41c938082ceee555143ddb21c3a95835f9832b9aff423142d78d3e52f12500561f80e0ce43c67a4f190065c9d045945741ac71ab3307db1419c4dc0686db09a'
            '3267427109b08b8c9336b187381ed14357a07370bdcdfc24da94555020ad0424c5bbebc09e131e981e0540c51db443ffe558209e1bca7c2a1b7b1f602ed0805b'
            'e734e28fbaa5d7143a5c31161b2de0beed5435cb4fa48521ec0e82ed81b045f07bd4ca61f9da81c87dfe2cc5a5ecf79eb9ed4c255a31f97c69fe97bf6df37073'
            '351f40a6bc1fd1735c883408f9941808e081206927a7c02467e03228243dd156378cac21f851b498702bd4f96fde8e4c80536dbcc6f3c384bf708bed4e988971')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # setting FHS compliant default paths
  patch -Np1 -i "../${pkgname}-1.3.2-settings.patch"
  touch settings_local.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python example_project/manage.py test --settings=hyperkitty.tests.settings_test hyperkitty
}

package() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # remove example_project from top level site-packages:
  # https://gitlab.com/mailman/hyperkitty/issues/279
  rm -rfv "${pkgdir}/${python_stdlib_basepath}/site-packages/example_project"
  # doc
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # django project
  install -vDm 644 example_project/{__init__,manage,settings,urls,wsgi}.py \
    -t "${pkgdir}/usr/share/webapps/${pkgname}"
  # symlink locale and static dirs to state dir
  ln -svf "/var/lib/${pkgname}/locale/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  ln -svf "/var/lib/${pkgname}/static/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  # state dir
  install -vdm 750 "${pkgdir}/var/lib/${pkgname}/data"
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/"{locale,static}
  # log dir
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  # config
  install -vDm 640 settings_local.py \
    -t "${pkgdir}/etc/webapps/${pkgname}"
  ln -svf "/etc/webapps/${pkgname}/settings_local.py" \
    "${pkgdir}/usr/share/webapps/${pkgname}/settings_local.py"
  # systemd service
  install -vDm 644 "../${pkgname}-qcluster.service" \
    -t "${pkgdir}/usr/lib/systemd/system"
  # uwsgi
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # tmpfiles.d
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # sysusers.d
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
