# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-parser
pkgver=1.65
pkgrel=1
pkgdesc="Modules for parsing/translating POD format documents"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Parser"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MA/MAREKR/Pod-Parser-$pkgver.tar.gz")
sha512sums=('0449ff9fcf8cbb5724d48d27da332a8c281c4412d0aa28464852f1557825070a729df2c620276f141effaeea742a840c8fc391261d8c8165a615cd539bbcce36')

build() {
  cd Pod-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Parser-$pkgver
  make test
}

package() {
  cd Pod-Parser-$pkgver
  make DESTDIR="$pkgdir" install
}
