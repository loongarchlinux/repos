# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Caleb Maclennana <caleb@alerque.com>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.17.0
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-docutils' 'python-jinja' 'python-markdown-it-py' 'python-mdit_py_plugins' 'python-typing_extensions' 'python-sphinx' 'python-yaml')
optdepends=('python-linkify-it-py: linkify support')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-beautifulsoup4' 'python-linkify-it-py' 'python-pytest' 'python-pytest-param-files' 'python-pytest-regressions')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9f4ea05ab55b0979b26f0211603aee0835a30fb37a0ca23f23b96e0b47670b6b8e443ee9759df5011d009e20d817eaffd4066c4be4920f96786c283aa206239')

build() {
  cd $_pkgname-$pkgver

  python -m build -wn
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -k 'not test_sphinx_builds and not fixtures and not test_myst_config'
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
