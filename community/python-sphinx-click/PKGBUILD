# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=3.0.2
pkgrel=3
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=('any')
url="https://github.com/click-contrib/sphinx-click"
license=('MIT')
depends=('python-pbr' 'python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-click')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('dcf60ca31dcc7c1f45157ed88a5d723d8b531f01b617fb0cc4ada8f09c709b23b55a86bcf8bbe66bf4c2f5ef77329123d271724887022a43b86747087e87ed7c')
b2sums=('7190958afaa8edab311e6728c5b2ea37e04d9402882c8bb0ceac935fc1bc93b5c087cbe6847396f9331b9b6b4d7fcb2a439d725661bd8d4cebdde6851f38f78a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}/docs/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
