# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=4.0.3
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(python-pbr python-sphinx)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-click)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('e5d6f82c49cd50309026b9697ea41827a216cf27001d1a38a9fc3f7fa6d0dacbbb7793853b27304e2cb5b584bb4eed491476595d543bcce9ddfbd21832cacb16')
b2sums=('dc6c5fa888168661c067206b063517f12ccaa6dec634b8c0f3743cd19264223043b196a4d3b2417fe5c28b350838386905ccbe0478a7e7cad792d95cc63d6472')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 docs/*.rst -t "$pkgdir/usr/share/doc/$pkgname/docs/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
