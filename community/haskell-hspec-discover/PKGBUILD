# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-discover
pkgname=haskell-hspec-discover
pkgver=2.9.6
pkgrel=11
pkgdesc="Automatically discover and run Hspec tests"
url="http://hspec.github.io/"
license=('MIT')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs')
makedepends=('ghc')
checkdepends=('haskell-quickcheck' 'haskell-hspec-meta' 'haskell-mockery')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3a883c8fb8dd29665b51fdfc9e22a2bae0010ea53b84696a71c827f8b234dadd200d8ace2200df7b2aa6bdd96dc0a543d7d841ae250fa3dd3e6d4e1d274ff335')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
   
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
