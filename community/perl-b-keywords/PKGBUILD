# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-b-keywords
pkgver=1.22
pkgrel=2
pkgdesc="Lists of reserved barewords and symbol names"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/B-Keywords"
depends=('perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/B-Keywords-$pkgver.tar.gz")
sha512sums=('9dd44cafe4502edb519d885d74688183275550c3ad4da61387e805cf7a4789434ebda8f1f71104515c688e0e0358ca38399a1d1bd2ba066c7e1e3de76ae68bb2')

build() {
  cd B-Keywords-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd B-Keywords-$pkgver
  make test
}

package() {
  cd B-Keywords-$pkgver
  make DESTDIR="$pkgdir" install
}
