# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname=power_assert
pkgname="ruby-${_gemname}"
pkgver=2.0.1
pkgrel=1
pkgdesc='Power Assert for Ruby'
arch=('any')
url="https://github.com/ruby/${_gemname}"
license=('custom:BSDL')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake')
checkdepends=('ruby-test-unit')
options=('!emptydirs')
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('717d163050e45d889bbf50ee5caf4d91b143a0df45d8ed1be9588595fd485f05662d59ae6166d44b5cf987d6b2a1e77f40441f2aefd1051ff9cf679d3bcdddc6')

prepare() {
  cd "${_gemname}-${pkgver}"
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"
  sed --in-place '/bundler/d' Rakefile
}

build() {
  cd "${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"
  rm -rf "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/.github"

  install -Dm 644 README.md  -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 BSDL COPYING LEGAL -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
