# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: cha.pa

pkgname=ttf-hack-nerd
pkgver=2.2.2
pkgrel=2
pkgdesc="Patched font Hack from nerd-fonts library"
arch=("any")
url="https://github.com/ryanoasis/nerd-fonts"
license=("MIT")
provides=(ttf-font-nerd)
conflicts=(nerd-fonts-hack)
replaces=(nerd-fonts-hack)
source=("hack-${pkgver}.zip::${url}/releases/download/v${pkgver}/Hack.zip")
sha256sums=('1f031b6d08d51f4830eacd00853dddc7fbd4ae0a05e169f2c13f3037dd3359b3')

prepare() {
  find "${srcdir}" -name '*Windows*' -delete
}

package() {
  install -Dm644 *.ttf -t "${pkgdir}/usr/share/fonts/TTF"
  install -Dm644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
