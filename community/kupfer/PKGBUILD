# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Asa Marco <marcoasa90[at]gmail[.]com>

pkgname=kupfer
pkgver=319
pkgrel=4
pkgdesc="Launcher application written in python. Similar to Gnome-Do / Launchy"
arch=('any')
url="https://kupferlauncher.github.io/"
license=('GPL3')
depends=('libkeybinder3' 'libwnck3' 'python-cairo' 'python-dbus' 'python-gobject' 'python-xdg')
makedepends=('intltool' 'itstool' 'python-docutils')
optdepends=('gvfs: Trash plugin'
            'xautomation: Send Keys plugin')
source=(https://github.com/kupferlauncher/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz{,.sig}
        0001-Use-itstool-instead-of-xml2po.patch)
validpgpkeys=('2578ABA1F9023159DFF211B7368712C4D7A44445') # Ulrik Sverdrup
sha256sums=('22357233984a6588a9f9743002363af38ea63b5c2684af487693a13c42973d31'
            'SKIP'
            'aae540ed3bd9330ac0a1675395be3a0ee2252315da9f2f2934d27a18a58404b3')

prepare() {
  cd $pkgname-v$pkgver
  # Don't use deprecated xml2po
  # https://github.com/kupferlauncher/kupfer/issues/128
  patch -Np1 -i ../0001-Use-itstool-instead-of-xml2po.patch

  sed -e 's|raise StopIteration|return|g' -i waflib/Node.py # Fix build with Python 3.7
}

build() {
  cd $pkgname-v$pkgver
  ./waf configure --prefix=/usr \
                  --no-update-mime \
                  --no-update-icon-cache
  ./waf
}

package() {
  cd $pkgname-v$pkgver
  ./waf install --destdir="$pkgdir"
}
