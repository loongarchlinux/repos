# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

_pkgname=mechanize
pkgname=python-mechanize
pkgver=0.4.6
pkgrel=3
epoch=1
pkgdesc='Stateful programmatic web browsing in Python'
arch=('any')
url="https://github.com/python-mechanize/mechanize"
license=('BSD')
depends=('python-html5lib')
makedepends=('python-setuptools')
checkdepends=('python-zope-interface' 'python-twisted')
optdepends=('python-html5-parser: faster parsing of HTML')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('d16cea241253b5eb6380bf8a46627cad91d1f2c3f93a33279a31ce276d6c5d44'
            'SKIP')
b2sums=('0adc7e0bcbef93051412d0d22e9f426984e197791b43b007087d70d3b15d4fce2f4868e8ad520e8621d35890d6454bd9bfc0910dc5af3d640e2620de4f536e16'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
  cd "mechanize-$pkgver"

  python setup.py build
}

check() {
  cd "mechanize-$pkgver"

  python setup.py test
}

package() {
  cd "mechanize-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
