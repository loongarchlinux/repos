# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tarantool
pkgver=2.6.0
pkgrel=1
pkgdesc='Lua application server integrated with a database management system'
arch=(loongarch64 x86_64)
url='https://www.tarantool.org'
license=(BSD)
depends=(readline ncurses zlib libyaml openssl libunwind curl icu)
makedepends=(git cmake systemd)
checkdepends=(python2-gevent python2-tarantool)
source=(git+https://github.com/tarantool/tarantool.git#tag=$pkgver
        git+https://github.com/tarantool/luajit.git#branch=tarantool-1.7
        git+https://github.com/tarantool/libyaml.git
        git+https://github.com/tarantool/msgpuck.git
        git+https://github.com/rtsisyk/luafun.git
        git+https://github.com/tarantool/test-run.git
        git+https://github.com/tarantool/small.git
        git+https://github.com/tarantool/zstd.git
        git+https://github.com/tarantool/luarocks.git#branch=luarocks-3.1.1-tarantool
        git+https://github.com/tarantool/decNumber.git
        git+https://github.com/tarantool/serpent.git
        git+https://github.com/tarantool/curl.git
        git+https://github.com/tarantool/c-ares.git
        tarantool.sysusers)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '2feccda75effbd44b40e43608e56395376ea7b9039a09aada91a05f9559b12fe')

prepare() {
  cd tarantool

  git submodule init
  git config -f .gitmodules 'submodule.third_party/luajit.url' "$srcdir/luajit"
  git config -f .gitmodules 'submodule.src/lib/msgpuck.url' "$srcdir/msgpuck"
  git config -f .gitmodules 'submodule.third_party/luafun.url' "$srcdir/luafun"
  git config -f .gitmodules 'submodule.third_party/libyaml.url' "$srcdir/libyaml"
  git config -f .gitmodules 'submodule.third_party/zstd.url' "$srcdir/zstd"
  git config -f .gitmodules 'submodule.test-run.url' "$srcdir/test-run"
  git config -f .gitmodules 'submodule.src/lib/small.url' "$srcdir/small"
  git config -f .gitmodules 'submodule.third_party/luarocks.url' "$srcdir/luarocks"
  git config -f .gitmodules 'submodule.third_party/decNumber.url' "$srcdir/decNumber"
  git config -f .gitmodules 'submodule.third_party/serpent.url' "$srcdir/serpent"
  git config -f .gitmodules 'submodule.third_party/curl.url' "$srcdir/curl"
  git config -f .gitmodules 'submodule.third_party/c-ares.url' "$srcdir/c-ares"

  git submodule sync
  git submodule update
}

build() {
  cd tarantool

  # tarantool uses git submodule, handle this
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DENABLE_BACKTRACE:BOOL=ON \
    -DWITH_SYSTEMD:BOOL=ON \
    -DENABLE_DIST:BOOL=ON \
    .
  make
}

check() {
  cd tarantool/test
  ./test-run.py --suite box
}

package() {
  cd tarantool
  make install DESTDIR="$pkgdir"

  install -D -m644 $srcdir/tarantool.sysusers "$pkgdir/usr/lib/sysusers.d/tarantool.conf"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
