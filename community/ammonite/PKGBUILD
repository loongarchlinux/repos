# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=2.5.5
_scalaversion=2.13.2
pkgrel=1
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('mill')
source=("https://github.com/lihaoyi/Ammonite/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('2aaa93098ef566eef796ce553f713c98f45680c929b37194c72482d26a67f50d41da0863a9713a68cbb567d6a9515125ca74d86f0fce67d67d936d8a692471f1')

prepare() {
  cd Ammonite-$pkgver
  sed -i "s/'git/'true/;s/\"git\"/\"true\"/;s/\$latestTaggedVersion-\$commitsSinceTaggedVersion-\$gitHash/$pkgver/;s/^    \\.toInt$//;s/(tagName, false)/(\"$pkgver\", false)/" build.sc
}

build() {
  cd Ammonite-$pkgver
  TRAVIS_TAG=$pkgver mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite-$pkgver
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly.dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
