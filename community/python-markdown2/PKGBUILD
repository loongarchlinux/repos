# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-markdown2
pkgver=2.4.3
pkgrel=1
pkgdesc='A fast and complete implementation of Markdown in Python'
arch=('any')
url='https://github.com/trentm/python-markdown2'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-tox-current-env' 'python-pygments')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f0c6eb93f040bbc09a3f6618e94d12f4bdc3d3f3810e8f09e5a2e3f24ebf4ae55602f1b3e1d08e1573ca067425452a3c7f8873b404df21e42373779ebe608f97')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() {
  cd $pkgname-$pkgver

  tox -e py310 --current-env
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
