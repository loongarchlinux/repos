# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-markdown2
pkgver=2.4.0
pkgrel=1
pkgdesc='A fast and complete implementation of Markdown in Python'
arch=('any')
url='https://github.com/trentm/python-markdown2'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-tox-current-env' 'python-pygments')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('11cf03040ddb48c7c38df4e85af668c69fe7d1454f3462dd32b5ef82b84cc4919def94e913a6673c120b6922631d7faa985157ebf7d515b4e7ea709f29a00c18')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() {
  cd $pkgname-$pkgver

  tox -e py39 --current-env
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
