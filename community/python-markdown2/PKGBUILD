# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-markdown2
pkgver=2.3.9
pkgrel=1
pkgdesc='A fast and complete implementation of Markdown in Python'
arch=('any')
url='https://github.com/trentm/python-markdown2'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-tox-current-env' 'python-pygments' 'python-pip')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('b4256310f16e7b2a5a3a1d48ebcb0bd90cda738e4586ed9b7919810e2c100394872c5d4301293f5f95c289666f847c7023fa078ee925a16920a72baed2554350')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() {
  cd $pkgname-$pkgver

  tox -e py38 --current-env
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
