# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=python-openpyxl
pkgver=3.0.9
pkgrel=3
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="https://openpyxl.readthedocs.org/"
license=('MIT')
depends=('python-jdcal' 'python-et-xmlfile')
makedepends=('python-setuptools')
optdepends=('python-pillow: needed to include images'
            'python-lxml: alternative XML backend'
            'python-defusedxml: guard against various XML vulnerabilities'
            'python-pandas: for iteration over Pandas DataFrames')
source=("https://foss.heptapod.net/openpyxl/openpyxl/-/archive/${pkgver}/openpyxl-${pkgver}.tar.bz2")
sha256sums=('351d38f1bba45a4a3cdcdc626d6c3f815677f5f0bc218315664678efe47e4e3f')

build() {
  cd "$srcdir"/openpyxl-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/openpyxl-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENCE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
