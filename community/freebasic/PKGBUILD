# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>
# Contributor: rabyte <gmail.com: rabyte>

pkgname=freebasic
pkgver=1.07.1
pkgrel=2
pkgdesc="A free BASIC compiler"
arch=('loongarch64' 'x86_64')
url="https://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
makedepends=('libxpm' 'gpm' 'libffi' 'libxrandr' 'mesa')
source=("https://downloads.sourceforge.net/fbc/FreeBASIC-$pkgver-source.tar.xz")
source_x86_64=("https://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86_64.tar.xz")
md5sums=('e1ecda91c89cfa28398df97f72c08481')
md5sums_x86_64=('de8d546ec6b07a1bb7e1ac69e2cc8663')

prepare() {
  [[ "$CARCH" = "x86_64" ]] && _arch=x86_64

  mkdir -p bootstrap
  cd "${srcdir}/FreeBASIC-${pkgver}-linux-$_arch"
  ./install.sh -i "${srcdir}/bootstrap"
  # Hack!
  ln -s /usr/lib/libncursesw.so "${srcdir}/bootstrap/lib/libtinfo.so.5"
}

build() {
  export PATH="${srcdir}/bootstrap/bin:$PATH"
  export LD_LIBRARY_PATH="${srcdir}/bootstrap/lib"

  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make CFLAGS="$CFLAGS $(pkg-config --cflags libffi)"
}

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make install prefix="${pkgdir}/usr"
}
