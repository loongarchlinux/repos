# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=9.0.3
pkgrel=1
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-typing-extensions')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest-asyncio' 'python-pytest-trio'
              'python-twisted')
optdepends=('python-trio' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('ab60ed0f00eb465b32e527df2159d4f32cf06f2239b511a45f5e80184ceb9d6f')
b2sums=('00960af1ac2dcbb8f743a19cd59e24c78745fa5063e34722a9a53ccf412ee0d433f3370239a77e4f57cf1ec846f1cdd47106c4f6b266afbecd3252270b0ea6d1')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requres list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python setup.py build
}

check() {
  cd pyee-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m pytest
}

package() {
  cd pyee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
