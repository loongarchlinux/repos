# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=pytest-xprocess
pkgname=python-pytest-xprocess
pkgver=0.18.1
pkgrel=3
pkgdesc='Pytest plugin to manage external processes across test runs.'
arch=('any')
url="https://github.com/pytest-dev/pytest-xprocess"
license=('MIT')
depends=('python-psutil' 'python-pytest')
makedepends=('python-setuptools-scm')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f19cba9954e4b2363db13b7dba373f5e50cfca6f43d6455f271bff4d01dcc2a3b15c13a47076dbeed34feffee856a8271df4837edab413e964a848cbb6228e7e')
b2sums=('9772fff488414406f487add01fba9ee2e10e12965d5b289b8c17cffb5a6566aaea0653ca1b390e6b4c70d79ce3698b0ce8122ed919f9778cb8cf3f7488cffc8d')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k "not test_interruption_cleanup and not test_interruption_does_not_cleanup"
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/${pkgname}"
}
