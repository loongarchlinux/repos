# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=2.4.5
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://github.com/jrnl-org/jrnl"
license=('GPL3')
depends=('python-pyxdg' 'python-cryptography' 'python-passlib' 'python-parsedatetime'
         'python-keyring' 'python-pytz' 'python-tzlocal' 'python-asteval' 'python-colorama'
         'python-dateutil' 'python-yaml' 'python-ansiwrap' 'python-packaging' 'python-setuptools')
makedepends=('python-dephell')
checkdepends=('python-pytest')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bc55c8b385dec0633b1e02108050e8775c0de73dd45fbbd683f145963720eb3f1940164309304df42c694db4564d1d7aefe27435fbdec4f701ed00db7f3805e8')

prepare() {
  cd jrnl-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd jrnl-$pkgver
  python setup.py build
}

check() {
  cd jrnl-$pkgver
  python -m pytest
}

package() {
  cd jrnl-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
