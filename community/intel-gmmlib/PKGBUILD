# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=intel-gmmlib
pkgver=21.1.3
pkgrel=1
pkgdesc="Intel Graphics Memory Management Library"
arch=(loongarch64 x86_64)
url="https://github.com/intel/gmmlib/"
license=(MIT)
depends=(gcc-libs)
makedepends=(cmake)
provides=(gmmlib)
conflicts=(gmmlib)
replaces=(gmmlib)
options=(!emptydirs)
source=("https://github.com/${pkgname/-//}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c459a95243075296c72a40674c449ffcc314ed6d21ad0291c1603dbc11f4a74e')

build() {
  cmake -B build -S gmmlib-${pkgname}-${pkgver} \
    -DBUILD_TYPE=Release \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DRUN_TEST_SUITE=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 gmmlib-${pkgname}-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
