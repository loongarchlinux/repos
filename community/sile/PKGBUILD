# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=sile
pkgdesc='The SILE Typesetter, a modern typesetting system inspired by LaTeX, customizable in Lua'
pkgver=0.13.3
pkgrel=1
arch=(loong64 x86_64)
url=https://www.sile-typesetter.org
_url="https://github.com/sile-typesetter/$pkgname"
license=(MIT)
_luadeps=(bit32
          cassowary
          cldr
          cliargs
          cosmo
          expat
          filesystem
          fluent
          linenoise
          loadkit
          lpeg
          luaepnf
          luarepl
          luautf8
          penlight
          sec
          socket
          stdlib
          vstruct
          zlib)
depends=(glibc
         fontconfig
         freetype2
         harfbuzz
         gentium-plus-font
         git
         icu
         libpng # this goes with libtexpdf if ever split out to a library package
         lua
         "${_luadeps[@]/#/lua-}"
         zlib)
depends+=(libfreetype.so
          libharfbuzz.so
          libicudata.so
          libicui18n.so
          libicuio.so
          libicuuc.so)
optdepends=('libertinus-font: math package default font'
            'noto-fonts-cjk: tate enabled classes default font')
checkdepends=(poppler)
provides=(libtexpdf.so)
options=(debug)
_archive="$pkgname-$pkgver"
source=("$_url/releases/download/v$pkgver/$_archive.tar.xz")
sha256sums=('1ab1c6f1898440822c1f85186f8b4676bab3eda3ad947d838ea0d991fb301c75')

build () {
	cd "$_archive"
	./configure \
		--prefix /usr \
		--with-system-luarocks
	make all
}

check () {
	cd "$_archive"
	make check
}

package () {
	cd "$_archive"
	make install DESTDIR="$pkgdir"
}
