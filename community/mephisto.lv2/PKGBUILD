# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.12.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('fontconfig' 'glibc' 'libevdev' 'libglvnd' 'libvterm' 'libx11'
'ttf-fira-code')
makedepends=('faust' 'fontconfig' 'glew' 'glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('beeff7e314d98f5c4611c5258065ce10149f55a281d3d367425e12755ffb29a55095b1a5d6296a76bc912d0bd27a313aac4f481178792418abc600b5cd8128af'
            'SKIP')
b2sums=('ac177e28c1394c8a0d52fc6f5f95a5c3c99ee77147d7898483ea5a85e1b07b312787b7d48480b15ded794c14284d55228f4a765d6ca2b69290f8ceb02c75ca62'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('audio_1x1' 'audio_2x2' 'audio_4x4' 'audio_8x8' 'cv_1x1'
  'cv_2x2' 'cv_4x4' 'cv_8x8')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/mephisto#${_plugin}"
  done
}

package() {
  depends+=('libGLEW.so' 'libfaust.so' 'libfontconfig.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  # devendoring ttf-fira-code
  rm -vf "${pkgdir}/usr/lib/lv2/${pkgname}/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf "/usr/share/fonts/TTF/FiraCode-${font_type}.ttf" \
      "${pkgdir}/usr/lib/lv2/${pkgname}/"
  done
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
