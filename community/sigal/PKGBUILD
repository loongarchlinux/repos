# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sigal
pkgver=2.2
pkgrel=2
pkgdesc="Yet another simple static gallery generator"
arch=('any')
url="http://sigal.saimon.org/en/latest/"
license=('MIT')
depends=('ffmpeg' 'python-blinker' 'python-click' 'python-jinja'
'python-markdown' 'python-natsort' 'python-pillow' 'python-pilkit')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-boto' 'python-brotli' 'python-cryptography'
'python-feedgenerator' 'python-pytest' 'python-zopfli')
optdepends=('python-boto: upload to S3 plugin'
            'python-brotli: compress assets plugin'
            'python-cryptography: encrypt plugin'
            'python-feedgenerator: feed plugin'
            'python-zopfli: compress assets plugin')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8354132baaed62d968e672b804abdeafbca16890852fb75db04d7f529af6f411ef8a7284e5f56533a7243c8ad588d375d8e727fbdb1a18c449fa49529b6b6bd1')
b2sums=('01d824e03518179456177a9cbddec00859890ef238b5f4ae34cb9da20a6f3c009699e9f7c3e41915114895db68c9ccf297d35ec0fe62392af182c852d50fb87e')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,{CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
