# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=vals
pkgver=0.16.2
pkgrel=1
pkgdesc="Helm-like configuration values loader with support for various sources"
arch=('loongarch64' 'x86_64')
url="https://github.com/variantdev/vals"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/variantdev/vals/archive/v${pkgver}.tar.gz")
sha512sums=('b52038ca16bc4720010dfba8c17613d706f89d18810a1d0baf95401a80e818beba730875dbc5d787b41d40917812ba8d29e607aaae66c90695d421d781236118')
options=(!lto)

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/variantdev "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/variantdev/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/variantdev/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOLDFLAGS="-linkmode=external"
  go build -v ./cmd/vals
  chmod +x ./vals
}

# Failing tests due to Google Cloud dependency
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./vals "${pkgdir}/usr/bin/vals"
}
