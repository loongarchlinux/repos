# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.7
pkgrel=1
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org/
license=('GPL3')
depends=('python-aiohttp' 'python-defusedxml' 'python-dulwich'
         'python-icalendar' 'python-jinja' 'python-multidict')
makedepends=('python-setuptools')
optdepends=('python-prometheus_client: for metrics with the standalone server'
            'uwsgi-plugin-python: for the WSGI interface')
source=("https://files.pythonhosted.org/packages/source/x/xandikos/xandikos-$pkgver.tar.gz")
sha256sums=('e5c7d6157b9b2b044f9de4b1387504e004d0e24a62dc0a2ebcaa751c6a3f7fea')
b2sums=('09e39f8302c60aea8785d6d344d006a071758f6831de4a7548fe71dc2db1f2dea6f301bf8e0cd21b54680fac17fab6ac8dc86ae75bf4a165d40aea0b41e4e95b')

build() {
  cd xandikos-$pkgver
  python setup.py build
}

check() {
  cd xandikos-$pkgver
  python -m unittest xandikos.tests.test_suite
}

package() {
  cd xandikos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos/examples examples/*
}

# vim:set ts=2 sw=2 et:
