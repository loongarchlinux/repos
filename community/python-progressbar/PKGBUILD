# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=3.55.0
pkgrel=3
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=('any')
depends=('python-six' 'python-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest' 'python-freezegun')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('16dcdffeba6e9cb6696ac41c16701874c5f314432332d7ae2d898d1040e603c428240a8ca5718789d0d431750e2ae923a6f63fe75785213e80494bcc5825aab5'
            'SKIP')
b2sums=('89034af2cc623063e98c9de1683f159c2eeb491354c66357d9c3fb5a7ac0503b5c9d2dd6ebf36d3e2803558f3841820188eaf6dc931e992c352e0e02fa020f87'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # don't care about custom pytest config
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  # Don't write pyc files of tests so the package is reproducible.
  PYTHONDONTWRITEBYTECODE=1 pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
