# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.4.1
pkgrel=3
pkgdesc='Professional vector animation program (CLI renderer only)'
arch=('loongarch64' 'x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('boost-libs' 'fftw' 'imagemagick' 'libjpeg-turbo' 'libmng' 'libxml++2.6' 'mlt6' 'openexr'
         'pango')
makedepends=('boost' 'etl' 'intltool')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz")
sha256sums=('f90f1dcff1a409bbe2bfbb2cf42b7019cf735bec3223722db2feba6e198d1bc1')

build() {
  cd $pkgname-$pkgver

  # temporary workaround for GCC 11; upstream has refactored etl::clock to
  # use std::chrono so this should not be needed in future synfig versions
  CXXFLAGS+=' -std=c++14'

  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
