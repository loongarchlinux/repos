# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.4.2
pkgrel=2
pkgdesc='Professional vector animation program (CLI renderer only)'
arch=('loong64' 'x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('boost-libs' 'fftw' 'imagemagick' 'libjpeg-turbo' 'libmng' 'libxml++2.6' 'mlt6' 'openexr'
         'pango')
makedepends=('boost' 'etl' 'intltool')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz")
sha256sums=('e66688b908ab2f05f87cc5a364f958a1351f101ccab3b3ade33a926453002f4e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
