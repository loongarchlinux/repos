# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=6.3.1
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libcanberra
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=14fbdf5e04c9c5d9c3676c5cd7cfcc8890950cbe
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd gala
  git describe --tags
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
