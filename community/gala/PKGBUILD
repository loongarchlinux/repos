# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=3.3.2
pkgrel=2
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon-unstable)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libdrm
  libgee
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter6
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
source=(git+https://github.com/elementary/gala.git#tag=94740b5c81c05f629874c850e3a9bb984a5cb57b)
sha256sums=(SKIP)

pkgver() {
  cd gala

  git describe --tags
}

prepare() {
  cd gala

  git cherry-pick -n 85d290c75eaa147b704ad34e6c67498071707ee8
}

build() {
  arch-meson gala build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
