# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=kubectx
pkgver=0.9.3
pkgrel=1
pkgdesc='Utility to manage and switch between kubectl contexts and Kubernetes namespaces'
url='https://github.com/ahmetb/kubectx'
arch=('any')
license=('Apache')
depends=('bash' 'kubectl')
provides=('kubectx' 'kubens')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('820b86241fa37586222dadebe72ec156069f9d0464d1ff78aef600730ebaf4201079682e661a1208e2ab5042fcff12334b94fb1d42f81b4a136f7c23ab2b6c80')
b2sums=('99a8d42704abae31da4339ae010b93d9e0a03c31c2022390972fbea8ba4b7ae13d9efeb0ddfd181201734205f5c44f9bc37e285f424a4e3bf638d6a2e0833446')

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 755 -p kubectx kubens -t "${pkgdir}/usr/bin"

  # completion
  install -Dm 644 -p completion/kubectx.bash "${pkgdir}/usr/share/bash-completion/completions/kubectx"
  install -Dm 644 -p completion/kubens.bash "${pkgdir}/usr/share/bash-completion/completions/kubens"
  install -Dm 644 -p completion/kubectx.zsh "${pkgdir}/usr/share/zsh/site-functions/_kubectx"
  install -Dm 644 -p completion/kubens.zsh "${pkgdir}/usr/share/zsh/site-functions/_kubens"
  install -Dm 644 -p completion/kubectx.fish "${pkgdir}/usr/share/fish/vendor_completions.d/kubectx.fish"
  install -Dm 644 -p completion/kubens.fish "${pkgdir}/usr/share/fish/vendor_completions.d/kubens.fish"
}

# vim: ts=2 sw=2 et:
