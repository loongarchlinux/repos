# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus-fixtures-async
pkgver=0.1.1
pkgrel=2
pkgdesc='Test fixtures for running in Async'
arch=(any)
url='https://github.com/socketry/sus-fixtures-async'
license=(MIT)
depends=(ruby-async ruby-sus)
checkdepends=(ruby-bake-test ruby-covered)
options=(!emptydirs)
source=(https://github.com/socketry/sus-fixtures-async/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4881cf089a4a8a4344659052f7a1611212845380675ab0053bd399797c7f3bdb')

prepare() {
  cd sus-fixtures-async-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i sus-fixtures-async.gemspec
  sed -i '/bake-gem/d;/bake-modernize/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus-fixtures-async-$pkgver
  gem build sus-fixtures-async.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-fixtures-async-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus-fixtures-async-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus-fixtures-async-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
