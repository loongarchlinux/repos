# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=1.5.2
pkgrel=3
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-numpy' 'python-feedparser' 'python-simplejson'
              'python-pymongo' 'python-ujson' 'python-pandas' 'python-pytest')
source=("https://github.com/jsonpickle/jsonpickle/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d7a84fab67c98c857d59983a19ae3fc4408f69ad95fa9332106a68eac1075b522703a1293d86b111297543b45cc6a0a3dee457902f1e384c32be8073415abbcc')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jsonpickle-$pkgver
  # do not run coverage and other non-integration tests in an integration test
  sed -i 's/--flake8 --black --cov//' pytest.ini
}

build() {
  cd jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd jsonpickle-$pkgver
  pytest
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
