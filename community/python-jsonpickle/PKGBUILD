# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=1.4.1
pkgrel=2
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-numpy' 'python-feedparser'
              'python-coverage' 'python-simplejson' 'python-pymongo' 'python-ujson' 'python-pandas')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jsonpickle/jsonpickle/archive/v$pkgver.tar.gz"
        fix-importlib_metadata-dependency.patch)
sha512sums=('aaaf579d6fafc28646f2566e9d8cfaf1d735e12a443c3dc107051f4229fc630d6539ff6b9a610518d02dc4d9704f4100310f81c38cf0ac53561ade874141143d'
            'ce5ab39821b02e4b6aa9b4380ad1681154b48e08af52f98fac93733e1cb07d4838d3b224569b6f74fe33df0f5dde7fd40af7f870b329308522861ed729209831')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jsonpickle-$pkgver
  sed -i 's/--flake8//' pytest.ini
  patch -Np1 < ../fix-importlib_metadata-dependency.patch
}

build() {
  cd jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd jsonpickle-$pkgver
  pytest
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
