# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.20.0
pkgrel=2
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=(loongarch64 x86_64)
url='https://github.com/sharkdp/bat'
license=(APACHE MIT)
depends=(gcc-libs libgit2 oniguruma)
makedepends=(clang cmake git rust)
source=("git+$url#commit=0655ecf23d778f1455e2d79f37a805a7bcca98d2") # tag: v0.20.0
b2sums=('SKIP')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path "$srcdir/$pkgname/Cargo.toml"
}

build() {
  export CFLAGS+=' -ffat-lto-objects -w'
  cargo build \
    --locked \
    --manifest-path "$srcdir/$pkgname/Cargo.toml" \
    --release
}

check() {
  cargo test \
    --locked \
    --manifest-path "$srcdir/$pkgname/Cargo.toml"
}

package() {
  install -Dm755 $pkgname/target/release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Package licenses
  install -Dm644 $pkgname/LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 $pkgname/LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd $pkgname/target/release/build

  # Find and package the man page (because cargo --out-dir is too new)
  find . -name bat.1 -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/man/man1/bat.1" \;

  # Find and package the zsh completion file (not in zsh-completions yet)
  find . -name bat.zsh -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/zsh/site-functions/_bat" \;

  # Find and package the fish completion file
  find . -name bat.fish -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/fish/vendor_completions.d/bat.fish" \;
}
