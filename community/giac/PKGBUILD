# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=giac
_pkgver=1.9.0-25
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='A free computer algebra system'
arch=(loong64 x86_64)
url='http://www-fourier.ujf-grenoble.fr/~parisse/giac.html'
license=(GPL3)
depends=(fltk mpfi gsl pari ntl lapack curl glpk libao gmp-ecm libsamplerate)
makedepends=(python libjpeg texlive-core texlive-science hevea nauty)
optdepends=('perl: for pgiac')
replaces=(libgiac xcas)
provides=(libgiac xcas)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        giac-pari-2.15.patch)
sha256sums=('a449c1de1136d0fe45c162fcd84821ff3e68f8995512affeac683e0abc1f8b71'
            'cb1d6e783f2a1f05c2ec7319874089e6a2ab6316f65661087796452b9b8efb21')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../giac-pari-2.15.patch # Fix build with pari 2.15
}

build() {
  cd $pkgname-${pkgver%.*}
  CXXFLAGS+=" -Wp,-U_GLIBCXX_ASSERTIONS" # Fix crashes
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
