# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-protego
pkgver=0.2.0
pkgrel=1
pkgdesc="A pure-Python robots.txt parser with support for modern conventions"
url="https://github.com/scrapy/protego"
license=('BSD')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/protego/archive/$pkgver.tar.gz")
sha512sums=('9b9c665eb49da34daeeb4b002682ecc908151010416aef5f49ada75bc92078c426197f8507b547c08247e7f0b1adb985932f87e040b06a68f20b97f28cc4e1f4')

build() {
  cd protego-$pkgver
  python setup.py build
}

check() {
  cd protego-$pkgver
  python setup.py pytest
}

package() {
  cd protego-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
