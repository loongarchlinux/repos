# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-h2
pkgver=3.2.0
pkgrel=2
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python-hpack' 'python-hyperframe')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-hyper/hyper-h2/archive/v$pkgver.tar.gz")
sha512sums=('103a4ea2cd29037ce03ae676864125b4111c7d31a82bbbe7028557da886d88fb5363d885f08d5d3f68dfa29013b7b03350cc0cb4d9dddf32735e98dae5d7fc7c')

build() {
  cd hyper-h2-$pkgver
  python setup.py build
}

check() {
  cd hyper-h2-$pkgver
  python -m pytest
}

package() {
  cd hyper-h2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
