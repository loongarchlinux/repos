# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hasql-implicits
pkgname=haskell-hasql-implicits
pkgver=0.1.0.2
pkgrel=42
pkgdesc="Implicit definitions for Hasql, such as default codecs for standard types"
url="https://github.com/nikita-volkov/hasql-implicits"
license=("MIT")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-hasql' 'haskell-network-ip' 'haskell-scientific'
         'haskell-uuid' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('2788d365dd9d0c4826562472b577ac3715043f3a722ef7c21aaed754f122e74d')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
