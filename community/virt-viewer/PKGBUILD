# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=virt-viewer
pkgver=8.0
pkgrel=2
pkgdesc="A lightweight interface for interacting with the graphical display of virtualized guest OS."
depends=('gtk-vnc' 'libvirt' 'spice-gtk3' 'desktop-file-utils' 'libvirt-glib' 'libgovirt')
makedepends=('intltool' 'spice-protocol')
replaces=('virtviewer')
url="https://virt-manager.org/"
arch=('x86_64')
license=('GPL')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF')
source=("https://virt-manager.org/download/sources/virt-viewer/virt-viewer-$pkgver.tar.gz"{,.asc})
sha512sums=('73317048949b6c49c28aab4685e3fb6533945b12c47078b522429b3e178423fcec5758c970d5048c8f65fee2b92a629b7e067cffa79c968292c73fd36133b918'
            'SKIP')

build() {
  cd "$srcdir/virt-viewer-$pkgver"
  ./configure --prefix=/usr --with-spice --disable-update-mimedb
  make
}

package() {
  cd "$srcdir/virt-viewer-$pkgver"
  make DESTDIR="$pkgdir" install
}
