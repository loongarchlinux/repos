# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor:Techlive Zheng <techlivezheng at gmail dot com>

pkgname=phpvirtualbox
pkgver=5.2_1
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/rc/-rc}
_commit=cf82d58f26ed82d3f7f4b5ba5e80f8754ee48c2f
pkgrel=6
pkgdesc="PHP/AJAX web interface for VirtualBox"
arch=(any)
url="https://github.com/phpvirtualbox/phpvirtualbox"
license=('GPL')
depends=('php7')
makedepends=('git')
backup=("etc/webapps/phpvirtualbox/config.php")
#source=("https://github.com/phpvirtualbox/phpvirtualbox/archive/${_pkgver}.tar.gz")
source=("git+https://github.com/phpvirtualbox/phpvirtualbox.git#commit=${_commit}")
sha256sums=('SKIP')

package() {
#  cd "$srcdir"/${pkgname}-${_pkgver}
  cd "$srcdir"/${pkgname}

  mkdir -p "$pkgdir"/etc/webapps/phpvirtualbox
  install -D -m644 config.php-example "$pkgdir"/etc/webapps/phpvirtualbox/config.php

  # Apache configuration
  cat > "$pkgdir"/etc/webapps/phpvirtualbox/apache.example.conf <<EOF
  Alias /phpvirtualbox "/usr/share/webapps/phpvirtualbox"
  <Directory "/usr/share/webapps/phpvirtualbox">
    Options FollowSymlinks
    AllowOverride All
    Require all granted
  </Directory>
EOF

  find . -type f -exec install -D -m644 {,"$pkgdir"/usr/share/webapps/${pkgname}/}{} \;
  rm -r "$pkgdir"/usr/share/webapps/${pkgname}/.git
  ln -s /etc/webapps/phpvirtualbox/config.php "$pkgdir"/usr/share/webapps/phpvirtualbox/config.php
}
