# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=24.1.0
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin-ruby'
license=(MIT)
depends=(ruby-cucumber-messages)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/cucumber/common/archive/gherkin/v$pkgver/cucumber-common-gherkin-$pkgver.tar.gz)
sha256sums=('0ae4f2e5a57cfd8f68f32fea5fa190e42fb1b2443de3caf6e6b7b4ed223ef91e')

prepare() {
  cd common-gherkin-v$pkgver/gherkin/ruby
  sed -r 's|~>|>=|g' -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd common-gherkin-v$pkgver/gherkin/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd common-gherkin-v$pkgver/gherkin/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd common-gherkin-v$pkgver/gherkin/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
