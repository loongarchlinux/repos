# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=copr-cli
pkgver=1.97
pkgrel=1
pkgdesc="CLI tool to run copr"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-copr' 'python-humanize' 'python-simplejson' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-responses')
source=("https://pagure.io/copr/copr/archive/copr-cli-$pkgver-1/copr-copr-cli-$pkgver-1.tar.gz")
sha512sums=('c9f8c9d61a26a847446f1cfa9db9622fbbc6fa86863ccae9687088ee5d1c6764d20fdd205573e4b18f9ae0193b98eb6409c1686ad3ab86a42181b2d179d26ee2')

build() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py build
}

check() {
  cd copr-copr-cli-$pkgver-1/cli
  python -m pytest
}

package() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py install --root="$pkgdir" --optimize=1
}
