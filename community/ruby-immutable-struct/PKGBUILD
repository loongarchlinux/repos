# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=immutable-struct
pkgname=ruby-${_gemname}
pkgver=2.3.0
pkgrel=2
pkgdesc="Create struct-like classes that don't have setters, but have an awesome constructor"
url='https://github.com/stitchfix/immutable-struct'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('rubygems' 'ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/stitchfix/immutable-struct/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('2f8539307cb104ba0e764b003302b2a79751518b4002be264692270b1dcc1e0a')
sha512sums=('6ae9ab0faab0ea5160ca96a6f2b2333e4e37b5f69e27bbe02a1880679ef34008bb9e79d834268c496571a01f3294599160f2d2ab05e0ac671cd4260876bc6352')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  rm Gemfile.lock
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
