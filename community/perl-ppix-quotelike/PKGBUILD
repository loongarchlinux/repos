# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-quotelike
pkgver=0.019
pkgrel=1
pkgdesc="Parse Perl string literals and string-literal-like things"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-QuoteLike"
depends=('perl-ppi')
makedepends=('perl-ppix-regexp' 'perl-readonly')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-QuoteLike-$pkgver.tar.gz")
sha512sums=('916ec0298122721cae57ee04ac79ace7f1d4667452ba6b9293bffe77d0b34c26f84780046b3f8d595bc3dc39147b46a6b879c091e36a31ce04a62f5af3524340')

build() {
  cd PPIx-QuoteLike-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-QuoteLike-$pkgver
  make test
}

package() {
  cd PPIx-QuoteLike-$pkgver
  make DESTDIR="$pkgdir" install
}
