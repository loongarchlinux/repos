# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

_pkgbase='websocket-client'
pkgname=python-$_pkgbase
pkgver=0.59.0
pkgrel=3
pkgdesc="WebSocket client library for Python"
arch=('any')
url="https://github.com/$_pkgbase/$_pkgbase"
license=('LGPL')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pysocks')
source=("https://pypi.io/packages/source/w/$_pkgbase/$_pkgbase-$pkgver.tar.gz")
sha512sums=('c7f4a0336093de86fd00828049a836477a3c859cc4bddebd9e7d31eb746dab4de8fa11205a5fb182e5df3590964aa73f279310575dfabc4abc32157e80df42b1')

prepare() {
  # Not sure why the conditions are not working :/
  sed -i "s/'backports.ssl_match_hostname'//" $_pkgbase-$pkgver/setup.py
}

build() {
  cd $_pkgbase-$pkgver
  python setup.py build
}

check() {
  cd $_pkgbase-$pkgver
  python setup.py test
}

package() {
  cd $_pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 ft=sh et:
