# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sortedcontainers
pkgver=2.2.2
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grantjenks/python-sortedcontainers/archive/v$pkgver.tar.gz")
sha512sums=('413212aa35a80d24ec208caf19314dcc42dae207ec4ea8fdd7dc240c6fbf17372cb4c3e2209f6e40094419fa3eeb2fc9b23ad8bd7fb4e6fdaca46fd2fb248281')

prepare() {
  # Don't use tox
  sed -i '/tests_require/d' python-sortedcontainers-$pkgver/setup.py
}

build() {
  cd python-sortedcontainers-$pkgver
  python setup.py build
}

check() {
  cd python-sortedcontainers-$pkgver
  python setup.py pytest
}

package() {
  cd python-sortedcontainers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
