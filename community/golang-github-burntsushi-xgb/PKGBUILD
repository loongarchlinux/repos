# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-burntsushi-xgb
pkgver=0.0.20160523
_commit=27f122750802c950b2c869a5b63dafcf590ced95
pkgrel=3
pkgdesc='The X Go Binding is a low-level API to communicate with the X server'
arch=('any')
url="https://github.com/BurntSushi/xgb"
license=('BSD' 'custom:WTFPL')
depends=('go')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$_commit.tar.gz::https://github.com/BurntSushi/xgb/archive/$_commit.tar.gz")
sha512sums=('889ff39e4e5614faf7ef49a9fbae3efdd0dd9232b8652c34f621208b4d136ff76864ceb61e2b6cb3796fbf51309b0c74e50e7f2444927e0e99072c65c4892cd5')

prepare() {
  cd xgb-$_commit
  sed -i 's|fmt.Println("\\n")|fmt.Print("\\n\\n")|' examples/randr/main.go
}

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/BurntSushi
  cp -a "$srcdir"/xgb-$_commit "$srcdir"/build/src/github.com/BurntSushi/xgb
  cd "$srcdir"/build/src/github.com/BurntSushi/xgb
  xvfb-run go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/BurntSushi
  cp -a "$srcdir"/xgb-$_commit "$pkgdir"/usr/share/gocode/src/github.com/BurntSushi/xgb

  install -Dm644 xgb-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.BSD
  install -Dm644 xgb-$_commit/xgbgen/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING.WTFPL
}
