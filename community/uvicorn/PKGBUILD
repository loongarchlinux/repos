# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=uvicorn
pkgver=0.14.0
pkgrel=1
pkgdesc='The lightning-fast ASGI server'
arch=('any')
url='https://github.com/encode/uvicorn'
license=('BSD')
depends=('python' 'python-click' 'python-h11' 'python-websockets' 'python-httptools' 'python-uvloop')
optdepends=('python-watchgod: watchgod reload')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f41a7f01ac6893ceaf4d1f06a4bccd7355e319eee11d6aa290b0d316fb356eb807c926941157ec64f1fc6b7c4601930cfbe271075d6c4cf684bd7e175617390f')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
