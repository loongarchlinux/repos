# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=1.49.2
pkgrel=3
pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')
depends=('libwebp' 'python-ijson' 'python-jsonschema' 'python-twisted'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-bcrypt' 'python-frozendict'
         'python-pillow' 'python-pysaml2'
         'python-systemd' 'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-idna' 'python-jinja'
         'python-bleach' 'python-typing_extensions' 'systemd')
makedepends=('python-setuptools')
checkdepends=('python-authlib' 'python-pyjwt' 'python-lxml' 'python-parameterized'
              'python-txredisapi' 'python-hiredis')
optdepends=('perl: sync_room_to_group.pl'
            'python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-psutil: metrics'
            'python-pyjwt: jwt'
            'python-txredisapi: redis'
            'python-hiredis')
source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'generic_worker.yaml.example'
        'synapse.service'
        'synapse.target'
        'synapse-worker@.service'
        'sysusers-synapse.conf'
        'tmpfiles-synapse.conf'
        'override-hardened.conf'
        'use-mock-from-unittest.patch'
        'latest-frozendict.patch')
sha256sums=('f5b0017e9d77db94fac853fbefbcb4538d879cb80f404b02003930c76f5cafab'
            'f67334856609997eac26939d77cfc520e78e98d3755543ab730d83a0f362a35e'
            '74af0bc2f57e5ced1a44f2438922d420cbb7defedae784cac02ef125f276a2ed'
            '408527271e1250beb20531f140b91201ed464e42f7eb3f47f02967a2ac23a661'
            'c9657c201ad89985c8c915bfa0ea7517a412071736b4d9545d8f6474fddc44e2'
            'aadfdd78fe73e6eb325ee4299b8db8b97bfa2f4e7df953aa8477f442598a7ec5'
            '65588c8c64dfb84cab831cd8d028a295d753cf7322dd63053e8488466047b45f'
            'd8e6b2a43a8a7d8f09c643f32e789a7ffeeb2d20bb07ee88ddc6923e1ab3b0e6'
            '84b5e9b32ace497f40d0facd27eec3552924a8384130cb70caa9b0db9a13de3c'
            'f1307d1db27ef293ee2430ec734ac7380e6022ba1396770b343359179f9b294e')
backup=('etc/synapse/log_config.yaml')
install=synapse.install

prepare() {
	cd synapse-$pkgver
	patch -Np1 -i ../use-mock-from-unittest.patch
	patch -Np1 -i ../latest-frozendict.patch
}

build() {
	cd synapse-$pkgver
	python setup.py build
}

check() {
	cd synapse-$pkgver
	PYTHONPATH=. trial -j8 tests
}

package() {
	cd synapse-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -vdm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -vDm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -vDm644 "$srcdir"/generic_worker.yaml.example "$pkgdir"/etc/synapse/workers/generic_worker.yaml.example

	install -vDm644 "$srcdir/override-hardened.conf" -t "$pkgdir/usr/lib/systemd/system/synapse.service.d"
	install -vDm644 "$srcdir/override-hardened.conf" -t "$pkgdir/usr/lib/systemd/system/synapse-worker@.service.d"
	install -vDm644 -t "$pkgdir"/usr/lib/systemd/system/ "$srcdir"/synapse{,-worker@}.service "$srcdir"/synapse.target
	install -vDm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
	install -vDm644 "$srcdir"/tmpfiles-synapse.conf "$pkgdir"/usr/lib/tmpfiles.d/synapse.conf
}
