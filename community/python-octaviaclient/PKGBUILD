# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=2.5.0
pkgrel=1
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
checkdepends=('python-hacking' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-octaviaclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('081d62f9bbc32a2ca2e5f39a7bc5172f969d9437bfe73caaa32ff0e53c3eead8769b2585084cd1a9343fe0fc6322271c00d61823b5e9c303ab0e9fc909ab2635')

export PBR_VERSION=$pkgver

build() {
  cd python-octaviaclient-$pkgver
  python setup.py build
}

check() {
  cd python-octaviaclient-$pkgver
  stestr run
}

package() {
  cd python-octaviaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
