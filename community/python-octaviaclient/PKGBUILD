# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=2.3.0
pkgrel=1
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
checkdepends=('python-hacking' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-octaviaclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d623a191f8571c1607d898d86bf76898b2fec68c3649bbd29b254e3893c6f2922ade10a98a96e8e563544bf76338c1ac3f12de96734b033d529766677cc6d09b')

export PBR_VERSION=$pkgver

build() {
  cd python-octaviaclient-$pkgver
  python setup.py build
}

check() {
  cd python-octaviaclient-$pkgver
  stestr run
}

package() {
  cd python-octaviaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
