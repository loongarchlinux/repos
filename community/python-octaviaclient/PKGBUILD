# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=2.4.0
pkgrel=3
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
checkdepends=('python-hacking' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-octaviaclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('995584d41d19a84a555e6d3b055a72c255339e77dfafd890580760e2f3ac2be4edc4e2a4affeea7f54b099ae30e0d4566400ebb1907645b9ca17b9ecf86fe786')

export PBR_VERSION=$pkgver

build() {
  cd python-octaviaclient-$pkgver
  python setup.py build
}

check() {
  cd python-octaviaclient-$pkgver
  stestr run
}

package() {
  cd python-octaviaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
