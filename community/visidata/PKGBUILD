# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pig Monkey <pm@pig-monkey.com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
# Contributor: Javier Ti√° <javier dot tia at gmail dot com>

pkgname=visidata
pkgver=2.8
pkgrel=2
pkgdesc='Terminal spreadsheet multitool for discovering and arranging data'
arch=(any)
url=https://www.visidata.org
license=(GPL3)
depends=(python
         python-dateutil)
optdepends=('python-dnslib: pcap support'
            'python-fonttools: ttf/otf support'
            'python-h5py: hdf5 support'
            'python-lxml: html/xml support'
            'python-openpyxl: xlsx support'
            'python-pandas: dta (Stata) support'
            'python-pdfminer: pdf support'
            'python-psycopg2: postgres support'
            'python-pyaml: yaml/yml support'
            'python-pypng: png support'
            'python-requests: http support'
            'python-tabulate: tabulate saver support'
            'python-vobject: vcf support'
            'python-wcwidth: tabulate saver with unicode support'
            'python-xlrd: xls support')
# optdepends+=( # AUR only optdepends
#             'python-dpkt: pcap support'
#             'python-mapbox-vector-tile: mbtiles support'
#             'python-namestand: graphviz support'
#             'python-pyshp: shapefiles support')
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
_archive="$pkgname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('26d6cdacdca394a4820bda7f142746e190c49a7871c109f477f8ffddc1ccd0c9')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
