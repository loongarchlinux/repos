# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.20
pkgrel=1
pkgdesc='Simple Python interface for Graphviz'
arch=(any)
url="https://github.com/xflr6/$_pkgname"
license=(MIT)
depends=(graphviz
         python)
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
checkdepends=(python-mock
              python-pytest
              python-pytest-mock)
#source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('d6e3467cacfcdc239bcd518107baba9665bb6aadf4f5a4c48c493c3ea2d11466')
b2sums=('89ba8f217a0fbcfb7e1fee460413fdb3f1d5f301e65a55f3ec3ef7711f4831cc10d33bea9f8457d9322091edc395c1a3ca24bec7048f31e9520b0a8c2e504851')

prepare() {
	cd "$_archive"
	# do not run python-coverage in unittests :/
	sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
	# don't add pointless dependency on python-mock needed on python 3.5
	sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive/tests"
	python -m pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
