# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.19
pkgrel=2
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
url="https://github.com/xflr6/${_pkgname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-mock')
#source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
source=("https://github.com/xflr6/graphviz/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha256sums=('47da628b46d5a9f8027170f0a3926ece5f9dbf0835f950d552a0547085c1f4e6')
b2sums=('d68acacaeff538c77484a8c63553095ea916c6e6dc24353351db57b6c9bd24429053643780461a44e5bb95e8d4cd76b5f3ca50279c93cd6ed26ea64a34d0709e')

prepare() {
    cd ${_pkgname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}/tests

    python -m pytest
}

package() {
    cd ${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
