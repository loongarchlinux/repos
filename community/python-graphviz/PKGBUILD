# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.14
pkgrel=1
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
url="https://github.com/xflr6/${_pkgname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
sha256sums=('e104ba036c8aef84320ec80560e544cd3cad68c9f90394b4e2b87bc44ab09791')
b2sums=('90be05243302890350551b19fc6dc6789a445a05700e5764a3b1b8e240e2cec23f6d008a44d279e2119236018f707b23071863da1578d328db187baf5512ef98')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
