# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rofimoji
pkgver=5.4.0
pkgrel=1
pkgdesc='Character picker for rofi'
arch=('any')
url=https://github.com/fdw/rofimoji
license=('MIT')
depends=('python-configargparse')
makedepends=('python-build' 'python-install' 'python-setuptools' 'python-wheel')
optdepends=('emoji-font: for the emojis character file'
            'nerd-fonts: for the nerd_font character file'
            'rofi: for the X.Org selector'
            'xclip: for one of the X.Org clipboarders'
            'xsel: for one of the X.Org clipboarders'
            'xdotool: for the X.Org typer'
            'wofi: for the Wayland selector'
            'wl-clipboard: for the Wayland clipboarder'
            'wtype: for the Wayland typer')
install=$pkgname.install
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('7c87d4c88382cc85a7b0ea2adb998f383c1310a04749bf0b3a7ddb4fb42a0a37')
b2sums=('44bde09f725d8a483b4be93aa64d15fd00c6c7dd555a6f8e0d2ac9e46f8c179833784c6716345c8eea44dd3dbba1475447560fb9d422a545a1bd90c9e2691425')

prepare() {
  cd $pkgname-$pkgver
  # Remove data_files section https://github.com/pypa/wheel/issues/92
  sed -i '/\[options.data_files\]/,/^$/d' setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 src/picker/docs/$pkgname.1

  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
