# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Legner <Simon.Legner@gmail.com>

pkgname=git-revise
pkgdesc='A git subcommand to efficiently update, split, and rearrange commits'
pkgver=0.6.0
pkgrel=4
url="https://mystor.github.io/git-revise.html"
arch=(any)
license=(MIT)
depends=(git
         python)
makedepends=(python-setuptools)
_archive="$pkgname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$_archive.tar.gz")
sha256sums=('21e89eba6602e8bea38b34ac6ec747acba2aee876f2e73ca0472476109e82bf4')

build() {
	cd "$_archive"
	python setup.py build
}

package() {
	cd "$_archive"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm0755 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
