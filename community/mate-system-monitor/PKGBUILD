# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-system-monitor
pkgver=1.24.2
pkgrel=1
pkgdesc="A system monitor for MATE"
url="https://mate-desktop.org"
arch=('loongarch64' 'x86_64')
license=('GPL')
depends=('gtkmm3' 'libgtop' 'libwnck3' 'gettext')
makedepends=('itstool' 'python')
conflicts=('mate-system-monitor-gtk3')
replaces=('mate-system-monitor-gtk3')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f7c39075f4c21ea3a3cd3e32d7924ad8432743dc9aed3c511a05a0a261f176d5')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--localstatedir=/var \
        	--enable-systemd
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
