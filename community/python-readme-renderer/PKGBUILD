# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-readme-renderer
pkgver=27.0
pkgrel=1
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('any')
depends=('python-bleach' 'python-docutils' 'python-pygments' 'python-six')
optdepends=('python-cmarkgfm: for Markdown support')
makedepends=('python-setuptools' 'python-cmarkgfm')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('a7e4bdce989c0790394a71ab185d538825e8a7dc70abd43b887aee804eb52d5ef976bc0c7655eda0c22f744568071239691e6708527b82372e94d12aafa10f11')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package_python-readme-renderer() {
  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
