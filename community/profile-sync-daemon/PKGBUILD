# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.42
pkgrel=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=('any')
url="https://github.com/graysky2/profile-sync-daemon"
license=('MIT')
depends=('bash' 'findutils' 'inetutils' 'procps-ng' 'rsync' 'systemd')
optdepends=('zsh-completions: for completion when using zsh')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('c7c23187d5badf0f47f66988331bc0fc3f639869a851b17dd6fd894a3c4d01715e6299475664fb93a7c4ad555d1c54553775a504ad9c108a99c6c564de383c33')
b2sums=('15a0e10e11fc81bd5be181b99691ef8f656016b6b692c6d17ebbd099bbef4da21187243e73a4c3ba5f78172b42e7b1ef585d00aea98e4541222b7d25a8a4b1af')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
