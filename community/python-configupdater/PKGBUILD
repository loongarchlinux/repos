# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configupdater
pkgver=3.1
pkgrel=1
pkgdesc="Parser like ConfigParser but for updating configuration files"
url="https://github.com/pyscaffold/configupdater"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov')
source=("https://github.com/pyscaffold/configupdater/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('89685a93f3bec907184d818b5adea4f87967df313fe4e4216aeef2c227fdf0b0f98757ec27da7f43adff6e2ee37020dd57b98569d5811c9dc7c3201852b27432')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd configupdater-$pkgver
  python setup.py build
}

check() {
  cd configupdater-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd configupdater-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
