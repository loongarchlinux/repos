# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Moritz Lipp <mlq@pwmt.org>
# Contributor: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=3.1.0
pkgrel=1
pkgdesc="Simple calendar (CalDAV) and contact (CardDAV) server"
arch=(any)
url="https://radicale.org/"
license=(GPL3)
# setuptools is required due to use of pkg_resources:
# https://github.com/Kozea/Radicale/issues/1184
depends=(python-bcrypt python-dateutil python-defusedxml python-passlib
python-setuptools python-vobject)
checkdepends=(python-pytest python-pytest-runner python-typeguard python-waitress)
backup=('etc/radicale/config'
        'etc/radicale/rights'
        'etc/radicale/users')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Kozea/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf"
        "${pkgname}.service")
sha512sums=('b2b4a9ba308650949d5cc5c8a88685b925dd1fce21251829e1ac11225699b5294964e91f37955d919cadfe9eca786dd805d0c141c6a9013c95c67511121b8788'
            '56dffb66e018cfbf158dc5d8fe638b3cb31229945f659aae5623f219bcd1d68ddc375f1633fa8e857a9b2f50c9e05a06efce165370137d6e116a4f187466637f'
            '9d0dd88e4a34e9f97abda1785698e4b2a5e8202063deeb91b84e13c05e00b07e45b8d4d9eca09b9241b1138bbbfdc999dba0135c18f5bc0c08d65b0cd83b367b'
            '0f74a662e2eee56a89f5735e686910043ad5589b638e56a7ad3caa6d3b111cfbf1c131fe1b2ee34ce0d10d2f8a041a183f1382b1e1c9594f7e793c92161ef17a')
b2sums=('e4ed7a2fb395fbcd16d9da26e764ccad1594de8ff7ec4394885256b7d09a360a510f85727e1a7e936a10d9132bed1a487dcfdece94449df4278dc843cef6c606'
        'b3af60e144ef857e42ec672e806e9600265ab7d2ea4a75011de9ab56918a008437afdacb301df210b54424fb7ff1e9a332831c67b2e58fd6bc0a0aa1eebe8909'
        '41916d62f5e3f1060bd21db0722abe837754a4cb915af218c904dafac4b06794f8fde2e34486fb7392777b4738502f3df4c1390b835050045337585b064e23bb'
        '8d7e732bb7430428db2b60ffd8b4b1c3e85cbda4a1b900ae28d80c46a64e97ab484d9cc13aaa2582eeca4063f4e74141754f1e67769d444b08b3663f62cf8bf1')

prepare() {
  cd ${_name}-${pkgver}
  touch "users"
}

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd ${_name}-${pkgver}
  mkdir -vp testdir
  python setup.py install --optimize=1 --root="testdir"
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v -c /dev/null
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --optimize=1 --root="${pkgdir}"
  # config
  install -vDm 644 {config,rights,users} -t "${pkgdir}/etc/${pkgname}/"
  # wsgi
  install -vDm 644 "${pkgname}.wsgi" -t "${pkgdir}/usr/share/${pkgname}/"
  # systemd service
  install -vDm 644 "${srcdir}/${pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
  # sysusers.d
  install -vDm 644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d
  install -vDm 644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # docs
  install -vDm 644 {NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
