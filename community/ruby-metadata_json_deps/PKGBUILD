# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='metadata_json_deps'
pkgname="ruby-${_gemname}"
pkgver=0.5.0
pkgrel=1
pkgdesc='Verify all your dependencies allow the latest versions on Puppet Forge'
arch=('any')
url="https://github.com/ekohl/${_gemname}"
license=('Apache')
depends=('ruby' 'ruby-puppet_forge' 'ruby-puppet_metadata')
checkdepends=('ruby-rspec')
makedepends=('ruby-rdoc' 'ruby-rake')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${_gemname}-${pkgver}.tar.gz")
sha512sums=('076c81bd06dcc5fd330f072fb65bf7a475677136e3777bcdbaee90926d729b931aebe761285d1abc5e57137d14323b1c4fbdfcd97ba43629517f572a603f483a')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake spec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
