# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='metadata_json_deps'
pkgname="ruby-${_gemname}"
pkgver=0.3.0
pkgrel=2
pkgdesc='Verify all your dependencies allow the latest versions on Puppet Forge'
arch=('any')
url="https://github.com/ekohl/${_gemname}"
license=('Apache')
depends=('ruby' 'ruby-puppet_forge' 'ruby-puppet_metadata')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${_gemname}-${pkgver}.tar.gz")
sha512sums=('e6aca450cc0fdcba367b01776bfd9c42e6c1876d37ea0055db6580bf7ff4d70fd1e2d825947798c0558727d7d71e956064d9cb1adc3acd0a9c16df3b6128994e')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # upstream has no tests
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
