# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2022.9.0
pkgver=2022.9.0
_upstreamver=${_pkgver%[[:lower:]]}
#_patchver=$_upstreamver
_patchver=2022.9.0 # almost always out of sync now
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=('any')
url='https://github.com/dani-garcia/bw_web_builds'
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-gallium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
        "$pkgname-v$_pkgver-$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        '0eb35247f40c5451bb69dc0e1a8d54fa189584059606ee2001fca495d3ace0118044070d96b6d7b6948c9da6f026d5452cc9d4d7411daa3c541db283588be9a8')

pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_pkgver-$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-clients/apps/web
  npm run dist:oss:selfhost
}

package() {
  cd bitwarden-clients/apps/web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
