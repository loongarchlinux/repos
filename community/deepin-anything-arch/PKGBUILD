# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=5.0.9
pkgrel=53
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('loongarch64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("https://github.com/linuxdeepin/deepin-anything/archive/$pkgver/deepin-anything-$pkgver.tar.gz")
sha512sums=('d7038f900c7f6cf2af156ced0fab6bcd46bff347011acc34b5d355ae8f09efe3f323724b728fbfe9c8b961e238ceb7c81aaf45f2e446b5d9ce908e8c0a658c5b')

build() {
  cd deepin-anything-$pkgver
  make -C kernelmod kdir=/usr/src/linux
}

package() {
  depends=('linux')

  cd deepin-anything-$pkgver
  local extradir=/usr/lib/modules/$(</usr/src/linux/version)/extramodules
  install -Dt "$pkgdir$extradir" -m644 kernelmod/*.ko
  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "$pkgdir" -name '*.ko' -exec xz {} +
}
