# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=xdg-desktop-portal-wlr
pkgver=0.4.0
pkgrel=2
pkgdesc='xdg-desktop-portal backend for wlroots'
url="https://github.com/emersion/xdg-desktop-portal-wlr"
arch=('loongarch64' 'x86_64')
license=('MIT')
provides=('xdg-desktop-portal-impl')
depends=('xdg-desktop-portal' 'pipewire' 'pipewire-media-session' 'libinih')
makedepends=('meson' 'wayland-protocols' 'wayland' 'scdoc')
optdepends=(
    'slurp: to choose which output to screencast using slurp'
    'wofi: to choose which output to screencast using wofi'
    'bemenu: to choose which output to screencast using bemenu'
)
source=(
    "${pkgname}-${pkgver}::https://github.com/emersion/xdg-desktop-portal-wlr/archive/v${pkgver}.tar.gz"
)
sha512sums=('544dea4601ce8aa8649d15b7c249aa5b0099fa5a6aec1b7a1433439265acede9ac442092da53080c36b372dd0ef5aaa15747b13a56f4a648e1c00ed3c2435e19')

build() {
    cd "${pkgname}-${pkgver}"
    arch-meson -Dsd-bus-provider=libsystemd build
    ninja -C build
}

package() {
    cd "${pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
