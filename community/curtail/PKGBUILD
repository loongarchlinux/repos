# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.1.0
pkgrel=3
pkgdesc='Image compressor GUI, supporting PNG and JPEG file types, both lossless and lossy compression mode'
arch=('any')
url='https://github.com/Huluti/Curtail'
license=('GPL3')
depends=('gtk3' 'jpegoptim' 'optipng' 'pngquant' 'python-gobject')
makedepends=('appstream-glib' 'meson')
source=("https://github.com/Huluti/Curtail/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('bb00badaace6e81291add96ef383cc0ae6f4aef675587eccdef875522c3b08a6')

build() {
  arch-meson Curtail-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
