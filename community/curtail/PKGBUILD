# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.2.1
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG and JPEG file types, both lossless and lossy compression mode'
arch=('any')
url='https://github.com/Huluti/Curtail'
license=('GPL3')
depends=('gtk3' 'jpegoptim' 'optipng' 'pngquant' 'python-gobject')
makedepends=('appstream-glib' 'meson')
source=("https://github.com/Huluti/Curtail/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('1f560020506aa12b09b31c0b1bca60c58bd9768e79108afbdf9e62e724ca49f2')

build() {
  arch-meson Curtail-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
