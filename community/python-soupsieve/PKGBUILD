# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-soupsieve
pkgver=2.2.1
pkgrel=4
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-html5lib' 'python-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c1339cfca237af7f8915b6d644b8271f769f9a37aa68f8acf536236551baaf9bb44a2f2a736f9168a7649cde14d37b8c24614c9491acbf81e3f4fdaea96b4928')

build() {
  cd soupsieve-$pkgver
  python setup.py build
}

check() {
  cd soupsieve-$pkgver
  # https://gitlab.gnome.org/GNOME/libxml2/-/issues/312
  pytest \
    --deselect tests/test_extra/test_soup_contains.py::TestSoupContains::test_contains_cdata_html \
    --deselect tests/test_extra/test_soup_contains_own.py::TestSoupContainsOwn::test_contains_own_cdata_html
}

package() {
  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
