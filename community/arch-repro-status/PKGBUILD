# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=arch-repro-status
pkgver=1.2.3
pkgrel=1
pkgdesc="Check the reproducibility status of your Arch Linux packages"
arch=('loongarch64' 'x86_64')
url="https://gitlab.archlinux.org/archlinux/arch-repro-status"
license=('MIT')
depends=('pacman')
makedepends=('rust')
groups=('archlinux-tools')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('55a293ad13ed847a54420d032d76d1edfb1446b7ae1edb11a44d4326c3a8592f32681b0acbe98a397fc6ba5bda4867411bd839c4b935dbac3a3c8354048dce9d')

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}
