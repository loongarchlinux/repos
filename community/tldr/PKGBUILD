# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=1.2.1
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-argcomplete' 'python-colorama' 'python-termcolor')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/tldr-pages/tldr-python-client/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('12a9cd1d69da1c50b5e4d6353dd1ca8d7b60dd570d73e8d6b1c83a0e2d59a621684033f2f2a13bf5db5f38e5f6f23935dbcf7836fbfbcb0718509285a7edd5bb')

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python -m pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
