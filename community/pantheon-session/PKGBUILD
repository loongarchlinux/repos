# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Helder Bertoldo <helder.bertoldo@gmail.com>

pkgname=pantheon-session
pkgver=6.0.0
pkgrel=3
pkgdesc='Session settings for Pantheon'
arch=(any)
url=https://github.com/elementary/session-settings
license=(GPL3)
groups=(pantheon)
depends=(
  cerbere
  dconf
  gnome-keyring
  gnome-session
  gnome-settings-daemon
  pantheon-applications-menu
  libgala.so
  libwingpanel-2.0.so
  onboard
  orca
  plank
  xdg-user-dirs-gtk
)
makedepends=(
  git
  meson
)
optdepends=(pantheon-default-settings)
_tag=48b482e4ad967522391a01391a1bb6ec66aff90d
source=(pantheon-session-settings::git+https://github.com/elementary/session-settings.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-session-settings
  git describe --tags
}

build() {
  arch-meson pantheon-session-settings build \
    -Dfallback-session=gnome
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
