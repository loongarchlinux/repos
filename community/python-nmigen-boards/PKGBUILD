# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nmigen-boards
pkgname=python-$_pkgname
_commit=9d24c51197fdc176dcaeaa0a2156b1cdc75bf1ac
pkgver=r186.9d24c51
pkgrel=1
pkgdesc='Board and connector definition files for nMigen'
arch=('any')
url='https://github.com/nmigen/nmigen-boards'
license=('BSD')
depends=('python-nmigen')
makedepends=('git' 'python-setuptools-scm' 'python-wheel')
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
