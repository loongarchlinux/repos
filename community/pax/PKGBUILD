# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=pax
pkgver=20200904
pkgrel=1
pkgdesc='Portable Archive Interchange - the POSIX standard archive tool for cpio and tar formats'
arch=('x86_64')
url='https://www.mirbsd.org/pax.htm'
license=('BSD')
depends=('glibc')
source=("https://www.mirbsd.org/MirOS/dist/mir/cpio/paxmirabilis-${pkgver}.cpio.gz")
noextract=("paxmirabilis-${pkgver}.cpio.gz")
sha256sums=('c893daefdaaeb564dcce62dede1739d9960a67ceae4271964753271a61c0a531')

prepare() {
    mkdir -p "${pkgname}-${pkgver}/build"
    bsdtar -x -f "paxmirabilis-${pkgver}.cpio.gz" -C "$pkgname-${pkgver}" --strip-components='1'
}

build() {
    cd "${pkgname}-${pkgver}/build"
    sh ../Build.sh -r -tpax
    
    # license
    sed -n '5,36p' ../pax.h > LICENSE # create file
    sed -i '1,32s/^.\{,3\}//' LICENSE # erase C comments
}

package(){
    # executables
    install -D -m755 "${pkgname}-${pkgver}/build/pax" -t "${pkgdir}/usr/bin"
    ln -s pax "${pkgdir}/usr/bin/paxcpio"
    ln -s pax "${pkgdir}/usr/bin/paxtar"
    
    # man pages
    install -D -m644 "${pkgname}-${pkgver}/build/mans/"*.1 -t "${pkgdir}/usr/share/man/man1"
    
    # license
    install -D -m644 "${pkgname}-${pkgver}/build/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
