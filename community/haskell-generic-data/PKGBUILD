# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-data
pkgname=haskell-generic-data
pkgver=0.9.2.1
pkgrel=88
pkgdesc="Deriving instances with GHC.Generics and related utilities"
url="https://github.com/Lysxia/generic-data"
license=('MIT')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-ap-normalize' 'haskell-base-orphans' 'haskell-contravariant'
         'haskell-show-combinators')
makedepends=('ghc' 'haskell-generic-lens' 'haskell-glob' 'haskell-inspection-testing'
             'haskell-one-liner' 'haskell-show-combinators' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-unordered-containers')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('98324ad85fdcdddceb6de74ddfbbc61e314243fe4632d893b744bec012f341c22e2813d4c36dd567361ae49e5f275f93cce926e91b97e37f9df5d285bc8e267f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
