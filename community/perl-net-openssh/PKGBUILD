# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-net-openssh
pkgver=0.82
pkgrel=1
pkgdesc='Perl SSH client package implemented on top of OpenSSH'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Net-OpenSSH
source=("https://cpan.metacpan.org/authors/id/S/SA/SALVA/Net-OpenSSH-$pkgver.tar.gz")
md5sums=(0b6a71ddfd2b0e356dc2c3e606ac0f0a)
sha512sums=(ff13519ba04ebb4ecc3185cc1c32ca6b46a8cc55108f35aeb424738676a41cd7a898443f91d64fe4877e2ab6b4ed30fa15dd3da02aa11a4892ff2c9944035314)
_ddir="Net-OpenSSH-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
