# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

pkgname=uranium
pkgver=4.13.1
pkgrel=1
pkgdesc="A Python framework for building Desktop applications."
url="https://github.com/Ultimaker/Uranium"
arch=('any')
license=('LGPL')
depends=('python' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'python-pyqt5' 'python-numpy'
         'arcus' 'python-shapely' 'python-scipy' 'python-cryptography' 'python-certifi')
makedepends=('cmake')
checkdepends=('mypy' 'python-pytest' 'python-twisted')
options=('debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('185db9e6353a4f44b69287ed5f6314d28f10a0dd43790485275d6db5b5907a41')

prepare() {
  cd Uranium-${pkgver}
  sed -i 's,/dist-packages,.${PYTHON_VERSION_MINOR}/site-packages,g' CMakeLists.txt
}

build() {
  cd Uranium-${pkgver}
  mkdir -p build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_TESTS=ON

  make
}

check() {
  cd Uranium-${pkgver}/build
  # few tests fail atm
  #make CTEST_OUTPUT_ON_FAILURE=TRUE test
}

package() {
  cd Uranium-${pkgver}/build
  make DESTDIR="${pkgdir}" install

# Move cmake module to right dir
  mv "$pkgdir"/usr/share/cmake{-*,}
}

# vim:set ts=2 sw=2 et:
