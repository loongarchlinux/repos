# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=cue
pkgver=0.4.3
pkgrel=1
pkgdesc="Validate and define text-based and dynamic configuration"
arch=('loong64' 'x86_64')
url="https://github.com/cue-lang/cue"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cue-lang/cue/archive/v${pkgver}.tar.gz")
sha512sums=('437330d8a5c1f2186e1655a4718ad86626ea3b3955c447bb101f89d391bffb154b32992b9f505ee39415b6e03453e294d4f6fed8abada5aaa68866e1c9fbcef3')

build() {
  cd "${pkgname}-${pkgver}/cmd/cue/"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-s -w -linkmode=external " -v .
  chmod +x cue
}

# check() {
#   cd "${pkgname}-${pkgver}"
#   go test -v ./...
# }

package() {
  cd "${pkgname}-${pkgver}/cmd/cue/"
  install -Dsm755 ./cue "${pkgdir}/usr/bin/cue"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./cue completion bash  > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./cue completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
