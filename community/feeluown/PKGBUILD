# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=3.5.3
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/cosven/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests')
makedepends=('python-setuptools' 'python-pip')
optdepends=('feeluown-local' 'feeluown-netease' 'feeluown-kuwo' 'feeluown-xiami' 'feeluown-qqmusic')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop")
sha256sums=('b06d3ee831e3accef9a52fa3e9ac100b6c63fe76caa58f5f3de53356823757f7'
            'f093cccd74e29115782b30fcda28fb0c3b935091673b50882b332c934ed56065')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -D -m644 "$srcdir/$pkgname-$pkgver/feeluown/feeluown.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/feeluown.png"
  install -D -m644 "$srcdir/feeluown.desktop" "$pkgdir/usr/share/applications/FeelUOwn.desktop"
}
