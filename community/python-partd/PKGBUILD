# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=partd
pkgname=python-${_pkg}
pkgver=1.2.0
pkgrel=3
pkgdesc="Concurrent appendable key-value storage"
arch=(any)
url="https://github.com/dask/partd"
license=(BSD)
makedepends=(python-setuptools)
depends=(
    python
    python-locket
    python-toolz
)
optdepends=(
    python-blosc
    python-numpy
    python-pandas
    python-pyzmq
)
checkdepends=(
    python-pytest
    python-blosc
    python-numpy
    python-pandas
    python-pyzmq
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('aa67897b84d522dcbc86a98b942afab8c6aa2f7f677d904a616b74ef5ddbc3eb')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
