# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.5.3
pkgrel=1
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rdoc)
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=(ruby-minitest ruby-minitest-focus ruby-minitest-reporters)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('641bb89a14e5bc65dcb078a93b5669266e2ffe10395416473c243b840c425de200bd9336817c44e7affe570fb0a41811cfba54a4c04d9fb6f56ceca96b504ac4')
b2sums=('a91c76530df7703c08aa58babcfde5520fc9ca00f804e2dd217b19664256986dadd3ffa19879d3a3f6402ae186faa08f14e5b080f415345b189821b578b76c1b')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
