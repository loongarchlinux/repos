# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.6.0
pkgrel=1
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rdoc)
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=(ruby-minitest ruby-minitest-focus ruby-minitest-reporters)
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::https://github.com/fxn/$_name/archive/v$pkgver.tar.gz)
sha512sums=('750aade9b995b3e889d15bd4d6bd6bf267999ee1fd4b44bd9a32db083f19e86d0ce12f673f84f2b79629e1fee409f587fb2829e76ac23a23898dbb18cbb58b0b')
b2sums=('5549624fdf793fa37bd8c45679246e0297ba96d95175a63f0cb7093fae85804d1eac9506ad539a256e9b3237816ec0ef29ecd4461bdfbef52bfbd1c6964d2f8d')

prepare() {
  cd $_name-$pkgver
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Rakefile
  # we can't rely on Gemfiles
  rm -v Gemfile

  sed '/minitest\/focus/d' -i test/test_helper.rb
  sed '/minitest\/reporters/d' -i test/test_helper.rb
}

build() {
  cd $_name-$pkgver
  gem build $_name.gemspec
}

# check() {
#   cd $_name-$pkgver
#   rake test
# }

package() {
  local _gemdir="$(gem env gemdir)"

  cd $_name-$pkgver
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_name-$pkgver.gem
  install -vDm 644 MIT-LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # remove all unneeded stuff
  cd "$pkgdir/$_gemdir"
  rm -rvf cache gems/$_name-$pkgver/{ext,lib/*/*.so} extensions/*/*/$_name-$pkgver/{mkmf.log,gem_make.out}
}
