# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.5.4
pkgrel=1
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rdoc)
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=(ruby-minitest ruby-minitest-focus ruby-minitest-reporters)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('f7a4b88c030b2eeae5ba07c6e01900fa66312a58e5c9da3d13a293d0959d67bbcfb385de5790e1764188752dfc9de161c202b35e0e35ed6e0cca7bbc2c2f6c83')
b2sums=('4c1302bdb6ac30afbb23ea06ef2ac2ecea9c8139c36f1b766ec6f62bb6a77faf1efa3f670cb31b5073a6dc36d6a0bd8d66a937d8b84b12438411a7f2df29eeed')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
