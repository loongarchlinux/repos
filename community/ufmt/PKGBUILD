# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>

pkgname=ufmt
pkgver=2.0.0
pkgrel=1
pkgdesc='Safe, atomic formatting with black and µsort'
arch=('any')
url='https://github.com/omnilib/ufmt'
license=('MIT')
depends=('python-black' 'python-click' 'python-libcst' 'python-moreorless'
         'python-tomlkit' 'python-trailrunner' 'python-typing_extensions' 'usort')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e8893619cf54be875f12d1b819db351862c668fc8ff68115d81ca4f31c8d4b9f970920e1ab30288f93e6234a1723a77f6cfa47987e87d8f8b744fffefb543867')

build() {
  cd $pkgname-$pkgver

  python -m build -nwx
}

check() {
  cd $pkgname-$pkgver

  python -m ufmt.tests
}

package() {
  cd $pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
