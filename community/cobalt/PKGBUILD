# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.16.3
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'libsass' 'glibc' 'gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('b7957b8365324dd18d29f7d4ff30231811a9746508ce90f067045ec3184c26b54aadfcecd25033bf10048eae48e60b8f63ef881372d59a19f4ccba7f9d7c8eb9')
b2sums=('ee9e70fa452e03f282103f170619ac2cb556c4f8bc44687d466820c11f1515d9e62e3da680e7ba392ddce5003f53bd5de4ff897dd7db13c277c9802e85bd3b5c')

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --release --locked --features 'syntax-highlight sass'
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
