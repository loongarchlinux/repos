# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=pugixml
pkgver=1.10
pkgrel=2
pkgdesc="Light-weight, simple and fast XML parser for C++ with XPath support"
arch=(x86_64)
url="https://pugixml.org"
license=(MIT)
depends=(gcc-libs)
makedepends=(cmake)
source=("https://github.com/zeux/pugixml/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('55f399fbb470942410d348584dc953bcaec926415d3462f471ef350f29b5870a')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON
    make
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${pkgname}-${pkgver}/readme.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
