# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-libevdev
pkgver=0.10
pkgrel=1
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/python-libevdev'
license=('MIT')
pkgdesc='Python wrapper for libevdev'
depends=('python' 'libevdev')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest' 'python-py' 'python-pluggy')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7cc5517b65f9e7749f5946bd4c3fc550ecabfb02de266e9b29329e9685e6a25e945513e2b6b58b68898e9e4ce8550cd95d537f6c40e35b28abafc13693b6bc64')

build() {
  cd $pkgname-$pkgver

  python setup.py build

  make doc
}

check() {
  cd $pkgname-$pkgver

  PYTHONPATH=. pytest test/*.py
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname
  install -D -m644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees
}
