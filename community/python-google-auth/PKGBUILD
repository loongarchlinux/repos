# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.35.0
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl' 'python-pyu2f')
source=("https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-cachetools-5.patch::https://github.com/googleapis/google-auth-library-python/commit/1eae37db7f6fceb32d6ef0041962ce1755d2116c.patch)
sha512sums=('6f849f390187411ab108e60c25e1ba33dd02520e9fbe6327e31eb78f1bc3c2ab262cb0ae6558ce07c21a6e0b11f695afc3e7bb9161df526f6c6f237c447e921e'
            'f51c25c7168ebc89eef985ab3f8898844c7c51128075d7d35a6cb8fecaedd1142316921a9473531cfe2b02d3a3845e2d96812367b6eb51c166d709a7f2bfe6ae')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         -e 's|import certifi|pass|' \
         google-auth-library-python-$pkgver/google/auth/transport/{urllib3,requests}.py
  patch -d google-auth-library-python-$pkgver -p1 < $pkgname-cachetools-5.patch
  sed -i 's|utils.int_from_bytes|int.from_bytes|' google-auth-library-python-$pkgver/google/auth/crypt/es256.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$pkgver
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
