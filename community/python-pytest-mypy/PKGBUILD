# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mypy
pkgver=0.8.1
pkgrel=3
pkgdesc='Mypy static type checker plugin for Pytest'
arch=('any')
license=('MIT')
url='https://github.com/dbader/pytest-mypy'
depends=('mypy' 'python-filelock' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-xdist' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dbader/pytest-mypy/archive/v$pkgver.tar.gz"
        $pkgname-mypy-0.9x.patch::https://github.com/dbader/pytest-mypy/commit/4b0a8bf541bd.patch)
sha512sums=('69f6ddf605bfcd075d6780f516f2f9821675762f8fee6a3d042f450f3544d8565ea0a3a905b5dd5d560278bffc28773faba9a09554ac9758ffcc24d13e3f316e'
            'b1afc1fe631b636692998f019d803ec751cf5476e9feeb1263ac177022bfe3fafec8423d67ba52288d2ed687fdf1159911a3d04f0556f1f919afdf6e2a262d27')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd pytest-mypy-$pkgver
  patch -Np1 -i ../$pkgname-mypy-0.9x.patch
}

build() {
  cd pytest-mypy-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-mypy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-mypy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
