# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hint
pkgname=haskell-hint
pkgver=0.9.0.5
pkgrel=40
pkgdesc="Runtime Haskell interpreter (GHC API wrapper)"
url="https://github.com/haskell-hint/hint"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-paths' 'haskell-random'
         'haskell-temporary')
makedepends=('ghc' 'haskell-extensible-exceptions' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ce7a5bfd2cb80a5c666608861a514fabb0a3e2678e80c4266672cb9c1e487300976613020005d2c64ee9706301c91fe7c4afa38a8257aa64991515e897b77b27')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
