# Maintainer: David Runge <dvzrv@archlinux.org>

_name=hsluv
pkgname=python-hsluv
pkgver=5.0.2
pkgrel=2
pkgdesc="A Python implementation of HSLuv (revision 4)"
arch=('any')
url="https://github.com/hsluv/hsluv-python"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0ad9ccb09412ae27f8ba6a160570b517939615eed19fe50f45a8def8ce99943752703c6c3ad2804d8abbafb680488070b48df50b275ebda742edd71f63ca457e')
b2sums=('287774633764fdd8f030a1f447e71ba5e6f99b30219905a8dd2d7632be7e99331244f765da1ded081f6c7250e94e8aa169a30160248221dee18a018118a2955b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
