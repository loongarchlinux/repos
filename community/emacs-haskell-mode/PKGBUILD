# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gleidson <gleidson.echeli@gmail.com>

pkgname=emacs-haskell-mode
pkgver=13.20
pkgrel=2
pkgdesc="Haskell mode package for Emacs"
arch=(any)
license=('GPL')
url="https://github.com/haskell/haskell-mode"
install=emacs-haskell-mode.install
makedepends=('emacs')
source=($pkgname-$pkgver.tar.gz::https://github.com/haskell/haskell-mode/archive/v$pkgver.tar.gz)
md5sums=('28dc2e6fb1dad0f488ceec80896f0088')

prepare() {
  cd "$srcdir"/haskell-mode-$pkgver
  sed -i 's|haskell-mode-pkg.el,|haskell-mode-pkg.el haskell-mode.el,|' Makefile
}

build() {
  cd "$srcdir"/haskell-mode-$pkgver
  export EMACS=/usr/bin/emacs
  make
}

package() {
  cd "$srcdir"/haskell-mode-$pkgver

  install -dm0755 "$pkgdir"/usr/share/emacs/site-lisp/haskell-mode
  install -m0644 *.el -t "$pkgdir"/usr/share/emacs/site-lisp/haskell-mode
  cp -a build-$(emacs --version | head -n1 | cut -f3 -d' ') -t "$pkgdir"/usr/share/emacs/site-lisp/haskell-mode/

  install -dm0755 "$pkgdir"/usr/share/doc/$pkgname
  install -m0644 NEWS README.md "$pkgdir"/usr/share/doc/$pkgname

  install -Dm0644 haskell-mode.info "$pkgdir"/usr/share/info/haskell-mode.info
}
