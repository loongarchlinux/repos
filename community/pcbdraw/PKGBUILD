# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=PcbDraw
pkgname=pcbdraw
pkgver=0.6.0
pkgrel=1
pkgdesc='Convert your KiCAD board into a nice looking 2D drawing suitable for pinout diagrams'
arch=('any')
url='https://github.com/yaqwsx/PcbDraw'
license=('MIT')
depends=('python' 'kicad' 'python-numpy' 'python-lxml' 'python-mistune' 'python-pybars3' 'python-wand' 'python-yaml')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4671ad9ed47e0119e2abbefed3b2b26f9765524da19e1866863c17124f0c809c7f079435565df1c0fac54bcdc4013dd4bd7d3578ff6f251882298ba1e176909f')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
