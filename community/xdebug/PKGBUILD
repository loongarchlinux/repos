# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.9.6
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('loongarch64' 'x86_64')
url="https://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("https://xdebug.org/files/$pkgname-${pkgver/rc/RC}.tgz"
	'xdebug.ini')
sha256sums=('ab03b6014706491b393aa8d520b5bdaf6735a2f1bc12a7772b2916ef2646e454'
            '7c66883dc2ade69069ef84e30188b25630748aa9c8b0dd123727c00505421205')

build() {
  cd "$srcdir"/$pkgname-${pkgver/rc/RC}
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd "$srcdir"/$pkgname-${pkgver/rc/RC}/debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-${pkgver/rc/RC}/debugclient
  install -D -m 755 debugclient "$pkgdir"/usr/bin/debugclient

  cd "$srcdir"/$pkgname-${pkgver/rc/RC}
  make INSTALL_ROOT="$pkgdir" install
  install -D -m 644 "$srcdir"/xdebug.ini "$pkgdir"/etc/php/conf.d/xdebug.ini
}
