# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sybil
pkgver=2.0.1
pkgrel=3
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz"
        'fix-tests-for-python-3-10.1.patch::https://github.com/simplistix/sybil/commit/3650f5e609a6da7e114224a578e4182e262cdec5.patch'
        'fix-tests-for-python-3-10.2.patch::https://github.com/simplistix/sybil/commit/4c12bfc0340a77cbe6496ebcef2fc777006a9c0e.patch')
sha512sums=('77892932f947ace293df1732a01c3272414ce22edefafc1ee09b28214b34278d5030edea1130aa0ffd7acce569cf0f877dbe21c21f32e4333ac8d25c5ec0a058'
            '84a8c0515dcc2b440e7785a05c6a4a2cb3a756a0cb4573b278e42cca711af4d9519102ad3df86ff93335e4d521db1e4be3b74d4c5b95e57d8be0f832586c3595'
            'a0c7efdcf72eb54660599f3ef76346f2c3582c37f71e0ea27fb24f08dd223e4cff601a104e7690f08312ea8576664be2f09f9715641df22df7d5550028276096')

prepare() {
  cd sybil-$pkgver
  patch -p1 <"$srcdir"/fix-tests-for-python-3-10.1.patch
  patch -p1 <"$srcdir"/fix-tests-for-python-3-10.2.patch
}

build() {
  cd sybil-$pkgver
  python setup.py build
}

check() {
  cd sybil-$pkgver
  python -m pytest
}

package() {
  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
