# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

pkgname=python-pyquery
pkgver=1.4.1
pkgrel=3
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('BSD')
depends=('python-lxml' 'python-cssselect')
optdepends=('python-webob: query wsgi app')
makedepends=('python-setuptools')
checkdepends=('python-requests' 'python-webob' 'python-nose' 'python-webtest')
source=("https://github.com/gawel/pyquery/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('25fe43155ecfcafb34863cee0a4690f51f23690b866a9e985f605f9995093e6698dbe867e6a98978f6f389735666fe17328d60c59904456ee1b35ff450eae463')

build() {
  cd pyquery-$pkgver
  python setup.py build
}

check() {
  cd pyquery-$pkgver
  nosetests3
}

package() {
  cd pyquery-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
