# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-http
pkgver=0.59.2
pkgrel=1
pkgdesc='A HTTP client and server library'
arch=(any)
url='https://github.com/socketry/async-http'
license=(MIT)
depends=(ruby-async ruby-async-io ruby-async-pool ruby-protocol-http ruby-protocol-http1
         ruby-protocol-http2 ruby-traces)
makedepends=(ruby-async-container ruby-async-rspec ruby-localhost ruby-rack-test ruby-rspec)
checkdepends=(ruby-bake ruby-bake-test ruby-thread-local)
options=(!emptydirs)
source=(https://github.com/socketry/async-http/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('096069f92e50ada06ee7af43153543f0379bb3abd8b3bb334c8d9ac1f41ef5e1')

prepare() {
  cd async-http-$pkgver
  sed -r -e 's|~>|>=|g' -e '/covered/d' -e '/signing_key/d' -i async-http.gemspec spec/spec_helper.rb
  sed -i '/bake-modernize/d;/bake-gem/d;/bake-github-pages/d;/utopia-project/d;/bake-test-external/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-$pkgver
  gem build async-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-http-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
