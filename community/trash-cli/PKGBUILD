# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Renato Garcia <fgarcia.renato@gmail.com>

pkgname=trash-cli
pkgver=0.21.10.24
pkgrel=2
pkgdesc="Command line trashcan (recycle bin) interface"
arch=('any')
url="https://github.com/andreafrancia/trash-cli"
license=('GPL')
depends=('python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('37601b714b9a42b217765c6eb7ae20b36796b92c642feea6bd275e1c702799ed')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # don't depend on thirdparty copies of the stdlib
    find tests -type f -name "*.py" -exec \
        sed -i 's/^import mock$/from unittest import mock/;s/from mock /from unittest.mock /' {} +
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # test_help failing on python 3.10
    python -m pytest -k 'not test_help'
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
