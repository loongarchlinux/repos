# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ptex
pkgver=2.4.0
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=('loongarch64' 'x86_64')
url="http://ptex.us/"
license=('BSD')
depends=('zlib' 'gcc-libs')
makedepends=('git' 'doxygen' 'cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('02879a7d8c57206d23ba6cacc460e681a4fc03e8939a42ac47e67dcfe0c2fbba247453ea1b6d6a4b8cef389a0830906b018afff6ec8691d01b28ac23fc748c5b')

build() {
	cd "$pkgname-$pkgver"

	cmake \
        -Bbuild \
        -GNinja \
        -DPTEX_SHA=$_commit_sha \
        -DPTEX_VER=$pkgver \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    ninja -C build
}

check() {
	cd "$pkgname-$pkgver"
    ninja -C build test
}

package() {
	cd "$pkgname-$pkgver"

    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
