# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.envs
pkgver=2.0.0
pkgrel=2
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=('any')
depends=('python-jaraco' 'python-path' 'python-virtualenv' 'python-tox')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a834f72ae5b3e0633f1d794a28914e653807ba27a9184724a651ed8ee7d8d0541fef6d5888a47ac53f386dcd8b701d305ae68ddcf56bec5992e49272a641ea38')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jaraco.envs-$pkgver
  sed -i '/tox-venv/d' setup.cfg
}

build() {
  cd jaraco.envs-$pkgver
  python setup.py build
}

check() {
  cd jaraco.envs-$pkgver
  python -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  rm "$pkgdir"/usr/lib/python3.8/site-packages/jaraco/__init__.py \
     "$pkgdir"/usr/lib/python3.8/site-packages/jaraco/__pycache__/__init__.cpython-38.pyc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
