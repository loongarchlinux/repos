# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-cssselect2
pkgver=0.6.0
pkgrel=1
pkgdesc="CSS selectors for Python ElementTree"
url="https://pypi.org/project/cssselect2/"
license=('BSD')
arch=('any')
depends=('python-tinycss2')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest-cov' 'python-pytest-isort' 'python-pytest-runner')
source=("https://pypi.io/packages/source/c/cssselect2/cssselect2-$pkgver.tar.gz")
sha512sums=('10cb55ebaaa16b755d85f1e511ae0281fb017d6937750392ae1da7f32d51ff944fb14c6d1c9ea5f42734450b719157ded116dc14ef471a9a20fcc43fb6682308')

prepare() {
  cd cssselect2-$pkgver
  sed -i 's/--flake8//' pyproject.toml
}

build() {
  cd cssselect2-$pkgver
  python -m build -nw
}

check() {
  cd cssselect2-$pkgver
  pytest
}

package() {
  cd cssselect2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
