# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-cssselect2
pkgver=0.3.0
pkgrel=1
pkgdesc="CSS selectors for Python ElementTree"
url="https://pypi.org/project/cssselect2/"
license=('BSD')
arch=('any')
depends=('python-tinycss2')
makedepends=('python-setuptools')
checkdepends=('python-pytest-flake8' 'python-pytest-cov' 'python-pytest-isort'
              'python-pytest-runner')
source=("https://pypi.io/packages/source/c/cssselect2/cssselect2-$pkgver.tar.gz")
sha512sums=('ae59314654d1c1ddf982b77521de7743d1496145a1367f51e02b6067d6757940e7ccb50b67959630f472972473a1e87cec8dfbc8eeb68ad5f16465a36b1447be')

build() {
  cd cssselect2-$pkgver
  python setup.py build
}

check() {
  cd cssselect2-$pkgver
  python setup.py pytest
}

package() {
  cd cssselect2-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
