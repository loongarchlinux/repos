# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.5
_commit=10328724474f4d377c876bd880da91766d9189ab
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://github.com/Zac-HD/hypothesmith/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('0a7ce47176c50de70a5a5eb6ac0a1da751f14c85b742e4808e25983b46535ce8467a4cd8c77c9f0a1b4b91db4eb9bd0d47b88a00002a81a900811127d6624710')

build() {
  cd hypothesmith-$_commit
  python setup.py build
}

check() {
  cd hypothesmith-$_commit
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}
