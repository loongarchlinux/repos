# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_pythons
pkgname=python-dephell-pythons
pkgver=0.1.14
pkgrel=1
pkgdesc="Work with python versions"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-dephell-specifier' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('a2da1ba905084f0935392be6e1fee019b98f45439429e45278a181174cff93a9')
b2sums=('e705f239cea702690607409e45ab766c4766ea13c3c3b2854b3ccddb33a40f9cacf70a744dc8a2339f86070045eba12f9b4e3292eb39d4ad893550d841262151')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
