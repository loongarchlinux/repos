# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=eva
pkgver=0.3.0
pkgrel=1
pkgdesc='simple calculator REPL, similar to bc(1)'
arch=(loongarch64 x86_64)
url="https://github.com/NerdyPepper/$pkgname"
license=(GPL3)
depends=(gcc-libs)
makedepends=(cargo)
options=(debug)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('05e2cdcfd91e6abef91cb01ad3074583b8289f6e74054e070bfbf6a4e684865e')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
