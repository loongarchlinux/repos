# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=psalm
pkgver=4.30.0
pkgrel=1
_commit=d0bc6e25d89f649e4f36a534f330f8bb4643dd69
pkgdesc='A static analysis tool for finding errors in PHP applications'
url="https://psalm.dev/"
arch=('any')
license=('MIT')
depends=('php')
makedepends=('composer' 'php7' 'git')
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vimeo/psalm/archive/refs/tags/${pkgver}.tar.gz"
source=("git+https://github.com/vimeo/psalm.git#commit=${_commit}"
        "composer.lock")
b2sums=('SKIP'
        '0ddef661e88f22fe4b1b4882c7cfd5877a232744655b486ca6b682bb93f0ba3e8d06d2dfb155c6146b8c411c04ce857862bcd08de3d2a64c94e61c93bc1b36f3')

prepare() {
  cd ${pkgname}
  cp ../composer.lock .
  composer install
  php7 /usr/bin/composer bin box install
  php bin/improve_class_alias.php
}

updlockfiles() {
  cd ${pkgname}
  rm -f composer.lock
  composer update
  cp composer.lock "${outdir}/"
}

build() {
  cd ${pkgname}
  vendor/bin/box compile
}

package() {
  cd ${pkgname}
  install -Dm 755 ./build/psalm.phar "${pkgdir}/usr/bin/psalm"
}

# vim: ts=2 sw=2 et:
