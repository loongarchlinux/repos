# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wails
pkgver=1.16.9
pkgrel=1
pkgdesc="Create desktop apps using Go and Web Technologies"
arch=(loongarch64 x86_64)
url="https://github.com/wailsapp/wails"
license=(MIT)
depends=(glibc)
makedepends=(go webkit2gtk gtk3)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wailsapp/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('66a29a8a1c45004dd5f950c8a2d00ad18280051f4e65a433d44fdf35edce332973da3572f1208d1cc82fc0d893e9f1a16546f6a405aad5fb581ef090cc33c1ea')
b2sums=('0c2d3df2f760811b3a43e14a4a4de068856d8f9d734f4e66242ae7fe629e745aaeef00778bd6e8c4feb13a37184563fcebec46d6ff406b147f510c4e184cd435')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/ ./cmd/wails
}

check() {
  cd "$pkgname-$pkgver"
  go test
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "build/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
