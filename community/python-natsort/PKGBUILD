# Maintainer: David Runge <dvzrv@archlinux.org>

_name=natsort
pkgname=python-natsort
pkgver=8.0.2
pkgrel=1
pkgdesc="Simple yet flexible natural sorting in Python"
arch=(any)
url="https://github.com/SethMMorton/natsort"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-hypothesis python-mock python-pytest-mock)
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fad31e1af1e1d4a33061b8b3e31d022f353f662d306136d05e0121a0531f33cf388467a40f91f51036f283ab8fbdefc1193d09f440052960ffdf733186b5fb27')
b2sums=('0e5d83e6991ef1208b84cbf17e287f487a2a059d68274876977ae6f18f89492284bc166499a9518bfc2df19999e5593f386ce88beca94c23d8a050697c0b2a36')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
