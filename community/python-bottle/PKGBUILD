# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-bottle
pkgname=("python-bottle" "python2-bottle")
pkgver=0.12.18
pkgrel=2
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="https://bottlepy.org"
license=('MIT')
options=(!emptydirs)
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-mako' 'python-jinja' 'python-cherrypy' 'python-twisted'
              'python-tornado' 'python-paste' 'python-gevent' 'python-eventlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/defnull/bottle/archive/$pkgver.tar.gz")
sha512sums=('90759ecd94dd489a1e66b2ddfadc0a0bbb88bf1b0c666e292f2466a88b9b0195fe3679928683ca58af94b0bbfa4f815269f6497477db51229cc7250b705e73ed')

prepare() {
  cp -a bottle-$pkgver{,-py2}
  sed -i "1s/python/python2/" bottle-$pkgver-py2/bottle.py
}

check() {
  cd "$srcdir"/bottle-$pkgver
  # https://github.com/bottlepy/bottle/issues/791
  python test/testall.py || echo "Tests failed"
}

build() {
  cd "$srcdir"/bottle-$pkgver
  python setup.py build
  cd "$srcdir"/bottle-$pkgver-py2
  python2 setup.py build
}

package_python-bottle() {
  depends=('python')

  cd bottle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-bottle() {
  depends=('python2')

  cd bottle-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/bottle.py{,2}
}

# vim: ts=2 sw=2 et:
