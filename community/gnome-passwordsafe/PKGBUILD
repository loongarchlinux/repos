# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-passwordsafe
pkgver=6.5
pkgrel=2
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://gitlab.gnome.org/World/PasswordSafe"
license=(GPL3)
depends=(gtk4 libadwaita libpwquality python-gobject python-pykeepass python-pyotp python-cairo)
makedepends=(git gobject-introspection meson)
_commit=2103661272e0f9b6b63db7103b08c818478debde
source=("git+https://gitlab.gnome.org/World/PasswordSafe.git/#commit=$_commit"
         pykeepass-4.0.3.patch)
sha256sums=('SKIP'
            '93baa0b0435f6c527b834ebf389ded9bae3033f7061c3f28faef8dd6582be124')

pkgver() {
  cd PasswordSafe
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd PasswordSafe
  patch -p1 -i ../pykeepass-4.0.3.patch # Port to pykeepass 4.0.3
}

build() {
  arch-meson PasswordSafe build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
