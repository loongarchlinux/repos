# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libmaa
pkgver=1.3.2
pkgrel=3
pkgdesc="Provides many low-level data structures which are helpful for writing compilers"
url="http://sourceforge.net/projects/dict/"
license=('GPL')
arch=('x86_64')
depends=('glibc')
makedepends=('flex')
source=("https://downloads.sourceforge.net/dict/${pkgname}-${pkgver}.tar.gz" libmaa-gcc8.patch)
sha256sums=('59a5a01e3a9036bd32160ec535d25b72e579824e391fea7079e9c40b0623b1c5'
            '7435ad846b1b3b6cb2a7473439d1e98986b83b050125c19eb263784eb2e4a2b6')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../libmaa-gcc8.patch # Fix build with GCC 8 (Debian)
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
