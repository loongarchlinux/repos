# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cerberus
pkgver=1.3.4
pkgrel=4
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=('ISC')
arch=('any')
depends=('python-setuptools')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-pytest-benchmark')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyeve/cerberus/archive/$pkgver.tar.gz")
sha512sums=('c816b426ed1b32367d1e8f1c26d6ddd4b49c6578540e688baf693e3094a05800f645cfe879be6378c1e9be9899cad7bc2ad1ed9844d6762d2ef6e0cc54a7476a')

build() {
  cd cerberus-$pkgver
  python -m build --wheel --no-isolation
  # NOTE: remove build dir as pytest otherwise chokes on duplicate imports
  rm -frv build
}

check() {
  cd cerberus-$pkgver
  pytest -vv
}

package() {
  cd cerberus-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
