# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ormolu
pkgname=haskell-ormolu
pkgver=0.1.3.0
pkgrel=2
pkgdesc="A formatter for Haskell source code"
url="https://github.com/tweag/ormolu"
license=("BSD")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-ghc' 'haskell-ghc-boot' 'haskell-ghc-boot-th'
         'haskell-gitrev' 'haskell-optparse-applicative' 'haskell-syb')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-path' 'haskell-path-io')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('47a2b0a5759e8d7199cd64fc66dd88b5db2d61d9dbae475582d91f1e668e488f')

prepare() {
  cd $_hkgname-$pkgver
  sed -i -e 's/ghc-lib-parser/ghc, ghc-boot, ghc-boot-th/' $_hkgname.cabal
  sed -i 's/using ghc-lib-parser/using ghc/;s/VERSION_ghc_lib_parser/VERSION_ghc/' app/Main.hs
  uusi $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
