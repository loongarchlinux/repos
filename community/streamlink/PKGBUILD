# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=4.1.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client})
checkdepends=("python-"{freezegun,pytest,requests-mock})
makedepends=("python-"{build,installer,setuptools,versioningit,wheel})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('0b492cf48cf1c8f12adc112e42df504857088363f036f400bc9330f80ff00893'
            'SKIP')
sha512sums=('f163766bffe079c10cb9f33792ca1e8fa806f46fac64716bada5221f97b6b7b6786f1a218a9ba0ba6ae4eb1b9e5bfc8bf2de1a6b2e5bf9d4c9ba8b11a6a99b9e'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  TZ=UTC PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m pytest
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
