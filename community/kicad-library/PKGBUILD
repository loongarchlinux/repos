# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=6.0.0
pkgrel=2
pkgdesc='KiCAD component and footprint libraries'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('cmake' 'make')
source=("https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/$pkgver/kicad-symbols-$pkgver.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-footprints-$pkgver.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/$pkgver/kicad-packages3D-$pkgver.tar.bz2")
sha512sums=('5891606d47004cc2a530e1d327ae6bd7a89c571bb60dbc4e2f8c6659277de361b6ea337939414028c834392a2f69668a3d3c99962765cc76048b8e4dd153cca3'
            'd8eac8fcc380ef67ccbd3b8f2529d7023aecef674299d83aabccd41beef1934507edf3a9bdbf4b440ae550470d7f16382aba4090fe5f1d5397a8e0a0f4364ec9'
            '48f6698d8b48483e484638f780cf5a9522cda5381c3858b15b0862994dfacc271426ecbaaa5168a2fc8f6be84ac6e9b160e080ac74183d5be282de36088416af')

build()
{
  mkdir kicad-{symbols,footprints,packages3D}-$pkgver/build

  cd "$srcdir"/kicad-symbols-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr

  make

  cd "$srcdir"/kicad-footprints-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr

  make

  cd "$srcdir"/kicad-packages3D-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package_kicad-library()
{
  pkgdesc="Kicad component and footprint libraries"

  cd "$srcdir"/kicad-symbols-$pkgver/build

  make DESTDIR="$pkgdir" install

  cd "$srcdir"/kicad-footprints-$pkgver/build

  make DESTDIR="$pkgdir" install
}

package_kicad-library-3d()
{
  pkgdesc="Kicad 3D render model libraries"

  cd kicad-packages3D-$pkgver/build

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
