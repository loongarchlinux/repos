# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=6.0.7
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  'git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#commit=482ac8a61e3aae333d783da68ce057eb63e35cd4'
  'git+https://gitlab.com/kicad/libraries/kicad-symbols.git#commit=df1ddda40d0a9f859da6b5185298c73557373e80'
  'git+https://gitlab.com/kicad/libraries/kicad-templates.git#commit=ae2b46f8756d79379b90fec01d4fdde1ccfd73c1'
  'git+https://gitlab.com/kicad/libraries/kicad-footprints.git#commit=cb374ed567a148b8e53b3f89918a2f4ba38dd93f'
)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
