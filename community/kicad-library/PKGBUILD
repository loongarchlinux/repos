# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=kicad-library
pkgname=('kicad-library' 'kicad-library-3d')
pkgver=5.1.10
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="https://kicad.github.io/"
license=('GPL')
makedepends=('cmake')
options=('!strip')
source=("https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/$pkgver/kicad-symbols-$pkgver.tar.gz"
        "https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-footprints-$pkgver.tar.gz"
        "https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/$pkgver/kicad-packages3D-$pkgver.tar.gz")
md5sums=('1d11e15b8e19cde771352801d86262ee'
         '2fc41850a9c1ab0cd4fb9f52f5ffcb9f'
         'e1d02d5de14bc905ee509a7e2dd4eb9f')

build() {
  cd "$srcdir/kicad-symbols-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-footprints-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-packages3D-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_kicad-library() {
  pkgdesc="Kicad component and footprint libraries"
  cd "$srcdir/kicad-symbols-$pkgver/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-footprints-$pkgver/"
  make DESTDIR="$pkgdir" install
}

package_kicad-library-3d() {
  pkgdesc="Kicad 3D render model libraries"
  cd "$srcdir/kicad-packages3D-$pkgver/"
  make DESTDIR="$pkgdir" install
}
