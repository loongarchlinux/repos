# Maintainer: David Runge <dvzrv@archlinux.org>

_name=doit
pkgname=python-doit
pkgver=0.34.0
pkgrel=2
pkgdesc="A task management and automation tool"
arch=(any)
url="https://pydoit.org/"
license=(MIT)
depends=(python-cloudpickle python-pyinotify)
makedepends=(python-setuptools)
checkdepends=(python-doit-py python-pyflakes python-pytest)
optdepends=(
  'python-toml: for TOML support'
  'python-setuptools: for plugin support'
  'bash-completion: autocomplete for bash'
  'zsh-completions: autocomplete for ZSH'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fec2f64d88f89f328bd06158da18016d4563819723d4eb4aa54c4aa9dc6aaf6f1ebae0e923d081867681ccf60c9df3ee0261dbb33a5251294c694b466d9b447f')
b2sums=('14f991197730cc482dd22dc64e8f1823b4991f01a4ee8f4913077e1df7b218a51964b4a4c0bb55ae1a074b3698ea3824a18f210efa9297684b0478c46c5a2d41')

build() {
  cd "$_name-$pkgver"
  python setup.py build
}

check() {
  cd "$_name-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$_name-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "bash_completion_${_name}" "${pkgdir}/usr/share/bash-completion/completions/${_name}"
  install -vDm 644 "zsh_completion_${_name}" "${pkgdir}/usr/share/zsh/site-functions/_${_name}"
}
