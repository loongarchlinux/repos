# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=3.31
pkgrel=2
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama' 'python-decorator'
         'python-pyte')
makedepends=('python-pip')
checkdepends=('python-pytest-mock' 'python-mock' 'python-pexpect' 'go')
source=("https://github.com/nvbn/thefuck/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6ba3546e44311fc5c91513e87eb3de3636369fa5c7aff7b78e24f65f3c7bee5a5ee1405e442558cfb0b2b643d21705ce28943afc3fdac717e038cd980c87966d')

build() {
  cd thefuck-$pkgver
  python setup.py build
}

check() {
  cd thefuck-$pkgver

  # Hack distribution test by installing it and set PYTHONPATH afterwards
  python setup.py install --root="$PWD/tmp_install" --optimize=1

  # Hack $HOME as it's / in the chroot and not writable
  cp tests/test_conf.py "$srcdir/test_conf.py.bak"
  sed -i "s|data = {}|data = {'HOME': '$srcdir'}|" tests/test_conf.py
  LC_CTYPE=en_US.utf8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" py.test tests
  mv "$srcdir/test_conf.py.bak" tests/test_conf.py
}

package() {
  cd thefuck-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
