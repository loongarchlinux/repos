# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-stestr
pkgver=3.2.0
pkgrel=1
pkgdesc="A test runner runner similar to testrepository"
url="https://stestr.readthedocs.io/en/latest/"
license=('Apache')
arch=('any')
depends=('python-future' 'python-cliff' 'python-pbr' 'python-subunit' 'python-fixtures'
         'python-testtools' 'python-yaml' 'python-voluptuous')
checkdepends=('python-hacking' 'python-sphinx' 'python-mock' 'python-subunit2sql' 'python-coverage'
              'python-ddt')
source=("https://pypi.io/packages/source/s/stestr/stestr-$pkgver.tar.gz")
sha512sums=('4f5e41c92518104b06f5b9717d59c214a8d87efe47fee199a602e71efd64e29c98711ffcf3b88cd00209946d0ab123c7ec5aa842e1d54bc65d83c8d70fa4898c')

build() {
  cd stestr-$pkgver
  python setup.py build
}

check() {
  cd stestr-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages"
    export PATH="$PWD/tmp_install/usr/bin:$PATH"
    stestr run
  )
}

package() {
  cd stestr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
