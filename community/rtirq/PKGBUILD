# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20210329
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('e08ee7768c93dd4e7244b61629272c4a4482d63b1fe37b7972a27703cd0a55cc12449589a781b33e9c92f5dd664c1a4ceba65caadb09c676deea86c161cc3d37'
            'SKIP')
b2sums=('278868817b349d0fd8bc70f0fb0374016d9117b593ed2fe710300fe194e7f8e640f4071f30e73688cca11d6da79fd93624a5e5e5246bfc2f9cd367397c00cadd'
        'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd services
  sed -e "s|etc/init.d|usr/bin|" -i *".service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 *".service" -t "${pkgdir}/usr/lib/systemd/system/"
}
