# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20191121
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('bb1ae0099e37220b13f91303e56e8ae7892964638c1000fd66b6ceb23925c8aa2ac0c629bbb799a59c1a8adb035c34c4b0bee31afe05fcc9f80cd94d5a84ddc1'
            'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd services
  sed -e "s|etc/init.d|usr/bin|" -i *".service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 *".service" -t "${pkgdir}/usr/lib/systemd/system/"
}
