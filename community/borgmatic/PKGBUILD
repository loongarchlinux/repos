# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.6.5
pkgrel=1
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=('any')
url=https://torsion.org/borgmatic
license=('GPL3')
depends=('borg' 'python-colorama' 'python-jsonschema' 'python-requests'
         'python-ruamel-yaml' 'python-setuptools')
checkdepends=('python-flexmock' 'python-pytest')
optdepends=('python-llfuse: for mount action')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('fbd219f1a05e11073d6133081ceeae803c865b1c05cabd7c7f785ef5a9e5057a')
b2sums=('8d342800ad92a9bddae95eb7b0ee8aabdf792f4b36e696ae15c7dd1318f18c983dad4984c7a7a0a0aeab21760533655d97bc199cd3066dbb01f329cc1c7d15d2')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,root/.local,usr,' sample/systemd/$pkgname.service
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest -c /dev/null \
    --ignore=tests/end-to-end
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system sample/systemd/*
  install -d "$pkgdir"/usr/share/bash-completion/completions
  ./test-env/bin/borgmatic --bash-completion > \
    "$pkgdir"/usr/share/bash-completion/completions/$pkgname
}
