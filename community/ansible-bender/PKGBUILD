# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=ansible-bender
pkgver=0.9.0
pkgrel=2
pkgdesc='Build container images using Ansible playbooks'
arch=('any')
url='https://github.com/ansible-community/ansible-bender'
license=('MIT')
depends=('ansible' 'python-yaml' 'python-tabulate' 'python-jsonschema' 'podman' 'buildah')
makedepends=('python-setuptools-scm' 'git' 'python-pip' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('9bf2095da798ffb0d3eff4ce2f84b9f96d04fd6526ba7ee559cd7009e501b823e5f64076d201d11f8be9995a3fb721cce69f366d195e564e5defc0cadef18189')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
