# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=ansible-bender
pkgver=0.9.0
pkgrel=4
pkgdesc='Build container images using Ansible playbooks'
arch=('any')
url='https://github.com/ansible-community/ansible-bender'
license=('MIT')
depends=(
  'ansible'
  'python-yaml'
  'python-tabulate'
  'python-jsonschema'
  'python-setuptools'
  'podman'
  'buildah'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools-scm'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('9bf2095da798ffb0d3eff4ce2f84b9f96d04fd6526ba7ee559cd7009e501b823e5f64076d201d11f8be9995a3fb721cce69f366d195e564e5defc0cadef18189')

prepare() {
  cd "$pkgname-$pkgver"

  # drop python-setuptools-scm-git-archive as it is included in
  # python-setuptools-scm >= 7.0.0
  sed -e '/setuptools_scm_git_archive/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"

  SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver" python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
