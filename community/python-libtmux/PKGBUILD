# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.11.0
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry)
checkdepends=(python-pytest procps-ng)
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/$_name/archive/v$pkgver.tar.gz")
sha512sums=('e09749ce4cf7cf7756e4e40e50c6eb0a00435f2b00c8436667b6b77a94078e9c380a811107f8129ea0d347355f3ce8bb213b8fb36de2becf3cf1a2dbebd97867')
b2sums=('4939d38894fd6a1492ff282c98243b2d5b3b6f787691031c74337629aed710d3caebeb3bfce571e8f38fe07ecc341b6b3f8aa0bc42979fa362f5f0679aabf21e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname"
}
