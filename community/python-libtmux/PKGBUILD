# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.15.4
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
optdepends=('python-pytest: for pytest plugin')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('5a0768f544b919d2d675edcd32d4582dfae73e8abd3102e96caf72b755a8edc9bcd8d262aad9fb2c5e8a4d9c8db1a8b778527b4ccd625eeedae973f3920837a0')
b2sums=('0c5c6865346bec1d74379c9330e8cb5034b93fa3af91ed5f6a08b7456980acc540ecb4e7b94811815a6777fee86f7bb0a1c727594073eef53109e58ef6c3c9f8')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
