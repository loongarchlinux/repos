# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.14.2
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('b229d36210c743ec9ca3169c5042ba725785f03cb7f13ad34dd3b94d38c86c6a77ab39abb3a64dcc5e6d7a1761f7851f1ecc5f5073dae7892e2606e3f4ff1afb')
b2sums=('b9988258619a95eabc07d6965dec2143ba3c1d5e71cb61fd482261c9c3579a0d43ca2d74c279f06c5416ee47b4121dc95a396038b0d19d98dd32ca841c7da903')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
