# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.8.3
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
depends=('python' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f7c5a84f16b400db672318d3c0b79cd8f328a0096d32349f6ce9360d72eecd75ed9915af5c37950adadd81943130b543716d718366522efee7780813a2c1a5d6')
b2sums=('7979dc907d5c7805cf56422aa0fed554c89f0b50cf1f13b150809a1c7e24b0eab125701eada0431212afccc26f6479642c886b3f701c8c2ffc9c3ebb7d08c4cc')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGES,README.rst,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
