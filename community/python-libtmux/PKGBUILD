# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.10.1
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
depends=('python' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('67ed162546a3dc5aa51e91ac03f015532d67847b0400766b442691a39a1a751c59a6b37811f6422ed713fca6a071767a8e13694b2722489d2e56cda711010228')
b2sums=('56028bca4e6a8df788d9fc9a112731fbf17f3617410d0632ac9d3f000ca130d2f4b21c377611e1ef95bcf493fa14f885cbcd7aa986d9c1b89d2b972fb4173f78')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGES,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
