# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxdumptool
pkgver=6.1.3
pkgrel=1
pkgdesc='Small tool to capture packets from wlan devices'
url='https://github.com/ZerBea/hcxdumptool'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'openssl')
source=(https://github.com/ZerBea/hcxdumptool/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxdumptool/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('99619fabf75c34c04395105e12f87635e419c76ed0bdb22926cc351d37e12f92075e00c6640090d446f681b2e33c22d6167127e88df946da6c652c67f6682aaf'
            'SKIP')
b2sums=('87338b85f27136219303e32ee6d5004d319d177ab4c1d215ef3f23d3d6d62a9ba917ee4f8dc218b675139e0c31d942197f01da13cfdc0a2c24e4b2d85763bd9e'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
