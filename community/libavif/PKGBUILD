# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libavif
pkgver=0.9.3
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=(loongarch64 x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
depends=(glibc aom dav1d rav1e svt-av1 zlib libpng libjpeg libyuv)
makedepends=(cmake nasm pkgconf gdk-pixbuf2)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bcd9a1f57f982a9615eb7e2faf87236dc88eb1d0c886f3471c7440ead605060d')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
