# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=bcprov
pkgver=1.71
_tag=1dcbde6d8761df7d88fc7432cb128533418adeca # git rev-parse "r${pkgver/./rv}"
pkgrel=1
pkgdesc='Bouncy Castle Crypto APIs for Java'
arch=('any')
url='https://www.bouncycastle.org/java.html'
license=('MIT')
depends=('java-runtime-headless')
makedepends=('git' 'ant' 'strip-nondeterminism')
source=("git+https://github.com/bcgit/bc-java.git#tag=$_tag")
sha512sums=('SKIP')

pkgver() {
	cd bc-java
	git describe --tags | sed 's/^r//;s/rv/./;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd bc-java
	ant -f ant/jdk18+.xml clean build-provider build

	# Timestamps in JAR files generated by Ant do not honour SOURCE_DATE_EPOCH
	# (https://bz.apache.org/bugzilla/show_bug.cgi?id=61269)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "build/artifacts/jdk1.8/jars/bcprov-jdk18on-${pkgver/./}.jar"
}

check() {
	cd bc-java
	ant -f ant/jdk18+.xml test
}

package() {
	cd bc-java
	install -Dm644 "build/artifacts/jdk1.8/jars/bcprov-jdk18on-${pkgver/./}.jar" -t "$pkgdir/usr/share/java/$pkgname"
	ln -s "bcprov-jdk18on-${pkgver/./}.jar" "$pkgdir/usr/share/java/$pkgname/bcprov.jar"
	install -Dm644 LICENSE.html -t "$pkgdir/usr/share/licenses/$pkgname"
}
