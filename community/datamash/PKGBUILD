# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>
# Contributor: Timothy Rice <timothy.rice@unimelb.edu.au>
pkgname=datamash
pkgver=1.6
pkgrel=1
pkgdesc='Performs basic numeric,textual and statistical operations on input textual data files'
url="https://www.gnu.org/software/datamash/"
arch=('x86_64')
license=('GPL')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz.sig")
sha256sums=('b9b9b79399616bb54722edbbcaa84303801eb62a338b3a20b6f029003deb78cb'
            'SKIP')
# Assaf Gordon <assafgordon@gmail.com>
validpgpkeys=('F576AAAC1B0FF849792D8CB129A794FD2272BC86')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
