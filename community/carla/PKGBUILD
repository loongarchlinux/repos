# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=2.4.3
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=(loongarch64 x86_64)
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=(GPL2)
groups=(pro-audio)
depends=(alsa-lib gcc-libs glibc hicolor-icon-theme libglvnd libx11
python-pyqt5 python-pyliblo python-rdflib qt5-base qt5-svg)
makedepends=(file fluidsynth freetype2 git libsndfile liblo libpulse qt5-tools)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
)
provides=(dssi-host ladspa-host lv2-host vst-host vst3-host)
options=(debug)
source=(
  git+https://github.com/falkTX/$pkgname#tag=v$pkgver?signed
)
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0')  # falkTX <falktx@falktx.com>

prepare() {
  cd $pkgname
  # fix issue with python-pyqt5/ python 3.10: https://github.com/falkTX/Carla/issues/1590
  git cherry-pick -n 7e16efba89c180f170c6fe22c773742cfbb89f17
}

build() {
  make features -C $pkgname
  make -C $pkgname
}

package() {
  depends+=(libasound.so libfluidsynth.so libfreetype.so liblo.so libmagic.so
  libpulse.so libsndfile.so)

  make DEFAULT_QT=5 DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
