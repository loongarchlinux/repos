# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=arch-release-promotion
pkgver=0.2.1
pkgrel=2
pkgdesc="Promote official releases with signatures and additional artifacts"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/arch-release-promotion"
license=('GPL3')
depends=('python-dotenv' 'python-email-validator' 'python-gitlab'
'python-orjson' 'python-prometheus_client' 'python-pydantic' 'python-pyxdg'
'python-toml' 'python-torrentool')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
backup=("etc/${pkgname}/projects.toml")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5a67197daa9f35f1aa9bc957f577281b519505600d93895bb3fe426ad1a24dcc')
b2sums=('4f4da3a74655c25003b8b9243cb1d2a521cded8e6109cc2667b108475e85e529acb5dabd10cced8ef96c2e515be68837922896ca52b7fc7a1bf4d5e722527e38')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {examples/example.toml,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/projects.toml -t "${pkgdir}/etc/${pkgname}"
  install -vDm 644 examples/systemd/*.{service,timer} -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 examples/sysusers.d/*.conf -t "${pkgdir}/usr/lib/sysusers.d/"
}
