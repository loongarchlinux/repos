# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

_pyname=mccabe
pkgname=python-$_pyname
pkgver=0.6.1
pkgrel=11
pkgdesc='McCabe complexity checker for Python'
arch=(any)
url="https://github.com/PyCQA/$_pyname"
license=('custom:Expat')
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest
              python-pytest-runner)
_archive="$_pyname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('2748af6516175b94be318cd8226fa50b5339b9dc886bce378ac2afb37a157524')

build() {
	cd "$_archive"
	python setup.py build
}

check() {
	cd "$_archive"
	pytest
}

package() {
	cd "$_archive"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
