# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=fragments
pkgver=1.4
pkgrel=4
pkgdesc="BitTorrent client for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/Fragments"
license=(GPL3)
depends=(curl libevent libhandy0 libnatpmp miniupnpc)
makedepends=(cmake git meson vala)
_commit=f91ea9b576f3ff2621d5f3fda417a9a43107607c  # tags/1.4
source=("git+https://gitlab.gnome.org/World/Fragments.git#commit=$_commit"
        "git+https://github.com/transmission/transmission"
        "git+https://github.com/transmission/dht"
        "git+https://github.com/transmission/libb64"
        "git+https://github.com/transmission/libevent"
        "git+https://github.com/transmission/libnatpmp"
        "git+https://github.com/transmission/libutp"
        "git+https://github.com/transmission/miniupnpc")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd Fragments
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd Fragments

  # Fix compile with Vala 0.50
  git cherry-pick -n ce38a28da8a4c3d0281a8187abad26627a50993f

  git submodule init
  git config --local submodule.submodules/transmission.url "$srcdir/transmission"
  git submodule update

  cd submodules/transmission
  git submodule init
  git config --local submodule.third-party/dht.url "$srcdir/dht"
  git config --local submodule.third-party/libb64.url "$srcdir/libb64"
  git config --local submodule.third-party/libevent.url "$srcdir/libevent"
  git config --local submodule.third-party/libnatpmp.url "$srcdir/libnatpmp"
  git config --local submodule.third-party/libutp.url "$srcdir/libutp"
  git config --local submodule.third-party/miniupnpc.url "$srcdir/miniupnpc"
  git submodule update
}

build() {
  arch-meson Fragments build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
