# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=fragments
pkgver=2.0.2
pkgrel=1
pkgdesc='BitTorrent client for GNOME'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/World/Fragments'
license=('GPL3')
depends=('libadwaita' 'transmission-cli')
makedepends=('git' 'meson' 'rust')
_commit=7a20eb0d8e1377ea0e93449047a8f24d6336e5d5
source=("git+https://gitlab.gnome.org/World/Fragments.git#commit=$_commit")
sha256sums=('SKIP')
options=(!lto)

pkgver() {
  cd Fragments
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson Fragments build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
