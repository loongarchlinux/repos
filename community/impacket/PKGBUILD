# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.9.22
pkgrel=1
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=('any')
license=('Apache')
depends=('python' 'python-pycryptodome' 'python-pycryptodomex' 'python-pyasn1' 'python-pcapy' 'python-pyopenssl'
         'python-six' 'python-ldap3' 'python-flask')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-cryptography')
source=(https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz
        impacket-python3.9.patch::https://github.com/SecureAuthCorp/impacket/pull/946.patch)
sha256sums=('f7d7ad797f833d242a849d131a4b3704dd526887018ff9bd1b22e0cbb827dce9'
            '61948a2dfe4e37fd33c3efa4459ec975ed32ff55d16992957b0de07c90901ca8')
sha512sums=('ff007f4570d875951615120cc5211ca4841e33e65a6508ba1a08c69dbf3d14c093a777c9d8489588ff5083db3045d919807b0288c6792ef7464810684eaa1e75'
            'dcfafdfc3576011c9104d0dd7215934145c301d69acd971431d2e9c49c24ec5258648d8d8d02844805534299f1ee2e8bcab16245c7035ccfe16381f87e368f9c')

prepare() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  patch -p1 -i ../impacket-python3.9.patch
  sed -e '/test_smb.py/d' \
    -e '/test_nmb.py/d' \
    -e '/test_ntlm.py/d' \
    -e '/test_ldap.py/d' \
    -e '/rundce.sh/d' \
    -i tests/runall.sh
}

build() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}/tests
  ./runall.sh
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  ln -s "/usr/lib/python${PYTHONVERSION}/site-packages/impacket/examples" "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
