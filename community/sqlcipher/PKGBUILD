# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: flu

pkgname=sqlcipher
pkgver=4.4.3
pkgrel=1
pkgdesc="SQLite extension that provides transparent 256-bit AES encryption of database files"
arch=('loongarch64' 'x86_64')
url="https://www.zetetic.net/sqlcipher/"
license=('BSD')
makedepends=('openssl' 'sqlite' 'tcl')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
#         https://www.zetetic.net/$pkgname/verify/$pkgver/$pkgname-$pkgver.zip.sig)
# validpgpkeys=('D83F5F9EB811D6E6B4A0D9C5D1FA3A2A97ED25C2') # Zetetic LLC <support@zetetic.net>
sha256sums=('b8df69b998c042ce7f8a99f07cf11f45dfebe51110ef92de95f1728358853133')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-tempstore=yes \
                CFLAGS="$CFLAGS -DSQLITE_HAS_CODEC" LDFLAGS="-lcrypto"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
