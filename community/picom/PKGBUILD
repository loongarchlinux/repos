# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=picom
pkgver=8.1
pkgrel=2
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/picom'
license=(MIT MPL)
replaces=(compton)
provides=(compton)
conflicts=(compton)
makedepends=(asciidoc git mesa meson ninja uthash)
depends=(hicolor-icon-theme libconfig libdbus libev libgl pcre pixman
         xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/picom.conf)
optdepends=('dbus:          For controlling picom via D-Bus'
            'python:        For running picom-convgen.py'
            'xorg-xwininfo: For picom-trans'
            'xorg-xprop:    For picom-trans')
# tag: v8.1
source=("git+$url?signed#commit=2c9e723e414c8f672117afb40c3f15a3f4b0528d"
        'https://raw.githubusercontent.com/yshui/picom/30bbfc28bbf7dd43fc8d4b1f7a2e5ea4a4cd6a3f/picom.sample.conf')
validpgpkeys=('A22F5C0F4FCF9C7C89A167462C965E9E5D45D730') # Yuxuan Shui <yshuiv7@gmail.com>
sha256sums=('SKIP' 'a00715dd0be2ae2192f7c66cd5a21580d36e9b76a4f10fb26583e11f256baf79')

build() {
  mkdir -p build
  arch-meson build $pkgname -D b_ndebug=true -D with_docs=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # The config that comes with picom
  install -Dm644 $pkgname/picom.sample.conf "$pkgdir/etc/xdg/picom.conf.example"

  # A more updated configuration file (FS#67839)
  install -Dm644 picom.sample.conf "$pkgdir/etc/xdg/picom.conf"

  # Man pages and license files
  install -Dm644 build/man/picom.1 "$pkgdir/usr/share/man/man1/picom.1"
  install -Dm644 build/man/picom-trans.1 "$pkgdir/usr/share/man/man1/picom-trans.1"
  install -Dm644 $pkgname/LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 $pkgname/LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# getver: github.com/yshui/picom/releases
# vim: ts=2 sw=2 et
