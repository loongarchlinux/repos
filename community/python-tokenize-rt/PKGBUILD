# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=tokenize-rt
pkgname=python-tokenize-rt
pkgver=4.2.1
pkgrel=3
pkgdesc='Wrapper around the stdlib `tokenize` which roundtrips'
arch=('any')
url=https://github.com/asottile/tokenize-rt
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('b8cee02bbb9bb559a92645d8230abc29a58f100b9d03a496106bf9301ff2fa6b47e472970dc71203c61a7d1d3b95f6bb88ef8da15641c509efd10a7711e85d2b')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
