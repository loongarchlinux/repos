# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.55.0
pkgrel=1
pkgdesc='Sass makes CSS fun again'
arch=('loong64' 'x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_commit='a65e504b484eeacfff5219a24ad4b89e5ee1f87f'
source=("$pkgname::git+https://github.com/sass/dart-sass.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  # disable analytics
  dart --disable-analytics

  # download dependencies
  dart pub get
}

build() {
  cd "$pkgname"
  dart compile exe -Dversion=$pkgver -o sass bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
