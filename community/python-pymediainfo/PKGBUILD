# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pymediainfo
pkgname=python-pymediainfo
pkgver=5.1.0
pkgrel=3
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
depends=('libmediainfo' 'python')
makedepends=('mediainfo' 'python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d3c48e37f6d59b0f5bc1d66d4fad9fea26724863dc2ae75a30c80d972f68f3be650298d25105d330c20b83083f94d2ff5e2690c29573897e09afa44931f3228e')
b2sums=('debef8de004232c4e39bd17018c447474a4f71bc386b01710bea8084febb5353cb70512ee77c33ea8b3e6c7b67baded99aecb77b60c52da58be35ef91919f34a')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
