# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-benchmark
pkgver=3.4.1
pkgrel=3
pkgdesc='A py.test fixture for benchmarking code'
arch=('any')
license=('BSD')
url='https://github.com/ionelmc/pytest-benchmark'
depends=('python-pytest' 'python-py-cpuinfo')
makedepends=('python-setuptools')
checkdepends=('python-pygal' 'python-freezegun' 'mercurial' 'python-aspectlib'
              'python-pytest-xdist' 'python-elasticsearch' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/pytest-benchmark/archive/v$pkgver.tar.gz"
        python310.patch)
sha512sums=('54fb5e3a176578b3c8f339878c7a9d047a0785befcbba42bb68c34b4b636d9b6f6a2097b36c5eefbbcf1a91f943bdae762c7978a0dba1af53514052f513ef4c5'
            '8f1c062bc502ea8ec0421509576bc1042354a5bc49f918ff7ceb8360387405f1897b1b79a1700d5ed506c326214a6222fc41353f68eeb72e37914e27ed30b1aa')

prepare() {
  cd pytest-benchmark-$pkgver
  sed -i 's/\[glob_or_file ...]]/...]/' tests/test_cli.py

  # https://github.com/ionelmc/pytest-benchmark/pull/210
  patch -Np1 -i ../python310.patch
}

build() {
  cd pytest-benchmark-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-benchmark-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" python -m pytest tests
}

package() {
  cd "$srcdir"/pytest-benchmark-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
