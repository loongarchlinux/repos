# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=2.4.0
pkgrel=2
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-babel' 'python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib'
         'python-prettytable' 'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils'
         'python-keystoneauth1' 'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("https://github.com/openstack/python-heatclient/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        python310.patch)
sha512sums=('1c6e64dd7d799d0ea7c29fbb36959c9e61cce620535b9952d05bf74cef48ded34465e7c3ab1fb1aefe60104c8cdf837047a68af6560fc37cf5a924962b9a8b99'
            '54b651d540235e83f1a7ef96c62f11c842564dbb522c6fe4ec9e5dfb42e965317d99d70bd1d6b2068b9a33159de4f40958f1b61a9439026f808e6fd9c70bd168')

export PBR_VERSION=$pkgver

prepare() {
  cd python-heatclient-$pkgver
  patch -Np1 -i ../python310.patch
}

build() {
  cd python-heatclient-$pkgver
  python setup.py build
}

check() {
  cd python-heatclient-$pkgver
  stestr run
}

package() {
  cd python-heatclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
