# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Duboc <guilduboc@gmail.com>

_pipname=jupyterlab_server
pkgname=python-$_pipname
pkgver=2.15.2
pkgrel=1
pkgdesc='Launch an application built using JupyterLab'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyter-server python-requests python-json5 python-babel)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-tornasync python-pytest-cov python-pytest-timeout
              python-openapi-core python-strict-rfc3339 python-ruamel-yaml python-parse python-pip)
conflicts=(jupyterlab_server)
replaces=(jupyterlab_server)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('c0bcdd4606e640e6f16d236ceac55336dc8bf98cbbce067af27524ccc2fb2640')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
# openapi-core is currently broken
#  pytest -v -k 'not test_translation_api.py' -W ignore::pytest.PytestAssertRewriteWarning -W ignore::DeprecationWarning
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
