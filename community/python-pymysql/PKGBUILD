# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMySQL
pkgname=python-pymysql
pkgver=0.10.1
pkgrel=1
pkgdesc="Pure Python MySQL Client"
arch=('any')
url="https://github.com/PyMySQL/PyMySQL/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'mariadb')
optdepends=('mariadb: for using a local MariaDB instance'
            'python-cryptography: for cryptography')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('33dba505fd937ddece7b4c52a1db64084656bccc0b3e98ecee1227732fe03f4e8c3f4bcb64bcf67db54f3407f3ce8a706a08d3f992450755267b0e6c5b123b99')
b2sums=('fe80c944d03a9e1206071c9217371feeb8806936b16c4bf1390a5fe67c9b4422cf86f1d09dd7e5d090bac585d762f8d2f69dea836d71ab13b92ca7d44b134e21')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# the tests are integration tests and can only be run with a running and
# configured mariadb server
# check() {
#   cd "$pkgname-$pkgver"
#   export PYTHONPATH="build:${PYTHONPATH}"
#   pytest -v pymysql/tests
# }

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
