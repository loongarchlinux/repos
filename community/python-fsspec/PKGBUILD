# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filesystem_spec
pkgname=python-fsspec
pkgver=0.8.7
pkgrel=1
pkgdesc="Specification that python filesystems should adhere to."
arch=(any)
url="https://github.com/intake/filesystem_spec"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(git
    python-pytest
    python-pytest-vcr
    python-aiohttp
    python-cloudpickle
    python-dask
    python-distributed
    python-lz4
    python-libarchive-c
    python-msgpack
    python-numpy
    python-paramiko
    python-pyftpdlib
    python-pygit2
    python-requests
    python-snappy
    python-zstandard
    python-aiobotocore
    python-httpretty
    python-moto
    python-flask
    python-arrow
    python-smbprotocol
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e1ce75f0750d3a2383b25656b262bc661a58d7e863bbf151a3b13ffad395e8f7')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # These tests do not work on CI, neither on our build server
  pytest --deselect fsspec/implementations/tests/test_http.py::test_processes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
