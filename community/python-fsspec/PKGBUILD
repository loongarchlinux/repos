# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filesystem_spec
pkgname=python-fsspec
pkgver=2022.02.0
pkgrel=1
pkgdesc="Specification that python filesystems should adhere to."
arch=(any)
url="https://github.com/intake/filesystem_spec"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(git
    python-pytest
    python-pytest-asyncio
    python-pytest-mock
    python-pytest-vcr
    python-aiohttp
    python-dask
    python-distributed
#    python-fusepy
    python-libarchive-c
    python-paramiko
    python-pyarrow
    python-pygit2
    python-requests
    python-smbprotocol
    python-cloudpickle
    python-jinja
    python-lz4
    python-msgpack
    python-numpy
    python-pandas
    python-pyftpdlib
    python-snappy
    python-tomli
    python-zstandard
    jupyter-notebook
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8a11efc985d39782ca64bd4010f32d189a39278b35c89e5e623c760564cea8b3')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # These tests do not work on CI, neither on our build server
  pytest -vv --color=yes -rs --deselect fsspec/implementations/tests/test_http.py::test_processes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
