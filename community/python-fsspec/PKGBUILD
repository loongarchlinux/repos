# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filesystem_spec
pkgname=python-fsspec
pkgver=2021.10.1
pkgrel=3
pkgdesc="Specification that python filesystems should adhere to."
arch=(any)
url="https://github.com/intake/filesystem_spec"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(git
    python-pytest
    python-pytest-vcr
    python-aiohttp
    python-dask
    python-distributed
    python-libarchive-c
    python-paramiko
    python-pyarrow
    python-pygit2
    python-requests
    python-smbprotocol
    python-cloudpickle
    python-jinja
    python-lz4
    python-msgpack
    python-numpy
    python-pyftpdlib
    python-snappy
    python-zstandard
    jupyter-notebook
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1a315d9f2e421139b3b82b9a7a8b858ac9bdb49e7e963971bdbf7fc23e958027')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # These tests do not work on CI, neither on our build server
  pytest -v -rs --deselect fsspec/implementations/tests/test_http.py::test_processes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
