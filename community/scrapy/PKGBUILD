# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=2.3.0
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=('python-twisted' 'python-cryptography' 'python-cssselect' 'python-itemloaders'
         'python-lxml' 'libxml2' 'python-parsel' 'python-pydispatcher' 'python-pyopenssl'
         'python-queuelib' 'python-service-identity' 'python-setuptools' 'python-w3lib'
         'python-zope-interface' 'python-protego' 'python-itemadapter')
checkdepends=('python-pytest-runner' 'python-pytest-twisted' 'python-botocore' 'bpython'
              'python-brotlipy' 'python-jmespath' 'ipython' 'mitmproxy' 'python-pillow'
              'python-sybil' 'python-testfixtures')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz")
sha512sums=('be1cc2b62d08b87989a1f33132223deabebb62cc126611d5473425fca46de88f95ad049ce9fb4195eaac57dbfaeb1a49f933fd3211ae602c9c43aea68ec49999')

build() {
  cd scrapy-$pkgver
  python setup.py build
}

check() {
  cd scrapy-$pkgver
  python setup.py pytest --addopts tests
}

package() {
  cd scrapy-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
