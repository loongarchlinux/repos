# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=2.5.1
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=('python-twisted' 'python-cryptography' 'python-cssselect' 'python-itemloaders'
         'python-lxml' 'libxml2' 'python-parsel' 'python-pydispatcher' 'python-pyopenssl'
         'python-queuelib' 'python-service-identity' 'python-setuptools' 'python-w3lib'
         'python-zope-interface' 'python-protego' 'python-itemadapter' 'python-h2'
         'python-priority')
checkdepends=('python-pytest' 'python-botocore' 'bpython' 'python-brotli' 'python-jmespath'
              'ipython' 'mitmproxy' 'python-pillow' 'python-sybil' 'python-testfixtures'
              'python-pyftpdlib' 'python-uvloop')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz"
        python310.patch
        $pkgname-add-peek-method-to-queues.patch::https://github.com/scrapy/scrapy/commit/68379197986a.patch)
sha512sums=('4285272412b4c474707e864ece701990435d206b0a1ef28cb9f99cff87a02e6b0323e744e96f15878a3b433a7efe4be413d24068bb252f00f9ab1b0a980d844a'
            '5a9e7567e7a62d2c7065ce95179f7f35f86d88947f3ba09d7a377318eb989b7799584389ac9223934b2807ba291454c4e1f5bcaad501590056b0c44d75297561'
            'defc36c52bc86d990e36a3de4d2e7bafc6c3183670c9dd99d659f41de9d5ce8247c5cbb0aba9a4a8713d0073498441919e8410975a65ccec8243ca4d31c63336')

prepare() {
  cd scrapy-$pkgver
  patch -Np1 -i ../python310.patch
  patch -Np1 -i ../$pkgname-add-peek-method-to-queues.patch
  sed -i 's/,<4.0//' setup.py
}

build() {
  cd scrapy-$pkgver
  python setup.py build
}

check() {
  cd scrapy-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest tests
}

package() {
  cd scrapy-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
