# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.12.1
pkgrel=1
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-pydantic' 'python-typeguard')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm'
             'python-toml' 'python-wheel')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('3b9da223fd6e7624aed047dec2cbb6ab471c1441d5b48369daad012594c11610')
b2sums=('ccfc747e9b645c5ffafadf7d7b6af25d3f014f3d7cf0888ca896feddca5ea3672af3b33d2aa7042819d4dbef44e1d49230e513ee795a618baa5484e7586c01b5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "$pkgdir"/usr/lib/site-packages

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
