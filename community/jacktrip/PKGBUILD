# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.2.1
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=('x86_64')
url="https://github.com/jacktrip/jacktrip"
license=('MIT')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'qt5-base')
makedepends=('jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('aba0df00e016d0c16ac5cc31af05ee490dfd5de1c910ecd78d8884e2a6aacf9d88a8b1f0071664fd94755c531b567b851835b974a3821a456cd0696750177523')
b2sums=('1660e3d5353055d2d860067e0e7152de817db5fc349317bc1e5476a12c607ca59a703973f7440e4435249508c70cc615ca5e43d4cc4f49aa59bc82a74d60b87a')

build() {
  cd "${pkgname}-${pkgver}/src"
  qmake-qt5 -spec linux-g++ "${pkgname}.pro"
  make release
}

package() {
  depends+=('libjack.so')
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "src/${pkgname}" -t "${pkgdir}/usr/bin/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGESLOG.txt,README.md,TODO.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
