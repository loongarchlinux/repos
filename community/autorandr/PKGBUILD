# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.10.1
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python' 'xorg-xdpyinfo' 'xorg-xrandr')
makedepends=('bash-completion')
optdepends=('bash-completion: auto-completion for autorandr in Bash'
            'zsh-completions: auto-completion for autorandr in ZSH')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('f1a132d56329247a572e223e471270342561c668da63d1c1769d5872daecbe0f3cea366524756d4cc624bb31673c0897fc788fdae6a3b404992e616463967eaf')

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  # zsh completion
  install -vDm 644 "contrib/zsh_completion/_${pkgname}" \
    -t "${pkgdir}/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
