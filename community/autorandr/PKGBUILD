# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.12.1
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=(any)
url="https://github.com/phillipberndt/autorandr"
license=(GPL3)
depends=(python xorg-xdpyinfo xorg-xrandr)
makedepends=(bash-completion systemd)
optdepends=(
  'bash-completion: auto-completion for autorandr in Bash'
  'zsh-completions: auto-completion for autorandr in ZSH'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('a7e095eb96e7e0d3a5b3b2122d68a3d3a5121d8be7a86ddd9f837ad42b7a53ed5ee45151f23861c5b900e214a4dfc0c575f45fe70c8c5587562a4035458137a0')
b2sums=('5b434325d13c00c3fa70b5d09edb540cdcf3bb921307c60f2c616231e7a87ea85f60e39500ed8116f28619139f71ecc4a8fa153ffadc6b8d74df570736a0d815')

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  # zsh completion
  install -vDm 644 "contrib/zsh_completion/_${pkgname}" -t "${pkgdir}/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
