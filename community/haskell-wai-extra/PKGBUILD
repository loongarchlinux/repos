# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-extra
pkgname=haskell-wai-extra
pkgver=3.1.12
pkgrel=6
pkgdesc="Provides some basic WAI handlers and middleware."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base64-bytestring'
         'haskell-case-insensitive' 'haskell-cookie' 'haskell-data-default-class'
         'haskell-fast-logger' 'haskell-http-types' 'haskell-http2' 'haskell-iproute'
         'haskell-network' 'haskell-resourcet' 'haskell-streaming-commons'
         'haskell-vault' 'haskell-wai' 'haskell-wai-logger' 'haskell-word8' 'haskell-hunit'
         'haskell-call-stack')
makedepends=('ghc' 'haskell-hspec' 'haskell-temporary' 'haskell-zlib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1578ce1579b06a3297159b718d9df06893a299b575c81b082466416046225580ea3ff3f74d44629031006f7e835e90a7625db3861b7eda608359f8c5c6618df2')

prepare() {
    cd $_hkgname-$pkgver
    # https://github.com/yesodweb/wai/issues/886
    echo "test" > test/json.gz
    echo "noprecompress" > test/noprecompress
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-build-example
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
