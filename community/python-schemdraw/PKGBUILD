# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sequencer <liujiuyang1994@gmail.com>

pkgname=python-schemdraw
pkgver=0.15
pkgrel=1
pkgdesc="Electrical circuit schematic drawing."
url="https://bitbucket.org/cdelker/schemdraw"
license=('BSD')
arch=('any')
depends=('python')
optdepends=('python-matplotlib: for matplotlib backend'
            'python-ziafont: for svg backend'
            'python-ziamath: for svg backend'
            'python-latex2mathml: for svg backend')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-nbval' 'python-matplotlib' 'python-ziafont'
              'python-ziamath' 'python-latex2mathml')
source=("$pkgname-$pkgver.tar.bz2::https://bitbucket.org/cdelker/schemdraw/get/$pkgver.tar.bz2")
sha512sums=('47d1231820b3402a3a150d7422f809c6d5bd48e9bbcdffeb5de3625f73d16d57f9212fd9fb3499e25eae58436482cf2892b48dee5f07b541a7cf1ffecce9c202')

prepare() {
  mv cdelker-schemdraw-* schemdraw-$pkgver
}

build() {
  cd schemdraw-$pkgver
  python setup.py build
}

check() {
  cd schemdraw-$pkgver
  python setup.py pytest --addopts --nbval-lax
}

package() {
  cd schemdraw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
