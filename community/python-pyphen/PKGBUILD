# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyphen
pkgname=python-pyphen
pkgver=0.13.0
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
license=(GPL2 LGPL2.1 MPL)
depends=(python)
makedepends=(python-build python-flit-core python-installer python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('0238627cbde87601be0731a097488342a2f6a48d9ca7510066bb7a3674e0a4581653bc829a22716873b7eafff750025e6ff5b5a8c9ec095d8ccd1e4f801a101b')
b2sums=('520ac4f0ce30fa4701a2ff72f03871c222a5529d79ff276c96cd4e7dec5a324a65878e7988666c972755f46b0a7359b82bcb67d33dfad838062bfeea143e9e04')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v -c /dev/null tests/
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
