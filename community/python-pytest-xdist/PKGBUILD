# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-xdist
pkgver=3.0.1
pkgrel=1
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-xdist'
depends=('python-execnet' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-filelock' 'python-psutil')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ae5ee63c6921513fb58453140200a61091f7d0d9a339d118103bd69114a3ff8d43397955e30e91bb96e809c94f78dcdba09080a10b35564b4edebb61542bd87f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
