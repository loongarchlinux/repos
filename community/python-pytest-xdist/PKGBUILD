# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-xdist
pkgver=2.3.0
pkgrel=1
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=('python-execnet' 'python-psutil' 'python-pytest-forked')
makedepends=('python-setuptools_scm')
checkdepends=('python-filelock')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('799b80256f0acca409c326e8fd9b505dbf486ade7e44d3710db913d508f4e2cec54539399af3de93bf213632708ff80cc8fb40b987227505391299171c00cb17')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest \
    --deselect testing/acceptance_test.py::TestWarnings::test_warning_captured_deprecated_in_pytest_6
}

package() {
  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
