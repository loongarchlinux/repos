# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-session
pkgver=2.2.8
pkgrel=2
pkgdesc='Session indicator for Wingpanel'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-session
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  gdk-pixbuf2
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=d6f04859901d6d56290564184f41580a4246644b
source=(git+https://github.com/elementary/wingpanel-indicator-session.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd wingpanel-indicator-session
  git cherry-pick -n ead6971c708eed8b844bd9acd7eed2ab8a97e803
  git cherry-pick -n 25386aca57a325b7fe5488f490f718646cd97a77
}

pkgver() {
  cd wingpanel-indicator-session
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-session build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
