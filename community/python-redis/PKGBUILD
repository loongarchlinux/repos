# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgname=python-redis
pkgver=4.3.5
pkgrel=2
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="https://github.com/redis/redis-py"
license=('MIT')
depends=('python-packaging' 'python-async-timeout')
makedepends=('python-setuptools')
checkdepends=('python-pytest-asyncio' 'pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('bff3db6e6336cf6787da3c7856ef9b122f07995ebab99d3e2d9b3ff42acd23241539971afd7b5a5f5136763e925086395feefb4c148296760906a5ad52216bea')

build() {
  cd redis-$pkgver
  python setup.py build
}

check() {
  cd redis-$pkgver
  # tests/test_ssl.py requires SSL keys
  pifpaf run redis -- pytest --ignore tests/test_ssl.py || echo "Tests failed"
}

package() {
  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
