# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgname=python-redis
pkgver=4.0.2
pkgrel=1
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="https://github.com/redis/redis-py"
license=('MIT')
depends=('python-deprecated')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('3f73ddd2ceb551fa447cfb4ecdc1d393957bbfd8c184ae3d357f9f47fff860f1c4684ec0fc3cb85fea530454456c17a98a2a56e592bef4cd9ad17bb405c1a0f7')

build() {
  cd "$srcdir"/redis-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/redis-$pkgver
  pifpaf run redis py.test || echo "https://github.com/andymccurdy/redis-py/issues/1073"
}

package() {
  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
