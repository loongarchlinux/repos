# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Kewl <xrjy@nygb.rh.bet(rot13)>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-rstr
_pkgname=rstr
pkgver=3.0.0
pkgrel=3
pkgdesc="Generate random strings in Python"
arch=('any')
url="https://github.com/leapfrogonline/rstr"
license=('BSD')
depends=('python')
makedepends=("python-setuptools" "python-dephell")
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('bc3d4f929e34dcde3d90884bc9217a465069f51164f4220c8e551205a882e9b7')

prepare() {
  cd $_pkgname-$pkgver
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
