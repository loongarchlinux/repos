# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=5.3.0
pkgrel=2
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz"
        $pkgname-requests-2.26.0.patch::https://github.com/evansd/whitenoise/pull/294.patch)
sha512sums=('480098a4f4ad313f0346d72f1eca5ee2f571056077edcf07f40f37356043663fc1fa99c7a33f840c176c74ee9894a99e15828ecadae944dc0c9445034aef2cb4'
            '8d8b1e028945bbe127dfd16999b6f7060b4f9e10d44f931974892c6b7f02e94d78e812301f33eed3c3d39b4fa282901ec076940431925181ad04f67908720c37')

prepare() {
  cd whitenoise-$pkgver
  patch -Np1 -i ../$pkgname-requests-2.26.0.patch
}

build() {
  cd whitenoise-$pkgver
  python setup.py build
}

check() {
  cd whitenoise-$pkgver
  DJANGO_SETTINGS_MODULE=tests.django_settings pytest
}

package() {
  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
