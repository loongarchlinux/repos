# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geographiclib
pkgname=python-geographiclib
pkgver=1.52
pkgrel=3
pkgdesc="Python implementation of the geodesic routines in GeographicLib"
arch=('any')
url="https://geographiclib.sourceforge.io/1.50/python/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c6c41a6c571f2028c24c0905155d0ea443e0c250963a89ae62237664d02df8b22977c2b6b230553ecbad01b61075a2f4874a6dcd86a041fd871eea3e9801d533')
b2sums=('b5251e3d3b0e1053ab063213d0616d9268e22ebff248d30cb3eb2f421315808b0a31c34a21008cb219db9364ef37fdf67adf63770e43e9ff287462284a3ec919')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
