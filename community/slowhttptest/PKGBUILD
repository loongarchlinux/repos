# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Changaco <changaco ατ changaco δοτ net>

pkgname=slowhttptest
pkgver=1.8.1
pkgrel=1
pkgdesc='Highly configurable tool that simulates some Application Layer Denial of Service (DoS) attacks'
url='https://github.com/shekyan/slowhttptest'
arch=('x86_64')
license=('Apache')
depends=('openssl' 'gcc-libs')
source=(https://github.com/shekyan/slowhttptest/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('95f43a18efbdfaa088acf0e2d6ce0fc4f4fc33a7486cd536d327a6ba71de30e7')
sha512sums=('1866c2bd7f314ec27c556ae6850e4aff4328cf3e9b14878bdbcbb17473b74dadb5a858adbaff4952d66a8d4a18a6655086797c1ccb7232a64cfc35aba3c59a83')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
