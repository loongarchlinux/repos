# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Moritz Schönherr <moritz.schoenherr@gmail.com>

pkgname=mdbook
pkgver=0.4.10
pkgrel=1
pkgdesc="Create book from markdown files, like Gitbook but implemented in Rust"
url="https://github.com/rust-lang/mdBook"
arch=('loongarch64' 'x86_64')
license=('MPL2')
depends=('gcc-libs')
makedepends=('rust')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-lang/mdBook/archive/v${pkgver}.tar.gz")
sha256sums=('ecd1d4cee5b5fa7781799f83010e2074262929a22e37d308061b54ffa7e42f69')
# https://github.com/rust-lang/mdBook/blob/master/CHANGELOG.md

build() {
  cd "mdBook-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "mdBook-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "mdBook-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
