# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guilhem Saurel <guilhem@saurel.me>
# Contributor: Michel Zou
# Contributor: Oliver Goethel

pkgname=med
pkgver=4.1.1
pkgrel=1
pkgdesc='Generic pre- and post-processing platform for numerical simulation'
url="https://www.salome-platform.org/downloads"
license=(LGPL)
arch=(loong64 x86_64)
depends=(hdf5 python tk)
makedepends=(cmake gcc-fortran ninja swig)
source=("https://files.salome-platform.org/Salome/other/$pkgname-$pkgver.tar.gz"
        hdf5-1.12.patch)
b2sums=('fb9bc3239c18c53fa42c5dd1101559746e8d38db2a3cb5fb47d1bf0cb578facc4ab95409af8524f5c20251848842cc1a331f6a816ce0754e9a6c95f507174ed1'
        '998958970918d98724b80f7b5ff09558f7abea39cdac5e558b66410c4d8d1476b0407295f3745ece5586482bf76db3eef0139918d27d1bbc3eb166ed7ab4745a')

prepare() {
  cd $pkgname-${pkgver}_SRC
  patch -p1 -i ../hdf5-1.12.patch # Fix build with hdf5 1.12 (Fedora)
  sed -i 's/if H5_VERS_MINOR > 10/if 0/g' src/ci/MEDfileCompatibility.c
}

build() {
  export CPPFLAGS+=' -DH5_USE_110_API'
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D MEDFILE_BUILD_PYTHON=ON \
    -D MEDFILE_BUILD_TESTS=OFF \
    -D MEDFILE_INSTALL_DOC=OFF \
    -G Ninja \
    -S $pkgname-${pkgver}_SRC
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
