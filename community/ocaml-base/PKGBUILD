# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Peukert <daniel@peukert.cc>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

_projectname='base'
pkgname="ocaml-$_projectname"
pkgver=0.15.0
pkgrel=1
pkgdesc='Full standard library replacement for OCaml'
arch=('loong64' 'x86_64')
url="https://github.com/janestreet/$_projectname"
license=('MIT')
depends=('ocaml>=4.13.1' 'ocaml-sexplib0')
makedepends=('dune')
options=('!strip')
source=("$pkgname-$pkgver-$pkgrel.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('56da1a3089ba94eb68aaa38a9c7ebc8ba4dcb290bbf8906bf68c463163560545')

_sourcedirectory="$_projectname-$pkgver"

build() {
	cd "$srcdir/$_sourcedirectory/"
	OCAMLPATH=/usr/lib/ocaml dune build --release --verbose
}

package() {
	cd "$srcdir/$_sourcedirectory/"
	DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' --release --verbose

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	mv "$pkgdir/usr/doc/$_projectname/"* "$pkgdir/usr/share/doc/$pkgname/"
	rm -r "$pkgdir/usr/doc/"

	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
