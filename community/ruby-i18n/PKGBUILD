# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.8.11
pkgrel=1
pkgdesc="New wave internationalisation support for Ruby"
arch=('any')
url="https://github.com/ruby-i18n/i18n"
license=('MIT')
depends=('ruby' 'ruby-activesupport' 'ruby-json' 'ruby-mocha')
makedepends=('ruby-bundler' 'ruby-rake' 'ruby-rdoc')
# # TODO: package ruby-pry
# checkdepends=('ruby-minitest' 'ruby-test_declarative')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ruby-i18n/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('af8fcc307de9959b5a01a79ac3c99d3a3573630f8cdf91c21208e07d35d1a0db60f4995f1b77921c5374a1a19ad66770a107cc41fb29cf480d02be00bf90634a')
b2sums=('e603077c6eb02f6fd89bafd6c06f85c72528d5eb4388854f060f05a4b49a3782a3830366a1f11bd95b191048f29fae251002a34d55cb185ecefb3afc06bb2f36')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i "${_name}.gemspec" Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

# # TODO: package ruby-pry
# check(){
#   cd "${pkgname}-${pkgver}"
#   rake test
# }

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
