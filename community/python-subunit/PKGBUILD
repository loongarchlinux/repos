# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-subunit
pkgver=1.4.1
pkgrel=1
pkgdesc="Python implementation of subunit test streaming protocol"
arch=('any')
license=('Apache')
url="https://launchpad.net/subunit"
depends=('python-extras' 'python-testtools')
makedepends=('python-setuptools')
checkdepends=('python-fixtures' 'python-hypothesis' 'python-testscenarios')
source=("https://github.com/testing-cabal/subunit/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c21ab0791ccc92e47df5d741deaae56fffe792777958193727f47c2739135fb77880d30b106e525239c0c35841834dd937e2b30fce27fbe73ca9116753147a65')

build() {
  cd subunit-$pkgver
  python setup.py build
}

check() {
  cd subunit-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m testtools.run subunit.test_suite || echo "Tests failed"
}

package() {
  cd subunit-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
