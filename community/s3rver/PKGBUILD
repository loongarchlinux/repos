# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=s3rver
pkgver=3.7.0
pkgrel=1
pkgdesc='A fake S3 server written in NodeJs'
arch=('any')
url='https://github.com/jamhall/s3rver'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('63fa26231935dea78d9a88adb11c5b143fcc13d76571bada8acedb8ae2ad7505f8dd64ae7dcf0fd80e3cda3dc1a648eb928035179ee806ae2c90c81e9632b372')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
