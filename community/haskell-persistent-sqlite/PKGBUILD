# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent-sqlite
pkgname=haskell-persistent-sqlite
pkgver=2.13.0.4
pkgrel=16
pkgdesc="Backend for the persistent library using sqlite3"
url="https://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'sqlite' 'haskell-aeson' 'haskell-conduit' 'haskell-microlens-th'
         'haskell-monad-logger' 'haskell-persistent' 'haskell-resource-pool' 'haskell-resourcet'
         'haskell-unliftio-core' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-fast-logger' 'haskell-hspec' 'haskell-hunit' 'haskell-microlens'
             'haskell-persistent-test' 'haskell-quickcheck' 'haskell-system-fileio'
             'haskell-system-filepath' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('401ca08f968ee3d82c6abbabeb36fba634652195aba46271e611c8c9e0edcb4aeeb850940a258e24230aabc9c81a13321bda526be41f436ce18c93f854f87583')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-build-sanity-exe -fsystemlib -fuse-pkgconfig
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
