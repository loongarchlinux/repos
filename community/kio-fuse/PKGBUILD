# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-fuse
pkgver=4.95.0
pkgrel=1
arch=(x86_64)
pkgdesc="FUSE interface for KIO"
url="https://kde.org/"
license=(GPL)
depends=(kio fuse3)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/unstable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d1168898a296c835c010bcbe48fbde0b90ba631baed8da7eb3ef228856fc41ed'
            'SKIP')
validpgpkeys=('21EC3FD75D26B39E820BE6FBD27C2C1AF21D8BAD') # Fabian Vogt <fabian@ritter-vogt.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
