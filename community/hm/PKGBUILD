# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=hm
pkgver=16.20
_rev=4994
pkgrel=1
pkgdesc='HEVC Test Model - the reference software for HEVC'
arch=('x86_64')
url='https://hevc.hhi.fraunhofer.de/'
license=('BSD')
depends=('gcc-libs')
makedepends=('nasm')
source=("${pkgname}-${pkgver}.zip"::"https://hevc.hhi.fraunhofer.de/trac/hevc/browser/tags/HM-${pkgver}?rev=${_rev}&format=zip"
        '010-hm-use-arch-flags.patch'
        '020-hm-fix-build-with-gcc8.patch')
sha256sums=('021ac73a08c23c4053f08636d3a435a1da02a56066f9f6e4710a7afadc154a9e'
            '3b95cbc9ed5f29fc2cae75493b438aa0de8eae5a20934d8bba3cff1c5215e2c4'
            'ac9cb3cd816a44f570cbfd16be3d1f8ef1d60694daeabc341acc105fe06aa66f')

prepare() {
    # use Arch Linux build flags
    patch -d "HM-${pkgver}" -Np1 -i "${srcdir}/010-hm-use-arch-flags.patch"
    
    # fix build with gcc8 and later
    patch -d "HM-${pkgver}" -Np1 --binary -i "${srcdir}/020-hm-fix-build-with-gcc8.patch"
}

build() {
    # may randomly fail with multiple jobs
    make -C "HM-${pkgver}/build/linux" -j1 -f makefile release{,_highbitdepth}
}

package() {
    cd "HM-${pkgver}"
    
    install -D -m755 bin/*     -t "${pkgdir}/usr/bin"
    install -D -m644 README    -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -D -m644 doc/*.pdf -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -D -m644 COPYING      "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
