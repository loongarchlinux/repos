# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-office
pkgver=1.1.105.202110151816
_featurever=1.1.104.202110151816
_uiver=1.0.1.202110151816
pkgrel=1
pkgdesc='DBeaver plugin to export data to Microsoft Office format'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-apache-poi')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.data.office_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.data.office.ui_${_uiver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar"
        "${pkgname}.info")
sha256sums=('b485e1c0feb5cb6e32c883975ea94e818073eea0514546732dd41391acd451c9'
            '074dfa9c826ac96b746c7c8552822806762296e1ab14878a4ce2f94a606aafda'
            '9ca7509c8a9319489f22f15d1b7d52b2e054d1f251ef8be6030a589a1c30f25c'
            '6fa42e1142f6b9fe1e03d60450e4dd0d8ae66711de831d8e0a6a3bfde7cdeb13')
noextract=("org.jkiss.dbeaver.data.office_${pkgver}.jar"
           "org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.data.office_${pkgver}.jar" \
    "org.jkiss.dbeaver.data.office.ui_${_uiver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
