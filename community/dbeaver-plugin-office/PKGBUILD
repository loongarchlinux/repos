# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-office
pkgver=1.1.115.202203060510
_featurever=1.1.114.202203060510
_uiver=1.0.2.202203060510
pkgrel=1
pkgdesc='DBeaver plugin to export data to Microsoft Office format'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-apache-poi')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.data.office_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.data.office.ui_${_uiver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar"
        "${pkgname}.info")
sha256sums=('cbe677c2d7a93217e9c2d5d94ebfe977589edbb7608be087001d5d5f555ee337'
            '85363230d0f04a691d5ead8817e3e79c83d5fb7d063ebc2f5fff9db66a20de94'
            '4896f92330fb7bc3cf1a69b5747cc722202ce3dacf682bb5ca2dddcf223376c3'
            '68b9003ba86cb5367de4b52fe2ab8d58d80d94f772d4883f107bd32df1ff666c')
noextract=("org.jkiss.dbeaver.data.office_${pkgver}.jar"
           "org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.data.office_${pkgver}.jar" \
    "org.jkiss.dbeaver.data.office.ui_${_uiver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.office.feature_${_featurever}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${_featurever}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
