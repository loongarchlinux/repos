# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Michael DÃ¼ll <mail@akurei.me> PGP-Key: AAAEE882
# Contributor: TDY <tdy@gmx.com>
# Contributor: Sergio Rubio <rubiojr.ml@gmail.com>
# Contributor: Hannes Rist <cowider@gmail.com>
 
pkgname=unp
_ver=2.0~pre9
pkgver="${_ver//[~]/.}"
pkgrel=2
pkgdesc='A script for unpacking a wide variety of archive formats'
arch=(any)
url="https://tracker.debian.org/pkg/unp"
license=(GPL2)
depends=(perl)
optdepends=(
  'unarj: arj support'
  'unrar: rar support'
  'unzip: zip support'
  'bzip2: bzip2 support'
  'p7zip: p7zip support'
  'unace: ace support'
  'cpio: cpio support'
  'sharutils: sharutils'
  'cabextract: cab support'
  'rpmextract: rpm support'
)
source=("https://ftp.debian.org/debian/pool/main/u/unp/unp_${_ver}.tar.xz")
sha512sums=('74c9a6e143c9023bdd456dacff5c3a6e30722e259f324c066fe3eed9e4a09c38b399b381719c8677c2fa84434c1e7720384d782d97c098fa6fbcbdbf4030986e')
b2sums=('9042f48be0f80d5e40a08161a35c282ba458dc98cf13992835bdeecebd5fbadd33b3af9165738332cb9f508550a234a48d314332f00ecdacc808a8051eb0740b')

prepare() {
  mv -v "${pkgname}-${_ver}" "${pkgname}-${pkgver}"
}

build() {
  cd unp-$pkgver/po
  make
}

package() {
  cd unp-$pkgver
  install -Dm755 unp "$pkgdir"/usr/bin/unp
  install -Dm755 ucat "$pkgdir"/usr/bin/ucat
  install -Dm644 debian/unp.1 "$pkgdir"/usr/share/man/man1/unp.1
  install -Dm755 debian/unp.bash-completion "$pkgdir"/usr/share/bash-completion/completions/unp

  cd po
  make DESTDIR="$pkgdir" install
}
