# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-click-didyoumean
pkgver=0.2.0
pkgrel=1
pkgdesc="Enable git-like did-you-mean feature in click"
url="https://github.com/click-contrib/click-didyoumean"
license=('MIT')
arch=('any')
depends=('python-click')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("https://github.com/click-contrib/click-didyoumean/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('690438809d103d2f2b54fc1dabb912b359f5355ba242bb37ef420cd8fcc3ed226911d7216acdcb3574bb0e93231c3a3ca7b00e9e99365955387cfeddeed449ae')

build() {
  cd click-didyoumean-$pkgver
  python -m build -nw
}

check() {
  cd click-didyoumean-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd click-didyoumean-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
