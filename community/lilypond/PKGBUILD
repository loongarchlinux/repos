# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=lilypond
pkgver=2.20.0
pkgrel=3
pkgdesc="An automated music engraving system"
arch=('x86_64')
url="http://lilypond.org"
license=('GPL')
depends=('fontconfig' 'freetype2' 'guile1.8' 'ghostscript' 'glib2' 'pango')
optdepends=('python2: for lilypond-book and other scripts')
makedepends=('flex' 'bison' 'gettext' 'mftrace' 'texinfo' 'fontforge' 't1utils'
             'gsfonts' 'texi2html' 'dblatex' 'texlive-langcyrillic' 'imagemagick'
             'zip' 'rsync' 'netpbm' 'texlive-core' 'tex-gyre-fonts' 'python2')
options=('emptydirs')
source=("http://lilypond.org/downloads/sources/v${pkgver%.*}/$pkgname-$pkgver.tar.gz"
	lilyfontsize.patch)
sha256sums=('595901323fbc88d3039ca4bdbc2d8c5ce46b182edcb3ea9c0940eba849bba661'
            '17b86b7a0b09b73cb5cf8751464571cf6a785c0b1a23db425cc828855a9d8ae6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -e 's|1.82, 1.82|1.82|g' -i configure.ac # Remove version constraint on texi2html

  patch -p1 -i "$srcdir/lilyfontsize.patch"

  ./autogen.sh --noconf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install
}
