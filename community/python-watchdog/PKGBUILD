# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=0.10.3
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
depends=('python-pathtools' 'python-yaml' 'python-argh')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('6061b1393cd07038ee22271101bd6d5c847ee9732b14f63bbb5d94fba9f8273ca728cdc58b6f53a95d8213d0ed35bc71e8bb9c54960bf3aea2f64da066a864f5')

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
