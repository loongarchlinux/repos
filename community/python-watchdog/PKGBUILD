# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=0.10.6
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
depends=('python-pathtools' 'python-yaml' 'python-argh')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('c36a3767d9e2552ebfd26e9791c20137574c308fec31d8977dc085d829bfe1a5204091e6b2610055acd7e1577de4dc066144266b1f0dd9b43ed9452e3706155a')

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
