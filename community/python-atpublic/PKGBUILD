# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=2.3
pkgrel=3
pkgdesc="@public decorator for Python"
arch=('any')
url="https://gitlab.com/warsaw/public"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('88c140c7ca0cfe3db791fd43a97f9fc63de979191a026df4d9f7a34a347b657d7d7b6fb7e31a231ac3adbc5e5d014b8e3c3606b93515a6ad42ea3d101b480329')
b2sums=('60d12db8c56f3e3ce45c2e300759334b662369980b3dc36bff1edbac2d4a13dcf1bd390725eac488969d5a80e5ee5bb598c80e31ed3667c2e7d4f1f88fd00f7c')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
