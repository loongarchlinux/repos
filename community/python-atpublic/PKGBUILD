# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=3.1
pkgrel=1
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache)
depends=(python)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-pytest python-sybil)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('93778e4ab7e109b2692a385bab180bfb709a01f0bbab5c24308635408d4bc6ea37144375b31f81a36288ec7993971e40b67cfd0f545f72b011b86ceb6d108ffe')
b2sums=('f163eff6e955412586517617fb28cd090fb37fdce013973ccbc539b1ae4c4a3c8511f08b026678a9e0310c8d245cb52c932422de65c2a70293d070387a413cac')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
