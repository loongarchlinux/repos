# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=3.0.1
pkgrel=1
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache)
depends=(python)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-pytest python-sybil)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('02c4af8aa02040bba2f09b852e17a0a90c0a6dce8fdabc8362a67c2f326b68369bf5f12f91291f9ff3395685530592561b132f09f9869867516592bd092e0e38')
b2sums=('3912c23c5ee575d7bdec1b598f1f360c54296211600115a413d97c73751b5301b2f3d22152aa62d4d7ac6da9a6cf4202a2deba1de374407ede4358620ee97391')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
