# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=2.0
pkgrel=1
pkgdesc="@public decorator for Python"
arch=('any')
url="https://gitlab.com/warsaw/public"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f48f67213b71a8f41c31c1eec4379ba0d2c3a22cf38d246911e223299700b368a070634a3eaa6c1860474b0025c67f75d870aa6340973e8f8c07ed40682156d2')
b2sums=('2eda14b12705624cc9e6a671bbcb1fc38775f8a040920971fb92ecb739d28b9ae880fc5d0724d618a226158e988cafbdb0e512e35de778880a1cea7cf3de69f1')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
