# Maintainer: David Runge <dvzrv@archlinux.org>

_name=findpython
pkgname=python-findpython
pkgver=0.1.6
pkgrel=1
pkgdesc="A utility to find python versions on your system"
arch=(any)
url="https://github.com/frostming/findpython"
license=(MIT)
depends=(python-packaging)
makedepends=(python-build python-installer python-pdm-pep517 python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('ad073bab732fb9941ec6604afe848c162e54acdd85019734a2583a817727f383a9f495e64c485d0618d2a099c4d7c8f992b1f62270ef4dd9590751686d9ffc62')
b2sums=('79efeedbdfd3bdbe5d03082a2f302c67e5d03ac0cf4c885b37b4aaa4c1cfb3c6258aa39b96abee24a3265a3828325be153a917dc414f474842f7cfb63dd5fdc2')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="test_dir" dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
