# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>

pkgname=python-mediafile
pkgver=0.8.1
pkgrel=3
pkgdesc='Elegant audio file tagging'
arch=(any)
url=https://github.com/beetbox/mediafile
license=(MIT)
depends=(
  python-mutagen
  python-six
)
makedepends=(
  git
  python-flit
  python-pip
  python-poetry
)
checkdepends=(python-nose)
_tag=055c4901a709a83a40d0788db8b92e3debf8c662
source=(git+https://github.com/beetbox/mediafile.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd mediafile
  git describe --tags | sed 's/^v//'
}

build() {
  cd mediafile
  flit build --format wheel
}

check() {
  cd mediafile
  python -m nose
}

package() {
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="${pkgdir}" --ignore-installed --no-deps mediafile/dist/*.whl
  install -Dm 644 mediafile/LICENSE -t "${pkgdir}"/usr/share/licenses/python-mediafile
}

# vim: ts=2 sw=2 et:
