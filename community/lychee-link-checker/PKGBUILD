# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=lychee
pkgname=lychee-link-checker
pkgver=0.10.0
pkgrel=1
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('loongarch64' 'x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
replaces=('lychee-rs')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('c6bf0110543dd941f7e683407d9ac518e19c520199ef4f1b27375f89804f5d5ce89477689731e9404b697d9ad6e4349a4f7b5b2cd1e93663659e5b4299f89dc2')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
