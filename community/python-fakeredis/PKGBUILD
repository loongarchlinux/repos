# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=fakeredis
pkgname=python-fakeredis
pkgver=1.7.0
pkgrel=2
pkgdesc="Fake implementation of redis API (redis-py) for testing purposes"
arch=('any')
url="https://github.com/jamesls/${_pkgname}"
license=('BSD' 'MIT')
depends=('python-redis' 'python-six' 'python-sortedcontainers')
makedepends=('python-setuptools' 'python-lupa')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-mock' 'python-hypothesis' 'python-aioredis')
optdepends=('python-lupa: for lua scripting support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('c3fe4608f8dbdd13288ff3cc45b7a43c246f9597bf0e8ea7f4c9da2b5de6c516176f62291b23b76a38af8298ea93c1d5870509662634c173c020f3c6fbdb4dd2')
b2sums=('7ca30667ca814d89c268e4a1f54192cd9557cd81ff12afb0fc7be7dab4c15a091306fce8a11446081e6be76fa6934821c4abb256b5566a85a585dcf5e8125ee2')

build(){
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}

    PYTHONPATH="$PWD" pytest
}

package() {
    cd ${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
