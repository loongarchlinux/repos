# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname=mini_portile2
pkgname=ruby-mini_portile2
pkgver=2.6.1
pkgrel=1
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/flavorjones/mini_portile/archive/v${pkgver}.tar.gz)
sha256sums=('81223ea6a0367d388ef698d0f2eee00c6a85e54a14bd96b6e5cb1296eb7d53e1')
sha512sums=('c19e25923a5ca1d89e249f6ad24331a1e1ab3450e6274793efacf8297a85e07b015c9de20ae6596e919b13a7ff953bd7bb51f272d3a5e75540acc1bc2355f266')

prepare() {
  cd mini_portile-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0|' -i ${_gemname}.gemspec
}

build() {
  cd mini_portile-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd mini_portile-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
