# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sorted_set
pkgver=1.0.3
pkgrel=1
pkgdesc='Implements a variant of Set whose elements are sorted in ascending order'
arch=(any)
url='https://github.com/knu/sorted_set'
license=(BSD)
depends=(ruby-set ruby-rbtree)
checkdepends=(ruby-rake ruby-test-unit)
options=(!emptydirs)
source=(https://github.com/knu/sorted_set/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8bb431626d5d4c6fd28a708eb46f8792da3f89855d79f6ade886c8ac65accfda')

prepare() {
  cd sorted_set-$pkgver
  sed -i -r 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -printf "%P\\\\0"|' sorted_set.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sorted_set-$pkgver
  gem build sorted_set.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sorted_set-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sorted_set-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sorted_set-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
