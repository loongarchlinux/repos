# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-keyboard
pkgver=2.7.0
pkgrel=1
pkgdesc='Switchboard Keyboard Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-keyboard
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  ibus
  libgee
  libgnomekbd
  libgranite.so
  libhandy-1.so
  libxml2
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=12283712b726ad7b3838fc1e5a5760246ebbad30
source=(git+https://github.com/elementary/switchboard-plug-keyboard.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-keyboard

  git describe --tags
}

build() {
  arch-meson switchboard-plug-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
