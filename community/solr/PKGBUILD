# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=solr
pkgver=8.8.2
pkgrel=1
pkgdesc="Open source enterprise search platform built on Apache Lucene"
arch=('any')
url="https://lucene.apache.org/solr/"
license=('Apache')
depends=('bash' 'java-runtime>=8')
makedepends=('ant' 'ivy' 'java-environment>=8' 'strip-nondeterminism')
backup=("etc/${pkgname}/server/jetty-http.xml"
        "etc/${pkgname}/server/jetty-https.xml"
        "etc/${pkgname}/server/jetty-https8.xml"
        "etc/${pkgname}/server/jetty-ssl.xml"
        "etc/${pkgname}/server/jetty.xml"
        "etc/${pkgname}/server/webdefault.xml"
        "etc/${pkgname}/${pkgname}.in.sh"
        "etc/${pkgname}/${pkgname}.xml"
        "etc/${pkgname}/zoo.cfg")
source=("https://archive.apache.org/dist/lucene/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tgz"{,.asc}
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha512sums=('0cd7a49107883dededcdd84af7cd3d810fcf88a5e22dace8554c8ef105692837bb527d8478e63a196b889246b11e893e4518c487e68260b5c53ba6de23e44f19'
            'SKIP'
            'a34121427e9d3362e3424c8bf1f13e50dccbee4a1870bd8713a39c8bf79450462b12a98cc981af3a3522d176094b05e300468465100f27bcd44f08129f324430'
            '06e5e40b96d2b6668790e4b166fc2867b9e694a2c72fd57eec702526e009b8b0495acbe16a5a27e259827477f4783ce87742f1f806254d8a2baec23b0b317058'
            '9cc97763a50c11c305b06ce07f0b2936b8fcb0b1d43f8b469fe1399850cb009fc1eba297d295a386a556e5c042e189dba1b1fc96a54aa46964ed4db8e17d40a8')
b2sums=('b504a07f0f62a071fb69c5ccd05eaf83ee42282df2177d08242c8f7ce2bb907d5a52b2019f21cb893cf9a1f380d8d27884dbb4206a7d73c8bff6b8cd3017c830'
        'SKIP'
        '05f5927019eb0e622417092399ed8358184cd332ba7bf929af74e64cfb190f7e7c88c0caaac9b3fa182b063085045431bf74e7b7dd9d31f7041778fc92c923f5'
        '60ff37059a4ab8362551518d56ee105e7d19199727605d5ad6f3236bd31dde1cc5fa37ffa37009820ee3115da36ae64df4754454cef1db51d1c13cac039245cb'
        'a596a3d33e8466861fc0947d9b573812835ca5adccf20dac7c9de5ba88b206de5f0cb2e317edae239b7c47cfb0e3ef5f5c8778ee719d6b955de934e9e8267596')
# list of trusted signing keys: https://downloads.apache.org/lucene/KEYS
validpgpkeys=('2085660D9C1FCCACC4A479A3BF160FF14992A24C'  # Ishan Chattopadhyaya <ishan@apache.org>
              'E58A6F4D5B2B48AC66D5E53BD4F181881A42F9E6'  # Ignacio Vera (CODE SIGNING KEY) <ivera@apache.org>
              '81D3EB0408B4E1EB10AF443BA4F4C886B29BC2F4'  # Alan Woodward (CODE SIGNING KEY) <romseygeek@apache.org>
              '86EDB9C33B8517228E88A8F93E48C0C6EF362B9E'  # Mike Drob (CODE SIGNING KEY) <mdrob@apache.org>
              '38DA0C3CE8181703A08E4D57377C3BA26AD29C0A'  # Bruno Roustant <broustant@apache.org>
              '50E3EE1C91C7E0CB4DFB007B369424FC98F3F6EC'  # Houston Paul Putman IV (CODE SIGNING KEY) <houston@apache.org>
              '902CC51935C140BF820230961FD5295281436075'  # Jason Gerlowski (CODE SIGNING KEY) <gerlowskija@apache.org>
              '7D8D90F8F64F23077AC87CF7CB77CB79928BB0EC'  # Atri Sharma <atri@apache.org>
              'CFCE5FBB920C3C745CEEE084C38FF5EC3FCFDB3E'  # Noble Paul (CODE SIGNING KEY) <noble@apache.org>
              'FBC25D7E1712025294FE66590A6AA179B9BBF45E'  # Timothy Potter (CODE SIGNING KEY) <thelabdude@apache.org>
)

prepare() {
  cd "$pkgname-$pkgver"
  ant ivy-bootstrap
  rm -rvf "${pkgname}/bin/init.d"
}

build() {
  cd "$pkgname-$pkgver"
  cd "${pkgname}"
  ant compile
  ant server
  ant dist
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . \
    -type f \
    -iname "*.jar" \
    -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

# TODO: make org.apache.solr.cloud.MetricsHistoryIntegrationTest.testGet pass
# seemingly requires lucene to be built as well X_X
# check() {
#   cd "$pkgname-$pkgver"
#   cd "${pkgname}"
#   ant test
# }

package() {
  cd "$pkgname-$pkgver"
  # removing unneeded sources and build artifacts
  rm -rvf ${pkgname}/contrib/*/src
  find . -type f \( -iname "*build.xml" -o -iname "*ivy.xml" \) -delete
  # script
  install -vDm 755 "${pkgname}/bin/${pkgname}" -t "${pkgdir}/usr/bin"
  # configuration
  install -vDm 644 "${pkgname}/bin/${pkgname}.in.sh" \
    -t "${pkgdir}/etc/${pkgname}"
  install -vDm 644 "${pkgname}/server/etc/"*.xml \
    -t "${pkgdir}/etc/${pkgname}/server"
  install -vDm 644 "${pkgname}/server/${pkgname}/${pkgname}.xml" \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vDm 644 "${pkgname}/server/${pkgname}/zoo.cfg" \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vdm 755 "${pkgdir}/usr/share/${pkgname}"
  # copy application
  cp -rvL "${pkgname}/"{bin,contrib,dist,docs,example,licenses,server} \
    "${pkgdir}/usr/share/${pkgname}"
  # symlink configuration into place
  ln -svf "/etc/${pkgname}/${pkgname}.in.sh" \
    "${pkgdir}/usr/share/${pkgname}/"
  ln -svf "/etc/${pkgname}/${pkgname}.xml" \
    "${pkgdir}/usr/share/${pkgname}/server/${pkgname}"
  ln -svf "/etc/${pkgname}/zoo.cfg" \
    "${pkgdir}/usr/share/${pkgname}/server/${pkgname}"
  for config in {jetty,jetty-{http,https,https8,ssl},webdefault}.xml; do
    ln -svf "/etc/${pkgname}/server/${config}" \
      "${pkgdir}/usr/share/${pkgname}/server/etc/${config}"
  done
  # logs directory
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  # docs
  install -vDm 644 "${pkgname}/"{CHANGES,LUCENE_CHANGES,NOTICE,README}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "../${pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
