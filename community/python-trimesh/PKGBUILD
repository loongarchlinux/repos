# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-trimesh
pkgver=3.8.11
pkgrel=1
pkgdesc='Trimesh is a pure Python library for loading and using triangular meshes with an emphasis on watertight surfaces'
arch=('any')
url="https://github.com/mikedh/trimesh"
license=('MIT')
depends=(python python-setuptools python-numpy)
optdepends=('python-networkx: graph operations'
            'python-pyglet: preview windows'
            'python-rtree: vector path handling'
            'python-scipy: convex hulls'
            'python-shapely: vector path handling'
            'python-pillow: load images'
            'python-jsonschema: validate JSON schemas like GLTF'
            'python-requests: network requests'
            'python-msgpack: serialize into msgpack'
            'python-lxml: handle XML better and faster than built- in XML'
            'python-sympy: do analytical math'
            'python-xxhash: hash ndarrays faster than built-in MD5/CRC')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mikedh/trimesh/archive/${pkgver}.tar.gz)
sha512sums=('c35c39069b3e03a2037811160b813b76cdf8a13cd41ca89cb157bd312992fb16638ab25a0a9930b969105c08c868a6ff0c0612b183003b32aa9249b842db5899')

build() {
  cd trimesh-${pkgver}
  python setup.py build
}

package() {
  cd trimesh-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE.md "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
