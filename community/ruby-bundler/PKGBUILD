# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonne Ha√ü <me@jhass.eu>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=ruby-bundler
_gemname="${pkgname#ruby-}"
pkgver=2.3.22
pkgrel=1
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably"
arch=('any')
url='https://bundler.io'
license=('MIT')
depends=('ruby' 'rubygems')
makedepends=('git' 'ruby-rdoc' 'ruby-rake')
options=('!emptydirs')
_commit='44fb4c9ef5a8b4892132d302daf0d50ec08c06b0'
source=("$pkgname::git+https://github.com/rubygems/rubygems#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed -e 's/^bundler-v//'
}

build() {
  cd "$pkgname/bundler"

  rake build
}

package() {
  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/$_gemname/pkg/$_gemname-$pkgver.gem"

  # remove unreproducible/unnecessary files
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.md"

  # move man pages
  install -vd "$pkgdir/usr/share/man/"{man1,man5}
  mv -vt "$pkgdir/usr/share/man/man1" "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/lib/bundler/man"/*.1
  mv -vt "$pkgdir/usr/share/man/man5" "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/lib/bundler/man"/*.5
}
