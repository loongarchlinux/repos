# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=python-pytest-tap
_pyname=${pkgname#python-}
pkgver=3.1
pkgrel=1
pkgdesc="Test Anything Protocol (TAP) reporting plugin for pytest."
arch=(any)
url="https://github.com/python-tap/pytest-tap"
license=('BSD')
depends=('python-pytest' 'python-tappy')
makedepends=('python-setuptools' 'python-tox')
source=("https://files.pythonhosted.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
md5sums=('bab3a774f4a4564c2196f6b40e5a81db')

build() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python setup.py build
}

check() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	tox
}

package() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
}
