# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.3.4
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'ansible-lint' 'python-cerberus' 'python-click'
'python-click-help-colors' 'python-cookiecutter' 'python-enrich' 'python-jinja'
'python-packaging' 'python-paramiko' 'python-pluggy' 'python-rich'
'python-selinux' 'python-setuptools' 'python-subprocess-tee' 'python-yaml')
makedepends=('python-setuptools-scm' 'python-simplejson' 'python-toml')
checkdepends=('python-ansi2html' 'python-mock' 'python-pexpect'
'python-pytest' 'python-pytest-helpers-namespace' 'python-pytest-html'
'python-pytest-mock' 'python-pytest-testinfra'
'python-pytest-verbose-parametrize' 'python-pytest-xdist' 'yamllint')
optdepends=('molecule-docker: for the docker driver'
            'molecule-podman: for the podman driver'
            'molecule-vagrant: for the vagrant driver'
            'python-pywinrm: for Windows support'
            'python-pytest-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8ccc6e121d028bc9d477504a3e5067486d4a3e37f2c27a9686b068c9dac2185db7f965960affb4f9e958bb68d3c3a72786459ea3d221d781377d899b0d75d5a5')
b2sums=('af704a8a8d709323441e6e6057bf7a62b0d89aa582eff4d9139b521ed0729c36836f324cd1741f441fae4ed4b9c148de76c2502a01ce5a7b142bfe32bc7fd391')

prepare() {
  cd "$pkgname-$pkgver"
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  sed -e '/addopts/d' \
      -e '/testpaths/d' \
      -e '/error/d' \
      -i setup.cfg
  # allegedly cerberus is not compatible with py39, but we don't mind:
  # https://github.com/pyeve/cerberus/issues/568
  sed -e '/cerberus/ s/,.*//' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  local _test_dir='test_dir'

  # install into test location
  mkdir -vp "${_test_dir}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${_test_dir}"

  export PYTHONPATH="${PWD}/${_test_dir}/usr/lib/python${_python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"

  pytest -v "${_test_dir}/usr/lib/python${_python_version}/site-packages/molecule/test/unit/"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
