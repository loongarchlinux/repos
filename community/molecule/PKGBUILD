# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=4.0.0
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-cerberus python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-packaging python-pluggy python-pyyaml python-rich)
makedepends=(python-build python-setuptools-scm python-setuptools-scm-git-archive python-installer)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-mock python-pytest-testinfra python-pytest-xdist
yamllint)
optdepends=(
  'ansible: for the ansible verifier'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('920a47ecdebb413c1faec09ca8e4597a15f6f073685136ecf5c30211685920156394f5af8618d1040b81513c7480bfc1c0778bac0a88e99c9afaedd83f306371')
b2sums=('cd22f094848bd92ab50fd18d32a2492a700fcc222c2d32b7884a7893c798c4b15abed2ba00a5b7874eef6850645e7b9bb696b500f1508335587cac37620722ba')

prepare() {
  cd $pkgname-$pkgver
  # allegedly cerberus is not compatible with py39, but we don't mind:
  # https://github.com/pyeve/cerberus/issues/568
  sed -e '/cerberus/ s/,.*//' -i setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  # install into test location
  mkdir -vp "$_test_dir"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$_test_dir/usr/bin:$PATH"
  pytest -v "$_test_dir/$_site_packages/molecule/test/unit/" -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
