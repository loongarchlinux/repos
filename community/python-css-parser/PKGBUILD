# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=css-parser
pkgname=python-css-parser
pkgver=1.0.6
pkgrel=5
pkgdesc="A CSS Cascading Style Sheets library for Python"
arch=('any')
url="https://github.com/ebook-utils/${_pkgname}"
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc}
        $pkgname-python310.patch::https://github.com/ebook-utils/css-parser/commit/6b5e22217d69.patch)
sha512sums=('4c623541e1145c91458e7e2028f6a0de652ae2189dc99672fc72f098799d6349edb051bafc0901258481fa3422346303d2ef634392889ee83a29814609cdbd43'
            'SKIP'
            '02d3b42cade5c6d959b9c1676793dd5098c8abed21b74f60d08e4da538af9a286aaaf84cd994285bf9647e4f565ba58a2f1b8e2e18cdb898d165f7a945019d65')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}
    patch -Np1 -i ../$pkgname-python310.patch
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
