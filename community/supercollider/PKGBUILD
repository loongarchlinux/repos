# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

_name=SuperCollider
pkgname=supercollider
pkgver=3.11.2
pkgrel=4
pkgdesc="Environment and programming language for real time audio synthesis and algorithmic composition"
arch=('loongarch64' 'x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc'  'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets')
makedepends=('abletonlink' 'alsa-lib' 'avahi' 'boost' 'cmake' 'emacs' 'fftw'
'libsndfile' 'qt5-tools' 'readline' 'systemd-libs' 'yaml-cpp')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=('libscsynth.so' "sclang=${pkgver}" "scsynth=${pkgver}")
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source.tar.bz2"{,.asc}
        "${pkgname}-3.11.1-devendor_abletonlink.patch::https://github.com/supercollider/supercollider/pull/4836/commits/95bd9721226f3cb1bba903bea8154cee5859cfb8.patch"
        "${pkgname}-3.11.2-fix_4992.patch::https://github.com/dvzrv/supercollider/commit/9563fb5e73425a2ee0370162178964cceb559d5d.patch"
        "${pkgname}-3.11.2-boost-1.74.patch::https://github.com/supercollider/supercollider/commit/cc873049ff9249a33567e4cbcdeffeb6806a30d6.patch"
)
install="${pkgname}.install"
sha512sums=('636062c6acf8be43260632287b587f167352079c4e86ede34d1c6056180bfdb3ae50d6a3ff4f349327a3ab6af0eb01ac503d6b22667b4799dfcddd9c6489a23c'
            'SKIP'
            '972aaebde8de5aaf5d6a01e59f3e6fe3b64fba63495bc0216cc91a5f7590fc987f8c592503556f97dd6c7e7fd7437915fc6d02fdd447fd8a80d43d24ab1216d1'
            'd957b7b1c272a121dd24308b1d5073a0bcce4c04aed56af05819003ed149e6ec632fb323c9cc195685a51f28cb9489fd179eff7d6c3be52158f0a404a1990b69'
            'a014924881ea8a7b84d7a3cc77d3e0e735ef5edb28062e7859a9a227f86cc00c1c04753b1ce7fca423be513dc8c476236814fcd16ab154fda948948736472814')
b2sums=('3713561e5ebee6dd4c08baa8aaaf4a422ab983480c76b2176373ce3cd080128f9392b50482e02dc82dce235e928d058c42722e0863dad9f6d2dd086894cd85da'
        'SKIP'
        '0c84bb1e31eee267a8bce5cdc8fd7da97e3336a565eacc8759bc3fe33c5f4c1f6cf6dfcf2ea01405676ada660d1b6ac09bce3fb2faa4987599289e5fc5cdc0d7'
        'd496b364f63c04e5bc93542fcdb537fcbc86a5687202555cabe68e28c1dbfc131aaab2f4b76a2a47d26785e9f029408f8fc02f1d5a2e19cfdea65c2159f5a26f'
        '734bee43a90951b646d3c4ae1273b85257765e9156bd1b9f3dabafeda9728d7af2257155f990bc0b1038c7d1e372f95f5c7686fb7bff9f5c092b988f52816faa')
validpgpkeys=('0B7D1EA4E32B5C3C4275D3C70796CC538BEFF177') # Brian Heim <self@brianlheim.com>


prepare() {
  mv -v "${_name}-${pkgver}-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # devendoring Ableton Link:
  # https://github.com/supercollider/supercollider/issues/4818
  # https://github.com/supercollider/supercollider/pull/4836
  echo "Applying patch: ../${pkgname}-3.11.1-devendor_abletonlink.patch"
  patch -Np1 -i "../${pkgname}-3.11.1-devendor_abletonlink.patch"
  # fix linker issues with libscsynth.so (backported patch)
  # https://github.com/supercollider/supercollider/issues/4992
  echo "Applying patch: ../${pkgname}-3.11.2-fix_4992.patch"
  patch -Np1 -i "../${pkgname}-3.11.2-fix_4992.patch"

  echo "Applying patch: ../${pkgname}-3.11.2-boost-1.74.patch"
  patch -Np1 -i "../${pkgname}-3.11.2-boost-1.74.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DLIBSCSYNTH=ON \
        -DFORTIFY=ON \
        -DSYSTEM_ABLETON_LINK=ON \
        -DSYSTEM_BOOST=ON \
        -DSYSTEM_YAMLCPP=ON \
        -DSC_VIM=OFF \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run make test ARGS="-VV -d -j1" -C build
}

package() {
  depends+=('libasound.so' 'libavahi-common.so' 'libavahi-client.so'
  'libboost_filesystem.so' 'libboost_program_options.so' 'libboost_regex.so'
  'libboost_thread.so' 'libfftw3f.so' 'libjack.so' 'libsndfile.so'
  'libreadline.so' 'libudev.so' 'libyaml-cpp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,{CHANGELOG,README,README_LINUX}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
