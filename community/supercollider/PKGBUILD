# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

_name=SuperCollider
pkgname=supercollider
pkgver=3.12.1
pkgrel=4
pkgdesc="Platform for audio synthesis and algorithmic composition"
arch=('loongarch64' 'x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc'  'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets')
makedepends=('abletonlink' 'alsa-lib' 'avahi' 'boost' 'cmake' 'emacs' 'fftw'
'libsndfile' 'qt5-tools' 'readline' 'systemd-libs' 'yaml-cpp')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=('libscsynth.so' "sclang=${pkgver}" "scsynth=${pkgver}")
source=(
    "https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source.tar.bz2"{,.asc}
)
install="${pkgname}.install"
sha512sums=('8fc8540e8ee952ff30f41feb49e915366ee7c5d44d7af6137bba2ccf98d521d05fd18e103d9943120370cbee970700ace15a090907a887acdc29405e919ad102'
            'SKIP')
b2sums=('ffb2d0313a27bc8a067588fa1ee26e59f0261b16c4928e48b94c3011d7fe58c7c9588e6ba3b7007b45eec74fbeedfaca781ebfd8baa93bd1f9c9797ea15a2b21'
        'SKIP')
validpgpkeys=('2E1C4FC2F6BB58FA157B21B8064B501EB7405F04') # Marcin PÄ…czkowski (dyfer)


prepare() {
  mv -v "${_name}-${pkgver}-Source" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DLIBSCSYNTH=ON \
        -DFORTIFY=ON \
        -DSYSTEM_ABLETON_LINK=ON \
        -DSYSTEM_BOOST=ON \
        -DSYSTEM_YAMLCPP=ON \
        -DSC_VIM=OFF \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run make test ARGS="-VV -d -j1" -C build
}

package() {
  depends+=('libasound.so' 'libavahi-common.so' 'libavahi-client.so'
  'libboost_filesystem.so' 'libboost_program_options.so' 'libboost_regex.so'
  'libboost_thread.so' 'libfftw3f.so' 'libjack.so' 'libsndfile.so'
  'libreadline.so' 'libudev.so' 'libyaml-cpp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,{CHANGELOG,README,README_LINUX}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
