# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=1.29.0
pkgrel=1
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-nio4r' 'ruby-timers')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/socketry/async/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c251cae1edae9fcb4687a29cc5b4a292817dea21159677a054abc56cfda07724')
sha512sums=('6f2149125b7b5d97cd72578f165b1f74cd36fd5bc00f060c5b941dfd70ceb2daaff49b5f7d6949ad97a02b086cb75e47cdab3a4ebcc59fae6ef79a4a0e98b514')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
