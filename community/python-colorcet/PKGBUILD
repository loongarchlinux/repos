# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=colorcet
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=2.0.2
pkgrel=2
pkgdesc="Set of useful perceptually uniform colormaps for plotting scientific data"
arch=(any)
url="https://colorcet.pyviz.org/"
license=(BSD)
makedepends=(python-setuptools python-param python-pyct python2-setuptools python2-param python2-pyct)
source=("https://files.pythonhosted.org/packages/source/c/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('514813790a74b578c3eaff76b2102274c2ba8b0239c9504586df685223007dee')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

package_python-colorcet() {
    depends=(python-param python-pyct)
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-colorcet() {
    depends=(python2-param python2-pyct)
    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    mv "${pkgdir}"/usr/bin/colorcet{,2}

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
