# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.36
pkgrel=5
pkgdesc='Plugins for Geany'
arch=(x86_64)
url='https://plugins.geany.org/'
license=(GPL)
depends=("geany>=$pkgver" ctpl gpgme gtkspell3 lua51 python vte3 webkit2gtk libgit2)
makedepends=(cppcheck intltool)
source=("$url$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig}
        9497d829e1b207eb83575dc6f617feecfb89bc16.patch)
sha256sums=('ebe18dd699292174622e8cb8745b020ada8a5be3b604ab980af36e8518df7ce6'
            'SKIP'
            'ecf3c6725c3d9f482274ecdbce66459d168162690beec2c8ff5b6ae9e0e8b318')
validpgpkeys=(6D0E68FCE198824C27C90EB0B507ACD04BA283C9) # Frank Lanitz <frank@geany.org>

prepare() {
  cd $pkgname-$pkgver

  # https://github.com/geany/geany-plugins/commit/9497d829e1b207eb83575dc6f617feecfb89bc16
  patch -Np1 -i ../9497d829e1b207eb83575dc6f617feecfb89bc16.patch

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr --libexecdir=/usr/lib -enable-all
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
