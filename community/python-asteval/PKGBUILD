# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.19
pkgrel=1
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/newville/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-numpy')
source=("https://github.com/newville/asteval/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5d5bd7cca7503f48c433b46996c5d23a7d13164bab8e26d3eb4bd721e7720fd43905dbe9a5eb19cb38fc8296aacf3fc7fa508ff80977ace2ed2eef16e5bbb977')

build() {
  cd asteval-$pkgver
  python setup.py build
}

check() {
  cd asteval-$pkgver
  python -m pytest
}

package() {
  cd asteval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
