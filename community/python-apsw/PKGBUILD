# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgname=python-apsw
pkgver=3.36.0
pkgrel=2
pkgdesc="Python wrapper for SQLite"
arch=('loongarch64' 'x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('python' 'sqlite')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('22848effb6d4d69e486750b1706316551b593c055b40b32e897161d6d78642a36ccf72cf86ff5e7e1a2520b33fcdcf50116338aaa689630b00e1b6b3362744c0')
b2sums=('fc942e9402488352b344e8e3a749194f84657d929b6e0258fe065aa3a9698e7e9744f61c328e0873f7c6bc1233cfbca0b019c4c801121c2d35d94c06f4766e37')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -fPIC -shared -o testextension.sqlext src/testextension.c
  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
}

package() {
  cd "$srcdir"/apsw-${pkgver}-r1

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
