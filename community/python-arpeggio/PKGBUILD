# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-arpeggio
pkgver=1.10.2
pkgrel=3
pkgdesc="Packrat parser interpreter"
url="https://github.com/textX/Arpeggio"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-pytest-runner' 'python-setuptools' 'python-wheel')
source=("https://github.com/textX/Arpeggio/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c6a162f9ab854bac7e2bc9e5d5553c2dbcc068fcf61607149727cd4c8a7c87c63cab765b423c8ea76e09fa81ccc42157d724388a81e22864580ccf734998142a')

build() {
  cd Arpeggio-$pkgver
  python setup.py build
}

check() {
  cd Arpeggio-$pkgver
  python setup.py pytest
}

package() {
  cd Arpeggio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
