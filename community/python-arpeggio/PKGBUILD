# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-arpeggio
pkgver=1.9.2
pkgrel=3
pkgdesc="Packrat parser interpreter"
url="https://github.com/textX/Arpeggio"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/textX/Arpeggio/archive/v$pkgver.tar.gz")
sha512sums=('fa1057caaa0b5d26937c6e5aed719f64b293d3332e3ce0277a586bd48ca136792a4811c292fd87f2e58148874e008a6f2f621630052c5dfaf0e0efe2e8039353')

build() {
  cd Arpeggio-$pkgver
  python setup.py build
}

check() {
  cd Arpeggio-$pkgver
  python setup.py pytest
}

package() {
  cd Arpeggio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
