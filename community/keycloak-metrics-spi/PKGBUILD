# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.5.3
_jarversion=2.5.4
pkgrel=2
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle' 'keycloak')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('74b76ea585ddfe9201dc355c0a401c64180a748abac600d1353453df97ab886a291cd930f372f7ec35a553b1f6498373e73056c6fe39623730f514ce9581d1fc')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/usr/share/java/keycloak/providers/"
  install -Dm644 "build/libs/keycloak-metrics-spi-${_jarversion}-SNAPSHOT.jar" "${pkgdir}/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar"
  ln -s "/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar" "${pkgdir}/usr/share/java/keycloak/providers/keycloak-metrics-spi-${pkgver}.jar"
}
