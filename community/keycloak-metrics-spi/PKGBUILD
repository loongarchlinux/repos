# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.3.1
_jarversion=2.3.2
pkgrel=1
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle' 'keycloak')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('c7ecfea5bedb4f4a2f798a32b472bd883fcd7850023c3515ae14329a6014b8b2247526707d2c4f4494e33d6ff69d876284fc42f9ba9ad8e1b9cfb323321e944c')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/keycloak/standalone/deployments/"
  install -Dm644 "build/libs/keycloak-metrics-spi-${_jarversion}-SNAPSHOT.jar" "${pkgdir}/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar"
  ln -s "/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar" "${pkgdir}/opt/keycloak/standalone/deployments/keycloak-metrics-spi-${pkgver}.jar"
}
