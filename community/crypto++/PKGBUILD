# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=8.2.0
pkgrel=3
pkgdesc='A free C++ class library of cryptographic schemes'
arch=(x86_64)
url=https://www.cryptopp.com/
license=(custom)
depends=(gcc-libs)
makedepends=(
  git
  unzip
)
source=(
  git+https://github.com/weidai11/cryptopp.git#tag=9dcc26c58213abb8351fbb1b2a7a1d2c667366e4
  #git+https://github.com/weidai11/cryptopp.git?signed#tag=9dcc26c58213abb8351fbb1b2a7a1d2c667366e4
  cve-2019-14318.patch
)
sha256sums=(
  SKIP
  d9cabc1eab0dfbab1d4bfff75fa99766995089e52b83a175918e738516efbb41
)
validpgpkeys=(B8CC19802062211A508B2F5CCE0586AF1F8E37BD) # Jeffrey Walton <noloader@gmail.com>

pkgver() {
  cd cryptopp

  git describe --tags | sed 's/^CRYPTOPP_//; s/_/./g'
}

prepare() {
  cd cryptopp

  patch -Np0 -i ../cve-2019-14318.patch
}

build() {
  export CXXFLAGS="$CXXFLAGS -DNDEBUG -fPIC"
  make -C cryptopp dynamic cryptest.exe libcryptopp.pc
}

check() {
  make -C cryptopp test
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C cryptopp install
  install -Dm 644 cryptopp/License.txt -t "${pkgdir}"/usr/share/licenses/crypto++/
  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm -rf "${pkgdir}"/usr/{bin,share/cryptopp}
}

# vim: ts=2 sw=2 et:
