# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=prettytable
pkgname=python-prettytable
pkgver=3.4.1
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://github.com/jazzband/prettytable"
arch=('any')
license=('BSD')
depends=('python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-lazy-fixture')
conflicts=('python-ptable')
source=("https://files.pythonhosted.org/packages/source/p/prettyTable/prettytable-${pkgver}.tar.gz")
sha512sums=('47a00d5d3bd6be77734abf0c6a50ac13f3d6fb72d8cc7bd2d760c860401f46ee94f766063eaeb30cf3d67c6f33fca9e3a1ae9d68af61139704bb1e23ba649ce4')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd prettytable-$pkgver
  python setup.py build
}

check() {
  cd prettytable-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd prettytable-$pkgver
  python setup.py install --root="$pkgdir/"
}
