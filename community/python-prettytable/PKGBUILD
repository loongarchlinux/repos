# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=prettytable
pkgname=python-prettytable
pkgver=3.3.0
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://github.com/jazzband/prettytable"
arch=('any')
license=('BSD')
depends=('python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-lazy-fixture')
conflicts=('python-ptable')
source=("https://files.pythonhosted.org/packages/source/p/prettyTable/prettytable-${pkgver}.tar.gz")
sha512sums=('e190717e2ea89e8faddc7015fd5118e0d93ac5f5318641ce5960b22b4ea3740ff12c4d15812b33655252f258c96f9e848f554d1e678d39761c28025ba7772609')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd prettytable-$pkgver
  python setup.py build
}

check() {
  cd prettytable-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd prettytable-$pkgver
  python setup.py install --root="$pkgdir/"
}
