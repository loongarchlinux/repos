# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Alexander Phinikarides <alexisph _AT_ gmail _DOT_ com>

pkgbase='python-pyroute2'
_pkgbase="${pkgbase//python-/}"
pkgname=("python-${_pkgbase}")
pkgdesc="A pure Python netlink and Linux network configuration library"
pkgver=0.6.9
pkgrel=1
arch=('any')
url="https://docs.pyroute2.org/"
license=('GPL2' 'Apache')
depends=('python')
makedepends=('python' 'python-pip' 'python-setuptools' 'python-setuptools-scm' 'python-sphinx' 'git' 'python-aafigure' 'twine' 'python-black')
checkdepends=('python-pytest')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/svinota/${_pkgbase}/archive/${pkgver}.tar.gz"
        'fix_makefile.patch')
sha512sums=('4de8b8dc897959e2dc367dec460a918ace6cc096c2f634433b39d1600bafa4ca6841f54280d581f6331bf2b9776cac75e721afa2bbabbd329cc0a12b34e91fe8'
            '05742f086de35b382aa97ac5e0bce0fd5bb2454f85d60c230a474700859af33806f45001ff7a072b71995494d747333a7f22761f5859bbf06fef59ccd81f86a9')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # Update the version manually. Otherwise it would look for a git tag which is not present in the distribution.
  sed -i "s/version = get_project_version.*$/version = '${pkgver}'/" util/update_version.py
  sed -i "s/^VERSION=.*\$/VERSION='${pkgver}'/" tests/conf.sh

  # there is some weirdness in the Makefile with respect to depencies
  # TODO : this needs to be submitted upstream
  patch -p1 -N -i "${srcdir}/fix_makefile.patch"

}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make dist
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # upstream tests are not meant to be executed downstream
  :
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make root="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
