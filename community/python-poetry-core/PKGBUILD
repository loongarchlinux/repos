# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_pkgname=poetry-core
pkgname=python-poetry-core
pkgver=1.0.8
pkgrel=1
pkgdesc="Poetry PEP 517 Build Backend & Core Utilities"
arch=('any')
url="https://github.com/python-poetry/${_pkgname}"
license=('MIT')
_deps=('jsonschema' 'lark-parser' 'packaging' 'tomlkit')
depends=("${_deps[@]/#/python-}")
makedepends=(python-{build,installer})
checkdepends=(git python-pytest python-pytest-mock python-virtualenv)
conflicts=('python-poetry<1.1.0')
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('b8795006e169a24e0e114ba1c03217716cd190e8d2716c796ddc5ed0382dbca8')
b2sums=('1f663d927aee1f18a9ff38cbf95809538bf9c9e8079d040c240fbc1ffa9e492c6fd20c69f89ddf7e68ddea05beee3b909fdee02e93f47437fffa687dc492ec9a')

prepare() {
	cd "$_archive"
	# remove vendored dependencies
	sed -i '/^__version__/!d' poetry/core/__init__.py
	rm -r poetry/core/_vendor
	# be a proper namespace, python3-only ;)
	rm poetry/__init__.py
}

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	# only works inside git repositories
	pytest \
		-k 'not test_default_with_excluded_data and not test_default_src_with_excluded_data'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
