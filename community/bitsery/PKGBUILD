# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=bitsery
pkgver=5.2.2
pkgrel=1
pkgdesc="Header only C++ binary serialization library"
arch=(any)
url="https://github.com/fraillt/bitsery"
license=(MIT)
makedepends=(cmake gtest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fraillt/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('54f62f91729a8ee161404a44398b0f770ec7378bb0def3d007c5fd60c421c855e533df2484346092b792039f53a7f4659b332dde101f23f1b335158b4dc639bb')
b2sums=('d926005ab8401b757feef217f0fe0782573441feca18d937dbfbdb988ec72cc14cbfb4becb04d26701c7f68833e9cd006ecddb091a94e92580abe0f82dfbad61')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DBITSERY_BUILD_TESTS=ON \
        -Wno-dev \
        -B build \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
}

check() {
  ctest --test-dir build/tests
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "$pkgname-$pkgver/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
