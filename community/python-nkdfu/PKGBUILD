# Maintainer: David Runge <dvzrv@archlinux.org>

_name=nkdfu
pkgname=python-nkdfu
pkgver=0.1
pkgrel=3
pkgdesc="Python tool for Nitrokeys' firmware update"
arch=('any')
url="https://github.com/Nitrokey/nkdfu"
license=('GPL2')
depends=('python-fire' 'python-tqdm' 'python-intelhex' 'python-libusb1')
makedepends=('python-build' 'python-flit' 'python-install' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('63c1a89df0818b704d32167df5237afd20d26b4f9d6df3c830fa15cc02f01f90')
b2sums=('d85d7280edabfe223a0c273d30d4388c78ba50db81a649eb6a45f678df3368e2700498eac12670342fad19f000108f14167adaa010e0ec48024a85c5222be70a')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
