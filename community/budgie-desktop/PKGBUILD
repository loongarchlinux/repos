# Maintainer: Campbell Jones <arch at serebit dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.6.4
pkgrel=1
pkgdesc='A familiar, modern desktop environment'
arch=('loong64' 'x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL' 'LGPL')
depends=('budgie-screensaver' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard'
         'libgee' 'libpeas' 'libwnck3' 'mutter' 'accountsservice' 'ibus')
makedepends=('gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'gnome-control-center: System settings'
            'network-manager-applet: Network management')
options=('debug')
source=("https://github.com/BuddiesOfBudgie/budgie-desktop/releases/download/v$pkgver/budgie-desktop-v$pkgver.tar.xz"{,.asc})
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
sha256sums=('7775699ea64d7317e4004b751ccd5ab1e38df172ce17abc241b8a22bb31c1cdb'
            'SKIP')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
