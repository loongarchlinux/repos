# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.5.1+12+g10b814fa
pkgrel=2
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://github.com/solus-project/budgie-desktop"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter6')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=10b814fa62bf774ca48e182548d3c6687a852314  # master
source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "desktop-override.patch"
        "budgie-desktop-vala0.50.patch")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            'baffb1d64715c20c1ae444a5b8ea0f8e9bf69cccf30e5e2c6729fe6d0c7491d2'
            'cf46448744b861702159e120263f6acd7f23f8b391108211f82b29fdefd4de71')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	cd $pkgname

	# Fix build with vala 0.50
	patch -Np1 -i ../budgie-desktop-vala0.50.patch

	# Override settings only for Budgie
	patch -Np1 -i ../desktop-override.patch

	# Don't use systemd session management, it's not supported by Budgie
	sed -i 's/exec gnome-session/exec gnome-session --builtin/' src/session/budgie-desktop.in

	git submodule init
	git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
	git submodule update
}

build() {
	arch-meson $pkgname build -D with-desktop-icons=none
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
