# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=16.4.0
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("https://github.com/jaraco/path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fefb50da55ad77cdb61c7c4a05eeffaae9f6db349ce0b75d5e37ca8e3152deff19c6c1556e9efbe7faeaf569342a670fc360a4e650e65f509a68a62130d7d093')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
