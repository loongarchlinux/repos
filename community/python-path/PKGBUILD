# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=16.2.0
pkgrel=3
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("https://github.com/jaraco/path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('565e49b45c6fdadcb8d4e15e3d529c10044c943b8adccc97ba08195423907335e9ae54272bb90bee6a3ee74791caf98f6f7bc402c36b84a8bd7b45019f2fc42f')

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
