# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=15.1.2
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("https://github.com/jaraco/path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a555fae28c3edfeb0f1114a205373ae3e7278373b98820877acafee6e88f4fcdf7eb2da576eafb2123fc77305e90a58b4c1b11193e092d0a5a6d2a779099faf6')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
