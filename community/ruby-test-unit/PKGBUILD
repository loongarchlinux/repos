# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname=test-unit
pkgname="ruby-${_gemname}"
pkgver=3.5.3
pkgrel=1
pkgdesc='An xUnit family unit testing framework for Ruby'
arch=('any')
url="https://github.com/${_gemname}/${_gemname}"
license=('GPL')
depends=('ruby' 'ruby-power_assert')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e6960bdcc67c8667cc13f0a4cf8b412be767124b0e555294be71afff71e0ce9a34e7cfa7413e33ee3c8477928a479e9439ada9e50866c0e8890b4be8dd0b38a2')

# fix wrong license identifier to match the official ones from https://spdx.org/licenses/
prepare() {
  cd "${_gemname}-${pkgver}"
  sed --in-place 's/spec.licenses.*/spec.licenses = ["BSD-2-Clause", "PSF-2.0", "Ruby"]/' "${_gemname}.gemspec"
}

build() {
 cd "${_gemname}-${pkgver}"
 gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  ruby test/run-test.rb
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"

  install -Dm 644 README.md doc/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 BSDL COPYING PSFL -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
