# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=2.2.0
pkgrel=1
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=('any')
depends=('python-termcolor')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("https://github.com/pavdmyt/yaspin/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5fa5126d940ceaf44386eea4bd185e57ae1289b4059149d27194d16a5efbafb45faa737895eb6ba0d8e35ea0b33c26f31af543ceb55041058bc9a2cb3b053e4e')

prepare() {
  cd yaspin-$pkgver
  # https://github.com/pavdmyt/yaspin/issues/211
  sed -i 's/^termcolor-whl =.*$/termcolor = "^2.0.0"/' pyproject.toml
}

build() {
  cd yaspin-$pkgver
  python -m build --no-isolation --wheel
}

check() {
  cd yaspin-$pkgver
  python -m pytest
}

package() {
  cd yaspin-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
