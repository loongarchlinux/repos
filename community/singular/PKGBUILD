# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-3
_patchver=2
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=4
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen polymake)
checkdepends=(python2)
optdepends=('polymake: Polymake module')
source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
         primdecSY-hang.patch::"https://github.com/Singular/Sources/commit/0d015456.patch"
         singular-polymake4.patch)
#source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('ee7ac6076d7f8622a1f24d37e9b632bae0700c6e314f56728400be136df64494'
            '46b7a7ed52ffc212693cafc46fefd1837268ea90636037da292dea6bdb205f2d'
            'fdc5a6945b90812b631b5ac4be5013313abdd8b032e66781df2713bc897f4f37')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -p1 -i ../primdecSY-hang.patch # https://www.singular.uni-kl.de:8002/trac/ticket/865
  patch -p1 -i ../singular-polymake4.patch # Fix build with polymake 4 (Fedora)
}
  
build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib --docdir=/usr/share/doc
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd singular-${_majver//-/.}
  make check
}
  
package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  install -Dm644 doc/singular.idx -t "$pkgdir"/usr/share/singular
  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/singular/singular.idx
}
