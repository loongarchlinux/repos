# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-sphinxcontrib-blockdiag
pkgver=3.0.0
pkgrel=1
pkgdesc='A sphinx extension for embedding block diagram using blockdiag'
arch=(any)
url='https://github.com/blockdiag/sphinxcontrib-blockdiag'
license=(BSD)
depends=(python blockdiag python-sphinx)
makedepends=(python-setuptools)
# otf-ipafont is used by test_latex
checkdepends=(python-nose python-sphinx-testing python-mock python-reportlab otf-ipafont)
optdepends=(
  'python-reportlab: for using PDF as the format of images'
)
source=("https://github.com/blockdiag/sphinxcontrib-blockdiag/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        ipafont-path.diff)
sha256sums=('9061847004d900b458c2d611b4b539b2a3d594648060a222959f49c837cb41dc'
            'cd127f6f66d49d91d1b8716b5361d917c354e8eed6b6817e037d2c2441b7e9e8')

prepare() {
  cd sphinxcontrib-blockdiag-$pkgver
  patch -Np1 -i ../ipafont-path.diff
}

build() {
  cd sphinxcontrib-blockdiag-$pkgver
  python setup.py build
}

check() {
  cd sphinxcontrib-blockdiag-$pkgver
  # Running tests with 'nosetests' fails with issues around namespace packages
  python -m nose -v
}

package() {
  cd sphinxcontrib-blockdiag-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
