# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Padraic Fanning <fanninpm AT miamioh DOT edu>

pkgname=python-darkdetect
pkgver=0.7.1
pkgrel=1
pkgdesc='Detect OS Dark Mode from Python'
arch=('any')
url='https://github.com/albertosottile/darkdetect'
license=('BSD')
depends=('python')
makedepends=('git' 'python-setuptools')
_commit='2f6eb7205a826af6e2a6d93e46934354ef80faf5'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
