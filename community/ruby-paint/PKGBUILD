# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

_gemname=paint
pkgname=ruby-${_gemname}
pkgver=2.2.0
pkgrel=1
pkgdesc='Ruby paint manages terminal colors and effects'
url='https://github.com/janlelis/paint'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rspec' 'ruby-rainbow' 'ruby-term-ansicolor')
makedepends=('ruby-rake')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('67f9752d270ff8cd2706318a16b3b3980515bd07a55a2b8900eb79b200d6da87993185c839748052457536b82d53694bba33c0f64d8ada50b61a34c9985ba009')
b2sums=('4f1e978023cd6edb371ae7315cc48451f3a911522f0d39b7dea264d8d112a6db4560507c33fdc55e247b782f30cd8b844d6c790ce86194c151acc7c2abc7f9c9')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i paint.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  rake gems
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 MIT-LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
