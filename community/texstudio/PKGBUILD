# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=4.1.2
pkgrel=1
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('loongarch64' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme' 'hunspell' 'quazip' 'desktop-file-utils' 'qt5-declarative')
makedepends=('qt5-tools' 'mercurial' 'imagemagick' 'librsvg')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/texstudio-org/texstudio/archive/${pkgver}.tar.gz"
        fs54269.patch
        fs72345.patch)
sha512sums=('0b88f82514462c363e6dcee899f3681883ce9db1bb21cbf9e5e657208338bba4a6cdb79feeaefb242350bc01b83851c5d2704a7d0e2888034070ef2588b77964'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            '6975eebd43b32fb7aa04f973562e66f6a83df0fb6a0b4d8221e124c9cc6959df11ec517e4c862921619c8b1c55800257017e26001c03186b84c567fc19c98726')

prepare() {
    cd texstudio-${pkgver}/src
    patch -Np1 -i "${srcdir}"/fs54269.patch
    patch -Np1 -i "${srcdir}"/fs72345.patch
}

build() {
    cd texstudio-${pkgver}
    qmake-qt5 CONFIG-="debug" USE_SYSTEM_QUAZIP=1 USE_SYSTEM_HUNSPELL=1 QUAZIP_LIB=-lquazip1-qt5 \
        QUAZIP_INCLUDE="/usr/include/QuaZip-Qt5-1.2/quazip" texstudio.pro
    make
}

package() {
    cd texstudio-${pkgver}
    make INSTALL_ROOT="${pkgdir}" install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert +set date:create +set date:modify -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done
}
