# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop-view
pkgver=1.1.1
pkgrel=1
pkgdesc="Desktop icons for the Budgie Desktop"
arch=('loongarch64' 'x86_64')
url="https://github.com/getsolus/budgie-desktop-view"
license=('Apache')
depends=('gtk3')
makedepends=('intltool' 'meson' 'vala')
source=("https://github.com/getsolus/budgie-desktop-view/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc})
validpgpkeys=('96B4A0291094A86A2B7E3367DD672FE9A2BE5892') # Joshua Strobl (Personal)
sha256sums=('cdb4bfa3d95cbaff1c837657584f74c924e0fb1dc04b51fb95410621f1493e31'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
