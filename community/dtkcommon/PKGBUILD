# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.5.23
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('qt5-tools')
source=("https://github.com/linuxdeepin/dtkcommon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2cda6f7a097664791ef6408e23eab7f70fc3e0d1021605eda66854d70259e903e8084faed9a81a8a93b7e5742b5527dc3a331dc58fdbf2f1709962a20c541355')

prepare() {
  cd dtkcommon-$pkgver
  sed -i 's|qt5/mkspecs|qt/mkspecs|;s|/etc/dbus-1|/usr/share/dbus-1|' dtkcommon.pro
}

build() {
  cd dtkcommon-$pkgver
  qmake-qt5 PREFIX=/usr VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkcommon-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
