# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.5.22
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('qt5-tools')
source=("https://github.com/linuxdeepin/dtkcommon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1ca81ecf74ef9732fd1a78dea2ff45d2c425d8f52fac4736b5c0b84112c6dec8a067316cec8af79a264b2a9b1b356ba230324caf39407120b2a6aeee9452c2a0')

prepare() {
  cd dtkcommon-$pkgver
  sed -i 's|qt5/mkspecs|qt/mkspecs|;s|/etc/dbus-1|/usr/share/dbus-1|' dtkcommon.pro
}

build() {
  cd dtkcommon-$pkgver
  qmake-qt5 PREFIX=/usr VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkcommon-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
