# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=routino
pkgver=3.3.1
pkgrel=2
pkgdesc='Router for OpenStreetMap Data'
arch=('x86_64')
url='https://www.routino.org/'
license=('AGPL3')
depends=('zlib' 'bzip2')
source=(https://www.routino.org/download/$pkgname-$pkgver.tgz)
sha256sums=('a954565ab60a5abebc47e8c6e8b496f972e8dd781810fa5548b6d7a9e3e5e135')

prepare() {
  cd "$pkgname-$pkgver"

#paths fix
  sed -i 's|prefix=/usr/local|prefix=/usr|' Makefile.conf
  sed -i 's|/doc/routino|/share/doc/routino|' Makefile.conf

#RPATH fix
  sed -i 's|LDFLAGS_LDSO=-Wl,-R.||' Makefile.conf
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
