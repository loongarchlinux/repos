# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-h11
pkgver=0.13.0
pkgrel=1
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/python-hyper/h11"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.tar.gz")
sha512sums=('c3a16dd66287ab6491b664aab9d72e14f1e61950c19182d60e195b81b2857e47157c38137b2ee2a9cd35d9d54997f554f768a3933051030895ddf7bc5b813e02')

build() {
  cd h11-$pkgver
  python setup.py build
}

check() {
  cd h11-$pkgver
  pytest
}

package_python-h11() {
  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
