# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-structlog
pkgver=21.1.0
pkgrel=1
pkgdesc="Structured Logging for Python"
url="https://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('8ae9a749d19c8dd7eb92d27c04c9f5b5c549eec694f7a3f2963b6479367d270ea175f3be75d90faa1812da1fceb6f189add42a5db19d8c416b2321d58bf7a338')

build() {
  cd structlog-$pkgver
  python setup.py build
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
