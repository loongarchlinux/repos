# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-structlog
# https://www.structlog.org/en/stable/changelog.html
pkgver=21.5.0
pkgrel=1
pkgdesc="Structured Logging for Python"
url="https://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-flit-core' 'python-install')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet' 'python-pytest-asyncio'
              'python-rich')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('d36388608ca540b6deec85b4d3ecc6878752ee3fc083fc37e51f5baa0e08ab45ade687461fdf071f580e226740498cf98d0ba159f6929cda2dee1d3e3b4f2a59')

build() {
  cd structlog-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd structlog-$pkgver
  python -m install --optimize 0 1 --destdir="$pkgdir" dist/*.whl
}
