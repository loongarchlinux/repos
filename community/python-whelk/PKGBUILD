# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whelk
pkgver=2.8.2
pkgrel=1
pkgdesc="Easy access to shell commands from python"
arch=('any')
license=('custom:zlib')
url="https://pypi.python.org/pypi/whelk"
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/seveas/whelk/archive/$pkgver.tar.gz")
sha512sums=('83c3f5ce1da07258ed4c3c22699be36fe960547619617de4fd384f3bf4b62a53a8c83dfbaef37d646542591d60a38ad567f9c3ecc2cb766db40d70cce27d60b2')

build() {
  cd whelk-$pkgver
  python setup.py build
}

check() {
  cd whelk-$pkgver
  python -munittest discover
}

package() {
  cd whelk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
