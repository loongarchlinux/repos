# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

pkgname=codeblocks
pkgver=20.03
pkgrel=2
pkgdesc='Cross-platform C/C++ IDE'
arch=('loongarch64' 'x86_64')
url='http://codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'wxgtk3' 'hunspell')
makedepends=('boost' 'imagemagick' 'zip')
source=("https://downloads.sourceforge.net/codeblocks/$pkgname-$pkgver.tar.xz"
         codeblocks-gcc11.patch)
sha256sums=('15eeb3e28aea054e1f38b0c7f4671b4d4d1116fd05f63c07aa95a91db89eaac5'
            '10b2b08440adc447d1d23eb122fa37beff1f26b9d7588b83f8435721586f4ed1')

prepare() {
  cd $pkgname-$pkgver
  convert src/mime/codeblocks.png +set date:create +set date:modify -background none -extent 64x64 src/mime/codeblocks.png
  sed -i 's|$(datadir)/pixmaps|$(datadir)/icons/hicolor/64x64/apps|' src/mime/Makefile.{am,in}
  sed -i 's|$(datarootdir)/appdata|$(datarootdir)/metainfo|' Makefile.{am,in} src/plugins/contrib/appdata/Makefile.{am,in}

  patch -p1 < ../codeblocks-gcc11.patch # Fix build with GCC 11 (Fedora)
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-contrib-plugins=all,-FileManager --with-wx-config=/usr/bin/wx-config-gtk3
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
