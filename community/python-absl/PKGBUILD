# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-absl
pkgver=1.3.0
pkgrel=1
pkgdesc="Abseil Python Common Libraries"
arch=('any')
url='https://github.com/abseil/abseil-py'
provides=('absl-py')
conflicts=('absl-py')
replaces=('absl-py')
license=('APACHE')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/a/absl-py/absl-py-$pkgver.tar.gz")
sha512sums=('20db49a4450758eeb56cc094a9503b00f7c7f41fb1ed3dcb612ac57dc641375fd5648aff679166c1db1936fa7840fe75eaabead71f9240587a25ab2546233d52')

build() {
  cd "$srcdir/absl-py-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/absl-py-$pkgver"

  python setup.py install -O1 --root="${pkgdir}" --skip-build
}

# vim:set ts=2 sw=2 et:
