#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2021.7.8
pkgrel=2
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://sources.archlinux.org/other/community/reflector/reflector-2021.7.8.tar.xz{,.sig}
)
sha512sums=(
  51114532eaae37cb1c0f0522e6e3a16955455289f88f802ac870c496d9dd6a62196c797c044e74240bcc1355905b503edec1353d98f477b40d218f290c2cecef
  656ff757d0d19ab689a036d163198c91d6a2b30ce2a10ee43a605dc0e6637976d0e0f5b79198ca08a8433ec92f7614e21eeb51a9fbf64a9fd4b22f74c82b0b7d
)
md5sums=(
  d269f659668ca546b721aef806bab71d
  bcc8dd00fff5c39aab19e2bc34bb8f14
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
