#Maintainer: Xyne <gro xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2021.11
pkgrel=5
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.dev/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.dev/projects/reflector/src/reflector-2021.11.tar.xz
  https://xyne.dev/projects/reflector/src/reflector-2021.11.tar.xz.sig
)
sha512sums=(
  8b7f3cebce9d1f18ecda55d3fe3440eaca51733826c764b2f6ee52edc1e8a31a4b4ae3aa415688c975425888eb30ff8791c8525a629928b064502ddbde93da3e
  7d982d39cac086f5d9db4b52857abba78c216567f3bab3d45044a5e3c99ec9ed0842b0b3ddd4baeb7bc6ef76c1189fb2df81921884aa940cbcc29f128960d0df
)
md5sums=(
  b3c5051d6aa5d80de77d9e3905953e39
  8fca72bd5501015927d56c70fa415cc2
)
validpgpkeys=('D89FAAEB4CECAFD199A2F5E612C6F735F7A9A519')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
