#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2021.4
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2021.4.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2021.4.tar.xz.sig
)
sha512sums=(
  4bae2191710a6321128a817cf98f8e2fa4a01e8437ef707488e07dec3f22efa84734766252b53fb0b2ab4fcf72600bd866e9c7a9caae68adaa3f485e5cc066ef
  fd7940e8cf7cf29c1694c4ba17da59f8437ea2323f3948c4c23ca0c98c4cb48c1115c97e94941192d3901980dab6bfc54399db309b98346eabd1b9f98e6fa88c
)
md5sums=(
  3e13e177b4644f2052bf09d991afa7c2
  faf78cbeccad91b687592c1fc93cf502
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
