# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Windel Bouwman <windel at windel dot nl>
# Contributor: Jakub Klinkovský <kuba.klinkovsky@gmail.com>

_pkgname=pyqtgraph
pkgname=python-$_pkgname
pkgver=0.12.4
pkgrel=1
pkgdesc='Scientific Graphics and GUI Library for Python'
arch=('any')
url='http://www.pyqtgraph.org'
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
depends=('python' 'qt5-python-bindings' 'python-numpy')
optdepends=('python-opengl: 3d graphics')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pyqtgraph/pyqtgraph/archive/pyqtgraph-$pkgver.tar.gz")
sha512sums=('8402b38fe1abaaf55d6dbc26775093993b9f642d7a9da59d2780ed38562c8653ab872c588d2e66373434491b8f397533cb3c222382065c812a28cf6b9ce34960')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python -m build -nw
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
