# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=archlinux-wallpaper
pkgver=1.4
pkgrel=7
pkgdesc='A selection of Arch Linux branded wallpapers'
arch=(any)
url='https://archlinux.org/'
license=('CCPL:by-nc-sa')
source=("https://sources.archlinux.org/other/artwork/$pkgname-$pkgver.tar.xz"
        archlinux.stw)
optdepends=('wallutils: support the simple timed wallpaper format')
sha256sums=('9184b825e3d538f74334b25fd2f1f7bb786e29a64d975de5a565584af9a400a3'
            '21058c100feb27d8d6fbaddf02c3892e8fd26e5cb665857bf5c1a81739d9595d')

prepare() {
  sed -i 's@archlinux/wallpaper@backgrounds/archlinux@g' \
    "$pkgname-$pkgver/arch-backgrounds-16-9.xml"
}

package() {
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/backgrounds/archlinux"
  install -m644 AUTHORS "$pkgdir/usr/share/backgrounds/archlinux/"
  install -m644 *.jpg "$pkgdir/usr/share/backgrounds/archlinux/"
  install -m644 *.png "$pkgdir/usr/share/backgrounds/archlinux/"
  install -Dm644 "$srcdir/archlinux.stw" \
    "$pkgdir/usr/share/backgrounds/archlinux/archlinux.stw"
  rm "$pkgdir/usr/share/backgrounds/archlinux/"*-43.*
  install -Dm644 arch-backgrounds-16-9.xml \
    "$pkgdir/usr/share/gnome-background-properties/arch-backgrounds.xml"
}

# vim: ts=2 sw=2 et:
