# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=torbrowser-launcher
pkgver=0.3.5
pkgrel=4
pkgdesc="Securely and easily download, verify, install, and launch Tor Browser in Linux"
url="https://github.com/micahflee/torbrowser-launcher"
depends=('python' 'python-pyqt5' 'python-requests' 'python-pysocks' 'python-gpgme' 'python-packaging'
         'gnupg' 'tor' 'hicolor-icon-theme' 'dbus-glib')
makedepends=('python-setuptools' 'python-distro')
optdepends=('apparmor: support for apparmor profiles')
arch=('any')
license=('MIT')
backup=(
  'etc/apparmor.d/local/torbrowser.Browser.firefox'
  'etc/apparmor.d/local/torbrowser.Tor.tor'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/micahflee/torbrowser-launcher/archive/v${pkgver}.tar.gz"
        https://github.com/micahflee/torbrowser-launcher/pull/593/commits/a4bc9789b0d25c5d604e310f313d85ee15871a65.patch
        https://github.com/micahflee/torbrowser-launcher/pull/599/commits/bcd65bdd45cb45efd568ed911b009665b061ac9e.patch)
sha512sums=('d9e30a721393d1530bc288fd74925731b71016d34670b6b33e1e76e271f956e2d2e35bd29f81389b1c90db15257e1e6281bd3aeeb9c3cc533ab9a5d109803254'
            'ff99b69786510db3ba0f5410dc3e858dcfbb402548e49e74fa5985d7c262a77ec17bb45e0ee39fb877628a68c3308e258296523f2839487516970fe0f4cd8691'
            'de2e0f905662d8f9ba93151037df7798fefd48fba186d687d708161ad8d0bb40be16d92ff8a41d124b9276dd5ce92d9a3c9b012b49bea631f75a928ace647511')
b2sums=('87ba04a5dd49316148631d3da6cf0b328160b817b7c42062bf6eeb1bedfc2cad6276640422139adc2b4031bfb6d0aa9bb93636b8e629e3b53433909bdf962768'
        '3a751b97878e20b97d74336697a3ad48b681bed3664c53ccef147141b91ea78af5b94df00a7b8b381c80bfbb1f3ee8081dc78b1c60ad0182eb7a0f70e1ff92b2'
        'f940dd72d7d077f09d0ebfe01d2c7ba6253a8375f52e5abadf7337541df491feeee5631b123c936d2d0c06c38bf2fcd3ab8e26b6aed7e3c6b47642d2101fc46c')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../a4bc9789b0d25c5d604e310f313d85ee15871a65.patch
  patch -Np1 -i ../bcd65bdd45cb45efd568ed911b009665b061ac9e.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
