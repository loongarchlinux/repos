# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=(php-igbinary php7-igbinary)
pkgver=3.2.9
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=(loong64 x86_64)
url="https://github.com/igbinary/igbinary"
license=(BSD)
depends=(glibc)
makedepends=(php7 php)
source=($pkgname-$pkgver.tar.gz::https://github.com/$_name/$_name/archive/$pkgver.tar.gz)
sha512sums=('2789f8517aaefa4e4e736592944361e8bad1ce50687aed161bb4e17bcdfbfec2320df8069c7b39704fa96c614727bd66fb65185b15f72f8c96bb5fda88f2e66f')
b2sums=('d44711ffc195572a83a000f225776191e7b421e4be3065a668287f356d46fd11536af0a0969a0fc219c38d027f13aec16cfe41f15ea778ef087f7082daa9b8dc')

prepare() {
  mv -v $_name-$pkgver $pkgbase-$pkgver
  # disable the extension by default
  sed 's/extension/;extension/g' -i $pkgbase-$pkgver/$_name.php.ini

  cp -av $pkgbase-$pkgver ${pkgname[1]}-$pkgver
  (
    cd $pkgbase-$pkgver
    phpize
  )
  (
    cd ${pkgname[1]}-$pkgver
    phpize7
  )
}

build() {
  (
    cd $pkgbase-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd ${pkgname[1]}-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    NO_INTERACTION=1 make -k test -C $pkgbase-$pkgver
  )
  (
    NO_INTERACTION=1 make -k test -C ${pkgname[1]}-$pkgver
  )
}

package_php-igbinary() {
  local _interpreter_version="$(php --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php)
  _provides=(php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 $_name.php.ini "$pkgdir/etc/php/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_php7-igbinary() {
  local _interpreter_version="$(php7 --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php7)
  _provides=(php-igbinary=$pkgver php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php7/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 "$_name.php.ini" "$pkgdir/etc/php7/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
