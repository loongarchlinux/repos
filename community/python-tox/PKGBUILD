# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch.de>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>

pkgname=python-tox
pkgver=3.25.0
pkgrel=1
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
url='https://tox.readthedocs.io'
license=('GPL2')
depends=('python-py' 'python-virtualenv' 'python-packaging' 'python-pluggy' 'python-toml' 'python-filelock' 'python-six')
makedepends=('python-setuptools-scm' 'python-py' 'python-virtualenv'
             'python-pluggy' 'python-toml'
             'python-filelock' 'python-freezegun')
checkdepends=('python-pytest-runner' 'python-pytest-mock'
              'python-flaky')
source=("https://pypi.io/packages/source/t/tox/tox-$pkgver.tar.gz")
sha512sums=('d98ed9f589c751ddc187618caf509fbc64e99f1b5eafeb9cbd919d94d10e15a55331658121efabac53ef07adeb71af15da8be86fc4f35688a8f9b93bf3e723d1')

prepare() {
  find tox-$pkgver -name "*.pyc" -delete
}

build() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/tox-$pkgver
  python setup.py build
}

check() {
  # Hack entry points

  (
    cd "$srcdir"/tox-$pkgver
    virtualenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  )
}

package() {
  cd "$srcdir"/tox-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  ln -s tox "$pkgdir"/usr/bin/tox3
}
