# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgname=python-arrow
pkgver=1.2.2
pkgrel=1
pkgdesc='Better dates and times for Python'
arch=(any)
url=https://arrow.readthedocs.io
license=(APACHE)
depends=(python-dateutil)
makedepends=(
  git
  python-setuptools
)
checkdepends=(python-tox)
_tag=f8a65212df1ab234013a8a116436b3532c82b05d
source=(git+https://github.com/crsmithdev/arrow.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd arrow

  git describe --tags
}

build() {
  cd arrow

  python setup.py build
}

check() {
  cd arrow

  tox -e py310
}

package() {
  cd arrow

  python setup.py install --skip-build --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
