# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=mediawiki
pkgname=('mediawiki' 'mediawiki-math')
#pkgname=('mediawiki')
pkgver=1.36.1
_basever=${pkgver%.*}
_hash=8c7c35b
pkgrel=1
pkgdesc="MediaWiki engine"
arch=('any')
url="https://www.mediawiki.org/wiki/MediaWiki"
# + https://www.mediawiki.org/wiki/Extension:Math
license=("GPL")
depends=('php7')
makedepends=('git' 'texvc')
optdepends=('texvc: for math rendering'
	    'pcre: for regular expressions support'
	    'php7-intl: to handle Unicode normalization'
#	    'php7-mysql: for MySQL database support'
	    'php7-pgsql: for PostgreSQL database support'
	    'php7-sqlite: for sqlite database support'
#	    'php7-apcu: for cache support'
#	    'php7-xcache: for cache support'
	    'memcached: for cache support'
	    'php7-gd: for thumbnails rendering'
	    'imagemagick: for thumbnails rendering'
	    'smtp-forwarder: for mail sending'
            'python: required for SyntaxHighlight/ConfirmEdit extensions'
            'perl: compare_schemas.pl and mediawiki_mysql2postgres.pl')
install=mediawiki.install
options=(!strip)
validpgpkeys=('41B2ABE817ADD3E52BDA946F72BC1C5D23107F8A'
              '1D98867E82982C8FE0ABC25F9B69B3109D3BB7B0'
              'C83A8E4D3C8FEB7C8A3A1998131910E01605D9AA')
#	mediawiki-math-${_basever}::git+https://git.wikimedia.org/git/mediawiki/extensions/Math.git#branch=REL${_basever/./_}
source=("https://releases.wikimedia.org/mediawiki/${_basever}/mediawiki-$pkgver.tar.gz"{,.sig}
	"mediawiki-math-${_basever}.tar.gz::https://codeload.github.com/wikimedia/mediawiki-extensions-Math/legacy.tar.gz/REL${_basever/./_}"
	apache.example.conf)
sha256sums=('e6698b19af16274e5ec2bfd36b890996cfa9f3617d2ec81761f12854fa50ad7a'
            'SKIP'
            '9afc3315c19d026b43649b3acf9399208985aa1a018c42d512e35eb4305833fc'
            'cfeff68331e930b6a93f166c12666ac59a84aa24334f94520eff3f988f37ce2b')

package_mediawiki() {

  cd "$srcdir"
  install -vdm0755 "$pkgdir"/usr/share/webapps
  install -vdm0755 "$pkgdir"/etc/webapps/mediawiki
  cp -a $pkgname-$pkgver "$pkgdir"/usr/share/webapps/mediawiki

  install -vDm0644 "$srcdir"/apache.example.conf "$pkgdir"/etc/webapps/mediawiki/apache.example.conf

  # move cache and images to /var
  install -vdm0755 -o 33 -g 33 "$pkgdir"/var/cache/mediawiki
  install -vdm0755 -o 33 -g 33 "$pkgdir"/var/lib/mediawiki

  cd "$pkgdir"/usr/share/webapps/mediawiki

  mv cache/.htaccess "$pkgdir"/var/cache/mediawiki/
  rmdir cache
  ln -sf /var/cache/mediawiki cache

  mv images/* "$pkgdir"/var/lib/mediawiki/
  mv images/.htaccess "$pkgdir"/var/lib/mediawiki/
  rmdir images
  ln -sf /var/lib/mediawiki images
}

package_mediawiki-math() {
  depends=('mediawiki')
  optdepends=('texvc')
  backup=()
  pkgdesc="MediaWiki math extension"
  unset install

  cd "$srcdir"
  install -vdm0755 "$pkgdir"/usr/share/webapps/mediawiki/extensions
#  cp -a mediawiki-math-${_basever} "$pkgdir"/usr/share/webapps/mediawiki/extensions/Math
  cp -a wikimedia-mediawiki-extensions-Math-${_hash} "$pkgdir"/usr/share/webapps/mediawiki/extensions/Math
  install -dm0755 "$pkgdir"/usr/share/webapps/mediawiki/extensions/Math/math
  ln -s /usr/bin/texvc "$pkgdir"/usr/share/webapps/mediawiki/extensions/Math/math/texvc
}
