# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-py-cpuinfo
pkgver=8.0.0
pkgrel=3
arch=('any')
pkgdesc='Get CPU info with pure Python 2 & 3'
url='https://github.com/workhorsy/py-cpuinfo'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/workhorsy/py-cpuinfo/archive/v$pkgver.tar.gz")
sha512sums=('a882516a062815782ebc2ad1edb8813ef566877444a6577d6a752b6e0bfce832010181ae20633d5338bdda3399c50fb670c650983b7ffa3064ff76c5cfb0f8d8')

prepare() {
  cp -a py-cpuinfo-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/py-cpuinfo-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/py-cpuinfo-$pkgver
  python setup.py pytest
}

package_python-py-cpuinfo() {
  cd py-cpuinfo-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
