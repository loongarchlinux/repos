# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.38
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('loongarch64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('64efbe848ebb813f6c7927a1abf11ba88eb622e4e9f3c04f0efb149e18cbdece11f17c53b2956e7939e6f9da0f486e9531aaeda012b52528cb7cf56afeed1958')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '1i#include <QPainterPath>' src/views/simplelistdelegate.cpp

  # debundle gmock/gtest
  sed -i '/third_party/d' CMakeLists.txt
  rm -r third_party
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
