# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.5.33
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center' 'deepin-wallpapers')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('9bc04fe1fe10870136086cf854409a74cad44a9d22bd6420c71413e90f75b1e25421ab411c3773096436760abadaf40d022105c3e121464964c1ec026a2eec5b')

prepare() {
  mkdir -p build

  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp
}

build() {
  cd build
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ../dde-session-ui-$pkgver
  ninja
}

check() {
  cd build
  # ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
