# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=3.4.2
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots"
arch=(loongarch64 x86_64)
url="https://matplotlib.org"
license=(custom)
depends=(freetype2 python-cycler python-dateutil python-kiwisolver python-numpy python-pillow python-pyparsing qhull)
optdepends=('tk: Tk{Agg,Cairo} backends'
            'pyside2: alternative for Qt5{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-gobject: for GTK3{Agg,Cairo} backend'
            'python-wxpython: WX{,Agg,Cairo} backend'
            'python-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python-cairocffi: alternative for Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies'
            'texlive-latexextra: usetex usage with pdflatex'
            'python-certifi: https support')
makedepends=(git rsync python-setuptools
             tk python-pyqt5 python-gobject
             python-wxpython python-cairocffi
             python-certifi python-tornado
             ghostscript texlive-bin)
             # agg missing some non-upstreamed patches?
checkdepends=(python-pytest python-pytest-xdist python-pytest-runner xorg-server-xvfb
              texlive-core texlive-latexextra ghostscript inkscape mencoder
              ffmpeg imagemagick python-pandas)
_tag=918149749ca2977d9ca43acda8eed4be811e81b4 # git rev-parse v${pkgver}
source=(git+https://github.com/matplotlib/matplotlib.git#tag=v${pkgver}?signed)
sha512sums=(SKIP)
validpgpkeys=(23CAB59E3332F94D26BEF0378D86E7FAE5EB0C10) # Elliott Sales de Andrade <quantum.analyst@gmail.com>

prepare() {
  cd matplotlib
# Use system freetype and qhull
  sed -e 's|#system_freetype = False|system_freetype = True|' -e 's|#system_qhull = False|system_qhull = True|' setup.cfg.template > setup.cfg
}

build() {
  cd matplotlib
  python setup.py build
}

check() {
  cd matplotlib
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python setup.py pytest --addopts="-n auto" || echo "Tests failed" # Different font rendering details
}

package_python-matplotlib() {
  cd matplotlib
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/license.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Needed since https://github.com/matplotlib/matplotlib/pull/14170
  rm -r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/{matplotlib,mpl_toolkits}/tests/
}
