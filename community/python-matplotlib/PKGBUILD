# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=3.6.1
pkgrel=2
pkgdesc="A python plotting library, making publication quality plots"
arch=(loong64 x86_64)
url="https://matplotlib.org"
license=(custom)
depends=(freetype2 python-contourpy python-cycler python-dateutil python-fonttools python-kiwisolver python-numpy
         python-packaging python-pillow python-pyparsing qhull)
optdepends=('tk: Tk{Agg,Cairo} backends'
            'pyside2: alternative for Qt5{Agg,Cairo} backends'
            'pyside6: alternative for Qt6{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-pyqt6: Qt6{Agg,Cairo} backends'
            'python-gobject: for GTK{3,4}{Agg,Cairo} backend'
            'python-wxpython: WX{Agg,Cairo} backend'
            'python-cairo: {GTK{3,4},Qt{5,6},Tk,WX}Cairo backends'
            'python-cairocffi: alternative for Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies'
            'texlive-latexextra: usetex usage with pdflatex'
            'python-certifi: https support')
makedepends=(git python-setuptools-scm-git-archive python-certifi)
checkdepends=(python-pytest python-pytest-xdist python-pytest-runner python-pytest-rerunfailures xorg-server-xvfb
              texlive-bin texlive-core texlive-latexextra texlive-pictures ghostscript inkscape ffmpeg imagemagick
              gtk4 python-cairo python-cairocffi python-gobject python-pyqt5 pyside2 python-pyqt6 pyside6 python-tornado python-wxpython tk
              jupyter-nbconvert jupyter-nbformat python-ipykernel python-pandas python-pikepdf python-pytz)
_tag=2aad0d94ff80a81cd2e735b46f11f4f4b5e3e59d # git rev-parse v${pkgver}
_ftver=2.12.1
source=(git+https://github.com/matplotlib/matplotlib.git#tag=${_tag}?signed
        https://github.com/QuLogic/mpl-images/archive/v${pkgver}-with-freetype-${_ftver}/mpl-images-${pkgver}-ft${_ftver}.tar.gz
        freetype.patch
        matplotlib-pyside-6.4.patch::https://github.com/matplotlib/matplotlib/commit/c744161b.patch)
b2sums=('SKIP'
        'b2980e8812bd2ede44e448f70ada23ec3c313fd9d1024daa71320468857546818bbc9461a9f9e511de7c7883d8d810271acc13db6fb36a56f82d60b2ed1ada5e'
        'b821f938cace434932a43b15b42b93d0f8eaffea4e28fbf1d5a7263ec947c26252bafccaea60c1a1cb2fad4c71280ca2cf62527994f270af2467a242287470bb'
        'f85435d7925a3b8300eff92f995d0a43f7bc9db3c078a76a8a6a0eb01be830bf6a72011bb149a12561abf562420c9403094e062be5d2d19623381d5ee717b254')
validpgpkeys=(23CAB59E3332F94D26BEF0378D86E7FAE5EB0C10) # Elliott Sales de Andrade <quantum.analyst@gmail.com>

prepare() {
  cd matplotlib
  # Fix SCM detected version
  rm -r .git
  echo "Version: ${pkgver}" > PKG-INFO
  # Use system freetype and qhull
  sed -e 's|#system_freetype = False|system_freetype = True|' -e 's|#system_qhull = False|system_qhull = True|' mplsetup.cfg.template > mplsetup.cfg
  patch -p1 < ../freetype.patch # From Fedora/upstream
  # Fix tests with PySide 6.4
  patch -p1 < ../matplotlib-pyside-6.4.patch
  # Install tests for check()
  sed -i 's|#tests = False|tests = True|' mplsetup.cfg
  # Use appropriate baseline images for tests
  for _module in matplotlib mpl_toolkits
  do
    cp -r ../mpl-images-${pkgver}-with-freetype-${_ftver}/${_module}/* lib/${_module}/tests/baseline_images/
  done
}

build() {
  cd matplotlib
  python setup.py build
}

check() {
  cd matplotlib
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --skip-build
  # test_ipynb: https://github.com/matplotlib/matplotlib/issues/21654 fixed but `ModuleNotFoundError: No module named 'matplotlib'`
  # test_cross_Qt_imports https://github.com/matplotlib/matplotlib/issues/23004
  xvfb-run -a -s "-screen 0 640x480x24" \
    test-env/bin/python -m pytest -ra -n auto -v --color=yes --pyargs matplotlib mpl_toolkits.tests -k 'not test_ipynb and not test_cross_Qt_imports'
}

package() {
  cd matplotlib
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/project/license.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Remove tests
  rm -r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/{matplotlib,mpl_toolkits}/tests/
}
