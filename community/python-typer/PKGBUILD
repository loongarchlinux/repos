# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Tony Benoy <me@tonybenoy.com>

_pkgname=typer
pkgname=python-typer
pkgver=0.4.1
pkgrel=1
pkgdesc="Build great CLIs. Easy to code. Based on Python type hints"
arch=('any')
url="https://typer.tiangolo.com/"
license=('MIT')
depends=('python-click')
makedepends=('python-setuptools' 'python-dephell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tiangolo/typer/archive/${pkgver}.tar.gz")
sha512sums=('1bf3b4ffaa9d92cc955e3e5480f34732da1c13778b6047f38e98f5460c7d85a8ebbfefe3ac0b0fefa46e4c37f0dbac50ce5e6670b70c7c2f94818c6bd815b210')

prepare() {
  cd "typer-$pkgver"
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "typer-$pkgver"
  python setup.py build
}

package() {
  cd "typer-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
