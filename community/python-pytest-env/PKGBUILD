# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Rafael Fontenelle <rafaelff@gnome.org>

pkgname=python-pytest-env
pkgver=0.6.2
_commit=afb13a0e908f649b69273f299262ac12f1b71113
pkgrel=4
pkgdesc='pytest plugin used to set environment variables'
arch=(any)
url='https://github.com/MobileDynasty/pytest-env'
license=(MIT)
depends=(python-pytest)
makedepends=(python-setuptools)
# Upstream does not use git tags, and the tarball on PyPI does not include the LICENSE file
# https://github.com/MobileDynasty/pytest-env/issues/6
source=($pkgname-$pkgver.tar.gz::https://github.com/MobileDynasty/pytest-env/archive/$_commit/$pkgname-$_commit.tar.gz)
sha256sums=('653b29ab430dc99ee442776415ebf4e82e225b203d993108f0bf4845f8dbeaa8')

build() {
  cd pytest-env-$_commit
  python setup.py build
}

package() {
  cd pytest-env-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
