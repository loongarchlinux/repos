# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-nenv
pkgver=0.3.0
pkgrel=1
pkgdesc="Convenient wrapper for Ruby's ENV"
arch=(any)
url='https://github.com/e2/nenv'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-rspec ruby-rake)
options=(!emptydirs)
source=(https://github.com/e2/nenv/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c2904fbecf6b37d4f9955770eafd22bb400fcc99f1847d7a6eda94bf04bf45ef')

prepare() {
  cd nenv-$pkgver
  sed -i 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -printf "%P\\\\0"|' nenv.gemspec
  sed -i '/rubocop/d' Rakefile
  sed -i '/[cC]overalls/d' spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd nenv-$pkgver
  gem build nenv.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    nenv-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd nenv-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd nenv-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
