# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pdm
pkgname=python-pdm
pkgver=2.2.0
pkgrel=1
pkgdesc="A modern Python package manager with PEP 582 support"
arch=(any)
url="https://github.com/pdm-project/pdm"
license=(MIT)
depends=(
  python-blinker
  python-cachecontrol
  python-certifi
  python-dotenv
  python-findpython
  python-installer
  python-packaging
  python-pdm-pep517
  python-pep517
  python-platformdirs
  python-requests-toolbelt
  python-resolvelib
  python-rich
  python-shellingham
  python-tomli
  python-tomlkit
  python-unearth
  python-virtualenv
)
makedepends=(python-build python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-xdist)
optdepends=('python-keyring: for storing credentials')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('63444c20358c680b6bb1977443796d1e9d1543199b809d823ed3681208e1bb47780f85b0794d93d17727baa86c02145c111b87ba8c05966dea10be9874763e61')
b2sums=('8305afca4cfa8f5714289131d29b53819f0ef7163a738191a922b80f0254c56e26450b95f3c7a3d12cf45511ff5a335a5f754a0d3a06d623e509e59e1390ec37')

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # skip tests that would fail because of missing interpreters: https://github.com/pdm-project/pdm/issues/1175
  pytest -vv -c /dev/null -k 'not test_basic_integration and not test_use_invalid_wrapper_python'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # completions
  install -vd "$pkgdir/usr/share/"{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}
  "$pkgdir"/usr/bin/pdm completion bash > "$pkgdir/usr/share/bash-completion/completions/$_name"
  "$pkgdir"/usr/bin/pdm completion fish > "$pkgdir/usr/share/fish/vendor_completions.d/$_name.fish"
  "$pkgdir"/usr/bin/pdm completion zsh > "$pkgdir/usr/share/zsh/site-functions/_$_name"
}
