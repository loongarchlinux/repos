# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pdm
pkgname=python-pdm
# WARNING: vendored libs may change in patch level versions...
pkgver=1.15.4
pkgrel=2
pkgdesc="A modern Python package manager with PEP 582 support"
arch=(any)
url="https://github.com/pdm-project/pdm"
license=(MIT)
depends=(
  python-blinker
  python-click
  python-dotenv
  python-findpython
  python-installer
  python-packaging
  python-pdm-pep517
  python-pep517
  python-pip
  python-platformdirs
  python-resolvelib
  python-shellingham
  python-tomli
  python-tomlkit
  python-wheel
)
# packages devendored from pdm sources
depends+=(
  python-colorama
  python-halo
  python-log_symbols
)
# packages devendored from pip sources
depends+=(
  python-html5lib
  python-requests
  python-setuptools
)

makedepends=(python-build)
checkdepends=(python-pytest python-pytest-mock python-pytest-xdist)
optdepends=('python-keyring: for storing credentials')
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-1.15.4-devendor.patch
)
sha512sums=('35e32d6ccb2e4aa874f413e3d5f3a92c772a7ee6d7dd3f75a40ae082148cfb389585a1371e130ebdbc75ec25180d5f525177012198342e3ece354bfc799735e1'
            'e878faa0e92847084788fb146bade64b0f99de1c74e85e1c6585e3f0b27fa5f643a0e3d177a370e3274fcc818b85270c112ee9484b2d5db5f59331cd55e22d50')
b2sums=('e2c2e3437ee290aa849b1e04e51932fe58af1c77581a37a53b2885db6fc622c819688e222b2ef889192a62c2552a3f180fa3924f9c688f72ad79c5ee16180d03'
        'a1345e217b6c0bc468f47c9ed4c8e0d4751b6bf2f6d88de91e35bd22f6df3888a0bda6d8f9f84b6a1085d6ed82a1b6be3efc8cdbb9fa367b0d3d42dfa2d68027')

prepare() {
  # devendor bundled libraries
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-1.15.4-devendor.patch
  # remove vendor dir as we use system dependencies
  rm -frv $_name-$pkgver/pdm/_vendor
}

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # skip tests that would fail because of missing interpreters: https://github.com/pdm-project/pdm/issues/1175
  pytest -vv -c /dev/null -k 'not test_basic_integration'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
