# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pdm
pkgname=python-pdm
pkgver=2.1.3
pkgrel=1
pkgdesc="A modern Python package manager with PEP 582 support"
arch=(any)
url="https://github.com/pdm-project/pdm"
license=(MIT)
depends=(
  python-blinker
  python-cachecontrol
  python-certifi
  python-dotenv
  python-findpython
  python-installer
  python-packaging
  python-pdm-pep517
  python-pep517
  python-platformdirs
  python-requests-toolbelt
  python-resolvelib
  python-rich
  python-shellingham
  python-tomli
  python-tomlkit
  python-unearth
  python-virtualenv
)
makedepends=(python-build python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-xdist)
optdepends=('python-keyring: for storing credentials')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('75355809a3848ad9fa4a723d04a26e6deac29155deb1180be8714fe572a439b68eafa6494b3babcd4ff512a77260d678d18dfd657c5c619fccfb071f6cf49eec')
b2sums=('17a7c37cf750152d03d6993e9a397be141c582ceb4ad12027a567369762fb5b0525879d89f513709336fbf5915b338a3b7bbaec570b61e63047c25a06d96ea0a')

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # skip tests that would fail because of missing interpreters: https://github.com/pdm-project/pdm/issues/1175
  pytest -vv -c /dev/null -k 'not test_basic_integration'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
