# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-canonicaljson
pkgver=1.6.3
pkgrel=1
pkgdesc='Canonical JSON'
url='https://pypi.python.org/pypi/canonicaljson/'
arch=('any')
license=('Apache')
depends=('python' 'python-simplejson' 'python-frozendict')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("python-canonicaljson-$pkgver.tar.gz::https://github.com/matrix-org/python-canonicaljson/archive/v${pkgver}.tar.gz")
sha512sums=('4b2cb7524425b8a4dde2359273f60110ca01081d31b0add158f106ff1f8874366978d6a15baed53a948503d4013b87a0b75ad7f6e8ea6ce42f3f9fc95672280f')
b2sums=('9d97ad9f8c0803fdfd1f4d2f85291fb175d19be23fb9c3bdf717030b9dae30f810329118c484cdf9dab5d77f01ea41b08653559fa188c90305126bdf3f4e5f9d')

build() {
	cd python-canonicaljson-$pkgver
	python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
	cd python-canonicaljson-$pkgver
	python -m venv --system-site-packages test-env
	test-env/bin/python -m installer dist/*.whl
	PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
	cd python-canonicaljson-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
