# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=fissix
pkgname=python-fissix
pkgver=21.6.6
pkgrel=3
pkgdesc="Fork of lib2to3 with patch for Python issue33983 support"
arch=('any')
url="https://github.com/jreese/${_pkgname}"
license=('PSF')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-tests')
source=(https://pypi.python.org/packages/source/f/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('756c47696a8d769ebf770ec7f018fd7b5948cd2cd6a204b5392bc9214ff795bc')
b2sums=('b913c29f49e1d49062d441f5a6064ccb7ff167dd92c874458b023e3a37079364b2e7b310ed502d3d88e7512d0752f84bc597a27dd6c48ca2975a28aae2019d56')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest --verbose fissix/tests
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
