# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aiounittest
pkgver=1.4.0
pkgrel=1
pkgdesc="Test asyncio code more easily"
url="https://github.com/kwarunek/aiounittest"
license=('MIT')
arch=('any')
depends=('python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/kwarunek/aiounittest/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5a8921debbf3be4a36e1ac4af39cdb46f802c06454b3be06c2b143122ba016d6badbb496b18d9379cb9a4b99c9fb7facc59f0676c5a776859c408fd1bdd74e1f')

build() {
  cd aiounittest-$pkgver
  python setup.py build
}

check() {
  cd aiounittest-$pkgver
  nosetests3
}

package() {
  cd aiounittest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
