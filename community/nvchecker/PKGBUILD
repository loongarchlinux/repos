# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=2.1
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
# setuptools is in depends as pkg_resources is used in sortversion.py
depends=('python' 'python-setuptools' 'python-toml' 'python-structlog' 'python-appdirs' 'python-tornado' 'python-pycurl')
makedepends=('python-docutils')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'git: support for git repositories'
)
source=("https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c9c000b0b648996888a7263fb722779287b5d94e4eb377caf115baa3d4efc9c7c334c1ead43855df057801a22cc86021a314b7c987f0b5596eee972526aba7f9')

build() {
  cd nvchecker-$pkgver
  python setup.py build

  make -C docs man
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  # bash completion scripts & docs
  install -Dm644 scripts/nvtake.bash_completion "$pkgdir"/usr/share/bash-completion/completions/nvtake
  install -Dm644 docs/usage.rst -t "$pkgdir"/usr/share/doc/$pkgname/
  install -Dm644 sample_config.toml -t "$pkgdir"/usr/share/doc/$pkgname/
  install -Dm644 docs/_build/man/nvchecker.1 -t "$pkgdir"/usr/share/man/man1/
}

# vim:set ts=2 sw=2 et:
