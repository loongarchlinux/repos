# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: cclin <cclinet@outlook.com>

pkgname=python-aiofiles
pkgver=22.1.0
pkgrel=2
pkgdesc='File support for asyncio'
arch=(any)
license=(APACHE)
url=https://github.com/Tinche/aiofiles
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
)
checkdepends=(
  python-coverage
  python-poetry
  python-pytest-asyncio
  python-pytest
  python-pytest-cov
  python-setuptools
  python-tox
)
_tag=9d697f5857b5e49bad485212d9d81917ce65b607
source=(git+https://github.com/Tinche/aiofiles.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd aiofiles
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiofiles
  python -m build --wheel --no-isolation
}

check() {
  cd aiofiles
  SETUPTOOLS_USE_DISTUTILS=stdlib tox -e py310
}

package() {
  python -m installer --destdir="${pkgdir}" aiofiles/dist/*.whl
  install -Dm 644 aiofiles/LICENSE -t "${pkgdir}"/usr/share/licenses/python-aiofiles/
}

# vim: ts=2 sw=2 et:
