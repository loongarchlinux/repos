# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Arnaud Durand <biginozNOSPAM_at_free.dot.fr>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>

pkgname=bleachbit
pkgver=4.0.0
pkgrel=3
pkgdesc='Deletes unneeded files to free disk space and maintain privacy'
arch=(any)
url='https://www.bleachbit.org/'
license=(GPL3)
depends=(python-gobject gtk3)
source=(https://download.bleachbit.org/$pkgname-$pkgver.tar.bz2
        maximize-infinite-loop.patch::"https://github.com/bleachbit/bleachbit/commit/933ea605.patch")
sha256sums=('34dedf9d1f7545bcf039c309f9a8e2b06b71d2a44d27fcf675abd194a981c3b6'
            '005f9e4ab4c1ed39379b0e02a9e15c2a92083a912f85447fef9701b2ffd33466')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../maximize-infinite-loop.patch # Fix infinite loop with maximized window
}

package() {
  cd $pkgname-$pkgver

  make prefix=/usr DESTDIR="$pkgdir" install
}
