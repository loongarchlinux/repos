# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgbase=python-pkginfo
pkgname=(python-pkginfo)
pkgver=1.5.0.1
pkgrel=4
pkgdesc='Query metadatdata from sdists / bdists / installed packages'
arch=('any')
url="https://pypi.python.org/pypi/pkginfo"
license=('PSF')
depends=("python")
makedepends=("python-setuptools")
checkdepends=("python-nose" "python-coverage")
source=("https://pypi.io/packages/source/p/pkginfo/pkginfo-$pkgver.tar.gz")
sha512sums=('319bb403970affd5a4222dfdb3d17738630d1350ea84e51ad1bebc6c8c72e0dcfd69a4eaf5e7bd156b8e605d806357922c05cc994dcafb01b7a33c617b2df447')

build() {
  cd "$srcdir"/pkginfo-$pkgver
  python setup.py build
}

check() { (
  cd "$srcdir"/pkginfo-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3
  ) || warning "Tests failed"
}

package_python-pkginfo() {
  cd pkginfo-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
