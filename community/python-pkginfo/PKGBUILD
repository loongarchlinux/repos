# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgname=python-pkginfo
_pkgname=pkginfo
pkgver=1.8.2
pkgrel=2
pkgdesc='Query metadata from sdists / bdists / installed packages'
arch=('any')
url='https://pypi.python.org/pypi/pkginfo'
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/p/pkginfo/pkginfo-$pkgver.tar.gz")
sha512sums=('06164b6c17000cf25e72d39e4e025a395cdd09afaab6169180cf096bae5a1ff863930d3b368274b6a03458a9ae1c3cb996559c0b042807141435b1868189b5f6')
b2sums=('bff34659d77c3f9f870b130ad0d86fa730c42f1f3db78468a4557df855eb57e6b1dc2fefbebf9126306736f680538bcc6cddcaefdcb9defdc99c22e04ae2e9fd')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"

  python -m nose
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
