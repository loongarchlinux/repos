# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgname=python-pkginfo
_pkgname="${pkgname#python-}"
pkgver=1.9.2
pkgrel=1
pkgdesc='Query metadata from sdists / bdists / installed packages'
arch=('any')
url='https://pypi.python.org/pypi/pkginfo'
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/p/pkginfo/pkginfo-$pkgver.tar.gz")
sha512sums=('16eb4708040811a21184e5fff1bbbde775360718fa67df97f56c646381f6f1e58967920753437e5759e4d88c31a4f10c1e4d8be2380981939e51e4ee140dbeda')
b2sums=('e217e3d58fbc7a4efe7321d1eb4f6c95ea75cfcdcbdd06c0f8b728eb1ec18c5800a7ed6f6338996c60f5f251f36d40b40c20d0b1700956dd8a23a6e40594a356')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"

  pytest -v
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
