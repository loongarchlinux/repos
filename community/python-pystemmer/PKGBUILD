# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pystemmer
pkgname=('python-pystemmer' 'python2-pystemmer')
pkgver=2.0.1
pkgrel=1
pkgdesc="Snowball stemming algorithms, for information retrieval"
arch=('x86_64')
license=('BSD' 'MIT')
url="http://snowball.tartarus.org"
makedepends=('python' 'python2' 'cython' 'cython2')
source=("https://pypi.io/packages/source/P/PyStemmer/PyStemmer-$pkgver.tar.gz")
sha512sums=('3ac0ef16f0caae74cb19a73e7de7a56e3947a6f81a14273885b3461392f7f33e82a433d4ffa7b4a61d87bb94aff7e12c8e0c9726de2eb6b386a9f2c558678a6c')

prepare() {
  # Force cython rebuild
  rm PyStemmer-$pkgver/src/Stemmer.c

  cp -a PyStemmer-$pkgver{,-py2}
}

build() {
  cd "$srcdir/PyStemmer-$pkgver"
  python setup.py build

  cd "$srcdir/PyStemmer-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyStemmer-$pkgver"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" python runtests.py

  cd "$srcdir/PyStemmer-$pkgver-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 runtests.py
}

package_python-pystemmer() {
  depends=('python')

  cd PyStemmer-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pystemmer() {
  depends=('python2')

  cd PyStemmer-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
