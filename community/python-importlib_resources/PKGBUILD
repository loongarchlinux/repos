# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.10.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=(any)
url="https://github.com/python/importlib_resources"
license=(Apache)
depends=(python-zipp)
makedepends=(python-build python-installer python-setuptools-scm python-toml python-wheel)
checkdepends=(python-pytest python-tests)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('ab0d39cbc8df4285b06b5d4bb4138426d99c4421373433cc7c1bcb05d0f21ef0ce5ef0e0c659e2f10b9cc075cefc4d267a76f27b773cb80d2faeaf3d9d83b36d')
b2sums=('dc5119fd41d85a5da6e311b4106ab2cfb9d669232d76838e807bee8f6ddcea826254d88bc005e3a8e9ae50a517feba144654fba3d787b1c9088ab2d38f94ce92')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
