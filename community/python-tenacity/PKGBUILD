# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=8.1.0
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-typeguard')
source=("https://github.com/jd/tenacity/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('cb81b6f6fdea4834483681544d138de6464ec94fc8464af898d3a8e9003a215aba0a3f7ce897f48e8f4948c26f1e7bc6548a16c7c7f3f784eb34e94de96223f1')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
