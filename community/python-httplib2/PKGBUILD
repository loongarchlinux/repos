# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgbase=python-httplib2
pkgname=python-httplib2
pkgver=0.20.3
pkgrel=1
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='https://github.com/httplib2/httplib2'
license=('MIT')
arch=('any')
depends=('python' 'ca-certificates')
makedepends=('python-setuptools')
checkdepends=('flake8' 'python-mock' 'python-pytest-cov' 'python-pytest-forked'
              'python-pytest-timeout' 'python-pytest-xdist' 'python-pytest-randomly'
              'python-future' 'python-cryptography')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/httplib2/httplib2/archive/v$pkgver.tar.gz"
        'cert.patch')
sha512sums=('6fe6c57e8d58ee75d46dfcca6751f74c182947592ae727a07513abcb74542a187e4887eac6437f169ab2a7057ca3440e928c41d7b6e3031a08512aa4cd25e3ae'
            '503719fb07b9ff1f06f252b73804787759139098dba990a7dcd9e9b3602f7271d2251e703993636b249b3597c9a240c721c536a4fe31526ec4ba9441f6c8da56')

prepare() {
  cd httplib2-$pkgver
  patch -p0 -i "$srcdir"/cert.patch
  sed -i 's/==/>=/' requirements-test.txt
}

build() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py build
}

check() {
  cd httplib2-$pkgver
  PYTHONPATH=build/lib pytest
}

package() {
  cd httplib2-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
