# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgbase=python-httplib2
pkgname=python-httplib2
pkgver=0.20.4
pkgrel=1
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='https://github.com/httplib2/httplib2'
license=('MIT')
arch=('any')
depends=('python' 'ca-certificates')
makedepends=('python-setuptools')
checkdepends=('flake8' 'python-mock' 'python-pytest-cov' 'python-pytest-forked'
              'python-pytest-timeout' 'python-pytest-xdist' 'python-pytest-randomly'
              'python-future' 'python-cryptography')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/httplib2/httplib2/archive/v$pkgver.tar.gz"
        'cert.patch')
sha512sums=('46396a9bb439e61939e481a2502567a6dc40686471267ea4c357f141ac138193107f90ac465db683e4ab71e5d92249b810657bff0cb74af7a37a9a23c8320741'
            '503719fb07b9ff1f06f252b73804787759139098dba990a7dcd9e9b3602f7271d2251e703993636b249b3597c9a240c721c536a4fe31526ec4ba9441f6c8da56')

prepare() {
  cd httplib2-$pkgver
  patch -p0 -i "$srcdir"/cert.patch
  sed -i 's/==/>=/' requirements-test.txt
}

build() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py build
}

check() {
  cd httplib2-$pkgver
  # test_client_cert_password_verified fails with cryptography 36 https://github.com/httplib2/httplib2/issues/221
  PYTHONPATH=build/lib pytest -k 'not test_client_cert_password_verified'
}

package() {
  cd httplib2-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
