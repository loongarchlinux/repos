# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sqlitedict
pkgver=2.0.0
pkgrel=1
pkgdesc="Persistent dict in Python, backed up by sqlite3 and pickle, multithread-safe"
url="https://github.com/RaRe-Technologies/sqlitedict"
license=('Apache')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RaRe-Technologies/sqlitedict/archive/$pkgver.tar.gz")
sha512sums=('1af65e2ab3c0b8b9ecbed18857a873e3a4868130b628468dabfd0dbf5e83b659703ba85f0a82cae030b120a8c549589ed798c09c2ec129fcd7d5f65c192830a0')

build() {
  cd sqlitedict-$pkgver
  python setup.py build
}

check() {
  cd sqlitedict-$pkgver
  mkdir -p tests/db
  python setup.py nosetests
}

package() {
  cd sqlitedict-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
