# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-plugin-api
pkgname=haskell-hls-plugin-api
pkgver=1.2.0.1
pkgrel=32
pkgdesc="Haskell Language Server API for plugin communication"
url="https://github.com/haskell/haskell-language-server/hls-plugin-api"
license=("Apache")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-data-default' 'haskell-dependent-map'
         'haskell-dependent-sum' 'haskell-dlist' 'haskell-ghc' 'haskell-ghc-api-compat'
         'haskell-hashable' 'haskell-hls-graph' 'haskell-hslogger' 'haskell-lens' 'haskell-lsp'
         'haskell-optparse-applicative' 'haskell-opentelemetry' 'haskell-regex-tdfa'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('e6c231bfe29b9fd299708a19f2388c74fb4aa5ac28a8a5150dc011290c0a87cbb38dbda615d6a52ffc70cb1d9078334ee2e2b31e251a5ec375230b11d6852a96')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -r ghc:ghc,ghc-api-compat $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
