# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=0.3.1
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=('any')
depends=('python-tomlkit')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/v$pkgver.tar.gz")
sha512sums=('ee3be2341b11f61f26755be01eb981f6200c44a1b470dac6a4586cd24a029e98da892408344e29ba0d9ff5b7f95dcbe29c82f9b6b3edfbdec51f3c1a8be0ef38')

build() {
  cd plette-$pkgver
  python setup.py build
}

check() {
  cd plette-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd plette-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
