# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-flask-httpauth
_pkgname=Flask-HTTPAuth
pkgver=4.4.0
pkgrel=3
pkgdesc='Basic, Digest and Token HTTP authentication for Flask routes'
url='https://github.com/miguelgrinberg/Flask-HTTPAuth'
arch=('any')
license=('MIT')
depends=('python' 'python-flask')
checkdepends=('python-pytest')
source=(https://github.com/miguelgrinberg/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('63aec9c0acdabcce45c12d19b17a96424884bfdcb080f0f0b38ee187ce97a6d316a48781e8741f154536f7d473b0228b6747f78f8a9ee4b37b42d50e5bf50c02')
b2sums=('ed84ff66389db1a6e8fc2c3d88df9d901af6aa703bc5f0135ae6d5d927d09f8f42f81e10b87b2953a0da7fbd50b21b3661ddce733eb364d4c85f0f59ac6615fe')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  pytest tests/
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
