# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-libarchive-c
pkgver=3.1
pkgrel=1
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('CC0')
depends=('python' 'libarchive')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=(https://github.com/Changaco/python-libarchive-c/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1dc931681b0df5bd76ba5314155a8bbee0d2bf1582f4e439f922a6173c90346c1d84dceb0186f583016b00e5a078c8f45265c6a4dc4c790f9afb88204911f88e')
b2sums=('fd81cb003489b6343d7c44000feea97ae6018c33a0ebccd4fbaa6952efbfbfa80568f8bac2459d241b13535b3b72e0c203a689b455d377eb970dd48c71fbac35')

prepare() {
  sed '/os.umask(2)/d' -i ${pkgbase}-${pkgver}/setup.py
}

check() {
  cd ${pkgbase}-${pkgver}
  LC_CTYPE=en_US.UTF-8 py.test
}

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
