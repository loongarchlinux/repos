# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.5.0
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url='https://github.com/Mic92/vicious'
arch=('any')
license=('GPL2')
depends=('lua53')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=(https://github.com/Mic92/vicious/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c25ce6e7e7539a5b62ff63ff2cc783b4f723cc1e3bcab1871804abc098d8fd96')
sha512sums=('deb21587d1d7f2e6e4dd77c74abbfa337acf6c99ef9bebc0e505f1508afc6e3b03f043a639f866fcda0191447c103d3fba51376682201482fb669a263321dee5')

package() {
  cd ${pkgname}-$pkgver
  install -Dm 644 *.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}"
  install -Dm 644 widgets/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/widgets"
  install -Dm 644 contrib/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/contrib"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
