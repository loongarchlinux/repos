# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.5.1
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url=https://github.com/Mic92/$pkgname
arch=(any)
license=(GPL2)
depends=(lua53)
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('29a0d645a131297dea83477a27c82dbb480c1f5288ecde911ad055b75dc9f278')

package() {
  cd "$_archive"
  install -Dm0644 -t "$pkgdir/usr/share/lua/5.3/$pkgname/" *.lua
  install -Dm0644 -t "$pkgdir/usr/share/lua/5.3/$pkgname/contrib/" contrib/*.lua
  install -Dm0644 -t "$pkgdir/usr/share/lua/5.3/$pkgname/widgets/" widgets/*.lua
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}
