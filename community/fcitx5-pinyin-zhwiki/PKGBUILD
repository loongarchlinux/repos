# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fcitx5-pinyin-zhwiki
pkgname=(fcitx5-pinyin-zhwiki rime-pinyin-zhwiki)
_converterver=0.2.3
_zhwikiver=20211220
_webslangver=20220101
pkgver=$_converterver.$_webslangver
pkgrel=1
epoch=1
pkgdesc="Fcitx 5 Pinyin Dictionary from zh.wikipedia.org"
arch=('any')
url="https://github.com/felixonmars/fcitx5-pinyin-zhwiki"
license=('CCPL:by-sa' 'custom:GFDL')
makedepends=('libime' 'opencc' 'pypinyin')
source=(https://github.com/felixonmars/fcitx5-pinyin-zhwiki/archive/$_converterver/$pkgbase-$_converterver.tar.gz
        https://github.com/felixonmars/fcitx5-pinyin-zhwiki/releases/download/$_converterver/web-slang-$_webslangver.source
        https://dumps.wikimedia.org/zhwiki/$_zhwikiver/zhwiki-$_zhwikiver-all-titles-in-ns0.gz
        https://www.gnu.org/licenses/fdl-1.3.txt)
sha512sums=('b09e4e29e4c3e5e9007f28d56e2ac07663ad56eb1e4004dad647de7f4b41522054a65c40f952bb3752ea6015b3b1adc30f95922749cd5653ea60b9d151dd4013'
            '64723f7eea758f21980ad2ff5df2855e089424d88c5111c2731807dd06df795d98d333ac73e860817d84b304ab4aacac34a79835e330d49a7db9de30e46f08b9'
            'cfc39aaced4fe5ed699bada325b5e9cc99c221bbbb750eeaa65a5d43f8a50e064a379fcb0585670914281936fc1ea4e23b233e756f038f1ff2dd2f41a34fd482'
            'bea1788b2bdc84f470e459114b871cf4ee991718964a63e18adde65116806d7676484cb30857cf74dece5eef5f96a015ee4a21900e019623e5d3484868b28b7f')

_make="make -L VERSION=$_zhwikiver WEB_SLANG_VERSION=$_webslangver"

prepare() {
  cd $pkgbase-$_converterver
  # Workaround pacman decompression
  touch zhwiki-$_zhwikiver-all-titles-in-ns0.gz
  ln -s ../zhwiki-$_zhwikiver-all-titles-in-ns0
  ln -s ../web-slang-$_webslangver.source
}

build() {
  cd $pkgbase-$_converterver
  $_make zhwiki.dict zhwiki.dict.yaml
}

package_fcitx5-pinyin-zhwiki() {
  cd $pkgbase-$_converterver
  $_make DESTDIR="$pkgdir" install
  install -Dm644 ../fdl-1.3.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_rime-pinyin-zhwiki() {
  cd $pkgbase-$_converterver
  $_make DESTDIR="$pkgdir" install_rime_dict
  install -Dm644 ../fdl-1.3.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
