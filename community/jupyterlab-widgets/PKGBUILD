# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=1.0.2
pkgrel=2
pkgdesc='JupyterLab extension to use ipywidgets'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyterlab python-ipywidgets)
makedepends=(python-setuptools python-jupyter_packaging)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz
        drop-ensure_python.patch)
sha256sums=('7885092b2b96bf189c3a705cc3c412a4472ec5e8382d0b47219a66cccae73cfa'
            '444045fbd2ac725d78936cb6e7d1306ad0e160a8c7e81b95c7e4419e0d10fa3b')

prepare() {
  cd $_pipname-$pkgver
  patch -Np1 -i ../drop-ensure_python.patch
}

build() {
  cd $_pipname-$pkgver
  python setup.py build 
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

# These should be installed by setup.py, but they are not
  mkdir -p "$pkgdir"/usr/share/jupyter/labextensions/@jupyter-widgets/
  cp -r jupyterlab_widgets/labextension "$pkgdir"/usr/share/jupyter/labextensions/@jupyter-widgets/jupyterlab-manager

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
