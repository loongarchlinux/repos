# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=repod
pkgver=0.1.0
pkgrel=1
pkgdesc="Tooling to maintain binary package repositories"
arch=(any)
url="https://repod.archlinux.page/"
license=(GPL3)
depends=(pyalpm python-aiofiles python-email-validator python-jinja python-magic python-orjson python-pydantic python-pyzstd python-subprocess-tee python-tomli)
makedepends=(python-build python-installer python-poetry python-sphinx python-sphinx-argparse python-wheel)
checkdepends=(python-pytest python-pytest-asyncio python-pytest-lazy-fixture)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('6fcaf6d9bbebb8b4f48f993b9738839154fb86b31c1d89b63d988a57965e0228')
b2sums=('8ea533427fd8975df40938048d45e624fd475f38eabebe8c8798a8d09d5eb6e9fff0c6b37b7c80190c117cb1cff489a0fed698f6ed90cbf5aea966ffab7ba0e3')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation

  python -c 'from repod import export_schemas; from pathlib import Path; export_schemas(Path("docs/schema/"))'
  PYTHONPATH="$PWD:$PYTHONPATH" make man -C docs
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -k 'not (integration or regex)'
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/man1/*.1 -t "$pkgdir/usr/share/man/man1/"
}
