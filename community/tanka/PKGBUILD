# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=tanka
pkgver=0.23.1
pkgrel=1
pkgdesc="Flexible, reusable and concise configuration for Kubernetes"
arch=('loong64' 'x86_64')
url="https://github.com/grafana/tanka"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/grafana/tanka/archive/v${pkgver}.tar.gz")
sha512sums=('83c7dcbc1f7f5f44fe43c3fe7c1c3454896b4a0548569385be5a66f03f978d375c910fbc9f06f881a965a4136354bdf6c12cf478c254d28fccaed3059e4cbb38')
options=('!lto')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/grafana "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/grafana/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/grafana/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-s -w -linkmode=external -X github.com/grafana/tanka/pkg/tanka.CURRENT_VERSION=${pkgver}" -v .
  chmod +x ./tk
}

#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  install -Dsm755 ./tk "${pkgdir}/usr/bin/tk"
}
