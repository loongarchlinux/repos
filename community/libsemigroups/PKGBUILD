# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=2.2.0
pkgrel=1
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io/libsemigroups'
arch=(loongarch64 x86_64)
license=(GPL3)
depends=(fmt)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('6485e353225ee4c62f55bbd93e48b0ba6fbea780b9791603011eae72b1d28f7a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
