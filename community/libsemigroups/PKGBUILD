# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=1.3.3
pkgrel=1
pkgdesc="C++ library for semigroups and monoids"
url="https://libsemigroups.github.io/libsemigroups/"
arch=(loongarch64 x86_64)
license=(GPL3)
depends=(fmt)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('91e40ba22c4e33cb0ef531fc9b8acdfafc5e65209fdfef612a4e68833cc7c08d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
