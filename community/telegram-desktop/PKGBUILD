# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: hexchain <i@hexchain.org>
pkgname=telegram-desktop
pkgver=4.2.0
pkgrel=1
pkgdesc='Official Telegram Desktop client'
arch=('loong64' 'x86_64')
url="https://desktop.telegram.org/"
license=('GPL3')
depends=('hunspell' 'ffmpeg4.4' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'qt6-5compat' 'xxhash' 'glibmm'
         'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
_kcoreaddons_version="5.98.0"
source=("https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
        "https://invent.kde.org/frameworks/kcoreaddons/-/archive/v$_kcoreaddons_version/kcoreaddons-v$_kcoreaddons_version.tar.gz"
        "bundle-kcoreaddons.patch::https://github.com/desktop-app/cmake_helpers/compare/b9f1b60...ilya-fedin:cmake_helpers:6bcdd3fa3618038b1824a2f772543d9db48e8b27.patch"
       )
sha512sums=('47d1e618a03f3b870b5aff81bc71793bcdd3fec91bb1263a65e0a935867ade4bc3de29f9f01ddce3837e7239e17a88caba6afc2b49b3181dd8cd6eeca597d6f2'
            'd84f0d0c29f819488fd7632df28b8a9da5fb3a0724652529d3f8a59b62ea12f5f0a1755b346d2d8b015ec492dd0fead8b74199f9a7e7ada26d45b8fd7ada21c8'
            '1edd0fc0a19de5cab558dd5721aa111ff9cb8f9bc24bad338173c696fc0c010a62eba1584d6deaad8940217b1725d15741cd55f546433bd62cccd0850036bce3')

prepare() {
    mv "$srcdir/kcoreaddons-v$_kcoreaddons_version" "tdesktop-$pkgver-full/Telegram/ThirdParty/kcoreaddons"
    cd tdesktop-$pkgver-full

    rm -rf Telegram/ThirdParty/libtgvoip/webrtc_dsp/absl

    # Using ilya-fedin/cmake_helpers bundled-kcoreaddons branch for now: https://github.com/telegramdesktop/tdesktop/issues/25073
    patch -Np1 -i "$srcdir"/bundle-kcoreaddons.patch -d cmake

    # Upstream suggested sed headers: https://github.com/telegramdesktop/tdesktop/issues/25073#issuecomment-1253908867
    find . -type f \( -iname '*.h' -or -iname '*.cpp' \) -print0 | xargs -0 sed -i 's/<KUrlMimeData>/<kurlmimedata.h>/g;s/<KShell>/<kshell.h>/g;s/<KSandbox>/<ksandbox.h>/g'
}

build() {
    cd tdesktop-$pkgver-full

    export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
    #Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c
    ninja -C build
}

package() {
    cd tdesktop-$pkgver-full
    DESTDIR="$pkgdir" ninja -C build install
    # They botched the release and put a lot of stuff here.
    rm -rf "$pkgdir/build"
}
