# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Ryan Corder <ryanc@greengrey.org>

pkgname=pam-krb5
pkgver=4.10
pkgrel=1
pkgdesc="PAM module providing Kerberos v5 support"
arch=('loongarch64' 'x86_64')
url="https://www.eyrie.org/~eagle/software/pam-krb5/"
license=('custom')
depends=('krb5' 'pam')
source=(https://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('e9608f7b8380788d838ca22fd12a44b3c86b11090e07b02efeabc8fc4beb9f27'
            'SKIP')
validpgpkeys=('E784364E8DDE7BB370FBD9EAD15D313882004173') # Russ Allbery <eagle@eyrie.org>

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-reduced-depends --libdir=/usr/lib

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
