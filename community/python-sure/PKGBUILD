# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sure
pkgver=2.0.0
pkgrel=4
pkgdesc="An idiomatic testing library for python with powerful and flexible assertions"
arch=('any')
url="https://github.com/gabrielfalcao/sure"
license=('GPL')
depends=('python-six' 'python-mock')
makedepends=('python-setuptools' 'git')
checkdepends=('python-nose' 'python-rednose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gabrielfalcao/sure/archive/$pkgver.tar.gz"
        "python310.diff")
sha512sums=('d113b08afd63c379ec9f05169b1ad15f3bb616a1f6ad7d0ef6513e98ba7ebb7462254b2d25307906d9f63603fed4f9e4b83ccbca0ec53a2b483f9395fa0fb94a'
            '0703cc88d01928e25e94e92fc0cfee68c6759322c9ee1d1603f381b6d67d6cf9db97f26aad1ccf7f6d6702370bc2c0194245aa3ba1c1b1384027a5712e469139')

prepare() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/sure-$pkgver
  # https://github.com/gabrielfalcao/sure/issues/169
  patch -Np1 -i ../python310.diff
}

build() {
  cd "$srcdir"/sure-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/sure-$pkgver
  python setup.py test
}

package() {
  cd sure-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
