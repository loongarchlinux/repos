# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deepdiff
pkgver=5.7.0
pkgrel=1
pkgdesc="Deep Difference and Search of any Python object/data."
url="https://github.com/seperman/deepdiff"
license=('MIT')
arch=('any')
depends=('python-ordered-set')
optdepends=('python-click: for cli'
            'python-yaml: for cli'
            'python-toml: for cli'
            'python-clevercsv: for cli')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-clevercsv' 'python-click' 'python-jsonpickle' 'python-numpy'
              'python-toml' 'python-yaml')
source=("https://github.com/seperman/deepdiff/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3910b7259443a2bd056a90dcb9910c1d0999dd06e98d28a3dba8ee9c6fe237bfe74780db8f277f3a2cfecc4c32c279f5c93b72de708d12832b987759cca0d9e2')

build() {
  cd deepdiff-$pkgver
  python setup.py build
}

check() {
  cd deepdiff-$pkgver
  pytest tests
}

package() {
  cd deepdiff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
