# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.5.1
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/bw2/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-tests' 'python-yaml')
optdepends=('python-yaml: for YAML support')
source=("https://github.com/bw2/ConfigArgParse/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2c3854a45d97f0352321d2442a30408248e990dd46e2515cd7e2576efe9a33e03495231f8397ba6e3d79bb04bf11edfe5930b4765d46f29ca66b7bb3d3c1e0c9')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  pytest
}

package() {
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
