# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.8.4
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('loongarch64' 'x86_64')
url='http://christophe.varoqui.free.fr/'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git')
source=("multipath-tools::git+https://git.opensvc.com/multipath-tools/.git#tag=${pkgver}"
        '0001-fix-boolean-value-with-json-c-0.14.patch'
        '0002-make.patch')
sha256sums=('SKIP'
            '9aa4970790616637e0cc1b2ae609ca6bffb57effda0e8263fa73cbefc7f16a11'
            '95de3c6f42c8c80ee3ced00cefad7f003abdbf9358fcf72042d17819e871048a')

prepare() {
  cd "${pkgname}"

  # Fix systemd version detection in Makefile
  sed -i 's/sed -n .*$/head -n1 | cut -d" " -f2\)/g' Makefile.inc

  # Fix bindir in Makefile
  sed -i 's|$(exec_prefix)/sbin|$(exec_prefix)/bin|g' Makefile.inc

  patch -Np1 < ../0001-fix-boolean-value-with-json-c-0.14.patch
  patch -Np1 < ../0002-make.patch
}

build() {
  cd "${pkgname}"

  make
}

package() {
  cd "${pkgname}"

  make -j1 \
    DESTDIR="${pkgdir}" \
    exec_prefix="/usr" \
    LIB="/usr/lib" \
    install
}
