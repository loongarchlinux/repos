# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=4.5.0
pkgrel=3
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('loongarch64' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib' 'libgl' 'harfbuzz'
         'qt5-base')
makedepends=('boost' 'cmake' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'doxygen'
             'graphviz' 'mesa' 'glu' 'ninja')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
        'mariadb-libs: for MySQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example')
backup=('etc/wt/wt_config.xml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kdeforche/wt/archive/${pkgver}.tar.gz"
        wt-gcc11.patch)
sha512sums=('4cb8d822c4be4f120c1718410d1f493c9a81f8356eaa1b8dd5aea5095b7258bb950706c7871a2be89b1068ad34fe4153c02fb5ca6d5cc5870cb34107bfc5fcc9'
            'f79028388ccd61ce8d8486fdaf6a253ada81b6308998a902e9fe8946f8cefcf19b5fa3bf8a3c37f1e4c4ef1f47fdeaf8c55f1b02e458b149d7cb69d5a174c720')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/wt-gcc11.patch
}

build() {
  cd ${pkgname}-${pkgver}

  cmake . \
      -Bbuild \
      -GNinja \
      -DCONNECTOR_HTTP=ON \
      -DWT_WRASTERIMAGE_IMPLEMENTATION=GraphicsMagick \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      -DBUILD_EXAMPLES=ON
  ninja -C build
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
