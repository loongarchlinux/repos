# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.2.7.0
pkgrel=2
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=(loongarch64 x86_64)
url='https://liballeg.org/'
license=(custom)
makedepends=(cmake glu libtheora mesa-libgl ninja opusfile pandoc xorgproto)
depends=(dumb gtk3 jack libgl libpulse libtheora libwebp libxpm opusfile physfs)
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz")
b2sums=('9cc588a42d461ce06db94c1dd57642a2e9445c4930fcc164f2da57edfb463552a279a5e50f46740561a88c974c96605bb09fcc596e7bc6afb5e30b18ff7b74d5')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D WANT_DOCS_HTML=OFF \
    -G Ninja
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname-$pkgver/LICENSE.txt" \
    -t "$pkgdir/usr/share/licenses/$pkgname"
  # Fix man path
  mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}
