# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.2.0
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python')
makedepends=('python-cppy' 'python-pip')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
sha512sums=('05541ff46cc64b9958d714452138dc7bb343e6cc9308408a3f8cf25242e9640af37f8bfd22a7038e8ca70a895f57bdb983346259bce22b1630ac10b974d00a0e')

build() {
  cd kiwi-$pkgver
  python setup.py build
}

check() {
  cd kiwi-$pkgver
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver/
  python3 setup.py install --root="$pkgdir" --optimize=1
}
