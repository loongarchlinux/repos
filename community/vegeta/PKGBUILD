# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.8.3
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(go)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/v$pkgver.zip)
sha512sums=('08ecb388b1985840589f2bec90ab6a24efdb708ae2d3f0f0b2a5bbd2fa5047606c647c4fe4f5f763553a3607b6df3b5d7e71e9b125397ae97c497dad39752d5e')

build() {
  cd vegeta-$pkgver
  BUILDDATE=`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +'%FT%TZ%z'`
  go build -ldflags "-X main.Version=$pkgver -X main.Date='$BUILDDATE'"
}

check() {
  cd vegeta-$pkgver
  go test ./...
}

package() {
  cd vegeta-$pkgver
  install -Dm755 vegeta "$pkgdir/usr/bin/vegeta"
}
