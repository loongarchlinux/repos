# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=SPIRV-Headers
pkgname=spirv-headers
pkgver=1.5.4.r106+ge71fedd
pkgrel=2
pkgdesc="SPIR-V Headers"
arch=(any)
url="https://www.khronos.org/registry/spir-v/"
license=(MIT)
makedepends=(cmake git)
# From either https://github.com/KhronosGroup/SPIRV-Tools/blob/master/DEPS
# or https://github.com/KhronosGroup/SPIRV-LLVM-Translator/blob/master/spirv-headers-tag.conf
# using whatever is newest for latest release
_commit=e71feddb3f17c5586ff7f4cfb5ed1258b800574b
source=(git+https://github.com/KhronosGroup/${_pkg}.git#commit=${_commit})
sha512sums=(SKIP)

pkgver() {
  cd ${_pkg}
  git describe --tags --exclude '*raytracing*' | sed 's/\([^-]*-g\)/r\1/;s/-/./;s/-/+/'
}

build() {
  cmake -B build -S ${_pkg} -DCMAKE_INSTALL_PREFIX=/usr .
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_pkg}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
