# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=1.7
pkgrel=1
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(x86_64)
url="https://gitlab.freedesktop.org/mesa/adriconf"
license=(GPL3)
depends=(boost-libs gtkmm3 libxml++ pciutils)
makedepends=(boost cmake git)
_commit=3b40e23cf9974159b09b0a65c43255d4a0ba8d39  # tags/v1.7
source=("git+https://gitlab.freedesktop.org/mesa/adriconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_UNIT_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.appdata.xml "$pkgdir/usr/share/metainfo/br.com.jeanhertel.adriconf.appdata.xml"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.desktop "$pkgdir/usr/share/applications/br.com.jeanhertel.adriconf.desktop"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/br.com.jeanhertel.adriconf.png"
}
