# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sawyer
pkgver=0.9.2
pkgrel=1
pkgdesc='Secret User Agent of HTTP'
arch=(any)
url='https://github.com/lostisland/sawyer'
license=(MIT)
depends=(ruby-faraday ruby-addressable)
checkdepends=(ruby-minitest ruby-rake)
options=(!emptydirs)
source=(https://github.com/lostisland/sawyer/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('678923b7de572da916a445d5ee422d6270c5a6a7d913b2a7274c697b569384ab')

build() {
  local _gemdir="$(gem env gemdir)"
  cd sawyer-$pkgver
  gem build sawyer.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sawyer-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sawyer-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sawyer-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
