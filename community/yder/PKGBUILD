# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=yder
pkgver=1.4.14
pkgrel=2
pkgdesc='Logging library for C applications'
arch=(loongarch64 x86_64)
url=https://github.com/babelouest/yder
license=(LGPL2.1)
depends=(
  glibc
  liborcania.so
  libsystemd.so
)
makedepends=(
  cmake
  doxygen
  git
  graphviz
  ninja
  systemd
)
provides=(libyder.so)
_tag=2c2cd0b4f428aa1ece0f4637897d3e1fd3ec0bd6
source=(git+https://github.com/babelouest/yder.git#tag=${_tag})
validpgpkeys=(8405B02FCC28EF9744C8F253FE82139440BD22B9) # Nicolas Mora <nicolas@babelouest.org>
b2sums=(SKIP)

pkgver() {
  cd yder
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S yder -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_YDER_DOCUMENTATION=ON
  ninja -C build doc
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -dm 755 "${pkgdir}"/usr/share/doc
  cp -dr --no-preserve=ownership yder/doc/html "${pkgdir}"/usr/share/doc/yder/
}

# vim: ts=2 sw=2 et:
