# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pytools
pkgver=2022.1.11
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
depends=('python-numpy' 'python-platformdirs' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('68c08320c15baa0d7e09baf13e311b4f4a354f59947a01753dad71fd239159d336186cf8bce66aa4b25c6404248d729b5de61ce29eca13e61fa6b48ad4fcc574')

build() {
  cd pytools-$pkgver
  python setup.py build
}

check() {
  cd pytools-$pkgver
  python -m pytest
}

package(){
  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
