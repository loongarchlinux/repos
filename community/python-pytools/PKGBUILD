# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pytools
pkgver=2022.1.12
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
depends=('python-numpy' 'python-platformdirs' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('66a4a60b26ee543a774322583a22105f5e886cb86956b4edb95ae361a5aa6cff28b1bcf6ad84b9e2f68c81bb11f5a18bd2e8d0335343550a6e9bca4cb0e9c0e8')

build() {
  cd pytools-$pkgver
  python setup.py build
}

check() {
  cd pytools-$pkgver
  python -m pytest
}

package(){
  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
