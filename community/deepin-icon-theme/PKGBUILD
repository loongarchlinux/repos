# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2021.03.12
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation-2021.03.12.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('cc5c22225ccef5d67e786ca2154f7473d8ff1823c42adfe722a45d4e46e3d3493f65213210692e95e5b229b295c49ee54041e7e292ee0443ac5e406ba61490b2'
            '5c2ed40893093ec161776a86093e2589bff95b2a8150d18e266a86a439c854dec9ec95304a0d8b684be0cc22aece35a0d1ee7a05484f03b63681a23000e26d67')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation-2021.03.12.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links

  # Broken filenames are not dealt since reported in June 2020. Let's clean them up ourselves.
  find $pkgname-$pkgver -name "* 2.svg" -delete
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
