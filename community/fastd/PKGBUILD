# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: NeoRaider

pkgname=fastd
arch=("x86_64")
pkgver=20
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"
makedepends=('meson')
checkdepends=('cmocka')
depends=('libcap' 'gcc-libs' 'libuecc>=6' 'libsodium' 'json-c' 'openssl')
url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
source=("https://projects.universe-factory.net/attachments/download/86/${pkgname}-${pkgver}.tar.xz")
sha256sums=('56cab8639218d63237d9a5508fb2bf6fa637374d53fb7fa55b7e92e4d4dfeb00')
sha512sums=('8cb722a12fff9f6ee068f67c7a03c287c82b97d851e580070a9bc94fac3b33395203617309e8aaea1c91781a7245ec872442f8567d10dc6d1efac8de89c050b3')

build() {
  cd "$srcdir/fastd-${pkgver}"
  meson setup               \
    --prefix=/usr           \
    -Dbuildtype=release     \
    -Db_lto=true            \
    -Dsystemd=enabled       \
    -Dbuild_tests=true      \
    . "$srcdir/build-${pkgver}"

  cd "$srcdir/build-${pkgver}"
  ninja
}

check() {
  cd "$srcdir/build-${pkgver}"
  ninja test
}

package() {
  cd "$srcdir/build-${pkgver}"
  DESTDIR="$pkgdir" ninja install
  
  install -dm755 "${pkgdir}/etc/fastd/"
  
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/examples/fastd@.service" "${pkgdir}/usr/lib/systemd/system/fastd@.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/fastd.1" "${pkgdir}/usr/share/man/man1/fastd.1"
}
