# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='sinatra'
pkgname="ruby-${_gemname}"
pkgver=2.2.0
pkgrel=1
pkgdesc='Classy web-development dressed in a DSL'
arch=('any')
url='https://github.com/sinatra/sinatra'
license=('MIT')
depends=('ruby' 'ruby-mustermann' 'ruby-rack' 'ruby-rack-protection' 'ruby-tilt')
makedepends=('ruby-rake')
checkdepends=('ruby-rspec' 'ruby-minitest' 'ruby-rack-test' 'ruby-activesupport' 'ruby-builder')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('be9432a8efa93578accc836cd0fecca128e56989d8596f1b3d33d2d956e90acd80244b98473fff2adae13ed4a1e933c41e73c80755a6f3418a65cdf33c011145')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # tests currently don't pass because a few tools are missing in our repo
  # cannot load such file -- coffee-script: skipping coffee tests
  # cannot load such file -- creole: skipping creole tests
  # cannot load such file -- erubi: skipping erubi tests
  # cannot load such file -- erubis: skipping erubis tests
  # cannot load such file -- haml: skipping haml tests
  #rake spec
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md RELEASING.md README.zh.md README.ru.md README.pt-pt.md README.pt-br.md README.malayalam.md README.ko.md README.ja.md README.hu.md README.fr.md README.es.md README.de.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG.md AUTHORS.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
