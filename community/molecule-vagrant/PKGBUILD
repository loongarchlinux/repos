# Maintainer: Ike Devolder <ike DOT devolder AT gmail DOT com>
# Contributor: Marc Straube <email@marcstraube.de>

pkgname=molecule-vagrant
_module_name=molecule_vagrant
pkgver=0.6.3
pkgrel=3
pkgdesc="Molecule Vangrant Driver"
arch=('any')
url="https://github.com/ansible-community/molecule-vagrant"
license=('MIT')
depends=('python' 'ansible' 'molecule' 'python-pyaml' 'python-vagrant')
makedepends=('python-setuptools' 'python-setuptools-scm' 'molecule'
'python-vagrant' 'python-pyaml' 'python-pip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/ansible-community/molecule-vagrant/archive/${pkgver}.tar.gz"
  '105.patch'
)
sha512sums=('09165da30b6d678084760f8bf8ae552a5ece255b98e1cc86e76a9294bcbf720c951641208fad34d926ef3b1a97a0f65d181982ffed6f058413d2bf0846ea846e'
            '566eca8d182b50658804b145d793eb4c9182c9499439f63a0a2bd94d13b1908ce084f25e5506f8690bdc7993b06384bf97df91ac5b3f0ea7a3042b328edb107d')

prepare() {
  cd ${pkgname}-${pkgver}
  echo "Version: ${pkgver}" > PKG-INFO
  patch -p1 -i "$srcdir/105.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

#check() {
#  cd ${pkgname}-${pkgver}
#  python setup.py test
#}

package() {
  pylibpath="$(dirname "$(python-config --configdir)")"
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  cp -R ${_module_name}/cookiecutter "${pkgdir}${pylibpath}/site-packages/${_module_name}"
  cp -R ${_module_name}/playbooks "${pkgdir}${pylibpath}/site-packages/${_module_name}"
}
