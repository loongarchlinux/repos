# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Ike Devolder <ike DOT devolder AT gmail DOT com>
# Contributor: Marc Straube <email@marcstraube.de>

pkgname=molecule-vagrant
pkgver=1.0.0
pkgrel=3
pkgdesc="Molecule Vagrant Driver"
arch=(any)
url="https://github.com/ansible-community/molecule-vagrant"
license=(MIT)
depends=(ansible-core molecule python-jinja python-selinux python-yaml python-vagrant)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-toml python-wheel)
checkdepends=(python-filelock python-pytest)
source=(
  https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  $pkgname-1.0.0-dependencies.patch
)
sha512sums=('de55cea15c5df876b21f91f7f27b6de88896ea301a9e7c240a82abfaaea44d09a6ea85941a5ff3a84cb28991c77e344f9c2aa27f852f652c98e70f51787c8ca5'
            '9ffa61e4338240bf0d7c0e6149584d8ea2d3c2ad8b7c9f40173d16da250127181dcf1a15e4412e0691c0d4a4999a3444b28e83485d9f64dbf6405ffa8651bbaf')
b2sums=('6267554e55717ecde97b062867f4a72691ffd912155fe05d7f50f61682b46ed955c26789819af1e3c386dea4f89e606810c19a65212859ac6d75a491c3ef927d'
        '66a19c9d2de3ead411aab50e94d83919db6a203be600ab4d9273c5ffdf214e870254c573ec98b3f0403947ca44a4538032e18baf2c30037a713e4060c7103965')

prepare() {
  # remove setuptools-scm-git-archive from makedepends: https://github.com/ansible-community/molecule-vagrant/pull/198
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.0.0-dependencies.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  # install to test location
  mkdir -vp "$_test_dir"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  # ignore tests that require running molecule
  pytest -v "$_test_dir/$_site_packages/molecule_vagrant/test/" --ignore "$_test_dir/$_site_packages/molecule_vagrant/test/functional/" -c /dev/null
}

package() {
  local _site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  # remove tests: https://github.com/ansible-community/molecule-vagrant/issues/199
  rm -frv "$pkgdir/$_site_packages/molecule_vagrant/test/"
}
