# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgbase='keras'
pkgbase="python-${_pkgbase}"
pkgname=("${pkgbase}")
pkgdesc='A deep learning API written in Python, running on top of the machine learning platform TensorFlow'
url='https://keras.io/'
license=('Apache')
pkgver=2.9.0
pkgrel=1
arch=('any')
source=("https://files.pythonhosted.org/packages/py2.py3/${_pkgbase::1}/${_pkgbase}/${_pkgbase//-/_}-${pkgver}-py2.py3-none-any.whl"
        "${_pkgbase}-${pkgver}-LICENSE::https://raw.githubusercontent.com/keras-team/${_pkgbase}/v${pkgver}/LICENSE")
makedepends=('python-pip')
depends=('absl-py' 'python' 'python-h5py' 'python-keras-preprocessing' 'python-numpy' 'python-pandas' 'python-pillow'
         'python-pydot' 'python-scipy' 'python-six' 'python-tensorflow' 'python-yaml' 'tensorboard')
sha256sums=('55911256f89cfc9343c9fbe4b61ec45a2d33d89729cbe1ab9dcacf8b07b8b6ab'
            '58d1e17ffe5109a7ae296caafcadfdbe6a7d176f0bc4ab01e12a689b0499d8bd')

package () {
    PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps *.whl
    install -Dm 644 "${_pkgbase}-${pkgver}-LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
