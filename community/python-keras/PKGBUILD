# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgbase='keras'
pkgbase="python-${_pkgbase}"
pkgname=("${pkgbase}")
pkgdesc='A deep learning API written in Python, running on top of the machine learning platform TensorFlow'
url='https://keras.io/'
license=('Apache')
pkgver=2.11.0
pkgrel=2
arch=('any')
source=("https://files.pythonhosted.org/packages/py2.py3/${_pkgbase::1}/${_pkgbase}/${_pkgbase//-/_}-${pkgver}-py2.py3-none-any.whl"
        "${_pkgbase}-${pkgver}-LICENSE::https://raw.githubusercontent.com/keras-team/${_pkgbase}/v${pkgver}/LICENSE")
makedepends=('python-build' 'python-installer' 'python-wheel')
depends=('absl-py' 'python' 'python-h5py' 'python-keras-preprocessing' 'python-numpy' 'python-pandas' 'python-pillow'
         'python-pydot' 'python-scipy' 'python-six' 'python-tensorflow' 'python-yaml' 'tensorboard')
b2sums=('d41941a7add4943c7752e0c65027a17a799b142c9a51c4991f2e67250e43ec3736cc5d7e2fff9f8e8903edebfe9b3a39cfe270e54cf172c8478b9dee6b5c0736'
        'dc6395f606b09f8a2fa6e8d28f8436a9b0d2ee7e43b4033542c55eb1bf26e9e6c01fd53770e825b9e996ef15fd2eb77f1e0524d4fc1a3e8bf52d72de3adbd653')

package () {
  python -m installer --destdir="$pkgdir" *.whl
  install -Dm 644 "${_pkgbase}-${pkgver}-LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
