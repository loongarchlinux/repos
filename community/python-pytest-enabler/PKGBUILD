# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
pkgver=1.2.0
pkgrel=3
pkgdesc='Enable installed pytest plugins'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-enabler'
depends=('python-toml' 'python-jaraco' 'python-jaraco.context' 'python-more-itertools')
makedepends=('python-setuptools-scm')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-checkdocs'
              'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/pytest-enabler/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/source/t/types-toml/types-toml-0.10.1.tar.gz")
sha512sums=('b21ea6cbb12704d2602e408ad8145c6e67949be0b5bcd7506252a3b6b7142484c3ff508e595d5097929f0c7b3231431d53cad3bffb350f03a24eb65e35424349'
            '001757f956dd3af0688a6544b90ba415b26ab3128e0fbb238f4dfaf0bab6b5795d5598bcc9422d854ea235acbc9e78b245c17f9ebc9ad9251ae3c0722b382812')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd pytest-enabler-$pkgver
  # no idea what this is or why mypy wants it
  ln -s ../types-toml-0.10.1/toml-stubs .
}

build() {
  cd pytest-enabler-$pkgver
  python setup.py build
}

check() {
  cd pytest-enabler-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-enabler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
