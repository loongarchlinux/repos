# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
pkgver=2.0.0
pkgrel=1
pkgdesc='Enable installed pytest plugins'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-enabler'
depends=('python-jaraco.context' 'python-jaraco.functools' 'python-pytest' 'python-toml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-mypy')
source=("https://github.com/jaraco/pytest-enabler/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/source/t/types-toml/types-toml-0.10.1.tar.gz")
sha512sums=('58eae8d2fce494f50f5848fd9a0d9afa6aec49016b018bbb355c578c21ad77893149fa0895f4e80b050542b044589fa25a62b5323ca3250caa2bcf9290f7c548'
            '001757f956dd3af0688a6544b90ba415b26ab3128e0fbb238f4dfaf0bab6b5795d5598bcc9422d854ea235acbc9e78b245c17f9ebc9ad9251ae3c0722b382812')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd pytest-enabler-$pkgver
  # mypy needs all type stubs :/
  ln -s ../types-toml-0.10.1/toml-stubs .
}

build() {
  cd pytest-enabler-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pytest-enabler-$pkgver
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-enabler-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
