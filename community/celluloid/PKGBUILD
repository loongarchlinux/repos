# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ainola

pkgname=celluloid
pkgver=0.23
pkgrel=1
pkgdesc='Simple GTK+ frontend for mpv'
arch=('loong64' 'x86_64')
url='https://celluloid-player.github.io/'
license=('GPL3')
depends=('gtk4' 'mpv')
makedepends=('appstream-glib' 'meson')
replaces=('gnome-mpv')
source=("https://github.com/celluloid-player/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
#validpgpkeys=('4BA23A4D2F798D926C7A0E0D0F9039A19BE2326C') # gnome-mpv
sha256sums=('b22d1963d03f0215bce07c2de8e77892c322f500ae5c0fd6e2e84d2e13f04374')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
