# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=21.06
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('loongarch64' 'x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'lirc' 'hicolor-icon-theme' 'qt5-base' 'zlib')
makedepends=('alsa-lib' 'cmake' 'dssi' 'fftw' 'jack' 'ladspa' 'liblrdf' 'liblo'
'libsamplerate' 'libsndfile' 'qt5-tools')
checkdepends=('xorg-server-xvfb')
optdepends=('cups: Printing support'
            'evince: Print preview support'
            'lilypond: Notation display support'
            'mupdf: Print preview support'
            'okular: Print preview support'
            'timidity++: Softsynth support'
            'flac: FLAC support'
            'wavpack: Lossless audio codec support')
provides=('dssi-host' 'ladspa-host')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('9cadfed3caba203ea6db78c3600600b9c5ed0e2963228dd6e8129291618e3b7c617b0d6dfaccce0dd753ce229dfaa8c65d1354f591e2923185fb76dcbe48d840')
b2sums=('f5bb9a471122857666edf033d4e760cbf81b0bbcf255e1248a1f865825677c41fb1a55a64835a168174b420793367214e8dfcedea25dc857e1ec847fae87770d')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="${PWD}/test-tmp"
  xvfb-run make VERBOSE=1 test -C build || echo "TestNotationViewSelection still fails: https://sourceforge.net/p/rosegarden/bugs/1585/"
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libjack.so' 'liblrdf.so' 'liblo.so'
  'libsamplerate.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,CHANGELOG,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
