# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pony
pkgname=python-$_pkgname
pkgver=0.7.13
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
depends=('python')
makedepends=('python-setuptools')
license=('Apache')
source=("$pkgname-$pkgver::$url/archive/v$pkgver.tar.gz")
sha512sums=('431d19e3297ba1cdae5313948123d8b8cd71ee8ad5868b8cc2f5447eb435875fdbcdc3768834b9afe22ce8e4c1abaabeb597f3e6974b00aab33aa61e497d6390')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py test
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  rm -rf "$pkgdir"/usr/lib/python*/site-packages/pony/orm/tests
}

