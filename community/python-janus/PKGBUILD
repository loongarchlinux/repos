# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=python-janus
pkgver=0.6.0
pkgrel=1
pkgdesc="Thread-safe asyncio-aware queue for Python"
arch=('any')
url="https://github.com/aio-libs/janus"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-asyncio')
source=("https://github.com/aio-libs/janus/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('5f4069eadc337381a1a60211af890f466e3d28c79117dbfdce8dd8d032d94432')

build() {
  cd janus-$pkgver
  python setup.py build
}

check() {
  cd janus-$pkgver
  python -m pytest
}

package() {
  cd janus-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
