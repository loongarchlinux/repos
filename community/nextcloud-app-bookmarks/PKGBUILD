# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=nextcloud-app-bookmarks
_name=bookmarks
epoch=1
pkgver=10.0.3
pkgrel=1
pkgdesc="Bookmarks app for nextcloud"
arch=(any)
url="https://github.com/nextcloud/bookmarks"
license=(AGPL3)
depends=(nextcloud)
makedepends=(ripgrep yq)
groups=(nextcloud-apps)
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/bookmarks/releases/download/$pkgver/bookmarks-$pkgver.tar.gz")
sha256sums=('09ef3fe2d133ddceb6db7af92d628f8dacad01008891457c43cd2b68fe334ee6')
b2sums=('092d6580f0fb48543c79dd180d34e76ba0fe479f1a16cfb3e31f47e68940a104b4fddfa8d513d8dd2e933120cfc1cb0da590a22a5b8988aeb1641fa286d606d5')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -d "$pkgdir"/usr/share/webapps/nextcloud/apps
  cp -a "$srcdir"/bookmarks "$pkgdir"/usr/share/webapps/nextcloud/apps/bookmarks
}
