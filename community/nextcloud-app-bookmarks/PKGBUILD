# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=nextcloud-app-bookmarks
_name=bookmarks
epoch=1
pkgver=4.2.2
pkgrel=1
pkgdesc="Bookmarks app for nextcloud"
arch=('any')
url="https://github.com/nextcloud/bookmarks"
license=('GPL')
depends=('nextcloud')
makedepends=('ripgrep' 'yq')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/bookmarks/releases/download/v$pkgver/bookmarks-$pkgver.tar.gz")
sha256sums=('9c98eb894ecc4fd2ccc7c67aabab0f1d3f3672426f393d7bc202d298edc89167')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}
sha256sums=('97259a848e778d19d9b28c9cb901f1e2953557b719302244bd0d391d344a3825')

package() {
  _get_nextcloud_versions

#  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")
depends=("nextcloud>=${_app_min_major_version}") # ignore for now, it works with 21

  install -d "$pkgdir"/usr/share/webapps/nextcloud/apps
  cp -a "$srcdir"/bookmarks "$pkgdir"/usr/share/webapps/nextcloud/apps/bookmarks
}
