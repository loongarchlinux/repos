# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

_name=setuptools_scm
pkgname=python2-setuptools-scm
pkgver=5.0.2
pkgrel=3
pkgdesc="Handles managing your python package versions in scm metadata"
arch=('any')
url="https://github.com/pypa/setuptools_scm"
license=('MIT')
depends=('python2-setuptools')
checkdepends=('python2-pytest' 'mercurial' 'git' 'python2-pip' 'python2-toml')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('83a0cedd3449e3946307811a4c7b9d89c4b5fd464a2fb5eeccd0a5bb158ae5c8')
b2sums=('a4166b293f26161be8748f0c4b819dba0c03186a95d0c06b6fd6fdee7910de60e034ab81f6f1d89f7a1b47381441deb8a38d05421aef3287b35705bbddee33ee')

build() {
  cd $_name-$pkgver
  python2 setup.py build
  python2 setup.py egg_info
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local site_packages=$(python2 -c "import site; print(site.getsitepackages()[0])")
  python2 setup.py install --root=temp --optimize=1
  PYTHONPATH="$PWD/temp/$site_packages" pytest2
}

package() {
  cd $_name-$pkgver
  python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
