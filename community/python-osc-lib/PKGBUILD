# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-osc-lib
pkgver=2.4.2
pkgrel=3
pkgdesc="OpenStackClient Library"
arch=('any')
url="https://docs.openstack.org/osc-lib/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-keystoneauth1' 'python-openstacksdk' 'python-oslo-i18n'
         'python-oslo-utils' 'python-simplejson' 'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/osc-lib/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3c47e3ff693d462fb24a26691d581aa51d025007b982c988bff7d2db9c2a12a00467510c4347bbf05493a4297de4a3741e9e896bb898a386aa2936fada18da79')

export PBR_VERSION=$pkgver

build() {
  cd osc-lib-$pkgver
  python setup.py build
}

check() {
  cd osc-lib-$pkgver
  # https://storyboard.openstack.org/#!/story/2008461
  stestr run || echo "Tests failed"
}

package() {
  cd osc-lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
