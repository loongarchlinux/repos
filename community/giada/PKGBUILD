# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.18.0.1
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('loongarch64' 'x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11' 'libxcursor' 'libxft'
'libxinerama' 'libxpm')
# upstream vendors a hacked rtaudio :(
makedepends=('alsa-lib' 'catch2' 'cmake' 'fltk' 'gendesk' 'imagemagick' 'jack'
'libpulse' 'libsamplerate' 'libsndfile' 'libxrandr' 'nlohmann-json' 'rtmidi')
checkdepends=('xorg-server-xvfb')
provides=('vst3-host')
source=("https://www.giadamusic.com/data/${pkgname}-${pkgver}-src.tar.gz"
        "$pkgname-0.17.2-devendor_nlohmann_json.patch")
sha512sums=('2bd4bc9fdc605ffbe182f35a9d165d891d64aba9ac22689b6b4ac275db619b96f02ed6523e26b7f5aa441e9cb4446804c50327fd8c8e813d4cfe7746c61f1666'
            'b9d952c0816dd2b81dadff24521b19f305ca5ddeb1964dd81ce93e6f63b25dbf291c07e528cc2836f268a095bf9920786331a615230416f44a9014ff581a0bce')
b2sums=('260ffbe64944f4b2eaafc9df9106248e68138f4535f29d16d81b3b9370532beaeb894d36b4e2b2cf8059611a6c1b5cf193bbdd0c18c55c0d518b548b991c009f'
        'a482f356969c5d184a1eb0945477040cc7c5d1447fee0159cc5d2da05faea0bd696123b0559bdced148b301181f93726a515df24a814dbd08d207e2959f31305')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"

  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.17.2-devendor_nlohmann_json.patch"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -W no-dev \
        -B build \
        -S .
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -DWITH_TESTS=ON \
        -W no-dev \
        -B build-test \
        -S .
  make VERBOSE=1 -C build
  make VERBOSE=1 -C build-test
}

# unclear how to run tests: https://github.com/monocasual/giada/issues/463
check(){
  cd "$pkgname-$pkgver"
  xvfb-run ./build-test/giada --run-tests
}

package() {
  depends+=('libasound.so' 'libfltk.so' 'libfreetype.so' 'libjack.so'
  'libpulse.so' 'libpulse-simple.so' 'librtmidi.so' 'libsamplerate.so'
  'libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
