# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.6.6
pkgrel=2
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('libmediainfo' 'qt5-declarative' 'qt5-multimedia' 'qt5-script' 'quazip')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('05c165256c3f0d27d424be8b2e635496a4f54f776563b3191707e65a94be52fc')
b2sums=('4f744d119f5baa50cb9639d799d018a972989cbaa9003c5b020da0b2b6172f6bba09778e9119857cae69f92cf52d4f57c850570ca17e6c55b502063e797f75a3')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip/quazip/|QuaZip-Qt5-1.0/quazip/|g' -i src/*/*.cpp src/*/*.h
  sed 's|lquazip5|lquazip1-qt5|g' -i MediaElch.pro
}

build() {
  cd MediaElch-${pkgver}
  qmake
  make SUBLIBS="-lquazip1-qt5"
}

package() {
  cd MediaElch-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
