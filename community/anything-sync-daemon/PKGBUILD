# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=anything-sync-daemon
pkgver=5.86
pkgrel=1
pkgdesc="Symlinks and syncs user specified dirs to RAM"
arch=(any)
url="https://github.com/graysky2/anything-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('bash-completion: for completion when using bash'
            'zsh-completions: for completion when using zhs')
install="${pkgname}.install"
backup=(etc/asd.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('1554dd382ebc2aa6887908d8165378aeaaa768dbd6412e97fc0a095020fcefd17fad973b771880d959e8f6a7856adfb905d3401d0b9eba0b3beebb1482521c8d')
b2sums=('f773c4d779c15c49f78fb659084dfdc0988be05f9dbf898b1a11b18a8b414df84b5645c0f299320194e6aa07100deaef7fbaa0c0453eb291108db50114ed1b61')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install-systemd-all
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
