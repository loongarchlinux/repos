# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=blosc
pkgver=1.20.1
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library.'
arch=('loongarch64' 'x86_64')
url='http://blosc.org/'
license=('BSD')
depends=('snappy' 'zstd')
makedepends=('cmake')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
sha256sums=('42c4d3fcce52af9f8e2078f8f57681bfc711706a3330cb72b9b39e05ae18a413')

build() {
  cd "${srcdir}"
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DDEACTIVATE_SNAPPY="OFF" \
        -DPREFER_EXTERNAL_LZ4="ON" \
	-DPREFER_EXTERNAL_ZLIB="ON" \
	-DPREFER_EXTERNAL_ZSTD="ON" \
        -DBUILD_TESTS="OFF" \
        ../c-blosc-${pkgver}

  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/c-blosc-${pkgver}/LICENSES/BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
