# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailmanclient
pkgname=python-mailmanclient
pkgver=3.3.4
pkgrel=1
pkgdesc="Official Python bindings for the GNU Mailman 3 REST API"
arch=(any)
url="https://gitlab.com/mailman/mailmanclient"
license=(LGPL3)
depends=(python-requests python-typing-extensions)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(mailman3 python-falcon python-httpx python-pytest python-pytest-asyncio
python-pytest-services)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('b34444263a39bc452aaf6e60850e223cead5fe450dc3d01d215b622d3cfbe6c9f7cdbebda204a628a3933a1eaffdef789a62afeca2e6e7fd6226d297ffe025da'
            'SKIP')
b2sums=('346eb7372dcc57f8fcd101f3f8d467ea97f7bf09f0a4855c1220a1e255a2b17ff47b0a7cd85c684a10639efd858f9e5f39978fa1b0d659ee1d38b29d1dce456a'
        'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v -k 'not using.rst and not test_find_user_page and not test_find_users'
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  # remove tests
  rm -frv "$pkgdir/$site_packages/$_name/tests/"
  find "$pkgdir/$site_packages/" -type f -iname "*conftest.py" -delete
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
