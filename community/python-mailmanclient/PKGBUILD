# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailmanclient
pkgname=python-mailmanclient
pkgver=3.3.3
pkgrel=3
pkgdesc="Official Python bindings for the GNU Mailman 3 REST API"
arch=('any')
url="https://gitlab.com/mailman/mailmanclient"
license=('LGPL3')
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('mailman3' 'python-falcon' 'python-httpx' 'python-pytest' 'python-pytest-asyncio'
'python-pytest-services')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('a4d18a7fc6084dfb20a7028cc085c31e696b8c2db4eec674852929b1d3a221a065b8e33d8047d018d4a0edf1afb3bcb55e76906808c0f6b3715abb8a81437b9e'
            'SKIP')
b2sums=('d354123fb7735d8b670422461f3d5697a30ac83d1b2f9c7f642583423ab383f58b0d0604f18be23175fcdabe42721ee02102e764211fd0723c9146f8c39581fd'
        'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k 'not using.rst and not test_find_user_page and not test_find_users'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
