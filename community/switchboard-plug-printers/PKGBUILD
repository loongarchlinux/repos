# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-printers
pkgver=2.1.9
pkgrel=2
pkgdesc='Switchboard Printers Plug'
arch=(loongarch64 x86_64)
url=https://github.com/elementary/switchboard-plug-printers
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcups
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=c96a383ab0023456326c63ac428cac1bd5026a35
source=(git+https://github.com/elementary/switchboard-plug-printers.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd switchboard-plug-printers

  git cherry-pick -n 5eced5ddda6f229d7265ea0a713f6c1cd181a526
}

pkgver() {
  cd switchboard-plug-printers

  git describe --tags
}

build() {
  arch-meson switchboard-plug-printers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
