# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-phitigra
_pyname=${pkgname#python-}
pkgver=0.2.5
pkgrel=1
pkgdesc='A graph editor for SageMath/Jupyter'
url='https://github.com/jfraymond/phitigra'
arch=(any)
license=(MIT)
source=(https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz)
depends=(sagemath python-ipycanvas)
makedepends=(python-build python-installer python-wheel)
sha256sums=('f6f4714926fa57f6076cb5b81d1166bf7f6f684a9678aa68c9cb6db77da99665')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
