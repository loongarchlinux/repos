# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tabulate
pkgname=python-tabulate
pkgver=0.8.10
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=(any)
url="https://github.com/astanin/python-tabulate"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-nose python-numpy python-pandas python-pytest python-wcwidth)
optdepends=(
  'python-numpy: NumPy array support'
  'python-pandas: pandas.DataFrame support'
  'python-wcwidth: wide-character support'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('da390eb620c257eb4a24393d011c8835c59b0da573ce2ed19950a8f16033ef432e7847d8a39f3e7907ba5502635b744ed69c4ebed9fa0efed9d2385a03798a3f')
b2sums=('4cc092e75f057e342ced8c317b9ec927c20dff4e55da40405712df231df6b7543a591d75de10e708b06ac6946714cb4cd1c1af568300e0d7f449640382516600')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -v
}

package_python-tabulate() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
