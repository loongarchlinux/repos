# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=4.0
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=(any)
url="https://gitlab.com/warsaw/flufl.i18n"
license=(Apache)
depends=(python-atpublic)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-sybil python-pytest)
provides=(python-flufl-i18n)
replaces=(python-flufl-i18n)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('03e953af7e406d24a64d334647008e222cf74e5e055865638a52d8ec7e25edae44046aa4dfa8766b5a1458b335e9e29bd8b3e89703d8c8b28ab777375da19712')
b2sums=('6bd4a311c3df56b03a0d46164f7f1ee1eeeb257478a82515261894f4cdf3017b150f1e13ce95a08f50ed5e01cf736884c7ba05a96e3f6b90917418ed18462753')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
