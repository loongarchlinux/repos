# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.0.1
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://flufli18n.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('72e926446826a0fa0e447ece6cbe79a9141cfc48550bda7eebc9ad851b654e3a1c39c80436a3a84d53f70869c8451426708e4eaecc9086e50819ef83fe266cb8')
b2sums=('db7007ffed95393ab02e14152eee1730b180b567351b2c6c83004a3a33916b4b9b27e6050e4c24034974bcdd5087f1250c0e77eff21f3b150292a3308b6b83e7')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
