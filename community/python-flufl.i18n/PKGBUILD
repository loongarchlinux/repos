# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=4.1
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=(any)
url="https://gitlab.com/warsaw/flufl.i18n"
license=(Apache)
depends=(python-atpublic)
makedepends=(python-build python-installer python-pdm-pep517)
checkdepends=(python-sybil python-pytest)
provides=(python-flufl-i18n)
replaces=(python-flufl-i18n)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('22952551538dcf625a6d86cf986af6f282bdf0f7fa1d19159dddd0b842cada99998c0a8ce86b5053a8918434f585d674823493cbb86be8890664acd5c0afe227')
b2sums=('f4df0b9d3bf088c2c45f3bdcd798206f8e9db1e7006cccdca71b3a1afb7ab03aa567c4247106c5f17ea8921349a4e205c403b16fde4c52f5b7e0fb4132b6ee2b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
