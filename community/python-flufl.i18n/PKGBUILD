# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.2
pkgrel=3
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://gitlab.com/warsaw/flufl.i18n"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('07f329d026f24db623045f3c9857bcaacc1f0982066d4409fc060c4f712716044f96321a4e4909cbc85932f36fc76f71b9d118662ba837fd85b22754ce2d25c7')
b2sums=('dc827eb4099670c6e5c39ebc8702fcda146b20a3978fa56108ab98a05d2c8911e705b71dacd6b9fc7fae9923d009487f6f047baa0dcd06a5de2e4f4d5fafc465')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
