# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rst2pdf
pkgver=0.98
pkgrel=4
pkgdesc="Use a text editor. Make a PDF"
arch=('any')
url="https://github.com/rst2pdf/rst2pdf"
license=('MIT')
depends=('python-docutils' 'python-importlib-metadata' 'python-jinja'
'python-packaging' 'python-pygments' 'python-reportlab' 'python-smartypants')
makedepends=('python-setuptools')
# TODO: package dvipng, python-svg2rlg, python-pypdf2, python-xhtml2pdf
checkdepends=(
  'plantuml'
  'python-aafigure'
  'python-matplotlib'
  'python-pytest'
  'python-pytest-xdist'
  'python-pymupdf'
  'python-sphinx'
  'python-svglib'
)
# TODO: package python-xhtml2pdf
optdepends=(
  'python-aafigure: for ASCII image support'
  'python-matplotlib: for math support'
  'python-sphinx: for sphinx support'
  'python-svglib: for SVG support'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('6e251528f4b7fc87c760542c7d53b69f5dff3cbefce8740e2591cc13a6bfeea68dda3e17033900dfcc33200fa8d150869369854174587dcba7cc51f978728365')
b2sums=('028b75573f4a939e58157f0b69eefe50cd8fb78874917495b5736594834bb1addb67dfaed674d13892d250c837358b230f2eaa7e8d9e36f26ee95c6eeceadde4')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"

  cd "$pkgname-$pkgver"
  mkdir -vp "${_test_dir}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${_test_dir}"

  export PYTHONPATH="${PWD}/${_test_dir}/${_site_packages}:${PYTHONPATH}"
  export PATH="${PWD}/${_test_dir}/usr/bin:${PATH}"
  pytest -vv || echo "Tests broken probably due to missing deps or other issues: https://github.com/rst2pdf/rst2pdf/issues/1015"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,MAINTAINERS,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
