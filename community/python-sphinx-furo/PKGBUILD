# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_pkgname=furo
pkgname=python-sphinx-$_pkgname
pkgver=2022.09.15
pkgrel=1
pkgdesc='A clean customizable documentation theme for Sphinx'
arch=('any')
url='https://github.com/pradyunsg/furo'
license=('MIT')
depends=('python-sphinx-basic-ng' 'python-pygments' 'python-beautifulsoup4')
makedepends=('python-build' 'python-installer' 'python-sphinx-theme-builder' 'python-flit-core'
             'nodejs-lts-gallium' 'npm' 'expac')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('4fa0641223eec65b37fff73c6cb852843efed2eacaa6a3841ef2dc86215d1c3d50c7e87055f970a27de5df159101e773eff9292a6bffad12dae898802ea3deb9')

prepare() {
  cd $_pkgname-$pkgver
  # force use of system nodejs
  local node_ver=$(expac %v nodejs-lts-gallium | cut -d - -f 1)
  sed -i "s/16.13.1/$node_ver/" pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python -m build -nw

  # docs disabled for now to unblock Python 3.10 update

  # sphinx needs this theme installed because it is uses it.
  # simply injecting the package to sys.path (via PYTHONPATH or similar)
  # is not enough because sphinx looks for themes in the registered
  # entrypoints, this means we actually have to install the package.
  # for this we will create a venv with access to system packages and
  # install the theme there, then we will build the documentation.

  #python -m venv --system-site-packages doc-env
  #doc-env/bin/python setup.py install --skip-build

  #doc-env/bin/python /usr/bin/sphinx-build -b dirhtml -v docs build/docs/html
}

package() {
  cd $_pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  #install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  #cp -r -a --no-preserve=ownership build/docs/html "$pkgdir"/usr/share/doc/$pkgname
  #rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
