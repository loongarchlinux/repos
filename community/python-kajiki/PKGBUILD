# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
pkgbase='python-kajiki'
_pkgbase="${pkgbase#python-}"
pkgname=("python-${_pkgbase}")
pkgver=0.9.0
pkgrel=1
pkgdesc='Python XML-based template engine with Genshi-like syntax and Jinja-style blocks'
arch=('any')
url='https://github.com/nandoflorestan/kajiki'
license=('LGPL')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-babel' 'python-nine' 'python-pytest')
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/nandoflorestan/kajiki/archive/v${pkgver}.tar.gz")
sha512sums=('44591a11969f8a242cd30321c57888d8bec48f375013f39e2c08eb173eb4aa6cdf8d35a22973bd50dc3cd167ba9c9a825f040ab5e1369dd3fa2044031dcba911')

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  py.test -vvv -s -ra --showlocals --noconftest "${_pkgbase}"/tests/
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
}
