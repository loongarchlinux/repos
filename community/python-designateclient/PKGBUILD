# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-designateclient
pkgver=4.5.0
pkgrel=1
pkgdesc="OpenStack DNS-as-a-Service - Client"
arch=('any')
url="https://docs.openstack.org/python-designateclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-jsonschema' 'python-osc-lib' 'python-oslo-serialization'
         'python-oslo-utils' 'python-pbr' 'python-keystoneauth1' 'python-requests' 'python-six'
         'python-stevedore' 'python-debtcollector')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-designateclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fc3835739ad81772c23db93faca29c2418d5b5dc8bd4c12643a541f8a931651a28c3628e2608d4be0fbbe0ec1073564d9a1033eb06605ac9a4a4aa656a81b06f')

export PBR_VERSION=$pkgver

build() {
  cd python-designateclient-$pkgver
  python setup.py build
}

check() {
  cd python-designateclient-$pkgver
  stestr run
}

package() {
  cd python-designateclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
