# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-designateclient
pkgver=4.4.0
pkgrel=1
pkgdesc="OpenStack DNS-as-a-Service - Client"
arch=('any')
url="https://docs.openstack.org/python-designateclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-jsonschema' 'python-osc-lib' 'python-oslo-serialization'
         'python-oslo-utils' 'python-pbr' 'python-keystoneauth1' 'python-requests' 'python-six'
         'python-stevedore' 'python-debtcollector')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-designateclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('452d8559ddbf77017e8a5c13f7c93f80a864bdfdf96c3de9b2d8f30761a1f7dfd0ae5a0778dc0eff1d86f45247c8cd0ac6b8224e6fe98efd9e3c3fdc619b1a1d')

export PBR_VERSION=$pkgver

build() {
  cd python-designateclient-$pkgver
  python setup.py build
}

check() {
  cd python-designateclient-$pkgver
  stestr run
}

package() {
  cd python-designateclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
