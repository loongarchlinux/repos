# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Adrien Wu <adrien.sf.wu@gmail.com>
pkgname=python-opt_einsum
pkgver=3.2.1
pkgrel=1
pkgdesc="Optimizing einsum functions in NumPy, Tensorflow, Dask, and more with contraction order optimization"
url="https://github.com/dgasmith/opt_einsum"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
_name=opt_einsum
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('83b76a98d18ae6a5cc7a0d88955a7f74881f0e567a0f4c949d24c942753eb998')

build() {
  cd "$srcdir/$_name-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
