# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Lucas Werkmeister <mail@lucaswerkmeister.de>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=mkosi
pkgver=13
pkgrel=1
pkgdesc="Build Legacy-Free OS Images"
arch=(any)
url="https://github.com/systemd/mkosi"
license=(LGPL2.1)
depends=(arch-install-scripts python qemu-img)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pexpect python-pytest)
optdepends=(
  'btrfs-progs: raw_btrfs and subvolume output formats'
  'debian-archive-keyring: build Debian images'
  'debootstrap: build Debian or Ubuntu images'
  'cryptsetup: add dm-verity partitions'
  'dosfstools: build bootable images'
  'gnupg: sign images'
  'qemu: run bootable images using QEMU'
  'sbsigntools: sign EFI binaries for UEFI SecureBoot'
  'squashfs-tools: raw_squashfs output format'
  'tar: tar output format'
  'ubuntu-keyring: build Ubuntu images'
  'xz: compress images with xz'
  'zstd: compress images with zstd'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('285964835daf5c971ff7580fd99a09107db057da15ad52be1f2aa0bd9be12909b5d214f24ffde857475f9a896731d201d193fc67423d56b6bb507f72a01f24ad')
b2sums=('b4a5b19cb97b73cc3e1269064320ce7856997d3c748666224b781c16e1dfda9d892a0b8163d8b41a2b980403142c5dfa6c9332b9a62320d18a2dc9343f98740f')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {NEWS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
