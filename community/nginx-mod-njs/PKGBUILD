# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.4.4 # https://hg.nginx.org/njs/tags
pkgrel=1

_modname="${pkgname#nginx-mod-}"

pkgdesc='nginScript module for nginx'
arch=('loongarch64' 'x86_64')
depends=('nginx')
makedepends=('nginx-src')
url="https://nginx.org/en/docs/njs_about.html"
license=('CUSTOM')

source=(
	njs-$pkgver.tar.gz::https://hg.nginx.org/njs/archive/$pkgver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('2eeee5e6776d91eb88e1ed56f418f98dbe3944a76457ca0cc014b48f01ce04bb')

build() {
	cp -r /usr/src/nginx .

	cd "$srcdir"/nginx
	./configure --with-compat --with-stream --add-dynamic-module=../njs-$pkgver/nginx
	make modules
}

package() {
	install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
