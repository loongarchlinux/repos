# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-get_process_mem
pkgver=0.2.7
pkgrel=1
pkgdesc='Get memory usage of a process in Ruby'
arch=(any)
url='https://github.com/zombocom/get_process_mem'
license=(MIT)
depends=(ruby-ffi)
makedepends=(ruby-sys-proctable ruby-rake ruby-test-unit)
options=(!emptydirs)
source=(https://github.com/zombocom/get_process_mem/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e177620e6cfe27d6936081dea5b0e4054f325bc62b26b484618398f21f346bc2')

prepare() {
  cd get_process_mem-$pkgver
  sed -r -e 's|~>|>=|g' -e 's|git ls-files|find -type f|' -i get_process_mem.gemspec
  sed -i '/Bundler.require/d' test/test_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd get_process_mem-$pkgver
  gem build get_process_mem.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    get_process_mem-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd get_process_mem-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd get_process_mem-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
