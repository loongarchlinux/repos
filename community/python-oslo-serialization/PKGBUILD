# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=5.0.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-pbr' 'python-msgpack' 'python-oslo-utils' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("https://github.com/openstack/oslo.serialization/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0fbe642a82485ee4989ee9811a46ffe7cbbbc8bd1737761e325ed61b93d7edcb325abc435780afb98839898702c5bdba5d0349bb14e3f7a670d15a252b570140')

export PBR_VERSION=$pkgver

build() {
  cd oslo.serialization-$pkgver
  python setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package() {
  cd oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
