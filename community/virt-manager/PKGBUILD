# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=3.1.0
pkgrel=1
arch=('any')
url="https://virt-manager.org/"
license=('GPL')
makedepends=('python-docutils')
checkdepends=('python-pytest' 'libosinfo' 'libvirt-python' 'python-gobject' 'python-requests' 'cpio' 'cdrtools')
source=("https://virt-manager.org/download/sources/virt-manager/virt-manager-$pkgver.tar.gz")
b2sums=('729a16ee419cfc9f118e4a59336e961f17f38908ab3aaa3598c83bff3e8bca75b150f0f38b021c5ccb839a7c43e87f071859f68c2a81425715e236e14d0a7929')

build() {
  cd $pkgbase-$pkgver
  python setup.py configure --default-hvs qemu,lxc
  python setup.py build
}

package_virt-install() {
  pkgdesc="Command line tool for creating new KVM , Xen, or Linux container guests using the libvirt hypervisor"
  depends=('libosinfo' 'libvirt-python' 'python-gobject' 'python-requests')

  cd $pkgbase-$pkgver
  python setup.py --no-update-icon-cache --no-compile-schemas install --root "$pkgdir"
  python -m compileall "$pkgdir"/usr/share/virt-manager
  python -O -m compileall "$pkgdir"/usr/share/virt-manager

  # Split virt-manager
  [[ -d "$srcdir"/virt-manager ]] && rm -r "$srcdir"/virt-manager/
  mkdir -p "$srcdir"/split/usr/{bin,share/{man/man1,virt-manager}}
  mv "$pkgdir"/usr/bin/virt-manager "$srcdir"/split/usr/bin/
  mv "$pkgdir"/usr/share/{applications,glib-2.0,icons,metainfo} "$srcdir"/split/usr/share/
  mv "$pkgdir"/usr/share/man/man1/virt-manager.1 "$srcdir"/split/usr/share/man/man1/
  mv "$pkgdir"/usr/share/virt-manager/{icons,ui,virtManager} "$srcdir"/split/usr/share/virt-manager/
}

package_virt-manager() {
  pkgdesc="Desktop user interface for managing virtual machines"
  depends=("virt-install=$pkgver" 'gtk-vnc' 'libvirt-glib' 'spice-gtk' 'vte3' 'python-cairo' 'gtksourceview4')

  mv -v split/* "$pkgdir/"
}
