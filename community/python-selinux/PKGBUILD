# Maintainer: David Runge <dvzrv@archlinux.org>

_name=selinux
pkgname=python-selinux
pkgver=0.2.1
_commit=dde60d08158f594933cc42a8fe2200ee88d890b5  # refs/tags/0.2.1
pkgrel=8
pkgdesc="Pure-python selinux shim module for use in virtualenvs"
arch=(any)
url="https://github.com/pycontribs/selinux"
license=(MIT)
depends=(python-distro)
makedepends=(git python-build python-installer python-setuptools python-setuptools-scm python-wheel)
# switch to git sources to not backport own patches: https://github.com/pypa/setuptools/issues/3672
source=(
  git+https://github.com/pycontribs/selinux#tag=$_commit
  # https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-0.2.1-dependencies.patch
)
sha512sums=('SKIP'
            'cadabec154eec83aae9184a51c0289b0c7c45e6b7987b6281fe745e5b298b878f9cd73e2acf63e7e874253b2ab100fb4e903068ee3c605536bd7248d6ff91307')
b2sums=('SKIP'
        '1cdd908ef1c9f4ed7294f44e87bfa8a31bb3e57b0dea3c0d41cdb7141a80788c59ac25dedaab9857a74844aac46c80e95f96e9abafc01dc5ffa400dc4ec0dd37')

prepare() {
  # remove unneeded build system and runtime requirements
  # https://github.com/pycontribs/selinux/pull/54
  patch -Np1 -d $_name -i ../$pkgname-0.2.1-dependencies.patch
}

build() {
  cd $_name
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
