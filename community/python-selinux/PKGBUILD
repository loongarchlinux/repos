# Maintainer: David Runge <dvzrv@archlinux.org>

_name=selinux
pkgname=python-selinux
pkgver=0.2.1
pkgrel=7
pkgdesc="Pure-python selinux shim module for use in virtualenvs"
arch=('any')
url="https://github.com/pycontribs/selinux"
license=('MIT')
# upstream ticket around whether setuptools is actually required:
# https://github.com/pycontribs/selinux/issues/50
depends=('python-distro' 'python-setuptools')
makedepends=('python-setuptools-scm' 'python-setuptools-scm-git-archive')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e0bec9113461a4dc53e4965522b222bd674212fe1fd987180488d6d9183a1c25588ac9bec4a040595f9313932bc04b83e7f27505d539412fc0d49c2188679831')
b2sums=('c0af839b6fd23f7577b05ebc6d8f4f29909a087bf1700256196a3b4a905ba38e08c78c60a0a6b7b6d7841620c23e4a574a2ac1c462c0a886e930738f012225de')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
