# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.13.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('9d17613ac54d321b03298139d6d971061058d9af9f0ff1a282aebb606a4da6ec4e15a6cf569767e5eae238df926b0c79f980bef592077e4cd42523ba9141979a')
b2sums=('5a78e1e01e9046bde4372ea011ea69230752e59aacec6252d79f96abeefe7014f3115463083dd2e2fd966836584f90fd060ae06a5e85802a4816b3a6ce2f8f96')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/788
    --deselect tests/test_workspacebuilder.py::test_automatic_rename_option
  )
  cd $pkgname-$pkgver
  pytest -vv "${_deselected[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
