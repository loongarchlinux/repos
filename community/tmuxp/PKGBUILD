# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.18.2
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux python-typing-extensions)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest python-pytest-mock python-pytest-rerunfailures)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('4e085f4ab8c2f7fa00c38fee812a5fc7bd36da1a820a838f9c5f54ce4301b6718eda24633c750a836791f187b55a67a2d6771b3880ee1d1855e23c7588a4c34a')
b2sums=('b0ceb165382e2147fb502ca7d0bfb9255f69dc4b3d9fdec7a8a782efcc25e8887b4d17e95577567e79b2a198ace897f4edcc5102565f1b85d7e4112485868607')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _deselected=(
    # https://github.com/tmux-python/tmuxp/issues/788
    --deselect tests/workspace/test_builder.py::test_automatic_rename_option
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv "${_deselected[@]}" tests
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
