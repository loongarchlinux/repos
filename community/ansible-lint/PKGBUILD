# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=5.3.1
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('MIT')
depends=('python' 'ansible-core' 'python-ruamel-yaml' 'python-pyaml' 'python-rich' 'python-packaging'
          'python-wcmatch' 'python-enrich' 'python-tenacity')
makedepends=('python-build' 'python-install' 'python-wheel')
optdepends=('yamllint: check for yaml syntax mistakes'
            'ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_use_scm_version.patch)
b2sums=('c297f1bdc4bfcf57cd16a77ee6576fb60b471ee72a280fad45573a7237b650df107321359230a0791e951c9027abcde17951166a7b81306fde34f650dd5f5dcb'
        '41a9f052e1a8cca7f720da3dd4ba7ccbbd7d0046bdb7008c58172ba388f798a4bca5faea15df3e9dc0f022b8c91354ff64595850dfeea47a40cfe2182beec072')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  patch -Np1 < "${srcdir}"/disable_use_scm_version.patch
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m install --optimize=1 --destdir="${pkgdir}" dist/*.whl
  chmod +x "${pkgdir}"/usr/bin/*
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
