# Maintainer: George rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-cli_helpers
pkgver=2.2.1
pkgrel=2
pkgdesc='Python helpers for common CLI tasks'
arch=('any')
url='https://github.com/dbcli/cli_helpers'
license=('BSD')
depends=(
  'python'
  'python-backports.csv'
  'python-terminaltables'
  'python-tabulate'
)
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-configobj')
_commit='03ffe9e307b38b99c41a16dedd25f0266c96cdc0'
source=("$pkgname::git+https://github.com/dbcli/cli_helpers.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  pytest \
    --deselect tests/tabular_output/test_tabulate_adapter.py::test_tabulate_wrapper
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
