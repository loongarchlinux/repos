# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-nbval
pkgver=0.9.6
pkgrel=4
pkgdesc="A py.test plugin to validate Jupyter notebooks"
url="https://github.com/computationalmodelling/nbval"
license=('BSD')
arch=('any')
# python-six can be removed from dependencies in the next version
# https://github.com/computationalmodelling/nbval/pull/162
depends=('python-pytest' 'python-six' 'python-jupyter_client' 'jupyter-nbformat' 'python-ipykernel'
         'python-coverage')
makedepends=('python-setuptools')
checkdepends=('python-doit' 'python-matplotlib' 'python-pytest-cov' 'python-sympy')
source=("https://github.com/computationalmodelling/nbval/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "0001-Make-tests-pass-with-ipykernel-6.0.0.patch")
sha512sums=('ce2122ab490b52537696b2a8e97fd3b604c547af93fccf7d83d82b9180d8bb50b57d002f4e16eb605acabab5b31e961fea97bf2d46bf45b9cd1f8b7fca028df2'
            '04299db828848a1babc6edbcd553300c8c543eccb35ff78eb24352d0c92ef802f6e245832d0412239d1adcab334957a55cab6f87477a8e832669b9f61655d1f7')

prepare() {
  cd nbval-$pkgver
  patch -Np1 -i ../0001-Make-tests-pass-with-ipykernel-6.0.0.patch
}

build() {
  cd nbval-$pkgver
  python setup.py build
}

check() {
  cd nbval-$pkgver
  python setup.py egg_info

  # XXX: workarounds for issues in dependencies...
  # 1. ipython uses a function deprecated in matplotlib 3.4 [1]
  #    This will be unneeded once the next version of IPython is out [2].
  #    Note that matplotlib.MatplotlibDeprecationWarning is inherited from UserWarning
  #    instead of DeprecationWarning [3] until its upcoming version [4].
  export PYTHONWARNINGS="ignore::UserWarning"
  # [1] https://github.com/computationalmodelling/nbval/issues/167
  # [2] https://github.com/ipython/ipython/pull/12889
  # [3] https://github.com/matplotlib/matplotlib/blob/v3.4.2/lib/matplotlib/_api/deprecation.py#L19
  # [4] https://github.com/matplotlib/matplotlib/pull/20046

  PYTHONPATH="$PWD" doit test
}

package() {
  cd nbval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
