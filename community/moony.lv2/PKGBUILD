# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=moony.lv2
pkgver=0.34.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('glu' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('4a83aa90495124f641a7f1f4147c0ef3e4cb05b8c92f016cb03e186039fa749b56cb527664d14f3bfe7b66ff5640193cf2ddc954cf21e9c6670531f236c8c027'
            'SKIP')
b2sums=('8d60c31216e5cd7a534a8387b968ec10b5d04a029099605e738ab5dc55438c0c6bb2e09de4e40df25b974cff7476497788881f4e054255095e34ed0ebb28208d'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('a1xa1' 'a2xa2' 'a4xa4' 'c1a1xc1a1' 'c1xc1' 'c2a1xc2a1'
  'c2xc2' 'c4a1xc4a1' 'c4xc4')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/moony#${_plugin}"
  done
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
