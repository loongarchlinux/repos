# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Roheim <david dot roheim at gmail dot com>
# Contributor: Chris Heien <chris.h.heien@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=s3fs-fuse
pkgver=1.93
pkgrel=1
pkgdesc="FUSE-based file system backed by Amazon S3"
arch=('loong64' 'x86_64')
url="https://github.com/s3fs-fuse/s3fs-fuse/wiki"
license=('GPL2')
depends=('fuse2' 'curl' 'libxml2')
conflicts=('s3fs')
provides=('s3fs')
replaces=('s3fs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/s3fs-fuse/s3fs-fuse/archive/v$pkgver.tar.gz")
sha512sums=('613ad713ac426236b471aa4b2f8292ee0558017f149bfe51aa7fad1bacd60fccf00c31404ebc14b692787e524704e9cd9111b84540f1309ba99900d42d03dc5d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
