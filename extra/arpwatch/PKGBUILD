# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SÃ©bastien Luttringer

pkgname=arpwatch
pkgver=3.5
pkgrel=1
pkgdesc='Ethernet/FDDI station activity monitor'
arch=('loong64' 'x86_64')
url='https://ee.lbl.gov/'
license=('BSD')
depends=('libpcap' 'smtp-forwarder')
source=("https://ee.lbl.gov/downloads/arpwatch/arpwatch-$pkgver.tar.gz"
        'LICENSE'
        "ethercodes.dat.gz"
        "$pkgname.service")
sha512sums=('e46b350c483f0e7c873eb177be337f238b2db3d859d8b305df5a74d9d97ca449750f9ed50a99c5d4c51618e22747731d70ceb3f13aae39c39d258b960258fb88'
            'ed46c36220507d8ffbd01a3cc90c9047536b8af26af8c9623fae7a6584ad922b6adebabb6de771b0dbdc802da1a6cb51b8b30a8902c92d71f8b31ee763d61624'
            'dc9818e6453e0d9ef38864536ea66b15a8acb48091b609e422227538d3d4c6b93cc853778f8f4ea8cd887dc15c1c62da66f4325c540eb80f9c78705d63a65226'
            '4a9460c8a376d9183265b73e2c3b02965223144fecfeeb630ec5e70d40d4aed5725e5a43ae370938afa0a2fd362785020eca679e585a7f400e4e67f255807a84')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/@HAVE_FREEBSD_TRUE@/d' Makefile.in
  sed -i '1i#include <time.h>' report.c
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --with-watcher=arpwatch
  make SENDMAIL=/usr/bin/sendmail
}

package() {
  cd $pkgname-$pkgver
  install -d -m 0755 "$pkgdir"/usr/{bin,share/man/man8}
  make DESTDIR="$pkgdir" install
  # install ether prefix database
  install -Dm644 "$srcdir"/ethercodes.dat "$pkgdir/var/lib/$pkgname/ethercodes.dat"
  # license
  install -Dm644 "$srcdir"/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # systemd
  install -Dm644 "$srcdir"/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
}
