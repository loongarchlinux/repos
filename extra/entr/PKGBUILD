# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=entr
pkgver=5.5
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('loong64' 'x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
depends=('glibc')
checkdepends=('procps-ng' 'git' 'vim' 'tmux')
validpgpkeys=('519151D83E83D40A232B4D615C418B8631BC7C26' '5C418B8631BC7C26') # Eric Radman <ericshane@eradman.com>
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('5150744142cf1639289c2d845cf8cd3cbc0ef57fce8285bc0e2908c430d8344659ff064810e175e69bd8a6f06144734b2efa4d733e0fba7fec83a1f48d053a0d'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  TERM='xterm' make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
