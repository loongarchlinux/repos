# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=entr
pkgver=5.4
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('loong64' 'x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
depends=('glibc')
checkdepends=('procps-ng' 'git' 'vim' 'tmux')
validpgpkeys=('519151D83E83D40A232B4D615C418B8631BC7C26' '5C418B8631BC7C26') # Eric Radman <ericshane@eradman.com>
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('c366254df6df28edbcb2a9d12ab97e063baa4d78889e54b3de6b75e8c7ef2468c661216485bbf297a4d539ac3d186fcc9b140a99a1a89306f6cac1f969a7d95f'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  TERM='xterm' make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
