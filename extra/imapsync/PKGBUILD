# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=imapsync
pkgver=2.264
pkgrel=1
pkgdesc='IMAP synchronisation, sync, copy or migration tool'
arch=('any')
url='https://imapsync.lamiral.info/'
license=('custom:imapsync')
depends=(
  perl-cgi
  perl-crypt-openssl-rsa
  perl-data-uniqid
  perl-date-manip
  perl-digest-sha1
  perl-encode-imaputf7
  perl-file-copy-recursive
  perl-file-tail
  perl-html-parser
  perl-io-socket-inet6
  perl-io-socket-ssl
  perl-io-tee
  perl-json-webtoken
  perl-libwww
  perl-mail-imapclient
  perl-module-runtime
  perl-module-scandeps
  perl-ntlm
  perl-package-stash
  perl-proc-processtable
  perl-readonly
  perl-regexp-common
  perl-sys-meminfo
  perl-term-readkey
  perl-unicode-string
)
makedepends=(
  cpanminus
  perl-par-packer
)
checkdepends=(
  inetutils
  lsb-release
  perl-test-deep
  perl-test-fatal
  perl-test-mock-guard
  perl-test-mockobject
  perl-test-pod
  perl-test-requires
  perl-test-warn
  procps-ng
  time
)
source=("https://imapsync.lamiral.info/dist2/${pkgname}-${pkgver}.tgz")
b2sums=('f123ab2033f91e196c30eba5d2468310e7249c93cc1b388dde6a0f0f06ec4a2290ba5d5e6d0bbe62d16f945a92d83db6bc1e97403c931b5876c863d4c43f6549')

build() {
  make -C ${pkgname}-${pkgver}
}

check() {
  cd ${pkgname}-${pkgver}
  perl imapsync --tests
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
