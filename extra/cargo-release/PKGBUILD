# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.24.11
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'libgit2.so' 'libssh2.so' 'libssl.so' 'zlib')
makedepends=('cargo' 'pkg-config')
options=('!lto')
source=(https://github.com/crate-ci/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('fd5c697d17b5ccb6d69e51062a93db40bc70c9949e1a56c0bcb23e543a4adf090225b17dc95cde08377cfaaa83e377314761dd3318efa9df65c3e7da5f88e8af')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release --no-default-features
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
