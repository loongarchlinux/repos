# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=21.12.0
pkgrel=3
pkgdesc='The ancient Chinese board game for 4 players'
url='https://apps.kde.org/kajongg/'
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(python-twisted python-pyqt5 libkmahjongg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('vorbis-tools: for playing sounds')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/games/kajongg/-/commit/779521fa.patch)
sha256sums=('e62c61d5b753d5db712e7bf9fdcef1687deed2bcb870421a51347d3eeb12d295'
            'SKIP'
            '3a0f97fb4272aa52bd7a8e1cdf2386bcac847a81a50eda55e410b5042ea0789f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 779521fa.patch # Fix running on Python 3.10
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
