# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kajongg
pkgver=23.04.1
pkgrel=2
pkgdesc='The ancient Chinese board game for 4 players'
url='https://apps.kde.org/kajongg/'
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(python-twisted python-qtpy qt5-python-bindings libkmahjongg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('vorbis-tools: for playing sounds')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        zope-interface-6.patch
        python-3.11.patch)
sha256sums=('782fb5887246de7afb8c6747296396431628369bb8185bb79fa94ce2b57cf1ca'
            'SKIP'
            'f260d6ef4b98b9c97a49244a64afa92bcc5a92407b72b9e20bce63e14bb3d40a'
            'd8f95a7271bde23bbc74b547cf519b000d289918cf4af2f96753b5140f0196c7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < zope-interface-6.patch
  patch -d $pkgname-$pkgver -p1 < python-3.11.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
