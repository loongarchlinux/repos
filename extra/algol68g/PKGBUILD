# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.2.0
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(loong64 x86_64)
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=(GPL)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'https://ftp.tw.freebsd.org/distfiles/a68g-doc.pdf')
b2sums=('78851971b00d82b323fd46ee6630714d8c42993e52629659c23d4691b808f8bea4a47e7c8af37ebeb31743608efd047fb6648a227d02dd561123f3f4739b3bd6'
        'f8ca710d9a4f611873de41785e70359bd566645268ebb848803d3cd2e9a9f873f8ac423e3e08699998083444b7f6d98df32fdb7a4c6b36692462ee50ddb1e410')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" \
    docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
