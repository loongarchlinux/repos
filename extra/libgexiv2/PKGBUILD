# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.14.1
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=(loong64 x86_64)
license=(GPL2)
depends=(exiv2 glib2)
makedepends=(gobject-introspection meson python-gobject vala)
source=("https://gitlab.gnome.org/GNOME/gexiv2/-/archive/gexiv2-$pkgver/gexiv2-gexiv2-$pkgver.tar.bz2"
         https://gitlab.gnome.org/GNOME/gexiv2/-/commit/06adc8fb.patch)
b2sums=('c2985185d07ebb3388c6bd735f492efab6d2f023a11a3cb2034b83997862b9fe995a56de6ba3d00b3126bb4fd153afaf68d1090f97247b572b30895b36cde114'
        '631deb48a9844be4dbdbb66a8ed8c866c8d712b581f8483ef24999f179fbf7ecf12b9196efcb5de0197bf586d86ea6f69ab5a5117bbf2de07061360637b5713d')

prepare() {
# Fix build with exiv2 0.28
  patch -d gexiv2-gexiv2-$pkgver -p1 < 06adc8fb.patch
}

build() {
  arch-meson gexiv2-gexiv2-$pkgver build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libexiv2.so)
  provides+=(libgexiv2.so)
  meson install -C build --destdir "$pkgdir"
}
