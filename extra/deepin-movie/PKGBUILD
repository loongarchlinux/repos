# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.10.31
pkgrel=1
pkgdesc='Movie player based on mpv'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg'
         'ffmpegthumbnailer' 'libxtst' 'libglvnd' 'dconf' 'gsettings-qt' 'libva' 'libxcb' 'libx11'
         'qt5-x11extras' 'mpv' 'qt5-multimedia' 'qtdbusextended' 'qtmpris')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5a3da8ae202f308d3c722f3376868132a6355ba641bf2fa750271fc9246765815509aa678cb5def936308677a565461d7ec78b63a0dec2bd839d6d6d9f153abf')

build() {
  cd deepin-movie-reborn-$pkgver
  export CXXFLAGS+=' -fpermissive'
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
