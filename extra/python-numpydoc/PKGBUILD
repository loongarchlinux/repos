# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=1.6.0
pkgrel=1
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja' 'python-tabulate')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-matplotlib' 'python-numpy' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('3846f395d8603a21d97a6c0fc57f0ea69e908ff2f672673d336a1f4ff02f5cb3f9e76d1e1ad6d990478b121a327bcca8179c3db6b5215f36818e41e4597878fd')

build() {
  cd numpydoc-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd numpydoc-$pkgver
  python -m pytest
}

package() {
  cd numpydoc-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
