# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.12
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(loong64 x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  jack
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('20c6160b2d3114bfd951d7fd20d57273363cb60e22e1e5b8c792215680d261c3fb3f5d00e1c4f2c82d15497b5ff07b4839095b3b09762e8dbf13ffaa021c84a3')
b2sums=('6f990a629e21b9ceea22fe21faa5071ad093f9e1b4c2fa8a676c82e8008d8e2b18ecd3ea26b5a957de825abd6e8607603f7b3dcac0d1407d7aee3e356994729f')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
