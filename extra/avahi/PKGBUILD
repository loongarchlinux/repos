# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=avahi
pkgver=0.8+r22+gfd482a7
pkgrel=1
epoch=1
pkgdesc="Service Discovery for Linux using mDNS/DNS-SD (compatible with Bonjour)"
url="https://github.com/lathiat/avahi"
license=(LGPL)
arch=(loong64 x86_64)
depends=(
  dbus
  expat
  gdbm
  glib2
  libcap
  libdaemon
)
makedepends=(
  doxygen
  git
  gobject-introspection
  graphviz
  gtk3
  libevent
  python-dbus
  python-gobject
  qt5-base
  xmltoman
)
optdepends=(
  'gtk3: avahi-discover, avahi-discover-standalone, bshell, bssh, bvnc'
  'libevent: libevent bindings'
  'nss-mdns: NSS support for mDNS'
  'python-dbus: avahi-bookmarks, avahi-discover'
  'python-gobject: avahi-bookmarks, avahi-discover'
  'python-twisted: avahi-bookmarks'
  'qt5-base: qt5 bindings'
)
provides=(
  libavahi-{client,common,core,glib,gobject,libevent,qt5,ui-gtk3}.so
  libdns_sd.so
)
backup=(
  etc/avahi/{hosts,avahi-daemon.conf,avahi-{autoip,dnsconf}d.action}
  usr/lib/avahi/service-types.db
)
_commit=fd482a74625b8db8547b8cfca3ee3d3c6c721423  # master
source=(
  "git+https://github.com/lathiat/avahi#commit=$_commit"
  0001-avahi-discover-Don-t-decode-unicode-strings-only-byt.patch
  0002-Fix-avahi-browse-Invalid-service-type.patch
  0003-Do-not-disable-timeout-cleanup-on-watch-cleanup.patch
  0004-HACK-Install-fixes.patch
)
b2sums=('SKIP'
        'a7d26bfbaa2bc488de81e9ec6d32ee64b1eaacd4e1cbecc6f896c263c2e37a036a5510d3f1c50ddcaec50251c4bd265aaf68ebe3c508016de9018aecaf2692e1'
        '79a881a7f45b4d6061285257b07aa0f72aa69cedc636c74ec12dd031372b3560b4013f18d821e31b323fd73af5de811a1f5ba94ba0e32ce1b1bb940af3bad54f'
        '04a100e1775e10c9dd23fb7e1c4f9f456ac9fd84abe649ca93758dfede262cfcf9dc114c40be08d53645de92bfd26071e8b1a1117e3f391ddad7c64aedc69de0'
        '1b3fd8221b3a2d79a9425f5bd630ace0ac345ae7b7c0741957beb6755664aaff5c84d441a5c399d86945ec876eef7bbda4f5162d39e2b1701da2c4b4c233b8cc')

pkgver() {
  cd avahi
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd avahi

  # https://bugs.archlinux.org/task/68518
  # https://github.com/lathiat/avahi/pull/282
  git apply -3 ../0001-avahi-discover-Don-t-decode-unicode-strings-only-byt.patch

  # https://bugs.archlinux.org/task/71781
  # https://github.com/lathiat/avahi/issues/212
  git apply -3 ../0002-Fix-avahi-browse-Invalid-service-type.patch

  # https://bugs.archlinux.org/task/78644
  # https://github.com/lathiat/avahi/pull/366
  git apply -3 ../0003-Do-not-disable-timeout-cleanup-on-watch-cleanup.patch

  # https://bugs.archlinux.org/task/47822
  git apply -3 ../0004-HACK-Install-fixes.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --sbindir=/usr/bin
    --disable-mono
    --enable-compat-libdns_sd
    --with-autoipd-group=avahi
    --with-autoipd-user=avahi
    --with-avahi-priv-access-group=network
    --with-distro=archlinux
    --with-systemdsystemunitdir=/usr/lib/systemd/system
    with_dbus_sys=/usr/share/dbus-1/system.d
  )

  cd avahi

  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(libdbus-1.so)

  cd avahi
  make DESTDIR="$pkgdir" install

  echo 'u avahi - "Avahi mDNS/DNS-SD daemon"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}

# vim:set sw=2 sts=-1 et:
