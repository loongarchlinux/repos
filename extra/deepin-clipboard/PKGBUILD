# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=6.0.6
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt' 'dtkcore' 'dwayland')
makedepends=('cmake' 'ninja' 'gtest' 'qt5-tools' 'extra-cmake-modules')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('ca8ed8dc23bbc41dfd9449a17c9944cf20d259d3a4c5ba96755394ab90ec86b4bab1b8df2847b3bcfbcb1b2782d4bf626cd155ed9ef5d24b137b6d4710af0cce')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
