# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-wayland
_qtver=6.5.2
pkgver=${_qtver/-/}
pkgrel=2
arch=(loong64 x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides APIs for Wayland'
depends=(qt6-declarative libxcomposite)
makedepends=(cmake ninja)
groups=(qt6)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        qtbug-115757.patch::https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=02e9e0a2)
sha256sums=('3020be86fb7fd0abb8509906ca6583cadcaee168159abceaeb5b3e9d42563c9a'
            '82b3418443ffd52e071a979aabef33dd51e93aec65fa9ec16e521dcff9e7cb2a')

prepare() {
  patch -d $_pkgfn -p1 < qtbug-115757.patch # Fix crash on drag and drop
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
