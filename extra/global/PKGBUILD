# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Fredy García <frealgagu at gmail dot com>
# Contributor: Randy Ramos <rramos1295 at gmail dot com>
# Contributor: Peter Vasil <mail@petervasil.net>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien <orontee@gmail.com>

pkgname=global
pkgver=6.6.11
pkgrel=1
pkgdesc='Source code tagging system'
arch=(loong64 x86_64)
url='https://www.gnu.org/software/global/'
license=(GPL)
depends=(libltdl perl python sqlite)
optdepends=(ctags emacs idutils python-pygments vim)
options=(!emptydirs !libtool)
validpgpkeys=("26F631B43D624A927E6F1C33969C3BE389DDA6EB")
source=("https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
b2sums=('6e78534eed182b7106fea1df4eea3f12a01e0ea22d5030ad43954ac2e05205ed5e64138af30fe5e969c3ef35c55fc0647a89629cc11d7e7692be3ec92b53205c'
        'SKIP')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/\.la/.so/g' gtags.conf.in
  # Rename lid to lid-idutils to avoid a name collision with idutils from AUR
  sed -i 's/usable("lid")/usable("lid-idutils")/g' "$pkgname/$pkgname.c"
}

build() {
  cd $pkgname-$pkgver

  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --with-exuberant-ctags=/usr/bin/etags \
    --with-sqlite3 \
    --with-universal-ctags=/usr/bin/ctags
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  # Package the emacs script
  install -d "$pkgdir/usr/share/emacs/site-lisp"
  mv "$pkgdir/usr/share/gtags/gtags.el" \
    "$pkgdir/usr/share/emacs/site-lisp/gtags.el"

  # Package the vim scripts
  install -d "$pkgdir/usr/share/vim/vimfiles/plugin"
  mv "$pkgdir/usr/share/gtags/gtags.vim" \
    "$pkgdir/usr/share/vim/vimfiles/plugin/gtags.vim"
  mv "$pkgdir/usr/share/gtags/gtags-cscope.vim" \
    "$pkgdir/usr/share/vim/vimfiles/plugin/gtags-cscope.vim"
}
