# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.7.0
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-loguru
  python-pytest-runner
)
optdepends=('python-loguru: for logging support')
source=(
  $url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
)
sha512sums=('df3b33dbf213c7099a5a17166ac1e01e9044bf6bb364de96a4b182831ff3216fe37edb7f94cb66f8d2fcab42cea404a9c4f27142890ae0e7510363815f47ad41'
            'SKIP')
b2sums=('ff306a2ee3e84585081f504ad50f117294698c75dd4d5015db6cf9326796d69aaf63365ac5bbedb3af4d41e4f18b7a05437860bf41cdb870e43f1ba75e92628c'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
