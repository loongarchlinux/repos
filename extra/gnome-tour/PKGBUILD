# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-tour
pkgver=44.0
pkgrel=2
pkgdesc='Guided tour and greeter for GNOME'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-tour'
license=('GPL3')
depends=('gcc-libs' 'glib2' 'glibc' 'gtk4' 'hicolor-icon-theme' 'libadwaita')
makedepends=('appstream-glib' 'meson' 'rust')
groups=('gnome')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('06de7677dd1c590d0ea33a032c9ccf4c37c62bc56215b823c879e42ee630c2b6')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
