# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=memray
pkgver=1.8.1
pkgrel=1
pkgdesc="A memory profiler for Python"
arch=('loong64' 'x86_64')
url="https://github.com/bloomberg/memray"
license=('Apache')
depends=('python' 'libunwind' 'python-rich' 'python-jinja' 'lz4' 'ipython')
makedepends=('git' 'python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel' 'cython' 'npm')
checkdepends=('python-pytest')
_tag_commit=e1c6bd610661c65a32762fd347fcbe383e3606ec
source=("git+$url.git#tag=${_tag_commit}?signed")
sha512sums=('SKIP')
validpgpkeys=('A035C8C19219BA821ECEA86B64E628F8D684696D') # Pablo Galindo Salgado <pablogsal@gmail.com>

build() {
  cd "${pkgname}"

  export DIST_NAME='Arch Linux'
  export DIST_PACKAGE="$pkgver-$pkgrel"
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"

  make build
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
