# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: bekks <eduard.warkentin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=4.1.0
pkgrel=1
pkgdesc='Extendable, high-performance executable packer for several executable formats'
arch=(loong64 x86_64)
url='https://github.com/upx/upx'
license=(custom GPL2)
makedepends=(cmake git perl)
# The fixed git commits are from the upstream "upx-vendor" branches, 2023-08-08
source=(
  git+$url-vendor-doctest#commit=15e49a5fb7e49dd8f590d3666534b209e7212c6c
  git+$url-vendor-lzma-sdk#commit=f0b8d236c1bdf66532a198c983469da5c05593cf
  git+$url-vendor-ucl#commit=4b58d592199dc1e5db691e1a54fb0e5e9af0ecaf
  git+$url-vendor-valgrind#commit=4edff0085c968515309446620901468a4a6eee96
  git+$url-vendor-zlib#commit=1e2d7fbf597e10396c1b3408eb3f30ee655e9fee
  "git+$url#commit=877911313161fbf86e2688e091b8fd1657985d19") # tag: v4.1.0
b2sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

prepare() {
  cd $pkgname
  # set up symbolic links for the vendor repos
  for x in doctest lzma-sdk ucl valgrind zlib; do
    rm -frv vendor/$x && ln -s "$srcdir/upx-vendor-$x" vendor/$x
  done
}

build() {
  make -C $pkgname \
    CHECK_WHITESPACE=/bin/true \
    CXXFLAGS_WERROR="" \
    UPX_CMAKE_CONFIG_FLAGS="-DUPX_CONFIG_DISABLE_GITREV=1" \
    UPX_LZMA_VERSION=0x465 \
    UPX_LZMADIR="$srcdir"
}

check() {
  # check that the version has the proper version number and not something like "git-877911+"
  $pkgname/build/release/upx --help | grep -q "^UPX $pkgver" || exit 1
}

package() {
  cd $pkgname
  install -Dm755 build/release/upx "$pkgdir/usr/bin/upx"
  install -Dm644 doc/upx.1 "$pkgdir/usr/share/man/man1/upx.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
