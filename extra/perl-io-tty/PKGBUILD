# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-io-tty
pkgver=1.18
pkgrel=1
pkgdesc="Provide an interface to TTYs and PTYs"
arch=('loong64' 'x86_64')
url="https://search.cpan.org/dist/IO-Tty/"
license=("GPL" "PerlArtistic")
depends=('glibc')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/IO-Tty-$pkgver.tar.gz")
sha256sums=('b0e45b186e4ca54636b1f15111ec0fee86b993e23db529aacb4759df946792ff')

build() {
  cd "$srcdir"/IO-Tty-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
package() {
  cd "$srcdir"/IO-Tty-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
