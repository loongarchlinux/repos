# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.9.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=(loong64 x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-5compat
)
makedepends=(
  alsa-lib
  cmake
  doxygen
  docbook-xsl
  fluidsynth
  graphviz
  libpulse
  qt6-tools
  sonivox
)
provides=(
  libdrumstick-alsa.so
  libdrumstick-file.so
  libdrumstick-rt.so
  libdrumstick-widgets.so
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('6bfd30574148eb7c509c08127787c14870a27d427d2715f9df1765eded905023fdc8b4e3bdcd40240567dcc99598b5a8b88aa3a3a254defe5539d82b660bcfe9')
b2sums=('c5b252242ac608b6e80f75dacf91497bd06f7a16cb915c740100deb018e4a0f59bdf1b652dc1d8c5e20ce37f9a8f41859fbf61a658866d5009b3cf5c993e6fe9')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(
    alsa-lib libasound.so
    fluidsynth libfluidsynth.so
    libpulse libpulse.so libpulse-simple.so
    sonivox libsonivox.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
