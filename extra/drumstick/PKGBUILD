# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.8.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=(loong64 x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL2)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-5compat
)
makedepends=(
  alsa-lib
  cmake
  doxygen
  docbook-xsl
  fluidsynth
  graphviz
  libpulse
  qt6-tools
  sonivox
)
provides=(
  libdrumstick-alsa.so
  libdrumstick-file.so
  libdrumstick-rt.so
  libdrumstick-widgets.so
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('d960b1ddd31d2197c4d5d0cea3ce3ca1dff11d5c2012a020c25207a5dd92d06b72fc63512eed878759ac7a9ac0e855e347061e6e6032b5ba4ae002ca089a79e3')
b2sums=('98560b484143c4a1cbac9ddf7da6dc82bd555dfafb499ed76db8f5260ed18bbdd3695e49c3e57150af9bd0fe210b45108c63ca4bf1a2acba9f4f8b9a4783f053')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(
    alsa-lib libasound.so
    fluidsynth libfluidsynth.so
    libpulse libpulse.so libpulse-simple.so
    sonivox libsonivox.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
