# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=5.0.1
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(
  python
  python-click
  python-docutils
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-pbr
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-click
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('3b20eaa66a23b1f0d2d5419eada2ef2badf86e05759669c27aa5b34d5988494750d3027d8ee16f2f559213efc20ae00d3620d69ed91d61020c77a07246af1f44')
b2sums=('fc9f95a70d939cb68aec760f394a91ef8d4f5b76f024f7029766a4a9393331ee5ad5520bfcb6915c1291213d8b167b4ba8ef590cef9164af31f836c1d0450391')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 docs/*.rst -t "$pkgdir/usr/share/doc/$pkgname/docs/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
