# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fnott
pkgver=1.4.1
pkgrel=1
pkgdesc='Keyboard driven and lightweight Wayland notification daemon'
url="https://codeberg.org/dnkl/$pkgname"
arch=(loong64 x86_64)
license=(MIT)
depends=(dbus
         fcft
         wlroots)
makedepends=(meson
             tllist
             scdoc
             wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('20da05357aa83b3541b6c02bb162af10c89519602bc91fdfaa190239ce303300')

build() {
	cd "$pkgname"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
