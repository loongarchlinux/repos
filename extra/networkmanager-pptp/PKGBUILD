# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.2.12
pkgrel=3
pkgdesc="NetworkManager VPN plugin for PPTP"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(loong64 x86_64)
license=(GPL)
_pppver=2.5.0
depends=(
  libnm
  libsecret
  "ppp=$_pppver"
  pptpclient
)
makedepends=(
  git
  libnma
  libnma-gtk4
  python
)
optdepends=(
  'libnma-gtk4: GUI support (GTK 4)'
  'libnma: GUI support (GTK 3)'
)
_commit=ad5a2951d5f281eeea482404058ace8d15e23f30  # tags/1.2.12^0
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-pptp.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd NetworkManager-pptp
  git describe --tags | sed 's/-dev/dev/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd NetworkManager-pptp
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  )

  cd NetworkManager-pptp
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-pptp
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
}

# vim:set sw=2 sts=-1 et:
