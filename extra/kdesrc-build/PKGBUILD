# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ben Isenhart <bisenhar(at)uvm(dot)edu>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesrc-build
pkgver=22.07
pkgrel=1
pkgdesc='Script to streamline setting up and maintaining KDE software development environment'
url='https://apps.kde.org/kdesrc_build/'
arch=(any)
license=(GPL)
depends=(perl-libwww perl-xml-parser dialog perl-json perl-io-socket-ssl perl-net-ssleay perl-yaml-syck)
makedepends=(cmake)
optdepends=('subversion: download source code using svn'
            'git: download source code using git')
source=(https://invent.kde.org/sdk/kdesrc-build/-/archive/v$pkgver/kdesrc-build-v$pkgver.tar.gz)
sha256sums=('1a27a89df41bf6dea40b8cd6171e3a74de587f68db08a199a5e2046c98b04b66')

build() {
  cmake -B build -S $pkgname-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
