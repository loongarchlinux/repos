# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-mitmproxy-rs
_pyname=mitmproxy_rs
pkgver=0.4.1
pkgrel=1
pkgdesc="Python bindings for mitmproxy's Rust code"
arch=('loong64' 'x86_64')
url='https://github.com/mitmproxy/mitmproxy_rs'
license=('MIT')
depends=('python')
makedepends=('maturin' 'python-installer' 'cargo')
options=(!lto)
source=("https://github.com/mitmproxy/mitmproxy_rs/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('f1d0ae7a98f1c45a524edded3ccd21a744fdac242d03fff39ee674762f664b1f')
b2sums=('f214a19ee7626a78e61237ad0d696725f155049f8e5e079f66295185a3a0b6fd6ee213daecc64019e04db8d5336ea5ff054186021d0a815d3f792a5a790377ed')

build() {
  cd ${_pyname}-${pkgver}/mitmproxy-rs
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python -c 'import mitmproxy_rs'
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
