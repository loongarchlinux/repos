# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-mitmproxy-rs
_pyname=mitmproxy_rs
pkgver=0.2.2
pkgrel=1
pkgdesc="Python bindings for mitmproxy's Rust code"
arch=('loong64' 'x86_64')
url='https://github.com/mitmproxy/mitmproxy_rs'
license=('MIT')
depends=('python')
makedepends=('maturin' 'python-installer' 'cargo')
options=(!lto)
source=("https://github.com/mitmproxy/mitmproxy_rs/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1d6895c282acb99e5e6159fcbc0da88710b28552d4d124726c01b821c736a2f5ed2e41b15095c27eacecfc0032922aa2fd4966e794eebe26beced938a008b838')

build() {
  cd ${_pyname}-${pkgver}/ffi
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python -c 'import mitmproxy_rs'
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
