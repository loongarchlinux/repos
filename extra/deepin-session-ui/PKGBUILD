# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.11
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
# startdde: org.deepin.dde.Display1
depends=('gsettings-qt' 'deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtkcore' 'dtkgui'
         'dtkwidget' 'glib2' 'libxext' 'qt5-base' 'qt5-x11extras' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'startdde')
makedepends=('cmake' 'deepin-dock' 'deepin-gettext-tools' 'qt5-tools' 'gtest' 'ninja')
source=("https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/dde-session-ui/pull/340.patch)
sha512sums=('3f00b0e3260bce81d7c90d23b7ee7cc9f8a514308bbb099abd8154af7649f0777183c49f1eea2a973f913f1a5686836eb2c4978777f27c5492107fd9c8e02eb5'
            'd9c0be00cd7ea91b75364e908f9a6c57bbca9ed5a536ad5116ecaccf2e8a23fe81e09f9ab62ffc89b84ab1aa8ae710484d4d8744c495010674124e237453e154')

prepare() {
  cd dde-session-ui-$pkgver
  patch -Np1 -i ../$pkgname-ldflags.patch
}

build() {
  cd dde-session-ui-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
