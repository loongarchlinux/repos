# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=6.0.14
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
# deepin-daemon: org.deepin.dde.daemon.*
# startdde: org.deepin.dde.Display1
depends=('gsettings-qt' 'deepin-daemon' 'deepin-wallpapers' 'deepin-pw-check' 'dtkcore' 'dtkgui'
         'dtkwidget' 'glib2' 'libxext' 'qt5-base' 'qt5-x11extras' 'systemd-libs' 'xcb-util-wm'
         'libxcrypt' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'startdde')
makedepends=('cmake' 'deepin-dock' 'deepin-gettext-tools' 'qt5-tools' 'gtest' 'ninja')
source=("https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f54bd81b7ac146fd83bb120cf473b866b651160e392110991c7d04117c2277a07fde7ca212a1d1be29965a28f8706d0a791b57c6cd3b077b4628b13366d95af8')

build() {
  cd dde-session-ui-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-session-ui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
