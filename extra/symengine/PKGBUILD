# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgname=symengine
pkgver=0.11.1
pkgrel=3
pkgdesc='Fast symbolic manipulation library, written in C++'
url='http://sympy.org/'
arch=(loong64 x86_64)
license=(MIT)
depends=(flint
         gcc-libs
         glibc
         gmp
         gmp-ecm
         gperftools
         libmpc
         llvm-libs
         mpfr
         primesieve)
makedepends=(boost
             cereal
             cmake
             llvm)
source=(https://github.com/symengine/symengine/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        llvm-shared.patch
        flint-3.patch)
sha256sums=('217b39955dc19f920c6f54c057fdc89e8e155ddee8f0e3c3cacc67b3e3850b64'
            'ef05a806139b35e0336f00cb6960ef6cf483f11b0041e436a99ab1f46bdcb11c'
            'ccd5475333e970eb911461041f2112ab8da949c30eeafdb300124cf788a7e5e0')

prepare() {
  patch -d $pkgname-$pkgver -p1 < llvm-shared.patch # Use shared LLVM (Gentoo)
  patch -d $pkgname-$pkgver -p1 < flint-3.patch # Fix build with flint 3
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TCMALLOC=ON \
    -DWITH_PTHREAD=ON \
    -DWITH_SYMENGINE_THREAD_SAFE=ON \
    -DWITH_ARB=ON \
    -DWITH_ECM=ON \
    -DWITH_LLVM=ON \
    -DWITH_MPFR=ON \
    -DWITH_MPC=ON \
    -DWITH_PRIMESIEVE=ON \
    -DWITH_BOOST=ON \
    -DWITH_COTIRE=OFF \
    -DWITH_SYSTEM_CEREAL=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
