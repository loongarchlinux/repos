# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname="rubocop"
pkgver=1.39.0
pkgrel=1
pkgdesc='A Ruby code style checking and code formatting tool'
arch=('any')
url='https://rubocop.org/'
license=('MIT')
provides=('ruby-rubocop')
depends=('ruby' 'ruby-memory_profiler' 'ruby-parallel' 'ruby-parser' 'ruby-rainbow' 'ruby-regexp_parser' 'ruby-rubocop-ast' 'ruby-ruby-progressbar' 'ruby-unicode-display_width')
checkdepends=('asciidoctor' 'procps-ng' 'ruby-bump' 'ruby-rspec' 'ruby-rubocop-performance' 'ruby-rubocop-rake' 'ruby-rubocop-rspec' 'ruby-simplecov' 'ruby-stackprof' 'ruby-test-queue' 'ruby-webmock' 'ruby-yard')
options=('!emptydirs')
source=("https://github.com/rubocop/rubocop/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${pkgname}_fix_changelog_duplication.patch::https://github.com/rubocop/rubocop/commit/60dbfdb8897c74726a2d9336c1f812dff3a94ea4.patch")
sha512sums=('0ad3d117907a327035dfbccdc220828d1ab543016c27b837b39a63faa036208c797d26903eb926ff97c7d05838e1f0f530c8f6bd57c807a2911452d1a565f102'
            '402cc04239ef126678326a8d3c5c213ec8d296b676850e1945155aee9cf6f7a3e28474ab2bc0be82e6a3978cc5be143a4417dbec57dc22ad60ab5c4ae2e95bf8')
b2sums=('7a27313959227ab5a74ef42625872daadd95a09b12bb1d080e59b4ae92663d73c0fbd03b067c2c2e77268849221484b653840f7df0f9bf7f7358bea025b11ae0'
        '9b9539c1e0a99b141d6035e2951a0aa2925b84830ebf1bb1ccbc75ac6544ba2814e251b5b22e3539541363c8fd23815b87735eed76468dfe52646f7b5a4e7a66')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_changelog_duplication.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${pkgname}.gemspec" Gemfile

  # don't force an upper bound on simplecov
  sed --in-place --regexp-extended "s|gem 'simplecov'.*|gem 'simplecov'|" Gemfile

  # we need webmock
  sed --in-place '/group/d' Gemfile
  sed --in-place '/end/d' Gemfile
}

build() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake ascii_spec spec
}

package() {
  cd "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
