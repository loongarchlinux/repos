# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-bluetooth
pkgver=7.0.1
pkgrel=1
pkgdesc='Wingpanel Bluetooth Indicator'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-bluetooth
license=(GPL3)
groups=(pantheon)
depends=(
  bluez
  glib2
  gtk3
  libgranite.so
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=97623fce7706e2a09a4719b83870d6636b8f0691
source=(git+https://github.com/elementary/wingpanel-indicator-bluetooth.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-bluetooth

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
