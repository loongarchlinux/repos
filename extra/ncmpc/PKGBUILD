# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.41
pkgrel=1
pkgdesc="Fully featured MPD client which runs in a terminal"
url='https://www.musicpd.org/clients/ncmpc/'
arch=('x86_64')
license=('GPL2')
depends=('gcc-libs' 'glibc' 'lirc' 'pcre')
makedepends=('boost' 'libmpdclient' 'meson' 'ncurses' 'python-sphinx')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('12ca7eeb284f1f94c200ccd164a522610e828e1c1778a71f0b1e2188f599dc2dc608094e81b7431433b3dd5e85d5e056947df3b9b84bdd2d0456bbb93bc1d6d0'
            'SKIP')
b2sums=('8f9339ab7104e6a01b689456c7bdc81e4b0961d3972e34132391b932710f853f5bdd625ed6c5d5adcdad759f92844443ef8dae6fad88e20eaa40f46bfbc8bb17'
        'SKIP')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix /usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -D lyrics_screen=true \
        -D chat_screen=true \
        build
  ninja -C build
}

package() {
  depends+=('libmpdclient.so' 'libncursesw.so')
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
}
