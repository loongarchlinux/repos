# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-time-tracker
pkgver=3.0.2
pkgrel=5
pkgdesc="Time tracking application that helps you to keep track on how much time you have spent during the day on activities you choose to track"
arch=('any')
url="https://github.com/projecthamster/hamster"
license=('GPL')
depends=('gtk3' 'python-dbus' 'python-cairo' 'python-gobject' 'python-xdg')
makedepends=('intltool' 'itstool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/projecthamster/hamster/archive/v$pkgver.tar.gz"
	"python3.11-compatibility.patch")
sha256sums=('8bf28fe1f06cd950583ebfb24acf5254d3406ee29c3046f6a6fb648b98c929ef'
            'cb06463e560031e9ee964f54127bcdfb89113891152ec7846ac0fad0aefabe6b')

prepare() {
  cd hamster-$pkgver
  patch -Np1 -i ${srcdir}/python3.11-compatibility.patch
}

build() {
  cd hamster-$pkgver
  python ./waf configure --prefix=/usr --libexecdir=/usr/lib  --skip-icon-cache-update
  python ./waf build
}

package() {
  cd hamster-$pkgver
  python ./waf install --destdir="$pkgdir"
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}
