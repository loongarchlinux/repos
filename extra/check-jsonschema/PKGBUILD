# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=check-jsonschema
pkgver=0.23.1
pkgrel=1
pkgdesc="A JSON Schema CLI and pre-commit hook built on jsonschema"
arch=(any)
url="https://github.com/python-jsonschema/check-jsonschema"
license=(Apache)
depends=(
  python
  python-click
  python-jsonschema
  python-requests
  python-ruamel-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-xdist
  python-responses
)
source=(
  # no tests in sdist tarballs
  # https://github.com/python-jsonschema/check-jsonschema/issues/258
  # https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  $url/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('c67a840f0e296e57df05aa3c9573e8db74fe2dae9bca65729079060ecc2a0af4dd2c7881c7a49e7cd8f6bc6aa957a3cac438147cfa532d9accf75f2f03bef296')
b2sums=('962ae74c72993543b2a18bd86aeb777da3996bd97a8e72a3bc0cd519af775c54b1ca3180896c9700c2d434223e0c97f9bd4c25748a5c24442ef6e01b0de66d40')

check() {
  # we don't support pinning
  sed -e 's/==/>=/g' -i $pkgname-$pkgver/setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
