# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Christopher Reimer <mail+aur[at]c-reimer[dot]de>

pkgname=hyperion
pkgver=2.0.15
pkgrel=2
_commit=24a00e3b0b9030b5d2cbf203b815397c737b25f9
pkgdesc="An opensource 'AmbiLight' implementation"
arch=('loong64' 'x86_64')
url="https://github.com/hyperion-project/hyperion"
license=('MIT')
depends=('libusb' 'protobuf' 'python' 'qt5-base' 'qt5-serialport' 'alsa-lib' 'qmdnsengine' 'mbedtls')
optdepends=('xorg-server: X11 grabbing')
makedepends=('cmake' 'flatbuffers' 'libcec')
backup=('etc/hyperion/hyperion.config.json')
install='hyperion.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hyperion-project/hyperion.ng/archive/$_commit.tar.gz"
        "hyperion.sysusers"
        "hyperiond.service"
        "hyperion.install")
sha512sums=('addad9dbdee1be26fde5dcba03b10a1c44b5a0c323464af00d6748b9fc0a50e9cd9017e981b35d730b4e13466a488418cb73ffd5dd7bfce87ea30a6f9f46b39f'
            '80764d3abdfb7e6e7f65913e9c89dd61b425ffb80e9ab4f9df7739abe32117441095ac7b45792acd5d2a516cf4aca3e21062b119f5790aae3b178036db74ce61'
            'f8c23ba920ce683e731237c164f4f70f94d635398be5cc43ce1af48bb8ab672da47397979a825a5761dacb1ae18a1c5fc840b168c8f1e3ae411908e0e92a0c5b'
            'ff5ed8ed647a621b73e922590616e4f5d63f0a7e243aa3de0f8382a835e83166a0e04944396a61a03645cc00853251690a514b3772f163f445240e4f55f18fb5')

prepare() {
    cd ${pkgname}.ng-${_commit}
    sed -e 's|libqmdnsengine.a|libqmdnsengine.so|' -i cmake/Findqmdnsengine.cmake
    sed -e 's|CMAKE_CXX_STANDARD 14|CMAKE_CXX_STANDARD 17|' -i CMakeLists.txt
}

build() {
    cd "${srcdir}/${pkgname}.ng-${_commit}"
    mkdir -p build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX="${pkgdir}/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_QT5=ON \
        -DPLATFORM=x86 \
        -DPROTOBUF_PROTOC_EXECUTABLE=/usr/bin/protoc \
        -DUSE_SYSTEM_PROTO_LIBS=ON \
        -DUSE_SYSTEM_FLATBUFFERS_LIBS=ON \
        -DUSE_SYSTEM_QMDNS_LIBS=ON \
        -DUSE_SYSTEM_MBEDTLS_LIBS=ON \
        -DENABLE_DEPLOY_DEPENDENCIES=OFF \
        ..
    make
}

package() {
    cd "${srcdir}/${pkgname}.ng-${_commit}"
    cd build
    make install
    cd ..

    install -Dm 644 config/hyperion.config.json.default \
        "${pkgdir}/etc/hyperion/hyperion.config.json"

    install -Dm 644 "${srcdir}/hyperiond.service" \
        "${pkgdir}/usr/lib/systemd/system/hyperiond.service"

    rm -rf "${pkgdir}/usr/share/hyperion/service"

    install -Dm 644 "${srcdir}/hyperion.sysusers" \
        "${pkgdir}"/usr/lib/sysusers.d/hyperion.conf

    install -Dm 644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
