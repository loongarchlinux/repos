# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kubeconform
pkgver=0.6.4
pkgrel=1
pkgdesc="FAST Kubernetes manifests validator, with support for Custom Resources!"
url="https://github.com/yannh/kubeconform"
arch=("loong64" "x86_64")
license=("Apache")
depends=('glibc')
makedepends=("go" "git")
source=("${pkgname}-${pkgver}::https://github.com/yannh/kubeconform/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3b5f946b62abbe3f32cbff8b85d973d192894697454b68600f497af323227e29a3a4e2d1d5fbdd202c4aed86b22e4049eb993937064e0554dff0dcf1a43199a7')
b2sums=('fe17874a4aa9c33b19ed6f28677923273b2ccf4ea971e4c7d17618f4e39be2ad953e24abfcafcec111e0bf9611b97441c1cd1a2080a57d791ff79080390ebf61')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}/cmd/kubeconform"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "cmd/kubeconform/kubeconform" "${pkgdir}/usr/bin/${pkgname}"
}
