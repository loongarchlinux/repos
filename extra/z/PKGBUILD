# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Semyon Maryasin <marsoft@ya.ru>

pkgname=z
pkgver=1.12
pkgrel=1
pkgdesc="Tracks your most used directories, based on 'frecency'"
arch=('any')
url="https://github.com/rupa/z/"
license=('WTFPL')
depends=('bash')
source=("$pkgname-$pkgver.tar.gz::https://codeload.github.com/rupa/$pkgname/tar.gz/v$pkgver")
sha256sums=('7d8695f2f5af6805f0db231e6ed571899b8b375936a8bfca81a522b7082b574e')
install=z.install

package() {
	cd "$srcdir/$pkgname-$pkgver"

	# update path in manpage
	sed -i z.1 -e "s:/path/to/z.sh:/usr/share/$pkgname/z.sh:g"
	install -Dm 644 z.1 "$pkgdir/usr/share/man/man1/z.1"

	install -Dm 644 z.sh "$pkgdir/usr/share/$pkgname/z.sh"
	install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
