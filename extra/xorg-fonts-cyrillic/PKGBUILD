# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-cyrillic
pkgver=1.0.3
pkgrel=7
pkgdesc="X.org cyrillic fonts"
arch=(any)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-alias-cyrillic')
makedepends=('xorg-util-macros' 'xorg-mkfontscale' 'xorg-bdftopcf')
source=(${url}/releases/individual/font/font-cronyx-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-screen-cyrillic-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-winitzki-cyrillic-${pkgver}.tar.bz2)
md5sums=('e452b94b59b9cfd49110bb49b6267fba'
         '96109d0890ad2b6b0e948525ebb0aba8'
         '6f3fdcf2454bf08128a651914b7948ca'
         '829a3159389b7f96f629e5388bfee67b')
sha256sums=('6e8631936157677c77ba032b5c7b1fb3cb2ee872dbcea0444f12cd602cd9212a'
            'e40fe3e3323c62b738550795457ad555c70c008aa91b5912dfd46f8e745f5e60'
            '824231e8dffe15299454e47259f29d98001c9cf8ad3d6b5171399e4d71705e79'
            'abd13b63d02fcaec488686c23683e5cf640b43bd32f8ca22eeae6f84df0a36a0')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
		      --with-fontdir=/usr/share/fonts/cyrillic
      make
      popd
    fi
  done
}

package() {
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi    
  done
  rm -f "${pkgdir}"/usr/share/fonts/*/fonts.*
}
