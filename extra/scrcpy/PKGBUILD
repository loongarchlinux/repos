# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=2.1.1
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(loong64 x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('3d1ec3829cc08d36a6b42f7429749000ea5a4aaf2b8501167fe03b68fb722ec9351da9f287aef752fbceebf04db1484071c4fdf3b84bc5d9eb3805f635502951'
        '6a089ca33025873f48d62389b17ba6fb6249c007ef7d7fd21883351e7724c3207dce1cea8b655cb949361d26210f5bb6115a2f5f9895c8a3b129652a77848c3d')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-v$pkgver \
    --buildtype release
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
