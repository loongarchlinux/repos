# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kommit
pkgver=1.0.2
pkgrel=2
pkgdesc='Git gui client'
arch=(loong64 x86_64)
url='https://apps.kde.org/kommit'
license=(GPL3)
depends=(gcc-libs
         git
         glibc
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         ki18n5
         kio5
         kparts5
         kservice5
         ktexteditor5
         kwidgetsaddons5
         kxmlgui5
         qt5-base
         syntax-highlighting5)
makedepends=(extra-cmake-modules
             kdoctools5)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1e8ecad23aa10508d0685a2e9169fc562e12fad5a6adaacd4542d9007a68e96e'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D') # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
