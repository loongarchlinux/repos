# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-type1
pkgver=7.7
pkgrel=7
pkgdesc="X.org Type1 fonts"
arch=(any)
license=('custom')
url="https://xorg.freedesktop.org/"
makedepends=('xorg-util-macros' 'xorg-mkfontscale')
source=(${url}/releases/individual/font/font-adobe-utopia-type1-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-bh-type1-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bitstream-type1-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-ibm-type1-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-xfree86-type1-1.0.4.tar.bz2)
md5sums=('fcf24554c348df3c689b91596d7f9971'
         '53ed9a42388b7ebb689bdfc374f96a22'
         '5e0c9895d69d2632e2170114f8283c11'
         'bfb2593d2102585f45daa960f43cb3c4'
         '3eeb3fb44690b477d510bbd8f86cf5aa')
sha256sums=('979435105f897a70f8993fa02c8362160b0513366c2ab896965416f96dbb8077'
            '761455a297486f3927a85d919b5c948d1d324181d4bea6c95d542504b68a63c1'
            'c6ea0569adad2c577f140328dc3302e729cb1b1ea90cd0025caf380625f8a688'
            'fddb28d3db5a07f4b4ca15388488a9680a10e1367a18f358f903b2a608a5d2df'
            'caebf42aec7be7f3bd40e0f232d6f34881b853dc84acfcdf7458358701fbe34a')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
        --with-fontdir=/usr/share/fonts/Type1
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/Type1/fonts.*
}
