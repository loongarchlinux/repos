# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=polysemy
pkgname=haskell-polysemy
pkgver=1.9.1.2
pkgrel=4
pkgdesc="Higher-order, low-boilerplate, zero-cost free monads"
url="https://github.com/isovector/polysemy"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-first-class-families' 'haskell-syb'
         'haskell-th-abstraction' 'haskell-type-errors' 'haskell-unagi-chan')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-hspec-discover' 'haskell-doctest' 'haskell-hspec'
             'haskell-inspection-testing')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('a1b3ab74acc2708bcce106fa6b386b382ac58825058bf9348b119702c9ca78c901516d2b2367c2b4733179b0ee6b97d3a2575b1402af9c0c7f24167efcc10a4b')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
