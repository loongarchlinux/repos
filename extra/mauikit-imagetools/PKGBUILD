# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-imagetools
pkgver=3.0.2
pkgrel=1
pkgdesc='MauiKit Image Tools Components'
url='https://mauikit.org/'
arch=(loong64 x86_64)
license=(LGPL)
depends=(mauikit kquickimageeditor5 qt5-location exiv2 tesseract)
makedepends=(extra-cmake-modules opencv)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        leptonica-1.83.patch)
sha256sums=('545f733016a6fa84151857a1712a9a2d35ef5c67038a7acc45980e568fae3ef7'
            'ffc6454feb8d2c11fcd83ab5323487d163e35bf37e43b5f3ff9f436a93fbb4d1')

prepare() {
  patch -d $pkgname-$pkgver -p1 < leptonica-1.83.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
