# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit-imagetools
pkgver=3.0.0
pkgrel=2
pkgdesc='MauiKit Image Tools Components'
url='https://mauikit.org/'
arch=(loong64 x86_64)
license=(LGPL)
depends=(mauikit kquickimageeditor qt5-location exiv2 tesseract)
makedepends=(extra-cmake-modules opencv)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        leptonica-1.83.patch
        exiv2-0.28.patch)
sha256sums=('2d3b7ad6a611c03a29db0d20515294bb5cc0dbc6104ac1181f3e7674a6694852'
            'ffc6454feb8d2c11fcd83ab5323487d163e35bf37e43b5f3ff9f436a93fbb4d1'
            '9302cc933ad5e1b4d92184fb52d34c5a27b423536185cdc1a9c9e3811c5ba931')

prepare() {
  patch -d $pkgname-$pkgver -p1 < leptonica-1.83.patch
  patch -d $pkgname-$pkgver -p1 < exiv2-0.28.patch # Fix build with exiv2 0.28
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
