# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gsasl
pkgver=1.8.1
pkgrel=2
pkgdesc="Simple Authentication and Security Layer framework and a few common SASL mechanisms"
arch=('x86_64')
url="https://josefsson.org/gsasl/"
license=('GPL')
depends=('gnutls' 'libidn' 'krb5' 'libgcrypt')
source=(http://download.savannah.nongnu.org/releases/gsasl/$pkgname-$pkgver.tar.gz{,.sig})
sha1sums=('82ba0079da6918784a8170d4a13ee133d9df1d7a'
          'SKIP')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE')  # Simon Josefsson <simon@josefsson.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-gssapi-impl=mit
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
