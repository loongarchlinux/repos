# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rauc
pkgver=1.10
pkgrel=2
pkgdesc="Safe and secure software updates for embedded Linux"
arch=(loong64 x86_64)
url="https://rauc.io"
license=(LGPL2.1)
depends=(
  gcc-libs
  glibc
  libnl
  openssl
  sh
)
makedepends=(
  curl
  dbus
  glib2
  json-glib
  python
  systemd
  util-linux
)
checkdepends=(squashfs-tools)
optdepends=(
  'casync: for casync conversion support'
  'cryptsetup: for disk encryption support'
  'dosfstools: for creating and checking FAT filesystems'
  'e2fsprogs: for creating and checking ext2/3/4 filesystems'
  'efibootmgr: for interacting with EFI boot manager'
  'grub: for GRUB support'
  'mtd-utils: for interacting with MTD devices'
  'squashfs-tools: for creating and inspecting bundles'
  'uboot-tools: for u-boot support'
)
source=(
  https://github.com/rauc/rauc/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
  $pkgname-1.10-efibootmgr_output.patch
)
sha512sums=('b87e3fef41a4e5c44513bae4366502f0285d0b0136c80fcdede36171710735a26dacecf324a8638feb23c1b60929fc25a53a58213294a93e488827aed3cba926'
            'SKIP'
            '3ad8bfe337b76b63e96a4d230203a84de500a307f0670b0e28f7bbba976b92203ae44069a7986f5a956cb6eeaee47c52e8c1e4518b0c99d279815d2ef14ced35')
b2sums=('389f29b3786c20ca26d28c39642e72895bfd5b5f88958facfe2b65796ecb4d18645361dd7d490df23b0561878f020fdf8ffbcb41efd4fa9226190a79bc9e01db'
        'SKIP'
        'b7f463e074b8fbcdb4ed4899ccc1d27758c0206977c31675aa8f1946ce92cb01559027b508ee1dea94a6dbe92fe6d19fc15109027283a48fae2e443773ee209b')
validpgpkeys=("977843FD5C3EBF76BAD4008EA58CC53DBF05E090")  # Jan LÃ¼bbe <jlu@pengutronix.de>

prepare() {
  # fix issues with efibootmgr >= 18 output: https://github.com/rauc/rauc/pull/1197
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.10-efibootmgr_output.patch

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --libexecdir=/usr/lib
    --enable-gpt
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    curl libcurl.so
    glib2 lib{gobject,gio,glib}-2.0.so
    json-glib libjson-glib-1.0.so
    util-linux-libs libfdisk.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vdm 755 "$pkgdir/etc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/contrib/{grub.conf,uboot.sh} -t "$pkgdir/usr/share/doc/$pkgname/"
}
