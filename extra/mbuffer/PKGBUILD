# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20231216
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(loong64 x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(https://www.maier-komor.de/software/mbuffer/mbuffer-$pkgver.tgz)
sha512sums=('24cd600127a1b06ed13600b050b45d2563beacaf97b3b78c6d70bf735b084209a048ae732aceb18561c0d01c72afef383d3245820991fa3410a5193e7eb493fe')
b2sums=('d077ecc77f9d772a11a11f83df1f0f5ad91b06b8845d2ab1b075649ab2efdba3d9cfa4c7053142d6b8fb3b31d63b02052d810b4e5f817d33fa6e79c858ae834b')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
