# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20240107
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(loong64 x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz)
sha512sums=('14773c1193ed5e4a0dfda4ef6bd6ccb0cf104f8c4ae1d22747e52e8247aeb1db64656700f9cf317b13140fb649db889831233f0b8fef0a8463d28713b5882977')
b2sums=('9fb8d1ead6424e676f6ec0dd591cdd48b53a5b1b4a980e23101ae1e52a0f2a0d07048208f8ae6c45aee66f963b7ed9720b47b16b04495bcba97489a87d8c8d8f')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
