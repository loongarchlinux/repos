# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <bytecurry.software@gmail.com>

pkgname=roswell
pkgver=23.10.14.114
pkgrel=1
pkgdesc='Environment for Common Lisp development'
arch=('loong64' 'x86_64')
url='https://roswell.github.io'
license=('MIT')
depends=('glibc' 'bash' 'curl')
makedepends=('git')
_commit='0e7a2f1b6d9153f33edf1cc7b780825913a93618'
source=("$pkgname::git+https://github.com/roswell/roswell#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  ./bootstrap

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
