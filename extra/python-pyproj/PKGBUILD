# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pyproj
pkgver=3.6.0
pkgrel=1
pkgdesc="Python interfaces to PROJ.4 library"
arch=(loong64 x86_64)
url="https://github.com/pyproj4/pyproj"
license=(custom)
depends=(proj python)
makedepends=(python-setuptools cython)
checkdepends=(python-pytest python-certifi python-numpy python-pandas python-shapely python-xarray)
source=(https://github.com/pyproj4/pyproj/archive/$pkgver/$pkgname-$pkgver.tar.gz
        cython3.patch)
sha256sums=('fc336e4ce7998f6bbffc6d7238faec87e70e9693a26f14237418b972464144e0'
            'b12e1a4f61fa9b7b0f0cfdfd5212fe78fc6b25d764774badea010c2f373030b5')

prepare() {
  # Remove RPATH
  sed -i 's/library_dirs if os.name.*/None/' pyproj-$pkgver/setup.py
  # Fix build with cython 3
  patch -d pyproj-$pkgver -p1 < cython3.patch
}

build() {
  export PROJ_DIR=/usr
  cd pyproj-$pkgver
  python setup.py build
}

# Import issues, no time to investigate
#check() {
#  cd pyproj-$pkgver
#  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
#  PYTHONPATH="${PWD}"/build/lib.linux-$CARCH-${python_version} pytest -vv --color=yes
#}

package() {
  cd pyproj-$pkgver
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE_proj "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
