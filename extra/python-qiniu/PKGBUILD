# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.11.0
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('b66a872564bc57a15706968e833b1d0a1bed6a07a80a54de9a27f6588c7b05c1e07c041e7668d989dd2cbfd708765bb7145a05315caebbf48bd17cdcbdc62260')

build() {
  cd python-sdk-$pkgver
  python setup.py build
}

check() {
  cd python-sdk-$pkgver
  pytest || echo "Tests failed"
}

package() {
  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
