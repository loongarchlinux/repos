# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: DrZaius <lou at fakeoutdoorsman dot com>
# Contributor: Victor Feight <vrfeight3 at gmail dot com>

pkgname=gsimplecal
pkgver=2.5
pkgrel=1
pkgdesc="Simple and lightweight GTK calendar"
arch=('loong64' 'x86_64')
url="https://dmedvinsky.github.io/gsimplecal/"
license=('BSD')
depends=('gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dmedvinsky/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('f0be977de36fabc4da489698d2c06e64143572e53f166ab26e6055f6b36deae4')

build() {
	cd "${pkgname}-${pkgver}"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
