# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-pkcs11
pkgver=1.2.0
pkgrel=1
pkgdesc="PKCS#11 driver for NetHSM"
arch=(loong64 x86_64)
url="https://github.com/Nitrokey/nethsm-pkcs11"
license=(Apache-2.0)
depends=(glibc)
makedepends=(rust)
options=(!lto)  # NOTE: as ring is used we can not use LTO
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('ee471f73fc4eeae65989e4a6af18bead4b875f1779464c3af59bfb5f7a2a40381ecf80c3236839114d6783f00b5a055d83321d9577cddd88a5660d799c1654e4')
b2sums=('6ae341a64654ad6eaaad0423de39e1deb95075ee1058156d0d996f220987fca2484237ee06dfbe9494a1cadc07732b90ef88e8c03ba4a9c1cea247af86dc9c15')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/lib${pkgname/-/_}.so -t "$pkgdir/usr/lib/"
  install -vDm 644 {{README,features}.md,p11nethsm*.conf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vdm 700 "$pkgdir/etc/nitrokey/"
}
