# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.201
pkgrel=1
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(custom:SIL)
makedepends=(meson appstream-glib git)
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('b61f64e5f6a48aa0abc7a53cdcbce60de81908ca36048a64730978fcd9ac9863')

build() {
  arch-meson $pkgname-$pkgver build -D useprebuilt=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}

# vim:set ts=2 sw=2 et:
