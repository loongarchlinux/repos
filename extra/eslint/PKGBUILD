# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=8.41.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('npm' 'rsync')
options=('!emptydirs')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('ddc847f907676606c87e4c9efd59c511bef0ddb48190b2894809ada3c81eba3071bf7b9dfad7dc26a29fb33ef1757870a839b7bb205513545a53b164ec4a0d56')

prepare() {
  cd $pkgname-$pkgver
  npm install
}

check() {
  cd $pkgname-$pkgver
  node Makefile mocha
}

package() {
  cd $pkgname-$pkgver

  npm prune --production

  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/usr/bin
  ln -s $_npmdir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  install -Dt "$pkgdir"/$_npmdir/bin bin/$pkgname.js
  rsync -r --exclude .cache --exclude .package-lock.json conf lib messages \
    node_modules package.json "$pkgdir"/$_npmdir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Experimental dedup
  rm -r "$pkgdir"/$_npmdir/node_modules/acorn
}
