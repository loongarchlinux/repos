# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pep517
pkgver=0.12.0
pkgrel=4
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
depends=('python-tomli')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest-flake8' 'python-testpath' 'python-pip' 'python-flit-core')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('5af1fd2e35414d2a91104c967c48e554c9f838d598e14bc55f9a824c16ba484c817a90065b6734568fc77660bd85b309347392774fb4d54dd969dab8680768e7')

prepare() {
  # Copied from openSUSE: 
  # Remove what appears to be overly cautious flag
  # that causes tests to require internet, both here
  # and the test suites of any dependencies. Tracking at:
  # https://github.com/pypa/pep517/issues/101
  sed -i "s/ '--ignore-installed',//" pep517-$pkgver/pep517/envbuild.py
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build
}

check() {
  cd pep517-$pkgver
  # setup.py: https://github.com/pypa/pep517/issues/35
  # tests/test_meta.py::test_classic_package: error in pip install command
  pytest --deselect setup.py --deselect tests/test_meta.py::test_classic_package
}

package() {
  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
