# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kio
pkgname=${_name}5
pkgver=5.110.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=(loong64 x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid5 kjobwidgets5 kbookmarks5 libxslt kwallet5 ktextwidgets5 kded5)
makedepends=(extra-cmake-modules kdoctools5 doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)'
            'kdoctools5: for the help kioslave'
            'kio-fuse: to mount remote filesystems via FUSE'
            'switcheroo-control: hybrid GPU support')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
provides=($_name=$pkgver)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/kio/-/commit/48322f44.patch)
sha256sums=('228f9abcdfce1b23deacca97e9dd68d019e6b20607ddfe2295050333e90fc96b'
            'SKIP'
            'bff9696fc152c06fee2c4f21f1dc8a06c42745dbfe7fe88d47596bfb03d38220')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  patch -d $_name-$pkgver -p1 < 48322f44.patch # Fix crashes
}

build() {
  cmake -B build -S $_name-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
