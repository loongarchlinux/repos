# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-ca
pkgver=0.25.2
pkgrel=1
pkgdesc="A private certificate authority (X.509 & SSH) & ACME server for secure automated certificate management, so you can use TLS everywhere & SSO for SSH."
url="https://github.com/smallstep/certificates"
arch=(loong64 x86_64)
license=(Apache)
depends=(glibc pcsclite)
makedepends=(go)
source=("$url/releases/download/v${pkgver}/step-ca_${pkgver}.tar.gz")
sha512sums=('17ff7bc1dd8585791e35c3c07b4fdf1ef74cee01ad34acf6b3f695a000402b47bec64aad5e71d3f8b350c92f37dcbd5189a8672febf5a6d53a470c6dcd66b48c')

prepare(){
  mkdir -p bin
}

build() {
  BUILD_DATE="$(date --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" '+%Y-%m-%d %H:%M UTC')"
  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o bin ./cmd/...
}

check() {
  go test -v ./... || true
}

package() {
  install -Dm755 "bin/step-ca" "$pkgdir/usr/bin/step-ca"
}
