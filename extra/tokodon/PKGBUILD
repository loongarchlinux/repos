# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=tokodon
pkgver=23.04.2
pkgrel=1
pkgdesc='A Mastodon client for Plasma'
url='https://apps.kde.org/tokodon/'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(qqc2-desktop-style kirigami-addons kdbusaddons knotifications kitemmodels kio qtkeychain-qt5 qt5-websockets)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ccfc4fcc4d4d99e1ebb3347b59c4da1403d6a41c12023639f58b64fdb14862af'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
