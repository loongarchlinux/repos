# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-database-knotinfo
_pyname=database_knotinfo
pkgver=2023.12.1
pkgrel=1
arch=(any)
pkgdesc='Snapshot of the complete content of the KnotInfo and LinkInfo databases for Python'
url='https://github.com/soehms/database_knotinfo'
depends=(python)
makedepends=(python-build
             python-installer
             python-setuptools-scm
             python-wheel)
license=(GPL)
source=(https://pypi.io/packages/source/d/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('ed777d0181df05d4080d5e788bf04fa1e6e8a05979f8ba46031143c583bc16a0')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
