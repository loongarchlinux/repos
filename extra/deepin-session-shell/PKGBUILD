# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=5.5.93
pkgrel=1
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
# qt5-tools: for qdbus executable
depends=('deepin-daemon' 'deepin-network-core' 'deepin-qt5integration' 'deepin-wallpapers'
         'gsettings-qt' 'liblightdm-qt5' 'lightdm' 'qt5-tools' 'startdde' 'kwayland'
         'deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'dtkwidget' 'libx11' 'openssl' 'libxcursor'
         'qt5-svg' 'qt5-base' 'libxi' 'dconf' 'qt5-base' 'libxtst' 'glibc' 'libxfixes' 'pam'
         'gcc-libs' 'libxrandr' 'libxcb' 'qt5-x11extras')
makedepends=('cmake' 'gmock' 'gtest' 'ninja')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        fix-dtk-linking.patch)
sha512sums=('84867d20ce329868251b4828aad597a3d3e2c12f4c0a9eb2e4203e5d2c56abc7c839f9dc7bc9dc0df94f2486d096627cbf859d11d83c0d69271afec921108b5b'
            '77265da51f5c8eb55589d0ba11fa4e216404b821bd95126669b0bc4ad7c47e06711e1ad4637a0ed58f45553a837e2d582341c66fb17ba026ef0bd1d6af09b765')

prepare() {
  cd dde-session-shell-$pkgver
  patch -p1 -i ../fix-dtk-linking.patch
  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/deepinauthframework.cpp
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-session-shell-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
