# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rinutils
pkgver=0.10.0
pkgrel=1
pkgdesc='C11 / gnu11 utilities C library'
arch=(any)
url='https://www.shlomifish.org/open-source/projects/'
license=(MIT)
depends=()
makedepends=(cmake)
source=(https://github.com/shlomif/rinutils/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('70d89f0a8464f8f494f33704b7c9396e7fca392e8aafaa4465710c1a389e9806')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TEST_SUITE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
