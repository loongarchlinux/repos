# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip
pkgver=6.7.10
pkgrel=1
arch=(loong64 x86_64)
pkgdesc='A tool that makes it easy to create Python bindings for C and C++ libraries'
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-toml python-packaging python-setuptools python-ply)
makedepends=(python-build python-installer python-wheel)
source=(https://pypi.python.org/packages/source/s/sip/$pkgname-$pkgver.tar.gz)
sha256sums=('398aeb84ee03f3a953947cac70e60d3b02dceba3c4f4dd46c5383e9dbe3936bb')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
