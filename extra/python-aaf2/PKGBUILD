# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-aaf2
pkgver=1.7.0
pkgrel=1
pkgdesc='Read and write Advanced Authoring Format (AAF) files'
url='http://pyaaf.readthedocs.io/'
license=(MIT)
arch=(any)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://pypi.python.org/packages/source/p/pyaaf2/pyaaf2-$pkgver.tar.gz)
sha256sums=('4d37dd8a2591e45a865ec6b0820d875d1dd48947b9604463fc9ec57b2c901a10')

build() {
  cd pyaaf2-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd pyaaf2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
