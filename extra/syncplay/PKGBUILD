# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexandria Pettit <alxpettit@gmail.com>
# Contributor: Jorge Israel Pe√±a <jorge.israel.p@gmail.com>
# Contributor: mrAppleXZ <mr.applexz@gmail.com>

pkgname=syncplay
pkgver=1.7.1
pkgrel=2
pkgdesc='Synchronize watching movies on mplayer2, vlc, mpv, and mpc-hc across many computers'
arch=(any)
url=http://syncplay.pl
_url="https://github.com/Syncplay/$pkgname"
license=(Apache)
depends=(python
         python-twisted)
optdepends=('pyside2: GUI'
            'python-service-identity: TLS'
            'python-certifi: TLS'
            'python-pem: TLS'
            'mplayer2: media player'
            'mpv: media player'
            'vlc: media player')
backup=("etc/$pkgname/server.conf")
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz"
        "$pkgname.service"
        "$pkgname.conf")
sha256sums=('f335787ff1d94cc990cdcec94f12f39a2068e5bba31db7dbac9ab42e1661d900'
            'bbbf8f9dfc36631b49ba7d0fe3f980066b30a544954680a677e5e8629b7e0a8b'
            'df3c7656024d60c59664c79f4890f7780a2c5b8b537ac61b017e16b3d4420808')

package() {
	install -Dm0644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
	install -Dm0644 "$pkgname.conf" "$pkgdir/etc/$pkgname/server.conf"
	cd "$_archive"
	make PREFIX=/usr DESTDIR="$pkgdir" install
}
