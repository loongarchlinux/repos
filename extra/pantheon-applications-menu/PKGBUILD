# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=2.11.1
pkgrel=2
pkgdesc='The Pantheon Application Menu'
arch=(loong64 x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL3)
groups=(pantheon)
depends=(
  appstream
  gdk-pixbuf2
  glib2
  gnome-menus
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libsoup
  libswitchboard-2.0.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=98bf930d5ab9f18411da0f397304137730211706
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-applications-menu
  git describe --tags
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
