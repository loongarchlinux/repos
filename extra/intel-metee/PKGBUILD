# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-metee
pkgname=(intel-metee intel-metee-doc)
pkgver=3.2.4
pkgrel=1
pkgdesc="Access library for Intel CSME HECI interface"
arch=(loong64 x86_64)
url="https://github.com/intel/metee/"
license=(Apache)
makedepends=(cmake doxygen)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('af6581beeab660a5519c3cfc751f2bc7a78eca376e43637568991a26bd66ca94')

build() {
  cmake -B build -S metee-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
}

package_intel-metee() {
  depends=(glibc)
  
  DESTDIR="${pkgdir}" cmake --install build
  mv "${pkgdir}"/usr/share/doc .
}

package_intel-metee-doc() {
  pkgdesc+=' (documentation)'
    
  install -dm755 "${pkgdir}"/usr/share
  mv doc "${pkgdir}"/usr/share
}
