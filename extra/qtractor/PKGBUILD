# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.38
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=(loong64 x86_64)
url="https://qtractor.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libmad
  libxcb
  qt6-base
  qt6-svg
  zlib
)
makedepends=(
  alsa-lib
  aubio
  clap
  cmake
  dssi
  jack
  ladspa
  liblo
  libogg
  libsamplerate
  libsndfile
  libvorbis
  lilv
  lv2
  qt6-tools
  rubberband
  suil
  vst3sdk
)
optdepends=(
  'new-session-manager: for session management'
  'qt6-wayland: for native wayland support'
)
provides=(
  clap-host
  dssi-host
  ladspa-host
  lv2-host
  vst-host
  vst3-host
)
source=(https://github.com/rncbc/$pkgname/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('bb9df485e552e5047a2b3fd14d71195358c6fa80df3a5ef5cd1124ff0aa730d681ce629341627d08f7e2f5c85c845a182cd72b69a136041625682e50f00a8b17')
b2sums=('33cb59c84ce3663c28a32b8b13dd941583f15af62ced88df09ee009f4ccb99c9264e7affed2c02cce8189327d3d1a0dd7ad6f2a2c84b3a440a029b7ed991784b')

prepare() {
  # symlink vst3sdk into place
  rm -frv $pkgname-${pkgname}_${pkgver//./_}/src/vst3
  ln -fsv /usr/src/vst3sdk $pkgname-${pkgname}_${pkgver//./_}/src/vst3
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -D CONFIG_LV2_UI_GTK2=OFF
    -D CONFIG_LV2_UI_GTKMM2=OFF
    -S $pkgname-${pkgname}_${pkgver//./_}
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    aubio libaubio.so
    jack libjack.so
    liblo liblo.so
    libogg libogg.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    libvorbis libvorbisenc.so libvorbisfile.so libvorbis.so
    lilv liblilv-0.so
    rubberband librubberband.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{README,README.VST} -t "$pkgdir/usr/share/doc/$pkgname/"
}
