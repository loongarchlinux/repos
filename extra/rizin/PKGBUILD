# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.6.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('loong64' 'x86_64')
license=('GPL3' 'LGPL3')
depends=(
  sh
  capstone libcapstone.so
  openssl
  libzip libzip.so
  zlib
  lz4
  xz
  xxhash libxxhash.so
  file libmagic.so
  tree-sitter libtree-sitter.so
  libmspack libmspack.so
)
makedepends=('meson' 'ninja' 'python' 'python-yaml')
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('56ed3c9f7e4e689dfc27461b5a2de9cc6d14fec2e4cc17af6a011975fbecd4f2ec44e8dd26f0ce8f99a448b75514f6083177acfd073182bbdf921bc264fa9561')
b2sums=('d641279505be2f58b1874d431922066e09affef20e18025c0d3781dbe1fad8f12db53335464ad645344daa25a0ed61fa842fa96bcca6c4832b359b424b5f09a3')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v4 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
