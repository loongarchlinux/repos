# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-connection_pool
pkgver=2.3.0
pkgrel=1
pkgdesc='Generic connection pool for Ruby'
arch=(any)
url='https://github.com/mperham/connection_pool'
license=(MIT)
depends=(ruby)
makedepends=(ruby-minitest ruby-rake)
options=(!emptydirs)
source=(https://github.com/mperham/connection_pool/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3f542341437394cc44e7d5e8310f59277388a304590c7b926d4618958a2f2873')

prepare() {
  cd connection_pool-$pkgver
  sed '/standard/d' -i Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd connection_pool-$pkgver
  gem build connection_pool.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    connection_pool-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd connection_pool-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake test
}

package() {
  cd connection_pool-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
