# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=faad2
pkgver=2.9.2
pkgrel=1
pkgdesc="ISO AAC audio decoder"
arch=('x86_64')
url="https://github.com/knik0/faad2"
license=('GPL2')
depends=('glibc')
provides=('faad' 'libfaad_drm.so' 'libfaad.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/knik0/faad2/archive/${pkgver//./_}.tar.gz)
sha256sums=('9728b2b94f94ab49363cd229e7377fbe6ef3c311c5cd80da662d53ddb0ee091d')

prepare() {
  mv -v "${pkgname}-${pkgver//./_}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README*,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
