# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
# NOTE: add the following packages to rebuild TODOs on soname change (although they do not link against libgit2.so):
# julia
pkgver=1.7.0
pkgrel=3
epoch=1
pkgdesc="A linkable library for Git"
arch=(loong64 x86_64)
url="https://github.com/libgit2/libgit2"
license=(GPL2)
depends=(
  gcc-libs
  glibc
  http-parser
  zlib
)
makedepends=(
  cmake
  libssh2
  openssl
  pcre2
  python
)
provides=(libgit2.so)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname-1.7.0-git_oidarray_include.patch
  $pkgname-1.7.0-cleanup_grafts.patch::https://github.com/libgit2/libgit2/commit/9d4c550564ee254dda9e2620c4c1e32ebb529728.patch
)
sha512sums=('68c8ed289de7daccaec17ea2ac49f4610325595cf90cddef763a31546a0a1c6bd400bf6180b68e2d3a8bdc3d031328efbbbaf3b61467dfc1b944e8cf3efcfd69'
            'c39992e337da29038784976ebcc37ee246870042c0342a4baa06ad0542df8bc20fcd8ad45ab4c45ab8074ce5905ae9d963d51a16f870afb59e8f6b71a357d4a5'
            '0ec0580e08df3f08d8609b35f91eba9b665fdf2ad1eb6ca68b0745fbf7b86c344bef370b325211892ac6f5040e147241808994edbc10ff3822363c800857eb93')
b2sums=('3382efb1f82e5598eae9f72b145fc8cc19876d925ae94513e42e5e8bedfbb923387d3c5d4c80f1333fe6b07e5d96866d4d0776b2190a50f6929862f943815d8f'
        '6b2cc62400816851c0e0b7b491aa78bb73fa8f15d37a31e816745a53c2a1a3d96fc724b5c9b050d2c4af7f31fc4856aa31c26c7b7a63525d008bb0a6d1bfdabf'
        'eb204485404ce55c0881a41b946c58b70e1814ee17e8accea786e802d4c8c3a069d63886615433f278277a8b4c67d9a3a6125fea78dafda4cbad3b5204f29245')

prepare() {
  # add missing include needed when registering custom transports: https://github.com/libgit2/libgit2/issues/6607
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.7.0-git_oidarray_include.patch
  # make reuse of grafts "safe"
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.7.0-cleanup_grafts.patch
}

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DREGEX_BACKEND=pcre2
    -DUSE_HTTP_PARSER=system
    -DUSE_SSH=ON
    -S $pkgname-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  local ignored_tests=(
    'invasive|'
    'online|'
    'ssh|'
    'network_url_parse__hostname_implied_root_empty_port|'
    'network_url_parse__hostname_empty_port|'
    'network_url_parse__ipv4_implied_root_empty_port|'
    'network_url_parse__ipv4_empty_port|'
    'network_url_parse__ipv6_implied_root_empty_port|'
    'network_url_parse__ipv6_empty_port'
  )
  local ifs="$IFS"
  IFS=
  # NOTE: disable tests requiring the internet, relying on non-existent
  # resources, or those that are only compatible with the (modified) vendored
  # version of http-parser, but not with upstream http-parser
  ctest --test-dir build --output-on-failure -E "${ignored_tests[*]}"
  IFS="$ifs"
}

package() {
  depends+=(
    libssh2 libssh2.so
    openssl libcrypto.so libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
