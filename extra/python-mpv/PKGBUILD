# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>
# Contributor: Ekin Dursun <ekindursun@gmail.com>

pkgname=python-mpv
pkgver=1.0.4
pkgrel=1
pkgdesc='Python interface to the awesome mpv media player'
arch=('any')
url=https://github.com/jaseg/python-mpv
license=('GPL' 'LGPL')
depends=('mpv' 'python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest' 'python-pyvirtualdisplay' 'xorg-server-xvfb')
optdepends=('python-pillow: for raw screenshot support')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('cb3b9042648ffd0472093f84116084f255893ab7068379a01011e8153da9c60c525c9b701e8f0be29ebbfaca9da9b4fd0024741e7b243b13c3b9e69f597dede8')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.rst
}
