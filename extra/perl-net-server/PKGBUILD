# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-net-server
pkgver=2.013
pkgrel=1
pkgdesc='Extensible, general Perl server engine'
arch=('any')
url="https://metacpan.org/release/Net-Server"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-$pkgver.tar.gz")
sha512sums=('0d86474911f92f87e91c8dc37e1bb589e7ce0c81df18fef46c38bda6c7e4970fd85f1c0ec178fba97755383704ef9215b49d92fa5f543b8e270c0fe2791ad59e')

build() {
  cd Net-Server-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-Server-$pkgver
  make test
}

package() {
  cd Net-Server-$pkgver
  make install DESTDIR="$pkgdir"
}
