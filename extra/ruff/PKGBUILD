# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=ruff
pkgname=($pkgbase python-$pkgbase)
pkgver=0.1.9
pkgrel=1
pkgdesc='An extremely fast Python linter, written in Rust'
arch=(loong64 x86_64)
url="https://github.com/astral-sh/$pkgbase"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  maturin
  python-installer
)
options=(!lto)
_archive="$pkgbase-$pkgver"
source=($url/archive/refs/tags/v$pkgver/$_archive.tar.gz
        $url/commit/a3e06e5a.patch) # https://github.com/astral-sh/ruff/issues/9234
sha512sums=('1dde9a28bd6dd95eb300d31250fe165e3057d3f4f0b25aa7118122ec4abd49ddf0844863e7073dd9b3eb55b03f1d2b7155b39b6c8e4a51ed5c3e23d2f4366659'
            '26c845d05ed337f393a2f586711d51416c9385797db5a64f977e2762eda9c4136889499ed2141264e0e10435a2cfdaeccd4f14c2bf7867c69caeef4fd56c4e19')
b2sums=('b6e70f1eeddae0e01c2eca0fa9c840a73b9a30080ced3e301cff7cf651daf8fd03a10de354e0e8b4cd2fa0bedaf62a65a45d91af1dbf25d0cc2c8ae643d07447'
        'b168c5e04cd2509c1d68b48d151ca458c9683d07141701e652a20fd0ee4225915eec0bff39d21a78461623e86b85dbbdba1ec8c52f232d3662daa7144f51e8ca')

prepare() {
  cd "$_archive"
  patch -p1 -i ../a3e06e5a.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_archive"
  maturin build --locked --release --all-features --target "$CARCH-unknown-linux-gnu" --strip
}

check() {
  cd "$_archive"
  cargo test -p ruff_cli --frozen --all-features
}

_package_common() {
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

package_ruff() {
  cd "$_archive"
  _package_common
  local _target="target/$CARCH-unknown-linux-gnu/release/ruff"
  install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
  $_target --generate-shell-completion bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgbase.bash"
  $_target --generate-shell-completion fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgbase.fish"
  $_target --generate-shell-completion zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_python-ruff() {
  cd "$_archive"
  _package_common
  depends=(python "$pkgbase")
  python -m installer -d "$pkgdir" target/wheels/*.whl
  rm -rf "$pkgdir/usr/bin"
}
