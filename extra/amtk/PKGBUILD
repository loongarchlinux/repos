# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=amtk
pkgver=5.6.1
pkgrel=2
pkgdesc='Actions, Menus and Toolbars Kit for GTK applications'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/Archive/amtk'
license=('LGPL3')
depends=('glib2' 'glibc' 'gtk3')
makedepends=('meson' 'gobject-introspection' 'gtk-doc')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('d50115b85c872aac296934b5ee726a3fa156c6f5ad96d27e0edd0aa5ad173228')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
