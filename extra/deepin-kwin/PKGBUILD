# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.25.6
pkgrel=1
pkgdesc='DDE window manager forked from KWin'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL3')
depends=(qt5-declarative qt5-x11extras kconfig kconfigwidgets kcoreaddons kdbusaddons kdeclarative
         kglobalaccel ki18n kiconthemes kidletime knotifications kcrash kpackage plasma-framework
         kwidgetsaddons kwindowsystem kcompletion kcmutils knewstuff kservice ktextwidgets kxmlgui
         krunner kdecoration kscreenlocker dwayland xcb-util-cursor xorg-xwayland libpipewire
         breeze lcms2 libqaccessibilityclient qt5-quickcontrols python fontconfig freetype2 libcap
         systemd-libs kauth bash kactivities libxkbcommon libxss libxi libinput xcb-util-wm wayland
         perl libxcb xcb-util-keysyms libx11 libxtst libdrm gcc-libs mesa qt5-base)
makedepends=(extra-cmake-modules kdoctools ninja qt5-tools wayland-protocols
             deepin-wayland-protocols)
source=("https://github.com/linuxdeepin/deepin-kwin/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('795634c4ee3053eebc5175b3e51cd3e10cb2a3f33640fc717c23c17840e43633d6e3eba771093e181fb6731f6799c696fdd8cf1f1597b3caf9636279caead61c')

prepare() {
  cd deepin-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp
}

build() {
  cd deepin-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd deepin-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install
}
