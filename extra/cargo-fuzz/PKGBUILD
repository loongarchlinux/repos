# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-fuzz
pkgver=0.11.4
pkgrel=1
pkgdesc="Command line helpers for fuzzing rust"
url="https://github.com/rust-fuzz/cargo-fuzz"
depends=('cargo')
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-fuzz/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('c06bd7c0e43332c4f9b761c60e7eb6f22a5a23b15ef9e549ecf3f8053d315300')
b2sums=('cf9ef2423e0409854721e1d5ac57a3059c075d00ca26c2897157a437bc4274b64e80a87b4843cfcff94f5e2ee4b03a93d90ab19c829693d12d4a01faaf40dc0e')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  # this is broken on linux-hardened
  #RUSTC_BOOTSTRAP=1 cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
