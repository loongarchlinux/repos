# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ccache
pkgver=4.8.1
pkgrel=1
pkgdesc='Compiler cache that speeds up recompilation by caching previous compilations'
url='https://ccache.dev/'
arch=('loong64' 'x86_64')
license=('GPL3')
depends=('glibc' 'gcc-libs' 'hiredis' 'zstd' 'libzstd.so')
makedepends=('cmake' 'asciidoctor' 'perl')
source=(https://github.com/ccache/ccache/releases/download/v${pkgver}/ccache-${pkgver}.tar.xz{,.asc}
        https://github.com/ccache/ccache/commit/e24cedf8.patch)
validpgpkeys=('5A939A71A46792CF57866A51996DDA075594ADB8') # Joel Rosdahl <joel@rosdahl.net>
sha512sums=('672971560a8aed7754c4280b8983fb185bc7f7f456651e952db3e32fb855ba430c7dcec1f45466e4d10aa59e80e30696f540d93b5f260a846a49dbf4901de514'
            'SKIP'
            '38c03e5005766715820d42fc4058189baba0f9a660bd10ef69525eb00e1c1948b416f25b42e61ab0891f82db376831837d03f82928949cc4123a2edd338a2c79')
b2sums=('01c676447aff5493f319df8360b5478b171a229dc47068bfda814bd30afeb28492a3b112532dbe93cf89d47796f4d682e37b22a1d1c31aaf46023e4f7682d80c'
        'SKIP'
        '2bda947a74611ce880bbe5ef07deecaf8fb8a7da461722eb27f5ee969a4b370021b48b5d01c082d79a523e71c623611aed2690e5a163be1f1ddddb8f249938ee')

prepare() {
# Fix test
  patch -d ${pkgname}-${pkgver} -p1 < e24cedf8.patch
}

build() {
  cd ${pkgname}-${pkgver}
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev \
    -B build \
    -S .
  make VERBOSE=1 -C build
}

check() {
  cd ${pkgname}-${pkgver}
  make VERBOSE=1 check -C build
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install -C build
  make DESTDIR="${pkgdir}" install -C build/doc

  install -Dm 644 doc/*.md doc/*.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -d "${pkgdir}/usr/lib/ccache/bin"
  local _prog
  for _prog in gcc g++ c++; do
    ln -s /usr/bin/ccache "${pkgdir}/usr/lib/ccache/bin/$_prog"
    ln -s /usr/bin/ccache "${pkgdir}/usr/lib/ccache/bin/${CHOST}-$_prog"
  done
  for _prog in cc clang clang++; do
    ln -s /usr/bin/ccache "${pkgdir}/usr/lib/ccache/bin/$_prog"
  done
}

# vim: ts=2 sw=2 et:
