# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdsingleapplication
pkgver=1.1.0
pkgrel=1
pkgdesc="KDAB's helper class for single-instance policy applications"
arch=(loong64 x86_64)
url='https://github.com/KDAB/KDSingleApplication'
license=(MIT)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake)
source=(https://github.com/KDAB/KDSingleApplication/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('31029fffa4873e2769c555668e8edaa6bd5721edbc445bff5e66cc6af3b9ed78'
            'SKIP')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products (user for KDAB products) <info@kdab.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDSingleApplication_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
