# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=liblouis
pkgver=3.25.0
pkgrel=3
pkgdesc='Braille translator and back-translator library'
arch=('loong64' 'x86_64')
url='https://liblouis.io/'
license=('LGPL3' 'GPL3')
depends=('glibc')
makedepends=('help2man' 'libyaml' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('libyaml: lou_checkyaml command'
            'python: Python bindings')
source=("https://github.com/liblouis/liblouis/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('d720aa5fcd51de925a28ae801b8b2ca76ee67e2360b40055c679bce8e565f251')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-ucs4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd python
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd python
  python -m installer --destdir="$pkgdir" dist/*.whl
}
