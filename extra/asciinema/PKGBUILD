# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcin Kulik <marcin@asciinema.org>

pkgname=asciinema
pkgver=2.3.0
pkgrel=1
pkgdesc='Record and share terminal sessions'
arch=(any)
url='https://asciinema.org/'
license=(GPL3)
depends=(python)
makedepends=(git python-setuptools python-wheel python-build python-installer)
source=('git+https://github.com/asciinema/asciinema#commit=791916d87d06b25ded09fefde33288d5c069c96d') # tag: v2.3.0
b2sums=(SKIP)

prepare() {
  echo -e '#!/bin/sh\n/usr/bin/python -m asciinema "$@"' > "$pkgname/asciinema.sh"
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  install -Dm755 -t "$pkgdir/usr/bin" asciinema.sh
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# getver: raw.githubusercontent.com/asciinema/asciinema/master/asciinema/__init__.py
