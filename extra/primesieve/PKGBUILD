# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=11.2
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(loong64 x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD)
depends=(gcc-libs
         glibc)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primesieve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('86c31bae9c378340b19669eafef8c5e45849adf7b9c92af1d212a2a2bfa0a5db')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
