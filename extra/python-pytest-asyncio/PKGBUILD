# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.23.4
_commit=b879130e3212884fde45f2ecfe48b41ce1eacce3
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages:$PYTHONPATH:$PWD/tests" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
