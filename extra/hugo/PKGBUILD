# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Christian Rebischke <christian.rebischke@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: fordprefect
# Contributor: Jonathon Fernyhough <jonathon_at_manjaro_dot_org>
# Contributor: Brenton Horne

pkgname=hugo
pkgver=0.122.0
pkgrel=1
pkgdesc="Fast and Flexible Static Site Generator in Go"
arch=('loong64' 'x86_64')
url="https://gohugo.io/"
license=('Apache-2.0')
depends=('glibc' 'gcc-libs')
makedepends=('go' 'git')
optdepends=('python-docutils: reStructuredText support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gohugoio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f2eac9b9b227b78647c8d9c5c48d409c8d056aaaf8e2523458f9c256fd49bd5b4f3392a80789133bf8603369ff3522783a7605a42f65f33070b26356070a51d1')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -tags extended

  ./hugo gen man
  ./hugo completion bash > "${pkgname}.bash-completion"
  ./hugo completion fish > "${pkgname}.fish"
  ./hugo completion zsh > "${pkgname}.zsh"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm 644 man/*.1 -t "${pkgdir}/usr/share/man/man1/"
  
  install -Dm 644 "${pkgname}.bash-completion" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm 644 "${pkgname}.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  install -Dm 644 "${pkgname}.zsh" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
