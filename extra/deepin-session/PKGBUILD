# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-session
pkgver=1.2.4
pkgrel=1
pkgdesc='New deepin session, based on systemd and existing projects'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-session"
license=('GPL3')
# deepin-session-ui: org.deepin.dde.Osd1
# deepin-kwin: dde-session@.service
# deepin-dock: dde-session-initialized.target.wants/dde-dock.service
depends=('glibc' 'gcc-libs' 'glib2' 'libx11' 'libsecret' 'qt5-base' 'gsettings-qt' 'dtkcore'
         'libxcursor' 'libxfixes' 'systemd-libs' 'deepin-session-ui' 'deepin-kwin' 'deepin-dock')
makedepends=('cmake' 'ninja')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-session/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b2b512ada599c1c85999cb4b0c1ba8bce595ad2d03457d80491e56fbaabda9e3988f91469397c9345cb920f64bcb561127ed5b12a433169fe544dc0da53c258d')

prepare(){
  cd dde-session-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' misc/Xsession.d/00deepin-dde-env
}

build() {
  cd dde-session-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd dde-session-$pkgver
  DESTDIR="$pkgdir" ninja install
}
