# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-warp-time
pkgver=0.7.2
pkgrel=2
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=(loong64 x86_64)
url="https://github.com/alerque/$pkgname"
license=(GPL-3.0-only)
depends=(gcc-libs
         glibc
         libgit2 libgit2.so)
makedepends=(cargo
             git
             jq)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.xz")
sha256sums=('8acada8b564e5a725640ecbc66051415b5bd3376c5f9cce4c7c6e83bce8e6b06')

prepare() {
	cd "$_archive"
	sed Makefile.am -i \
		-e 's/cargo \(build\|install\|test\)/cargo --offline \1/'
	autoreconf
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_srcenv() {
	cd "$_archive"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
}

build() {
	_srcenv
	CFLAGS+=' -ffat-lto-objects'
	./configure --prefix /usr --enable-shared
	make
}

check() {
	_srcenv
	make check
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
