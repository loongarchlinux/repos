# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.3.6
pkgrel=4
pkgdesc="Free, open source software for digital DJing"
arch=(loong64 x86_64)
url="https://www.mixxx.org"
license=(GPL-2.0)
groups=(pro-audio)
# TODO: package libshout-idjc
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  hidapi
  lame
  libkeyfinder
  libmad
  libmodplug
  libx11
  openssl
  opus
  opusfile
  qt5-base
  qt5-script
  qt5-svg
  qt5-x11extras
  qtkeychain
  soundtouch
  speex
  sqlite
  taglib
  upower
  wavpack
)
makedepends=(
  chromaprint
  cmake
  faad2
  ffmpeg
  flac
  glib2
  glu
  gperftools
  libid3tag
  libogg
  libsndfile
  libusb
  libvorbis
  lilv
  lv2
  qt5-tools
  portaudio
  portmidi
  protobuf
  rubberband
  vamp-plugin-sdk
)
checkdepends=(xorg-server-xvfb)
source=(https://github.com/${pkgname}dj/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
        taglib-2.patch)
sha512sums=('a92c2c92dc7619ac135d940f23ffdc101c528eca6322517805afb9eb48a84c6339cefb88b96bdac6ffb23967c2f93f621daf3e98bfecbf7f3fe7626ddcec4398'
            'ded7ebd30ee24da0e956a14201499a7e85e77ff898f500eeb999e843a02d4e693aa857a607dcf22112b2e2e808e54a64fc3d5199a3a0efa29010dfdac323a2b7')
b2sums=('35bedb5f41a56577bc59411ca979e3e780c1da2420bc0a7ad98d998c89f8c272202ba42e8b8089904ca8e1a1b6ac5155d08d556538245ce3242b4ea5a670e6d2'
        '3963647ae9514b713be9e083ce648fea4a2966c0368d61613124f6937ad3f603c0fb3e3bf471332eec60c61420b5388f092aaaaf164dbc73ded832ebff037b0a')

prepare() {
  patch -d $pkgname-$pkgver -p1 < taglib-2.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  xvfb-run ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    chromaprint libchromaprint.so
    flac libFLAC.so
    ffmpeg libavcodec.so libavformat.so libavutil.so
    glib2 libgobject-2.0.so
    libid3tag libid3tag.so
    libogg libogg.so
    libsndfile libsndfile.so
    libusb libusb-1.0.so
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    lilv liblilv-0.so
    portaudio libportaudio.so
    portmidi libportmidi.so
    protobuf libprotobuf-lite.so
    rubberband librubberband.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
