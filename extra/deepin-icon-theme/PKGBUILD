# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2024.01.15
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('88fb2776e2ebcbaf69bed408502ab66b1593547742a4518e580fd8979509fac9cd37ab8ebc88ddb9b8f3dd69510102dbecd00c091269d5e0ea66f69b35143bba')

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
