# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Sander Jansen <sander@knology.net>

pkgname=gogglesmm
pkgver=1.2.4
pkgrel=2
pkgdesc="Music Manager and Player"
arch=('loong64' 'x86_64')
url="https://gogglesmm.github.io/"
license=('GPL3')
depends=('libepoxy'
         'glu'
         'libsm'
         'libxcursor'
         'libxrandr'
         'libxi'
         'libxft'
         'libvorbis'
         'libwebp'
         'libjpeg-turbo'
         'openssl'
         'libmad'
         'flac'
         'faad2'
         'opus'
         'taglib>=1.9.0'
         'sqlite'
         'dbus'
         'hicolor-icon-theme')
optdepends=('libpulse: PulseAudio Output'
            'alsa-lib: ALSA Output')
makedepends=('cmake'
             'pkgconfig'
             'alsa-lib'
             'libpulse')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gogglesmm/gogglesmm/archive/$pkgver.tar.gz"
         taglib-2.patch)
sha512sums=('01fa9d3fab42dc8a319b489161e322b4d23a3f54fcf1ab3eb7683483ec49d31c47bae92cdcd2c7f853628db31ea7e1b03369617ff4bb20e318032922454af56e'
            '709690498d3aede02505ab9e4c8cb9482734a6eafba49308202f3533d1ac3b81e2a187d363d10c7b1b9c1a8d9b1e696da7de66936938f2e8c6ecf95765940622')

prepare() {
  cd "${pkgname}-${pkgver}"
# Fix build with taglib 2
  patch -p1 < ../taglib-2.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
