# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=6.5.3
pkgrel=1
pkgdesc='Switchboard Online Accounts Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL2)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk3
  json-glib
  libcamel-1.2.so
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libgee
  libgranite.so
  libhandy-1.so
  libswitchboard-2.0.so
  rest
  webkit2gtk
)
makedepends=(
  git
  meson
  vala
)
_tag=0e1416580fb13985dfce953b9559cb936a24f9b3
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-online-accounts
  git describe --tags
}

build() {
  arch-meson switchboard-plug-online-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
