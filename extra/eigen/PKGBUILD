# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Kringel

pkgname=eigen
pkgver=3.3.8
pkgrel=3
pkgdesc="Lightweight C++ template library for vector and matrix math, a.k.a. linear algebra."
arch=('any')
url='https://eigen.tuxfamily.org'
license=('GPL3' 'LGPL2.1' 'BSD' 'custom:MPL2' 'custom:MINPACK')
makedepends=('cmake' 'pkg-config' 'freeglut' 'gcc-fortran' 'fftw' 'suitesparse' 'boost')
source=(https://gitlab.com/libeigen/eigen/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
        eigen-bug2011.patch::"https://gitlab.com/libeigen/eigen/-/commit/ef3cc72c.patch"
        eigen-paradiso.patch::"https://gitlab.com/libeigen/eigen/-/commit/bfdd4a99.patch")
sha256sums=('146a480b8ed1fb6ac7cd33fec9eb5e8f8f62c3683b3f850094d9d5c35a92419a'
            'c04d624d550b119be0f810786baba7e0d7809edefd4854a2db6dbd98a7da5a7d'
            '14c239a5b6c1efe1c2e6f33fc9c449a500b74ded2e865ca730b213c142f1f750')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../eigen-bug2011.patch # https://gitlab.com/libeigen/eigen/-/issues/2011
  patch -d $pkgname-$pkgver -p1 -i ../eigen-paradiso.patch # Fix Paradiso
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # install custom licenses
  install -Dm644 $pkgname-$pkgver/COPYING.* -t "$pkgdir/usr/share/licenses/$pkgname"
}
