# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.12.0
pkgrel=2
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('loong64' 'x86_64')
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=('libfmt.so')
makedepends=('cmake')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz"
    "spdlog_fmt_external.patch"
    "spdlog_constexpr.patch"
)
provides=(
    'libspdlog.so'
)
sha256sums=('4dccf2d10f410c1e2feaff89966bfc49a1abb29ef6f08246335b110e001e09a9'
            'a0eb34b7c6920f0db2587460071f53372663c191cdfe34bf5ea2704c309c745f'
            'a2519d22ef312d5277b6b890ada5e6c5b5323cb34db618630d1d4e979c5f8722')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 <../spdlog_fmt_external.patch
    patch -p1 <../spdlog_constexpr.patch
}

build() {
    export CFLAGS+=" ${CPPFLAGS}"
    export CXXFLAGS+=" ${CPPFLAGS}"
    # FIXME: Stop -DSPDLOG_BUILD_TESTS=ON from downloading bundled catch2
    cmake -B build -S "$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DSPDLOG_BUILD_SHARED=ON \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev
    make -C build
}

# See FIXME above
#check() {
#    make -C build test
#}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
