# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.7.3
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=(any)
url="https://www.laborejo.org/fluajho/"
license=(GPL3)
groups=(pro-audio)
depends=(hicolor-icon-theme libcalfbox-lss python python-pyqt5 ttf-dejavu)
optdepends=('new-session-manager: Session management')
provides=(soundfont-synthesizer)
source=(https://www.laborejo.org/downloads/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('07724e56a88aac4686ee489ee5e6c52490d8df225db23f3f9b9b10ec385fbc2769b07a571b58f36669f3c88dbf0eb580211c369cb76c29676a1756c31536aef4'
            'SKIP')
b2sums=('855b0fbb9ec5d3f5cdbd6cb1f16ad1a0093633c7467352be615af3adbc38546ab3469cb76aa3512bc3dfed4f06986b71e01a385c56fc0458dab2f767317b6b6f'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
