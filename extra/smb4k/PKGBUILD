# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=3.2.3
pkgrel=2
pkgdesc='A KDE program that browses samba shares'
arch=(loong64 x86_64)
url='https://smb4k.sourceforge.net/'
license=(GPL)
depends=(gcc-libs
         glibc
         kauth5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         kdnssd5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         knotifications5
         kparts5
         kwallet5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         qt5-base
         qt5-declarative
         smbclient
         solid5)
makedepends=(extra-cmake-modules
             kdoctools5
             plasma-framework)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('2fe14e4cae1502ffb04cda578e5418e40fdadb5a753ecebfd4bfc378b5a70554')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
