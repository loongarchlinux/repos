# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=msolve
pkgver=0.5.0
pkgrel=1
pkgdesc='Library for polynomial system solving through algebraic methods'
url='https://msolve.lip6.fr/'
arch=(loong64 x86_64)
license=(GPL2)
depends=(flint)
makedepends=()
source=(https://github.com/algebraic-solving/msolve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('08daa2fc7447ba7074b28a4d263ef05fad42dca5b30310971948307ee3ed38d3')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
