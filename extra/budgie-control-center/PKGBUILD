# Maintainer: Campbell Jones <serebit@archlinux.org>

pkgname=budgie-control-center
pkgver=1.3.0
pkgrel=2
pkgdesc="Budgie's main interface to configure various aspects of the system"
arch=(loong64 x86_64)
url="https://github.com/BuddiesOfBudgie/budgie-control-center"
license=("GPL-2.0-or-later")
groups=("budgie")
depends=(accountsservice gnome-bluetooth gnome-online-accounts gnome-settings-daemon ibus
         libgtop libpwquality smbclient libcheese libnma udisks2 libhandy gsound colord-gtk)
makedepends=(docbook-xsl modemmanager meson)
optdepends=("system-config-printer: Printer settings"
            "gnome-user-share: WebDAV file sharing"
            "gnome-remote-desktop: Screen sharing"
            "rygel: Media sharing"
            "openssh: Remote login"
            "power-profiles-daemon: Power profiles support")
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
b2sums=("9cf5e9fe9bd11212298fed67afab2ef1b51ac21fee1f931dc1b724011be1e9349be0d767241dbe4f989d2c4497340260c5ab6b02d2c4bc7c153829961fa062c2"
            "SKIP")
validpgpkeys=("1E1FB0017C998A8AE2C498A6C2EAA8A26ADC59EE" # David Mohammed <fossfreedom at ubuntu dot com>
              "0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B") # Joshua Strobl <me at joshuastrobl dot com>

build() {
    arch-meson "$pkgname-$pkgver" build -Ddocumentation=true
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
