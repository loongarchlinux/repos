# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.23.1
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(
  python
  python-typing-extensions
  tmux
)
makedepends=(
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  procps-ng
)
optdepends=('python-pytest: for pytest plugin')
source=($_name-$pkgver.tar.gz::https://github.com/tmux-python/libtmux/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('7ec3cbda013d46c6d6313eb3a3c1b12e9dd099c2a2ff9f5cce2e80691fb49328ac98d49a3c9572f84ea27ccf3061882b7631ac02a985a071f75ab89706f97ca0')
b2sums=('0cdb7fbc7d6931e21a59134bb5bd17dcb0e75aa5fcc4e0a0e76aef81e3c2bb0891cb2c56c9b0a16836f6ffda25846df3bf7cf54cc74d6c459d4194bdab29dbfa')

prepare() {
  cd $_name-$pkgver
  # remove unknown pytest option: https://github.com/tmux-python/libtmux/issues/496
  sed 's/doctest-docutils-modules/doctest-modules/' -i setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
