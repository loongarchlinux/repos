# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.23.2
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(
  python
  python-typing-extensions
  tmux
)
makedepends=(
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  procps-ng
)
optdepends=('python-pytest: for pytest plugin')
source=($_name-$pkgver.tar.gz::https://github.com/tmux-python/libtmux/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('f8952cffa94fcf9b345275d3d5e59ea0a164bbcb93aacbd2820c8127b34526635d7b9a25f5684cd385744b1de626b8bce19d2c735579c2bdffed6a17c9f8f394')
b2sums=('a5e7aece4d7160f87c48c2c02371c6d6b423aad979d91683561b1d8a7f750c626b522af5d1d97157c536adfd612948750433e77faaeb633343af37648bc9bf01')

prepare() {
  cd $_name-$pkgver
  # remove unknown pytest option: https://github.com/tmux-python/libtmux/issues/496
  sed 's/doctest-docutils-modules/doctest-modules/' -i setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
