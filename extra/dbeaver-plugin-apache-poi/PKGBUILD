# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-apache-poi
pkgver=5.2.4
pkgrel=1
pkgdesc='DBeaver library for Microsoft Office documents'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.bundle.apache.poi_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('59061d440466b1869528adc1ee7c934421e9cae8008a8caa3801d34967ec3735'
            '5a280fb2b234f76842c3e3a41d08cfecb0f159cf5ca322b3f0001755e7093150')
noextract=("org.jkiss.bundle.apache.poi_${pkgver}.jar")

package() {
  # Extract plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.bundle.apache.poi_${pkgver}.jar" \
        -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
