# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Thayne McCombs <astrothayne at gmail dot com>

pkgname=wev
pkgver=1.0.0
pkgrel=13
pkgdesc="A tool for debugging wayland events on a Wayland window, analagous to the X11 tool xev"
url='https://git.sr.ht/~sircmpwn/wev'
license=('custom')
arch=('loong64' 'x86_64')
depends=('wayland' 'libxkbcommon')
makedepends=('scdoc' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~sircmpwn/wev/archive/$pkgver.tar.gz")
b2sums=('e777cf1f19a5ad051b81ccd878067ba043c5121f460aed26fa41c999812d9c52e30837cbcb72351de4e532f0427cb2e8f707ca1ddaa09d992cdff3c422638ec2')

build() {
    cd "$pkgname-$pkgver"
    # LDFLAGS are not respected
    make CFLAGS="$CFLAGS $LDFLAGS"
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" PREFIX=/usr MANDIR=/usr/share/man install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
