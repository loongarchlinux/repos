# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=('python-jsbeautifier' 'python-cssbeautifier')
pkgbase=python-jsbeautifier
pkgver=1.14.9
pkgrel=1
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-editorconfig' 'python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/beautify-web/js-beautify/archive/v$pkgver/js-beautify-v$pkgver.tar.gz")
b2sums=('9bf1d2940a6f767a948801ca20457651ca04e825760a9f6cf32c6438f712cba9b4d051784ff99a19e3178e45ac6fd96d0fa6fbd4a14beb93d11d36ca72cf6bd3')

prepare() {
  cd js-beautify-$pkgver
  cp -a python python-css
}

build() {
  cd js-beautify-$pkgver/python
  mv setup-js.py setup.py
  python -m build --wheel --skip-dependency-check --no-isolation
  cd ../python-css
  mv setup-css.py setup.py
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd js-beautify-$pkgver
  python python/js-beautify-test.py
}

package_python-jsbeautifier() {
  pkgdesc='JavaScript unobfuscator and beautifier'
  cd js-beautify-$pkgver/python
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" ../LICENSE
}

package_python-cssbeautifier() {
  pkgdesc='CSS unobfuscator and beautifier'
  depends+=('python-jsbeautifier')
  cd js-beautify-$pkgver/python-css
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" ../LICENSE
}
