# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=helvum
pkgver=0.4.1
pkgrel=1
pkgdesc='GTK patchbay for PipeWire'
arch=(loong64 x86_64)
url='https://gitlab.freedesktop.org/pipewire/helvum'
license=(GPL3)
depends=(
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libpipewire
  pango
  pipewire
)
makedepends=(
  appstream-glib
  cargo
  clang
  meson
)
source=($url/-/archive/$pkgver/helvum-$pkgver.tar.gz)
sha512sums=('66ef92f6be05f73ae561fdc3db110ea3841ea55104ffca06fc11b30dc4d72ba908fe5a7e10acb952844daea6efca717f269d36883ccb5d0f75b9b4433ea1e8c6')
b2sums=('96a8d594fb9861101df276717d7c49b0e70ae6693b78ac0275e3d134bbb7a2f19fa0f0116cdf34545def251c895ca66da3d1284b805e6fa70e21ec5f98ae90ca')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
