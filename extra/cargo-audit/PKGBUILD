# Maintainer: kpcyrd <git@rxv.cc>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-audit
pkgver=0.18.2
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('loong64' 'x86_64')
license=('MIT' 'APACHE')
options=(!lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d513e2811912d0f82b7c245eb2fdeda8b8aa2c81cca969ed56ef2afbe1fc2e7a')
b2sums=('cba044d24c955d18c28f1cf8c5999feea8dc815b7607dc5576cc9e9ac4f40492bf75742beffc7f43d674e92b3e6e70afbecd51bbe7234782520b59b1a38a40e7')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features fix
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 "../target/release/${pkgname}" -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
