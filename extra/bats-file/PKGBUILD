# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=bats-file
pkgver=0.4.0
pkgrel=1
pkgdesc='Common filesystem assertions for Bats'
url='https://github.com/bats-core/bats-file'
arch=(any)
license=("custom:CC0")
depends=(
  bash
  bats
  bats-support
)
checkdepends=(
  coreutils
  python
)
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('373262eb0dd01b9a46ab6f74e74f8cca867048fc757ad48126b7aa26ba173989e77c3ae9007e1bbe646f4f461fcd1c2be6e60b3a55242ae4b73f41d42c323bb6')
b2sums=('1fe64ed497ddc96450f4f014f8eafe3bd9da31a9fdea78eebda7d76ad82f911672dda588bf57b39bd2ce62260cbc3cc055b469f1f40692c6b8bf94e74db2fe8f')

prepare() {
  cd "${pkgname}-${pkgver}"
  # disable tests that require root
  sed 's/command -v sudo/command -v doesnotexist/g' -i test/test_helper.bash
  rm test/54-assert-10-assert_character_exists.bats \
    test/54-assert-11-assert_character_not_exists.bats \
    test/55-assert-10-assert_block_exists.bats \
    test/55-assert-11-assert_block_not_exists.bats \
    test/57-assert-10-assert_socket_exists.bats \
    test/59-assert-10-assert_file_owner.bats \
    test/59-assert-11-assert_not_file_owner.bats \
    test/70-temp-11-temp_del.bats
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: --jobs "$(nproc)" report upstream: tests can't run in parallel as
  # different tests interact with the same files
  bats --tap test
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 644 *.bash -t "${pkgdir}/usr/lib/bats/${pkgname}"
  install -Dm 644 src/*.bash -t "${pkgdir}/usr/lib/bats/${pkgname}/src"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
