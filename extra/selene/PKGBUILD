# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=selene
pkgver=0.26.1
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('loong64' 'x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('git' 'rust')
conflicts=("$pkgname-linter")
replaces=("$pkgname-linter")
options=('!lto')
source=("git+$url.git#tag=$pkgver")
b2sums=('SKIP')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}

# vim:set ts=2 sw=2 et:
