# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=6.0.16
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'dtkcore' 'dtkgui' 'dtkwidget' 'dconf' 'glib2'
         'qt5-base' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'xdg-user-dirs' 'deepin-application-manager'
         'deepin-appearance')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-launcher/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('53eacf35ac88365ea015f38250772b3a267a9ad03833a95aa4787df06b816cc49c259a797aaae88f604aa3051f343dfce49def423070221214a0930a2510e24b')

prepare() {
  cd dde-launcher-$pkgver

  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

check() {
  cd dde-launcher-$pkgver
  # ninja test
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
