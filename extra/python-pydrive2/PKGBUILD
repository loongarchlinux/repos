# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pydrive2
_pkgname="${pkgname#python-}"
pkgver=1.17.0
pkgrel=1
pkgdesc='Google Drive API Python wrapper library (maintained fork of PyDrive)'
arch=('any')
url='https://github.com/iterative/PyDrive2'
license=('Apache')
depends=(
  'python'
  'python-google-api-python-client'
  'python-oauth2client'
  'python-pyaml'
  'python-pyopenssl'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'python-setuptools-scm'
)
_commit='c473d631d9c47559d66943ecedfaec46af1aa78a'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
