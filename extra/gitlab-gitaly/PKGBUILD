# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=15.11.6
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('loong64' 'x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby2.7 libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go cmake git)
backup=("etc/gitlab-gitaly/config.toml")
_tag=v${pkgver}
source=("https://gitlab.com/gitlab-org/gitaly/-/archive/${_tag}/gitaly-${_tag}.tar.gz"
        "configs.patch"
        "gitlab-gitaly-15.11.0-ruby-grpc-1.54.0.patch"
        "gitlab-gitaly.service"
        "ruby27.patch")
options=(!debug) # ruby/grpc does not like being debugged
sha512sums=('4e69efdfe9cf15a86522192b9c597d86279be2751be999f85606b1b3babaadbfbcde5564ae2909db87473935d045d85518d267af30fe146bc94e63595469a254'
            '9b824a791c7f563857e99a7faad013d50eb1ca93d49089742dd7ccdfbb325c1c4a48db4dc83820fda5dfedfbd3e2b03c786c8fa0f2eb3c4b78ce3bbeefb97584'
            '7ef9cba040c0013dbc7a30a48e8b6b18036e260c495e71922ed305b660f4d92b08e0fb066fbe51bbd0dc79e38c85d29c02085a71a7e5c3c13ddadbb2489cdd9a'
            '7f5cd528c873a5e43e18aa6a88bd7298422c047e0e61cf3208be7d9fcfdfdc8a844b5c439ab6afc2098c5c4c60ed9c3d167c2f87517f1e93b34f39be3d3dad09'
            '2e4171e42cf7d5be91ef58e720b53159c1cdc4cb6932e9044094a99e9ee21ee8792067c0f539e11336c59748ea03291c069ff265cdaf5b4aea4b14f6d0929bd8')

prepare() {
  cd gitaly-$_tag

  patch -p1 < ../ruby27.patch

  # Update grpc gem to v1.54.0 needed to build with gcc 13
  patch -p1 < ../gitlab-gitaly-15.11.0-ruby-grpc-1.54.0.patch

  patch -p1 < ../configs.patch
  # At this point the config file should not contain any references to '/home/git'

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd gitaly-$_tag

  pushd ruby
  bundle-2.7 config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  bundle-2.7 install --path vendor/bundle
  popd

  make V=1 BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd gitaly-$_tag

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/etc/gitlab-gitaly
  mkdir -p "${pkgdir}"/usr/share/webapps/gitlab-gitaly
  cp -r ruby "${pkgdir}"/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}"/etc/${pkgname}/config.toml
  install -Dm644 "LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/gitlab-gitaly.service "${pkgdir}"/usr/lib/systemd/system/gitlab-gitaly.service
}
