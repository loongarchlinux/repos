# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Aram Drevekenin <aram@poor.dev>

pkgname=bandwhich
pkgver=0.21.0
pkgrel=1
pkgdesc='Terminal bandwidth utilization tool'
url='https://github.com/imsnif/bandwhich'
arch=('loong64' 'x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/imsnif/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('b0b0f31b1d53771ae9660dae48c57b23de0fa3b2d8788434af470d4a01e8aa5736c53a4e5a329bef94ffea6232b9025ec905b83de0e70b08df074d9909f98904')
b2sums=('b903c7e4be58d13ceac86744f1f5b8039b367ef728ab4b728ffc629bb9661c98fa654cb0e4841c6a4267786b58ee66e9e9758099359ec657ced80a8295beba68')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --frozen -- --skip layout_under_50_width_under_50_height
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "docs/bandwhich.1" -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
