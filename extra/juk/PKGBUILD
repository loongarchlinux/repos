# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=juk
pkgver=23.08.4
pkgrel=2
pkgdesc='A jukebox, tagger and music collection manager'
url='https://apps.kde.org/juk/'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-multimedia)
depends=(kio5 taglib phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools5)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        taglib-2.patch)
sha256sums=('595d2138756ab360929d3f9fc9faca0c2f65090511e86847105e8720512029ab'
            'SKIP'
            '7e8676a275e3cb58b9bfb3433802ea7174867487f258d6afef4649adecb7e8ff')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < taglib-2.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
