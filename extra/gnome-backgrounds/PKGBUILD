# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=40.1
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
makedepends=(glib2 meson)
license=(GPL)
groups=(gnome)
_tarver=${pkgver/rc/.rc}
source=(https://download.gnome.org/sources/$pkgname/${_tarver%%.*}/$pkgname-$_tarver.tar.xz)
sha256sums=('60df8a0da301ce425b7043d42ae32ec4011ff08f18e0fc62f29401305da3a70e')

build() {
  arch-meson $pkgname-$_tarver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
