# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.11
pkgrel=1
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='https://www.fresse.org/dateutils/'
arch=('loong64' 'x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha512sums=('601fa8197acdb68fe0688aed3fa2ba2fcf66b9a8ad23a0c7cb816a2cba9c645854ec8715a2127e02294cc068f63b09d0596eee60e151a8ad0edff22f54ec9d7a'
            'SKIP')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --with-old-links=no
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
