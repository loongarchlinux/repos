# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libei
pkgver=1.2.0
pkgrel=1
pkgdesc="Library for Emulated Input"
url="https://libinput.pages.freedesktop.org/libei/"
arch=(loong64 x86_64)
license=(custom:X11)
depends=(
  libevdev
  systemd
)
makedepends=(
  git
  libxkbcommon
  meson
  python-attrs
  python-dbusmock
  python-jinja
  python-pytest
  python-structlog
  python-yaml
)
checkdepends=(
  python-pytest-xdist
  valgrind
)
provides=(
  lib{ei,eis,oeffis}.so
)
_commit=493631688437657a87e9169a76f14a3216c0024f  # tags/1.2.0^0
source=(
  "git+https://gitlab.freedesktop.org/libinput/libei.git#commit=$_commit"
  "git+https://github.com/nemequ/munit.git"
)
b2sums=('SKIP'
        'SKIP')

pkgver() {
  cd libei
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libei
  ln -sr ../munit subprojects/munit
}

build() {
  arch-meson libei build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --skip-subprojects
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libei/COPYING
}

# vim:set sw=2 sts=-1 et:
