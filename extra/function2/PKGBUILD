# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=function2
pkgver=4.2.2
pkgrel=1
pkgdesc="An improved drop-in replacement to std::function"
arch=(any)
url="https://naios.github.io/function2/"
license=(Boost)
makedepends=(cmake gtest)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/Naios/$pkgname/archive/refs/tags/$pkgver.tar.gz
  $pkgname-4.2.0-system_gtest.patch
  $pkgname-4.2.0-skip_docs.patch
)
sha512sums=('f10724639f1f4d7e2ad20671d1bb4238f3b26edaa829a4bb064fa4be0fe46bb55dfed24e0ab4756a0bd8969610a7ef30fb8059a0a72d4e93e2688e3025e54545'
            'fe7e5802dc150d415b43fecaf88d1fae3c38389844135bb20d66a577aa24d811ab9be9d662dcc03504eff77a54f2a42ba3516ae0f7d4ec552db39de78b7b1df1'
            'fec342025298f2a74e1b1daf381ff80b13cdc6238577379a1d805f3e38e26c8d40a24e7894d943fcb1a1dd27dafb22b521c8e6847e0d155400871bcec0e15681')
b2sums=('7af88893ae81cc5679bb9101b862dc5e2bb44b9444faf115087649bbee1efa45f182638dd43370da9b3230d1cb1ce42426f21638d28294dde8ca24d0b1a04eba'
        'ec067d26551b1139acd133b04bf89a67d314e21025e39b38d9bdc134ba50eb58f8903eb6668a487bf07f29e308bd9b55a7aed90befbed06f48eaf020245a4535'
        '83f7efdbaa4032716cc6a3718c8c6a1028961862c1305ebd39c2665da705fbe2ed66cd9d1806697436bdbedc2e60aa86403547e7f47fb1edfe01361253e36b20')

prepare() {
  # use the system installed gtest
  patch -Np1 -d $pkgname-$pkgver -i ../"$pkgname-4.2.0-system_gtest.patch"
  # skip installing docs (to a wrong location)
  patch -Np1 -d $pkgname-$pkgver -i ../"$pkgname-4.2.0-skip_docs.patch"
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make VERBOSE=1 -k test -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "$pkgname-$pkgver/Readme.md" -t "$pkgdir/usr/share/doc/$pkgname/"
}
