# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-csv23
pkgver=0.3.3
pkgrel=1
pkgdesc="Python 3 unicode CSV compatibility layer"
url="https://github.com/xflr6/csv23"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pytest-mock')
source=("https://github.com/xflr6/csv23/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e84af2f1ecb6313da3715650a7079c50044c60a7cdb26a152f19874211c108bd8975cec33f0fecfba69e53dfb54dc84337a0269543a21e83fedc11543acc9d7f')

prepare() {
  cd csv23-$pkgver
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd csv23-$pkgver
  python setup.py build
}

check() {
  cd csv23-$pkgver
  python -m pytest
}

package() {
  cd csv23-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
