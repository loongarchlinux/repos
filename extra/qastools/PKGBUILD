# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=1.3.0
pkgrel=1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=(loong64 x86_64)
url="https://gitlab.com/sebholt/qastools"
license=(MIT)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
  systemd-libs
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('77b61cb207036a45cfcba57dc81f7fe40231453135d0eb3b8c99f392cb761fa4a8a311aab7b1c4d37139ac527c27a3df6fe7781366f88e601105054df89f2011')
b2sums=('da31b1885720e3bacf8504556b15171ac2329beb063ac94d2730f95ab1033312c313adfa7e6f40c85542520bfe4b6f56323acb0b789899e38f96be8cea3dcaa0')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    systemd-libs libudev.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}

