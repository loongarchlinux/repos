# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.6.0
pkgrel=1
pkgdesc="cffi-based cairo bindings for Python"
arch=('any')
url="https://doc.courtbouillon.org/cairocffi"
license=('BSD')
depends=('python-cffi' 'cairo')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-xcffib' 'gdk-pixbuf2')
checkdepends=('python-pytest' 'python-numpy' 'python-pikepdf')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf'
            'python-xcffib: for cairo xcb support')
source=("https://github.com/Kozea/cairocffi/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1f554afe0b1a8c40a5408b18caf3eacf88d84d7780fdf23dad550a584ba8bc3a988bfebf01b0e7e8518d233c66ed969c4ff2182efbefcdd77f66ddbef125c23c')

build() {
  cd cairocffi-$pkgver
  python -m build -nw
}

check() {
  cd cairocffi-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd cairocffi-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
