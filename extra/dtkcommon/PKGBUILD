# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.6.19
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/dtkcommon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f8423ae4608a35298e0977aad2c1570e7ad8474d11546e5714570cf21a4ae9ed9e229f21584940bc17ca16810d3a5c0936f2f8e42696e95fd9183518fc961668')

build() {
  cd dtkcommon-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr  \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd dtkcommon-$pkgver
  DESTDIR="$pkgdir" ninja install
}
