# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=0.9.7
pkgrel=1
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-netease/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e7bacf7b4aa4ce3b15bfbb699196ba37df7d0b67b86a9a42348a964946cf44d766841bc1c5d8ed70e4038b5ddae571ea1804a55b8890f5f53501d0c59fc39b6c')

build() {
  cd feeluown-netease-$pkgver
  python setup.py build
}

check() {
  cd feeluown-netease-$pkgver
  pytest
}

package() {
  cd feeluown-netease-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
