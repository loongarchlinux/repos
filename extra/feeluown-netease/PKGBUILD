# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=0.9.6
pkgrel=1
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-netease/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3b2643ceb3aea27ae83bdb44f4f4f5981eeee475cca5a56160cdc58dda5c9a202fb06874c64efcea065d0cfae009886c4e8b0f236ab5aff04016651edf271c6c')

build() {
  cd feeluown-netease-$pkgver
  python setup.py build
}

check() {
  cd feeluown-netease-$pkgver
  pytest
}

package() {
  cd feeluown-netease-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
