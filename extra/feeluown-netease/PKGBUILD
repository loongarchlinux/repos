# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=0.9.8
pkgrel=1
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-netease/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('614bc19a8e40ca1b372ad0ef82b1888c1e22b8eecbaf14dd43f75a290c2e007ff954ee342e4e72d64868de5e44f871e059e0c2681c765fb8d7c266675025ffbb')

build() {
  cd feeluown-netease-$pkgver
  python setup.py build
}

check() {
  cd feeluown-netease-$pkgver
  pytest
}

package() {
  cd feeluown-netease-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
