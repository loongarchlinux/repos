# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-cli-tools
pkgver=5.27.8
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=2
pkgdesc='Tools based on KDE Frameworks 5 to better interact with the system'
arch=(loong64 x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kdesu5 kcmutils5 kactivities5 kparts5)
makedepends=(extra-cmake-modules kdoctools5 plasma-workspace)
optdepends=('plasma-workspace: for kcmshell5')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a2ba02957ba9077f4d097b2aa1c5c3f7f4f25ffd72a18c14a2d8371e941467a3'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  ln -s /usr/lib/kf5/kdesu "$pkgdir"/usr/bin/
}
