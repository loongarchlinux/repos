# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=23.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(loong64 x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  lirc
  hicolor-icon-theme
  qt5-base
  zlib
)
makedepends=(
  alsa-lib
  cmake
  dssi
  fftw
  jack
  ladspa
  liblrdf
  liblo
  libsamplerate
  libsndfile
  qt5-tools)
checkdepends=(
  lilypond
  python
  tex-gyre-fonts
  ttf-dejavu
  xorg-server-xvfb
)
optdepends=(
  'cups: Printing support'
  'evince: Print preview support'
  'flac: FLAC support'
  'lilypond: Notation display support'
  'mupdf: Print preview support'
  'okular: Print preview support'
  'timidity++: Softsynth support'
  'wavpack: Lossless audio codec support'
)
provides=(
  dssi-host
  ladspa-host
)
source=(
  https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz
)
sha512sums=('a51a5ddb1fe9c2ef0c2f7b16f8fbc4fd4fd727e7a3873bd744177a00188fa13d696fd800d38f0a67e16c33c3a98a8a924e72a4a6fa347ea68f1dd1cdf6492d24')
b2sums=('7010657bdc3f159982b1f6eff83a5cbf1ca2c8300ccde786995645712471f31ed8b6ca1faa54e63748745de7731a29144a4144b59801d9c41bedfe4574ee4bdc')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="$(pwd)/test-tmp"
  # disable broken lilypond_export_test: https://sourceforge.net/p/rosegarden/bugs/1640/
  # disable tests incompatible with debug symbols: https://sourceforge.net/p/rosegarden/bugs/1641/
  xvfb-run ctest --output-on-failure --test-dir build/test -E '^(lilypond_export_test|test_notationview_selection|convert)$'
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    jack libjack.so
    liblrdf liblrdf.so
    liblo liblo.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
