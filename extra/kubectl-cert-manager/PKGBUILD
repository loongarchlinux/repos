# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: ml <>
pkgname=kubectl-cert-manager
pkgver=1.13.3
pkgrel=1
pkgdesc='Automatically provision and manage TLS certificates in Kubernetes'
arch=('loong64' 'x86_64' 'aarch64')
url='https://github.com/jetstack/cert-manager'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
groups=('kubectl-plugins')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ab8d05a09bee1c92b4e68d57ccee9406211b02f75d6faa6b4b54ac2f49c9954de24d93467849f7d295a6b69838465bf11d420ce48e75ba9cd1946f99205c3798')
b2sums=('3e8ebe7b02bedd78ba65afbcddd3f7ba32959c8af487bcc285f4cfa6bf4033cb9bc43b7f3907db7df7fa27cc355cc0988c3980a8c44c4c8539f2a2284e0daca2')

build() {
  local _x _commit
  _commit=$(bsdcat "$pkgname-$pkgver.tar.gz" | git get-tar-commit-id)
  _x=(
    AppVersion="v$pkgver"
    AppGitCommit="${_commit:?}"
    AppGitState="clean"
  )

  cd cert-manager-"$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  cd cmd/ctl
  go build -ldflags="-linkmode=external ${_x[*]/#/-X=github.com/jetstack/cert-manager/pkg/util.}" .
}

check() {
  cd cert-manager-"$pkgver"
  # we don't want e2e and other tests that explicitly want bazel
  # go test -short ./cmd/ctl/...
}

package() {
  cd cert-manager-"$pkgver"/cmd/ctl
  install -Dm755 ctl "$pkgdir"/usr/bin/kubectl-cert_manager
}
