# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=45.2
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(
  git
  meson
)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=b02e43d84cf7a1956d2d8cc77079a64cddef4d40  # tags/45.2^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
)
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
