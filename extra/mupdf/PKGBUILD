# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu

pkgbase=mupdf
pkgname=(libmupdf mupdf mupdf-gl mupdf-tools python-mupdf)
pkgver=1.23.9
pkgrel=4
pkgdesc='Lightweight PDF and XPS viewer'
arch=('loong64' 'x86_64')
url='https://mupdf.com/'
license=('AGPL-3.0-or-later')
makedepends=(
  'clang'
  'curl'
  'desktop-file-utils'
  'freeglut'
  'freetype2'
  'glu'
  'gumbo-parser'
  'harfbuzz'
  'jbig2dec'
  # 'lcms2'  # requires fork (not the version we are packaging)
  'leptonica'
  'libjpeg'
  'libx11'
  'libxext'
  'mesa-libgl'
  # 'mujs'  # is hardcoded and needs a patch to debundle
  'openjpeg2'
  'openssl'
  'python'
  'swig'
  'tesseract'
  'zlib'
)
options=('staticlibs')
source=(
  "https://mupdf.com/downloads/archive/$pkgbase-${pkgver/_/}-source.tar.gz"
  $pkgbase.desktop
  $pkgbase.xpm
  $pkgbase-1.23.9-no_venv.patch
  $pkgbase-1.23.9-cpp_ldflags.patch
  $pkgbase-1.23.9-install_targets.patch
)
sha256sums=('d4a5d66801d85f97d76da978b155f66a5659d1131070c0400f076883f604e297'
            'ccff66979249bd4ab4ba8918660f194eb90eb0ae231b16e36a6cecdcf471883f'
            'a435f44425f5432c074dee745d8fbaeb879038ec1f1ec64f037c74662f09aca8'
            'fed5ccebf5c8d3a4f4b777ce058f647cde673826b465c777994fc436dc1dca5a'
            '152222c0bc066404d9e38be7abbba20748b0a4bbc9cb30403d19e3b176ab257f'
            '87fd0544112ca8e8396dd3eb989907c110bad35de02ea425677b99a5d2532666')

prepare() {
  cd $pkgbase-${pkgver/_/}-source

  # do not use a venv for building language bindings
  patch -Np1 -i ../$pkgbase-1.23.9-no_venv.patch
  # use our LDFLAGS when building the C++ bindings to have full RELRO
  patch -Np1 -i ../$pkgbase-1.23.9-cpp_ldflags.patch
  # alter install-shared-* targets to not call one another (which fails on installing headers twice) and install libmupdfcpp.so with soname postfix
  patch -Np1 -i ../$pkgbase-1.23.9-install_targets.patch

  # remove bundled packages, we want our system libraries
  rm -rf thirdparty/{curl,freeglut,freetype,gumbo-parser,harfbuzz,jbig2dec,leptonica,libjpeg,openjpeg,tesseract,zlib}

  {
    printf "LINUX_OR_OPENBSD := yes\n"  # required so that Makefile sets soname symlink
    printf "USE_SYSTEM_CURL := yes\n"
    printf "USE_SYSTEM_FREETYPE := yes\n"
    printf "USE_SYSTEM_GLUT := yes\n"
    printf "USE_SYSTEM_GUMBO := yes\n"
    printf "USE_SYSTEM_HARFBUZZ := yes\n"
    printf "USE_SYSTEM_JBIG2DEC := yes\n"
    printf "USE_SYSTEM_JPEGXR := yes\n"  # not used without HAVE_JPEGXR
    printf "USE_SYSTEM_LCMS2 := no\n"  # need lcms2-art fork
    printf "USE_SYSTEM_LEPTONICA := yes\n"
    printf "USE_SYSTEM_LIBJPEG := yes\n"
    printf "USE_SYSTEM_LIBS := yes\n"
    printf "USE_SYSTEM_MUJS := no\n"  # needs patch to debundle
    printf "USE_SYSTEM_OPENJPEG := yes\n"
    printf "USE_SYSTEM_TESSERACT := yes\n"
    printf "USE_SYSTEM_ZLIB := yes\n"
    printf "USE_TESSERACT := yes\n"
  } > user.make
}

build() {
  cd $pkgbase-${pkgver/_/}-source
  # Enforce -j1 to avoid concurrency issue during build
  make -j1 shared=yes build=release libs apps c++ python
}

package_libmupdf() {
  pkgdesc='Library for Lightweight PDF and XPS viewer'
  depends=(
    'freetype2'
    'gcc-libs'
    'glibc'
    'gumbo-parser'
    'harfbuzz'
    'jbig2dec'
    'leptonica'
    'libjpeg'
    'openjpeg2'
    'tesseract'
    'zlib'
  )
  provides=(
    'libmupdf.so'
    'libmupdfcpp.so'
  )

  cd $pkgbase-${pkgver/_/}-source
  make prefix=/usr DESTDIR="$pkgdir" install-shared-c install-shared-c++
}

package_python-mupdf() {
  pkgdesc='Library for Lightweight PDF and XPS viewer - Python bindings'
  depends=(
    'gcc-libs'
    'glibc'
    'libmupdf'
    'python'
  )

  cd $pkgbase-${pkgver/_/}-source
  make prefix=/usr DESTDIR="$pkgdir" install-shared-python
}

package_mupdf() {
  pkgdesc='Lightweight PDF and XPS viewer'
  depends=(
    'desktop-file-utils'
    'glibc'
    'libmupdf'
    'libx11'
    'libxext'
  )

  cd $pkgbase-${pkgver/_/}-source

  install -vDm 0755 build/shared-release/$pkgbase-x11 "$pkgdir"/usr/bin/$pkgbase

  install -vDm 0644 docs/man/$pkgbase.1 -t "$pkgdir"/usr/share/man/man1/

  install -vDm 0644 README COPYING CHANGES -t "$pkgdir"/usr/share/doc/$pkgbase/

  install -vDm 0644 ../$pkgbase.desktop -t "$pkgdir"/usr/share/applications/
  install -vDm 0644 ../$pkgbase.xpm -t "$pkgdir"/usr/share/pixmaps/
}

package_mupdf-gl() {
  pkgdesc='Lightweight PDF and XPS viewer with OpenGL backend'
  conflicts=('mupdf')
  provides=("mupdf=${pkgver}")
  depends=(
    'desktop-file-utils'
    'glibc'
    'glut'
    'libgl'
    'libmupdf'
    'openssl'
  )

  cd $pkgbase-${pkgver/_/}-source

  install -vDm 0755 build/shared-release/$pkgbase-gl "$pkgdir"/usr/bin/$pkgbase

  install -vDm 0644 docs/man/$pkgbase.1 -t "$pkgdir"/usr/share/man/man1/

  install -vDm 0644 README COPYING CHANGES -t "$pkgdir"/usr/share/doc/$pkgbase/

  install -vDm 0644 ../$pkgbase.desktop -t "$pkgdir"/usr/share/applications/
  install -vDm 0644 ../$pkgbase.xpm -t "$pkgdir"/usr/share/pixmaps/
}

package_mupdf-tools() {
  pkgdesc='Tools for Lightweight PDF and XPS viewer'
  depends=(
    'glibc'
    'libmupdf'
    'openssl'
  )

  cd $pkgbase-${pkgver/_/}-source

  install -vDm 0755 build/shared-release/{mutool,muraster} -t "$pkgdir"/usr/bin/

  install -vDm 0644 docs/man/mutool.1 -t "$pkgdir"/usr/share/man/man1/

  install -vDm 0644 README COPYING CHANGES -t "$pkgdir"/usr/share/doc/$pkgname/
}
