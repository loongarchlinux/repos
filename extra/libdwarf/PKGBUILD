# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
epoch=1
pkgver=0.7.0
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(loong64 x86_64)
license=('GPL' 'LGPL')
url="https://www.prevanders.net/dwarf.html"
depends=('elfutils')
checkdepends=('python')
provides=('libdwarf.so')
options+=('staticlibs' 'debug')
#_commit="245262d6ccaeed17f7564c31b78da2d4df1a3ee0"
#source=(https://sourceforge.net/code-snapshots/git/l/li/libdwarf/code.git/libdwarf-code-$_commit.zip)
source=(https://www.prevanders.net/libdwarf-$pkgver.tar.xz)
sha512sums=('0fe027e517551d138c15695ffdebe2dfbb0e89184fc0cc93bed5c342cd1cd901dc23f6b2aa8643ec8e62808cbdc0d94389fe44438c694d19c45c6a7367e8f7af')

build() {
  cd "$srcdir"/libdwarf-$pkgver
  # cd "$srcdir"/libdwarf-code-$_commit
  CFLAGS+=" -ffat-lto-objects"
  ./configure --prefix=/usr --includedir=/usr/include/libdwarf --enable-shared
  make
}

check() {
  cd "$srcdir"/libdwarf-$pkgver
  # cd "$srcdir"/libdwarf-code-$_commit
  make -j1 check
}

package() {
  cd "$srcdir"/libdwarf-$pkgver
  # cd "$srcdir"/libdwarf-code-$_commit
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README NEWS "$pkgdir"/usr/share/doc/$pkgname/
}
