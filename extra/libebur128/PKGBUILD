# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BoosterDEV <booster DOT devel AT gmail DOT com>

pkgname=libebur128
pkgver=1.2.4
pkgrel=3
pkgdesc="A library that implements the EBU R 128 standard for loudness normalisation."
arch=(x86_64)
url="https://github.com/jiixyj/libebur128"
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/jiixyj/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('2ee41a3a5ae3891601ae975d5ec2642b997d276ef647cf5c5b363b6127f7add8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
