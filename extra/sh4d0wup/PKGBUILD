# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=sh4d0wup
pkgver=0.9.1
pkgrel=1
pkgdesc="Signing-key abuse and update exploitation framework"
url='https://github.com/kpcyrd/sh4d0wup'
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=('gcc' 'pcsclite' 'podman' 'openssl' 'shared-mime-info' 'xz' 'libzstd.so')
checkdepends=('sequoia-sq')
makedepends=('cargo' 'clang')
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5f74ad2cfc4babf0a718003e9940892250715f413efa321e0c53aedaed568f65')
b2sums=('91a783a3aaf026f53e8f430c8999c3d8fa78b478441eb5fe32aa0574899ba245bafa134f2059bbd6bec510df0f367e332af49f4140a6f8e954d49ed3a98d2b3a')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sh4d0wup

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
