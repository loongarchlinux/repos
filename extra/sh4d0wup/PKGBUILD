# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=sh4d0wup
pkgver=0.9.0
pkgrel=1
pkgdesc="Signing-key abuse and update exploitation framework"
url='https://github.com/kpcyrd/sh4d0wup'
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=('gcc' 'pcsclite' 'podman' 'openssl' 'shared-mime-info' 'xz' 'libzstd.so')
checkdepends=('sequoia-sq')
makedepends=('cargo' 'clang')
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ec6aa007417e76338bc903d969e88f038ba982eb603f227f85caea5b8ad715c3')
b2sums=('ef63fd17daae0ef1f3f57651d1ff2dcca7fa69c802bb270d1c72a2644965d4bd5007cf757f9bc1beb7e1534ccbc4752b27e48e0745774bb4f8e257a2f72ca6a3')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/sh4d0wup

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
