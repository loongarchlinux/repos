# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=neochat
pkgver=23.08.4
pkgrel=2
pkgdesc='A client for matrix, the decentralized communication protocol'
url='https://apps.kde.org/neochat/'
arch=(loong64 x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-network)
depends=(qqc2-desktop-style5 kirigami-addons5 kquickcharts5 qt5-location
         libquotient kquickimageeditor5 kitemmodels5 kio5 syntax-highlighting5 cmark hicolor-icon-theme)
makedepends=(extra-cmake-modules qcoro-qt5 kdoctools5)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6d98038200bc042f2a8b96032eca531a39c98a3616ff6a34aa713fa08978ace9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
