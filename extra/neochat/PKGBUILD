# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=neochat
pkgver=23.08.0
pkgrel=2
pkgdesc='A client for matrix, the decentralized communication protocol'
url='https://apps.kde.org/neochat/'
arch=(loong64 x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-network)
depends=(qqc2-desktop-style kirigami-addons
         libquotient kquickimageeditor kitemmodels kio syntax-highlighting cmark hicolor-icon-theme)
makedepends=(extra-cmake-modules qcoro-qt5 kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4ead1a9fa308e53173229a744075c59580ed3364bd206a08c07d9db163758bfb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
