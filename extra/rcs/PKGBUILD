# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=rcs
pkgver=5.9.4
pkgrel=3
pkgdesc='Revision Control System: manages multiple revisions of files'
url='https://www.gnu.org/software/rcs/'
license=('GPL3')
arch=('x86_64')
depends=('ed')
validpgpkeys=('748EA0E81CB8A7489BFA6CE4670322244C807502')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        rcs-c++11.patch
        t810.diff)
sha256sums=('063d5a0d7da1821754b80c639cdae2c82b535c8ff4131f75dc7bbf0cd63a5dff'
            'SKIP'
            '569a8d5c851d2619393474a681e05c2eb83912e71adcf1124b895478c91e1fbb'
            'f7d8f458c6c7a0d435183378eb222244e07d9d66c8013b2fea4efca9a3e66a6d')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../rcs-c++11.patch # Fix build with C++11
  patch -p1 -i $srcdir/t810.diff # fix test suite failure - git f2330a6268d244e97f47a97f4767736fc3b31455
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
}
