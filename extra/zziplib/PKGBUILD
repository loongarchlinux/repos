# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=zziplib
pkgver=0.13.71
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('x86_64')
url="http://zziplib.sourceforge.net"
license=('LGPL' 'MPL')
depends=('zlib')
makedepends=('python' 'xmlto' 'zip' 'cmake' 'ninja')
checkdepends=('unzip')
source=($pkgname-$pkgver.tar.gz::"https://github.com/gdraheim/zziplib/archive/v$pkgver.tar.gz"
        https://github.com/gdraheim/zziplib/commit/75e22f3c365b62acbad8d8645d5404242800dfba.patch
        https://patch-diff.githubusercontent.com/raw/gdraheim/zziplib/pull/89.patch)
sha256sums=('2ee1e0fbbb78ec7cc46bde5b62857bc51f8d665dd265577cf93584344b8b9de2'
            '5a5ceed4a9a5c22a72b7a06bab2f43701b775202a93b13f393b1fc3c8f708b1c'
            'a9e908938e07394158f4ea7e57cde6241bbab7949ec752cbedaf951b7acbc0c0')
sha512sums=('e035d0ac26dca78335ae3defc652543ff7b353a1a95d76ed1beeb21a08e16f287a62d488f528cfbb77d5b558581b68d439aa0823577524e9aa61a3cf5f208cb5'
            '5fc25cb0663f40a7338a44219793c5d9e1f58fbf3f613aa1f89d925812e13b75c1db5e1a7709edc042c89e3717d5947d38c4ca1fae0c037f776ce656c2e57833'
            'e3a805dc8a812f45fb0dbcb2aeee44ac7f94f99bf888997bcf188f898fba3ea81441875ececbe460edc8ded0b79a9584a83735876d60a88083ac7d2a1541896a')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}/75e22f3c365b62acbad8d8645d5404242800dfba.patch"
  patch -Np1 -i "${srcdir}/89.patch"

  mkdir build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -GNinja \
    -DZZIPDOCS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd ${pkgname}-${pkgver}/build
  ninja check
}

package() {
  cd ${pkgname}-${pkgver}/build
  DESTDIR="${pkgdir}" ninja install

  # Fix permissions
  # chmod 644 "${pkgdir}/usr/share/man/man3/"*
  # chown -R root:root "${pkgdir}/usr/share/man/man3"
}

# vim: set ts=2 sw=2 et:
