# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-appearance
pkgver=1.1.21
pkgrel=1
pkgdesc='Application resource management and control services for the dde desktop environment'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-appearance"
license=('GPL3')
# startdde: org.deepin.dde.XSettings1
depends=('qt5-base' 'dtkcore' 'kconfig5' 'kwindowsystem5' 'kglobalaccel5' 'libxcursor' 'libxfixes'
         'libx11' 'gtk3' 'libxcb' 'xcb-util-cursor' 'fontconfig' 'openssl' 'gsettings-qt' 'glib2'
         'dtkgui' 'glibc' 'gcc-libs' 'startdde')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-appearance/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9bed461189f639fbc3b6bb28b45f9d8e40dbaad8d34cd95a71114dc85f930199387cb6b048e49c26a28c7857603f2535c66b46676e47fe340dd358741907c22')

build() {
  cd dde-appearance-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd dde-appearance-$pkgver
  DESTDIR="$pkgdir" ninja install
}
