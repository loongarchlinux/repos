# Maintainer: David Runge <dvzrv@archlinux.org>

# TODO: devendor clap and minizip
pkgbase=spectmorph
pkgname=(spectmorph libspectmorph spectmorph-{clap,instruments,lv2,tools,vst})
pkgver=0.6.0
pkgrel=2
pkgdesc="Analyze samples of musical instruments and combine them (morphing)"
arch=(loong64 x86_64)
url="https://spectmorph.org/"
license=(LGPL-2.1-or-later)
makedepends=(
  cairo
  fftw
  freetype2
  gcc-libs
  glib2
  glibc
  jack
  lv2
  libao
  libglvnd
  libsndfile
  qt5-base
)
checkdepends=(
  lv2lint
  python
)
source=(https://github.com/swesterfeld/spectmorph/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('f25fd15b8d06844dbafb50dfc105a50f744253693d5b2dca9282a9f4a6eeff690f99f7a938e3c9ef2d41cb833d9a2fbdf2ce738fa0bc18ec83250a7f21bc6318')
b2sums=('17e34772c8ebe5029231183f86adfe645555d61a2a0db8a83057fa306b9cd6957bd550183239801e54a8c40842a911597bb841dc4508a58e9a9637911bd22ebf')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --with-clap
    --with-lv2
    --with-jack
    --with-qt
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver

  cd $pkgname-$pkgver
  LD_LIBRARY_PATH="$PWD/glui/.libs/:$PWD/lib/.libs/:$LD_LIBRARY_PATH" lv2lint -I lv2/ -Mpack "http://spectmorph.org/plugins/spectmorph"
}

package_spectmorph() {
  depends=(
    libspectmorph
    spectmorph-{clap,lv2,tools,vst}
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    _pick lib$pkgbase usr/include
    _pick lib$pkgbase usr/lib/libspectmorph*
    _pick lib$pkgbase usr/lib/pkgconfig
    _pick lib$pkgbase usr/share/$pkgbase/templates

    _pick $pkgbase-clap usr/lib/clap
    _pick $pkgbase-instruments usr/share/$pkgbase/instruments

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-tools usr/bin
    _pick $pkgbase-tools usr/share

    _pick $pkgbase-vst usr/lib/vst
  )
}

package_spectmorph-clap() {
  pkgdesc+=" - CLAP plugin"
  groups=(
    pro-audio
    clap-plugins
  )
  depends=(
    clap-host
    gcc-libs
    glibc
    libspectmorph
  )
  optdepends=(
    'spectmorph-instruments: for standard instruments'
  )

  mv -v $pkgname/* "$pkgdir"
}

package_spectmorph-instruments() {
  pkgdesc+=" - standard instruments"

  mv -v $pkgname/* "$pkgdir"
}

package_libspectmorph() {
  pkgdesc+=" - libs"
  depends=(
    cairo
    gcc-libs
    glibc
    fftw libfftw3f.so libfftw3f_threads.so
    freetype2 libfreetype.so
    glib2 libglib-2.0.so
    jack libjack.so
    libglvnd libGL.so
    libsndfile libsndfile.so
    libx11
    qt5-base
    zlib
  )
  provides=(
    libspectmorphjack.so
    libspectmorphgui.so
    libspectmorphglui.so
    libspectmorph.so
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,DESIGN,NEWS.md,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgbase/"
}

package_spectmorph-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    pro-audio
    lv2-plugins
  )
  depends=(
    gcc-libs
    glibc
    libspectmorph
    lv2-host
  )
  optdepends=(
    'spectmorph-instruments: for standard instruments'
  )

  mv -v $pkgname/* "$pkgdir"
}

package_spectmorph-tools() {
  pkgdesc+=" - tools"
  groups=(
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    glib2 libglib-2.0.so
    jack libjack.so
    libao
    libspectmorph
    qt5-base
    python
    python-lxml
  )
  optdepends=(
    'spectmorph-instruments: for standard instruments'
  )

  mv -v $pkgname/* "$pkgdir"
}

package_spectmorph-vst() {
  pkgdesc+=" - VST plugin"
  groups=(
    pro-audio
    vst-plugins
  )
  depends=(
    gcc-libs
    glibc
    libspectmorph
    vst-host
  )
  optdepends=(
    'spectmorph-instruments: for standard instruments'
  )

  mv -v $pkgname/* "$pkgdir"
}
