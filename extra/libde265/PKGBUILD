# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Daniel Nagy <danielnagy at gmx de>

pkgname=libde265
pkgver=1.0.12
pkgrel=1
pkgdesc='Open h.265 video codec implementation'
arch=(loong64 x86_64)
url='https://github.com/strukturag/libde265'
license=(LGPL3)
depends=(gcc-libs)
makedepends=(ffmpeg sdl qt5-base)
optdepends=('ffmpeg: for sherlock265'
            'qt5-base: for sherlock265'
            'sdl: dec265 YUV overlay output')
source=(https://github.com/strukturag/libde265/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('62185ea2182e68cf68bba20cc6eb4c287407b509cf0a827d7ddb75614db77b5c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

# Remove useless tests binary
  rm "$pkgdir"/usr/bin/tests
}
