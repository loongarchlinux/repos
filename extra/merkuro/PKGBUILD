# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=merkuro
pkgver=23.08.0
pkgrel=2
pkgdesc='A calendar application using Akonadi to sync with external services'
arch=(loong64 x86_64)
url='https://apps.kde.org/merkuro.calendar/'
license=(GPL)
depends=(kdepim-runtime mailcommon qqc2-desktop-style kirigami-addons)
makedepends=(extra-cmake-modules)
conflicts=(kalendar)
replaces=(kalendar)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/merkuro/-/commit/b15f8629.patch)
sha256sums=('2aea56404f2b2a88151a81bef4ea2c187a2435cc0828ab5fc7806f914a973977'
            'SKIP'
            'd1a649e6b4c92a90f62e581fcb05251cf9f58cdefc711cba18be2a13ca3fc201')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < b15f8629.patch # Fix crash at startup
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
