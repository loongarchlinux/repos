# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Axel Hinrichs <hinrichs@bitscape.de>

pkgname=catatonit
pkgver=0.2.0
pkgrel=1
pkgdesc="A container init that is so simple it's effectively brain-dead."
arch=(loong64 x86_64)
url="https://github.com/openSUSE/catatonit/"
license=(GPL-2.0-or-later)
depends=(glibc)
source=(
  $pkgname-$pkgver.tar.xz::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz
  $pkgname-$pkgver.tar.xz.asc::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz.asc
  $pkgname-0.2.0-ldflags.patch
)
sha256sums=('9843c2b47421f0f1959b18b786f736cde1fbf124b1d1fa0d292be2773d2d2b90'
            'SKIP'
            '22a667e4b65f3454d80217bd4b6977c520250f6da6002ca0848c8f14d98347e3')
b2sums=('bb4adf83da1e21bc54e86ec0f5c5368aeef29d3f9d71d68dcdd911d3f58813a0fb0ab45fbeb1626d167e92d1e73ac757a904e87d0432b56c7d59d3eeee04455d'
        'SKIP'
        'ca428822fd2578c2c7db6a806ea82d49f47aaee0b31fcc04f012fba7ad873d47f231c29ef25bd685134192196154400eb27ba468089b692cb6f1067c99c1743f')
validpgpkeys=('5F36C6C61B5460124A75F5A69E18AA267DDB8DB4')  # Aleksa Sarai <asarai@suse.com>

prepare(){
  # pass in distribution LDFLAGS for full RELRO and PIE
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.0-ldflags.patch

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make V=1
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vdm 755 "$pkgdir/usr/lib/podman/"
  ln -sv /usr/bin/$pkgname "$pkgdir/usr/lib/podman/"
}

# vim: ft=sh syn=sh
