# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libdc1394
pkgver=2.2.6
pkgrel=3
pkgdesc='Library to control IEEE 1394 based cameras'
arch=('loong64' 'x86_64')
url='https://damien.douxchamps.net/ieee1394/libdc1394/'
license=('LGPL')
depends=('glibc' 'libraw1394' 'libusb')
makedepends=('doxygen')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('2b905fc9aa4eec6bdcf6a2ae5f5ba021232739f5be047dec8fe8dd6049c10fed')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-doxygen-html
  make
  make doc
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  cp -r doc/html "$pkgdir/usr/share/doc/$pkgname/"
}
