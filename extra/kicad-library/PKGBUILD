# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=7.0.9
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  'git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#commit=5d1487be36acba15ed4052af95a31b08ba54ceb3'
  'git+https://gitlab.com/kicad/libraries/kicad-symbols.git#commit=7909e277751e04703521ed4d708a39f8685b9c11'
  'git+https://gitlab.com/kicad/libraries/kicad-templates.git#commit=d7c8c3e950046c095824851880de02d4a1f63786'
  'git+https://gitlab.com/kicad/libraries/kicad-footprints.git#commit=11fa67c0a871ac81dd61f4cfb18f365f43763cec'
)
sha512sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
