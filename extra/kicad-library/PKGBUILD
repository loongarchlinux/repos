# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=7.0.7
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  'git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#commit=17c8ec5c53c46dbcd447521e29f98c60af647488'
  'git+https://gitlab.com/kicad/libraries/kicad-symbols.git#commit=06d20a4b9f7e5375329194d141b096dcdcb7518a'
  'git+https://gitlab.com/kicad/libraries/kicad-templates.git#commit=867eef383a0f61015cb69677d5c632d78a2ea01a'
  'git+https://gitlab.com/kicad/libraries/kicad-footprints.git#commit=182635b4b42ff2cececf298abaf11145944c6e57'
)
sha512sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
