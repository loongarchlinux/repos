# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.5
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=23a3eb7c2448b4f35398116df7d01b075361ef1f
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd wingpanel
  git describe --tags
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
