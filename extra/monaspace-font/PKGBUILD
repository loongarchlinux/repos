# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kent Hou Man <knthmn0 at gmail dot com>

_name=monaspace
pkgbase=$_name-font
pkgname=(otf-$_name ttf-$_name-variable)
pkgver=1.000
pkgrel=2
pkgdesc='An innovative superfamily of fonts for code, by GitHub'
arch=(any)
url="https://github.com/githubnext/$_name"
license=(OFL)
_archive="$_name-v$pkgver"
source=("$url/releases/download/v$pkgver/monaspace-v$pkgver.zip")
sha256sums=('3e08376fd0aeca1f851fde0c08e18ca2d797f6a4c7a449670bf4d1270303c8f6')

package_otf-monaspace() {
	provides=("$pkgbase")
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" fonts/otf/*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_ttf-monaspace-variable() {
	provides=("$pkgbase-variable")
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" fonts/variable/*.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
