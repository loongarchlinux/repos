# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=elf2uf2-rs
pkgver=1.3.8
pkgrel=1
pkgdesc="Convert ELF files to UF2 for USB Flashing Bootloaders"
url='https://github.com/JoNil/elf2uf2-rs'
arch=('loong64' 'x86_64')
license=('0BSD')
depends=('libudev.so')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('75c37c96b4bf24b1003436fcbbcc2989f50252eb2866a15647ba1509f49cf602')
b2sums=('4124a5edc5c5738f97f556822da1ca26bc15b35651d25d8f15d2324382b6a19fd27d6b0183e897992492a40f14953b425fd62021a058b91240041282c1b3618e')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/elf2uf2-rs

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
