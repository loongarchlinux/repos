# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.0.8
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'dwayland' 'qt5-base' 'qt5-svg' 'gsettings-qt'
         'icu' 'dconf' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'libnl' 'libcap' 'libpcap')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools' 'deepin-dock')
groups=('deepin')
install=deepin-system-monitor.install
source=("https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f14f87041a658b4f0dde4058de6b5fa5a0631e1a094f7ee67cf933ef708577d5dc22d0de236ca30f6ac1141ed255eca07c8f11e676718822b3f1c6bf5bc2be34')

build() {
  cd deepin-system-monitor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd deepin-system-monitor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
