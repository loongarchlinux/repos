# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=puppet
pkgver=7.27.0
pkgrel=3
pkgdesc='Server automation framework and application'
arch=('any')
url='https://www.puppet.com/community/open-source'
license=('Apache')
depends=(ruby facter ruby-semantic_puppet ruby-augeas ruby-shadow ruby-concurrent ruby-multi_json ruby-puppet-resource_api ruby-deep_merge ruby-fast_gettext hiera ruby-locale ruby-scanf ruby-msgpack)
makedepends=('git' 'rake' 'ruby-rdoc')
checkdepends=('ruby-rspec' 'ruby-yard' 'ruby-rspec-its' 'ruby-webmock')
backup=('etc/puppetlabs/puppet/auth.conf'
        'etc/puppetlabs/puppet/puppet.conf'
        'etc/puppetlabs/puppet/hiera.yaml')
install="$pkgname.install"

_augeas_commit=76e9ee2238036658286fca8c80b34a947214635d
_cron_commit=1d605e813f62a45dcf7881e07f25ba05fef816a6
_host_commit=d6b6393d559f889438fad1570883b0d6d5a381ab
_mount_commit=404da7fa334c29aa387a14e12bdf4691504498ea
_selinux_commit=4ad08a07350283766d32266c15617237a2562e1f
_sshkeys_commit=bf6cf14c4dc27e2b1b4a7b1f32d57ff7ff3aeb8f
_yumrepo_commit=fdda12d4b9475a6c3ba49d629c290ab6897b481e
_zfs_commit=0af39efa1d812c926dd3dbd0ca63ef7ee8538583
_zone_commit=1d227dcccaf34435fe5c8a7abf25c319e7d96e21

source=("https://github.com/puppetlabs/puppet/archive/${pkgver}/puppet-${pkgver}.tar.gz"
        "augeas_core::git+https://github.com/puppetlabs/puppetlabs-augeas_core#commit=${_augeas_commit}"
        "cron_core::git+https://github.com/puppetlabs/puppetlabs-cron_core#commit=${_cron_commit}"
        "host_core::git+https://github.com/puppetlabs/puppetlabs-host_core#commit=${_host_commit}"
        "mount_core::git+https://github.com/puppetlabs/puppetlabs-mount_core#commit=${_mount_commit}"
        "selinux_core::git+https://github.com/puppetlabs/puppetlabs-selinux_core#commit=${_selinux_commit}"
        "sshkeys_core::git+https://github.com/puppetlabs/puppetlabs-sshkeys_core#commit=${_sshkeys_commit}"
        "yumrepo_core::git+https://github.com/puppetlabs/puppetlabs-yumrepo_core#commit=${_yumrepo_commit}"
        "zfs_core::git+https://github.com/puppetlabs/puppetlabs-zfs_core#commit=${_zfs_commit}"
        "zone_core::git+https://github.com/puppetlabs/puppetlabs-zone_core#commit=${_zone_commit}"
        )
sha512sums=('f28b8ebb61a951a4e6176269e5fcf6066cc853b61a1f8c0dff974ab42914b34197a200800c37b71f5ab4d4b5e70929931d464c7a8abb1682e316d1150312f330'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
conflicts=('puppet5' 'puppet6')
provides=('puppet')

prepare() {
  cd "${pkgname}-${pkgver}"

  # FS#45044: fix file location
  sed -i 's_/opt/puppetlabs/puppet_/usr_' ext/systemd/puppet.service
}

check() {
  cd "${pkgname}-${pkgver}"
  # This breaks right now due to missing dependencies
  #rake --tasks             # show all tasks
  #rake commits             # requires a git repo
  #rake warnings            # requires a git repo
  #rake spec                # run tests single threaded
  #rake parallel:spec[8]
}

package() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  # install the gemspec file
  # this allows other gems to load puppet as a library (for example puppet-strings)
  install -Dm644 .gemspec "${pkgdir}/${_gemdir}/specifications/puppet-${pkgver}.gemspec"

  # generate manpages | this breaks right now..
  #rake gen_manpages

  ruby install.rb --destdir="$pkgdir" --sitelibdir="$( ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]' )"

  install -d "$pkgdir"/usr/lib/{sysusers.d,tmpfiles.d}

  echo 'u puppet 52 "Puppet" /var/lib/puppet' > "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  echo "d /run/puppetlabs 0755 $pkgname $pkgname -" > "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  # systemd
  install -Dm644 "${srcdir}/$pkgname-${pkgver}/ext/systemd/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Match permissions that puppet sets when running
  install -dm775 "$pkgdir"/var/lib/puppet
  chmod 750 "$pkgdir"/var/log/puppetlabs/puppet

  rm -r "$pkgdir"/var/run

  cd ..
  install --directory "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  mv *_core "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  # Search for unwanted directories and files and delete them
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type d \( -name .git -o -name spec -o -name rakelib \) -exec rm -rf {} +
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -name '.*' -exec rm -rf {} +
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type f \( -name pdk.yaml -o -name appveyor.yml -o -name Rakefile -o -name Gemfile \) -delete
}
