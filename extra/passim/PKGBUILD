# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=passim
pkgver=0.1.1
pkgrel=1
pkgdesc="A local caching server"
arch=(loong64 x86_64)
url="https://github.com/hughsie/passim"
license=(LGPL)
depends=(
  glib2 libgio-2.0.so
  gnutls
  hicolor-icon-theme
  libsoup3 libsoup-3.0.so
)
makedepends=(
  cmake
  gobject-introspection
  meson
  systemd
)
provides=(libpassim.so)
backup=(
  etc/passim.conf
)
source=("https://github.com/hughsie/passim/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('f35079d3f242430e28d3ef55ade253509ba3bb986a4edde3e2b29f83e915c95564c91badd966969daa4de18e33f01d178a86704fef3c52162097048805be1780'
            'SKIP')
b2sums=('acf69c418f00b1c4f64adb56a2b46423b20dbeaa69d2695e78ec959487dd57c3f01d2826e5b2b10c52869709272a09d033826133653775cea14e9bb030d01265'
        'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
