# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=passim
pkgver=0.1.2
pkgrel=1
pkgdesc="A local caching server"
arch=(loong64 x86_64)
url="https://github.com/hughsie/passim"
license=(LGPL)
depends=(
  avahi
  glib2 libgio-2.0.so
  gnutls
  hicolor-icon-theme
  libsoup3 libsoup-3.0.so
)
makedepends=(
  cmake
  gobject-introspection
  meson
  systemd
)
provides=(libpassim.so)
backup=(
  etc/passim.conf
)
source=("https://github.com/hughsie/passim/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('42cb12f010bc5d2173da162f95966a43e213fe5c07cbe82e99cfdfef86ae9f78c46d9962b5671df0e7efb9e53e979039f8981173fea14c84e82e65811d88389d'
            'SKIP')
b2sums=('31674980515ddb6098957670ecf4343a049265891c6b24918c94f178a727ae60066f8100030205c3f0aa032bfb0570e7dadfbfe28941a07fa69eca4de8eb9fa1'
        'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
