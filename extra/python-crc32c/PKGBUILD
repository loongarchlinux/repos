# Maintainer: David Runge <dvzrv@archlinux.org>

_name=crc32c
pkgname=python-crc32c
pkgver=2.3.post0
pkgrel=1
pkgdesc="A python package implementing the crc32c algorithm in hardware and software"
arch=(loong64 x86_64)
url="https://github.com/ICRAR/crc32c"
license=(
  BSD-3-Clause
  LGPL-2.1-or-later
)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('fea7723ebd4e9da447123cd3ecabba62626da4c1a3ab51c9087f712a36ff9770f4a090b7a33615b4b519c30f0075077a2096dcf3562247fca5665d746f61f6db')
b2sums=('89540473bf7efed62e6fb6db62de4cb71a9428c7e3ef4adbb3ff500287df69203abba29f0ffa98717896cd35197ce60e934453b881bf9c43c2b971953a96574e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.* -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
