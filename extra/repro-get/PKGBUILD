# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-get
pkgver=0.4.0
pkgrel=1
pkgdesc="Reproducible apt/dnf/apk/pacman, with content-addressing "
url='https://github.com/reproducible-containers/repro-get'
arch=('loong64' 'x86_64')
license=('Apache')
optdepends=('kubo: for ipfs support')
makedepends=('go')
source=(https://github.com/reproducible-containers/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('b1e3f126ab7e021e843e816bc1c3fe4fdbf59bfd57cd2631e397d92c853c72d4efa4268389f1a7b441aa18c5b0b3ea1017bf811915a66076b46ee640fd21bbbc')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X github.com/reproducible-containers/repro-get/pkg/version.Version=${pkgver}" \
    ./cmd/repro-get
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "${pkgname}" -t "${pkgdir}/usr/bin"

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
