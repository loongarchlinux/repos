# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziamath
pkgver=0.8.1
pkgrel=1
pkgdesc="Render MathML and LaTeX Math to SVG without Latex installation"
url="https://ziamath.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python-ziafont' 'python-latex2mathml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cdelker/ziamath/archive/$pkgver.tar.gz")
sha512sums=('b05f34d145ff3bc9e35046ead693d0257a9fa7d08a46956b8b97a76542d97f118b3b4ae4eb42a184b3bddf3aed665da59a7261760f06c32abacd78a4f50cbd1d')

build() {
  cd ziamath-$pkgver
  python -m build -nw
}

package() {
  cd ziamath-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
