# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=4.0.9
pkgrel=1
pkgdesc='JupyterLab computational environment'
arch=(any)
url='https://github.com/jupyterlab/jupyterlab'
license=(custom)
depends=(jupyter-notebook-shim
         jupyter-server
         python
         python-async-lru
         python-importlib-metadata
         python-ipykernel
         python-jupyter-core
         python-jupyterlab-server
         python-packaging
         python-tornado
         python-traitlets)
makedepends=(python-build
             python-hatch-jupyter-builder
             python-installer)
optdepends=('jupyter-collaboration: collaborative mode'
            'jupyterlab-widgets: ipywidgets support'
            'npm: to install extensions')
checkdepends=(npm
              python-jupyter-server-terminals
              python-pytest-jupyter
              python-pytest-tornasync)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('9ebada41d52651f623c0c9f069ddb8a21d6848e4c887d8e5ddc0613166ed5c0b')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
