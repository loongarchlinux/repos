# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=4.0.2
pkgrel=2
pkgdesc='JupyterLab computational environment'
arch=(any)
url='https://github.com/jupyterlab/jupyterlab'
license=(custom)
makedepends=(python-build python-installer python-hatch-jupyter-builder)
depends=(python-jupyterlab_server python-jupyter_packaging python-async-lru jupyter-notebook-shim nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support' 'jupyter-collaboration: collaborative mode')
checkdepends=(python-pytest-tornasync python-pytest-jupyter npm python-jupyter-server-terminals)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('0a77898aebb55da391e5f57022774c089fb075e98803ff3d514a79b727dc934d')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
