# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>

pkgname=gitlab
pkgver=16.1.0
pkgrel=2
pkgdesc='Project management and code hosting application'
arch=(loong64 x86_64)
url='https://gitlab.com/gitlab-org/gitlab-foss'
license=(MIT)
depends=(git
         gitlab-gitaly
         http-parser
         icu
         libxslt
         nodejs
         openssh
         openssl
         perl-image-exiftool
         re2
         redis
         ruby2.7)
makedepends=(cmake
             go
             nodejs
             postgresql
             yarn)
optdepends=('postgresql: database backend'
            'python-docutils: reStructuredText markup language support'
            'smtp-server: mail server in order to receive mail notifications')
backup=("etc/webapps/$pkgname/database.yml"
        "etc/webapps/$pkgname/gitlab.yml"
        "etc/webapps/$pkgname/resque.yml"
        "etc/webapps/$pkgname/puma.rb"
        "etc/webapps/$pkgname/smtp_settings.rb"
        "etc/logrotate.d/$pkgname")
options=(!buildflags !debug)
source=("git+$url.git#tag=v$pkgver"
        "${url%-foss}/-/commit/b899702f585290b6a64c4762bf2bf2dffbd114c5.patch"
         https://github.com/grpc/grpc/pull/33408/commits/ffd057b399c1f68d43a68b960dd9bdf7a29fdd09.patch
        "$pkgname-configs.patch"
        "$pkgname-environment"
        "$pkgname-puma.service"
        "$pkgname-sidekiq.service"
        "$pkgname-backup.service"
        "$pkgname-mailroom.service"
        "$pkgname-workhorse.service"
        "$pkgname-backup.timer"
        "$pkgname.target"
        "$pkgname.tmpfiles.d"
        "$pkgname.logrotate")
provides=(gitlab-workhorse) # FS78036
conflicts=(gitlab-workhorse)
replaces=(gitlab-workhorse)
install=gitlab.install
sha256sums=('SKIP'
            'e30ba2112a7fe30c8b4644d4f08373987dcdd51c595d07bf03445a9bd093fae5'
            '51b1048cfa90fc9cc58193092837e85d8a81d6e73d52d48608d1e091f114c3f0'
            'e6f9c11f7cccc1b7c689af5cd10aa8fdda46c484e5764964da91c9ee4b080fed'
            '8cc4d933743906b4213b8ea8d8c5a62535e27e4073f73581a5dad40078dde000'
            'f1cec302a551de5e06a2651a1d24f9697fa4f8be08eede65af6b4c5774476591'
            '5531cf40d749dd039eb358c19563a4b4aa9829887c6cb0bacaab9e5a3260d7cf'
            'ae6617b3a0d0808684cb1fff9cfbfa46f6f847baba61fdfa7ebf458cb2890e6f'
            'ff4496eaff0fb47aa0fa10ca08a08257291566a8dbe0566570f493053da04f6b'
            '7581d07e650c112a06af4a52ab9cf3bf0deb11564d734d5adabcdb537140f1c0'
            '869b3e682e9fb26551a19c0cd0b200a6fdb594396f325e237d58e1a8a8a96f73'
            '6c96a5d20c03bd626d9408cb1e41ab131d67610be586475af17c1e52e27ec697'
            '84614a2bfbd734f09c2c91531dd3c13e795186b50c0780a120c8e5bc2a892607'
            '13e4588b62ebaa6b410c2192cafbd2b9f2c99b8fff7b02782c2968c8256f762a')

_appdir=/usr/share/webapps/gitlab # the app source code location
_etcdir=/etc/webapps/gitlab
_datadir=/var/lib/gitlab # directory with gitlab data and it also $HOME for 'gitlab' user
_logdir=/var/log/gitlab

prepare() {
	cd gitlab-foss
	# Revert downgrade of gprc gem from v1.55.0 to v1.42.0 as the newer version is needed to build with gcc 13
	# The issue the revert addresses is patched later in build()
	patch -Rp1 -i ../b899702f585290b6a64c4762bf2bf2dffbd114c5.patch

	# GitLab tries to read its revision information from a file.
	git rev-parse --short HEAD > REVISION

	patch -p1 -i ../$pkgname-configs.patch
	# '/home/git' path in the config files indicates a default path that need to be adjusted
	grep -FqR '/home/git' config || exit 1

	cp config/gitlab.yml.example config/gitlab.yml
	cp config/database.yml.postgresql config/database.yml
	cp config/puma.rb.example config/puma.rb
	cp config/resque.yml.example config/resque.yml
	cp config/initializers/smtp_settings.rb.sample config/initializers/smtp_settings.rb

	echo "Setting up systemd service files ..."
	for service_file in gitlab-sidekiq.service gitlab-puma.service gitlab.logrotate gitlab-backup.service gitlab-mailroom.service; do
		sed -i "s|<DATADIR>|${_datadir}|g" "${srcdir}/${service_file}"
		sed -i "s|<APPDIR>|${_appdir}|g" "${srcdir}/${service_file}"
		sed -i "s|<LOGDIR>|${_logdir}|g" "${srcdir}/${service_file}"
	done
}

build() {
	cd gitlab-foss

	echo "Fetching bundled gems..."
	# Gems will be installed into vendor/bundle
	bundle-2.7 config build.gpgme --use-system-libraries # See https://bugs.archlinux.org/task/63654
	bundle-2.7 config force_ruby_platform true # some native gems are not available for newer ruby
	BUNDLER_CHECKSUM_VERIFICATION_OPT_IN=1 bundle-2.7 install --jobs=$(nproc) --no-cache --deployment --without development test aws kerberos

	# https://github.com/grpc/grpc/issues/33283
	patch -Np1 -i "${srcdir}"/ffd057b399c1f68d43a68b960dd9bdf7a29fdd09.patch -d vendor/bundle/ruby/2.7.0/gems/grpc-1.55.0

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	make -C workhorse

	yarn install --production --pure-lockfile
	bundle-2.7 exec rake gettext:compile RAILS_ENV=production NODE_ENV=production USE_DB=false SKIP_STORAGE_VALIDATION=true NODE_OPTIONS="--max_old_space_size=3584"
	bundle-2.7 exec rake gitlab:assets:compile RAILS_ENV=production NODE_ENV=production USE_DB=false SKIP_STORAGE_VALIDATION=true NODE_OPTIONS="--max_old_space_size=3584"
}

package() {
	depends+=('gitlab-shell')

	cd gitlab-foss

	install -d "${pkgdir}/usr/share/webapps"

	cp -r "${srcdir}"/gitlab-foss "${pkgdir}${_appdir}"
	# Remove unneeded directories: node_modules is only needed during build
	rm -r "${pkgdir}${_appdir}/node_modules"
	# https://gitlab.com/gitlab-org/omnibus-gitlab/blob/194cf8f12e51c26980c09de6388bbd08409e1209/config/software/gitlab-rails.rb#L179
	for dir in spec qa rubocop app/assets vendor/assets; do
		rm -r "${pkgdir}${_appdir}/${dir}"
	done

	chown -R root:root "${pkgdir}${_appdir}"
	chmod 755 "${pkgdir}${_appdir}"

	install -Dm755 "workhorse/gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
	install -Dm755 "workhorse/gitlab-zip-cat" "${pkgdir}/usr/bin/gitlab-zip-cat"
	install -Dm755 "workhorse/gitlab-zip-metadata" "${pkgdir}/usr/bin/gitlab-zip-metadata"

	install -dm750 -o 105 -g 105 "${pkgdir}${_datadir}"
	install -dm750 -o 105 -g 105 "${pkgdir}${_datadir}/satellites"
	install -dm750 -o 105 -g 105 "${pkgdir}${_datadir}/shared/"{,artifacts,lfs-objects}
	install -dm750 -o 105 -g 105 "${pkgdir}${_datadir}/builds"
	install -dm700 -o 105 -g 105 "${pkgdir}${_datadir}/uploads"
	install -dm750 -o 105 -g 105 "${pkgdir}${_datadir}/backups"
	install -dm755 -o 105 -g 105 "${pkgdir}${_etcdir}"
	install -dm755 -o 105 -g 105 "${pkgdir}${_logdir}"
	install -dm755 "${pkgdir}/usr/share/doc/gitlab"

	rm -r "${pkgdir}${_appdir}"/{.git,builds,tmp,log,shared}

	# Rails app hardcodes/configures by default that data is stored under $_appdir
	# Create symlinks that point to data directories under /var
	ln -fs "${_logdir}" "${pkgdir}${_appdir}/log"
	ln -fs "${_datadir}/builds" "${pkgdir}${_appdir}/builds"
	ln -fs "${_datadir}/uploads" "${pkgdir}${_appdir}/public/uploads"
	ln -fs "${_datadir}/shared" "${pkgdir}${_appdir}/shared"
	# The path to backups is configured in gitlab.yml, but the gitlab:backup rake
	# task writes a PID file in this directory (the path is hardcoded in
	# /usr/share/webapps/gitlab/lib/tasks/gitlab/backup.rake).
	# See https://bugs.archlinux.org/task/76630
	ln -fs /var/tmp "${pkgdir}${_appdir}/tmp"

	# TODO: workhorse and shell secret files are the application data and should be stored under /var/lib/gitlab
	ln -fs "${_etcdir}/gitlab_workhorse_secret" "${pkgdir}${_appdir}/.gitlab_workhorse_secret"
	ln -fs /etc/webapps/gitlab-shell/secret "${pkgdir}${_appdir}/.gitlab_shell_secret"

	# Install config files
	for config_file in gitlab.yml database.yml puma.rb resque.yml; do
		mv "config/${config_file}" "${pkgdir}${_etcdir}/"
		# TODO: configure rails app to use configs right from /etc
		ln -fs "${_etcdir}/${config_file}" "${pkgdir}${_appdir}/config/"
	done
	mv "config/initializers/smtp_settings.rb" "${pkgdir}${_etcdir}/"
	ln -fs "${_etcdir}/smtp_settings.rb" "${pkgdir}${_appdir}/config/initializers/smtp_settings.rb"

	# Install secrets symlink
	# TODO: ruby uses _appdir to load config files. Figure out if we can load files directly from /etc
	ln -fs "${_etcdir}/secrets.yml" "${pkgdir}${_appdir}/config/secrets.yml"

	# files with passwords/secrets are set world-unreadable
	for secret_file in smtp_settings.rb; do
		chmod 660 "${pkgdir}${_etcdir}/${secret_file}"
		# TODO: should we just leave the secret files root owned?
		chown root:105 "${pkgdir}${_etcdir}/${secret_file}"
	done

	install -Dm644 "${srcdir}/$pkgname-environment" "${pkgdir}${_appdir}/environment"

	# Install license and help files
	mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG.md PROCESS.md VERSION config/*.{example,postgresql} "${pkgdir}/usr/share/doc/gitlab"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/gitlab/LICENSE"

	# TODO: structure.sql looks more like an application data and should be stored under /var/lib/gitlab
	chown 105:105 "${pkgdir}${_appdir}/db/structure.sql"

	# Install systemd service files
	for service_file in gitlab-puma.service gitlab-sidekiq.service gitlab-backup.service gitlab-backup.timer gitlab.target gitlab-mailroom.service gitlab-workhorse.service; do
		install -Dm644 "${srcdir}/${service_file}" "${pkgdir}/usr/lib/systemd/system/${service_file}"
	done

	install -Dm644 "${srcdir}/gitlab.tmpfiles.d" "${pkgdir}/usr/lib/tmpfiles.d/gitlab.conf"
	install -Dm644 "${srcdir}/gitlab.logrotate" "${pkgdir}/etc/logrotate.d/gitlab"
}
