# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libnumbertext
pkgver=1.0.6
pkgrel=1
pkgdesc="Number to number name and money text conversion library"
arch=('x86_64')
url="https://github.com/Numbertext/libnumbertext"
license=('LGPL3' 'custom')
depends=('gcc-libs')
source=(https://github.com/Numbertext/libnumbertext/archive/$pkgver.tar.gz)
sha256sums=('d03860c98ec01bc52a2a65f04b1fc4c7e7823c76d54dec800ca32ab2196023bc')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # add dual license file
  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 COPYING
}
