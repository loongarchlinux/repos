# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=corrosion
pkgver=0.4.2
pkgrel=1
pkgdesc='Tool for integrating Rust into an existing CMake project'
arch=(loong64 x86_64)
url='https://github.com/corrosion-rs/corrosion'
license=(MIT)
depends=(rust cmake)
source=(https://github.com/corrosion-rs/corrosion/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('16cd5c4b29a859790c446c4c7aecea21140fd06657c016672746020e2019841e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCORROSION_BUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
