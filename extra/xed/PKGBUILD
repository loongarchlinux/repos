# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.4.5
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=(loong64 x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(gspell gtksourceview4 libpeas xapp)
makedepends=(meson samurai gobject-introspection intltool itstool)
optdepends=('python: required for some plugins')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        libxml-2.12.patch)
sha256sums=('a3bb70759d85ae84b69db40ac3c6f954d59ab3c66325ae23050c9ffbf6a9c26b'
            'ed19b6f419a4f703d290d8c97ed5d904c88a90fc475c738a0e65f7ab31697c40')
b2sums=('936e6f790234bd5b1a3f90f807e542001761265943e555557e39fca5e7d9e22d0653b5e0f87a7b954c8cff831a992f6daefe331a3a55e6b3c9ed7f930ca75d7e'
        'bd3f1c73b5873b05db1a734d2b42025d460f6862dbb13813e3fc97602c43e6758e2857f2e2db14e739780a7d58fdb48e87186cfde4393b7799070def14750353')

prepare() {
  patch -d $pkgname-$pkgver -p1 < libxml-2.12.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
