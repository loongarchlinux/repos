# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=6.2.30
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'qtdbusextended' 'dtkcore' 'dtkgui'
         'dtkwidget' 'qt5-base' 'glib2' 'gcc-libs' 'icu' 'libpulse' 'kcodecs' 'qt5-multimedia'
         'qtmpris' 'ffmpeg' 'taglib' 'udisks2-qt5' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-music/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('576d03b2847a93fca0e68bdfa999a0802f7905c66b2010fab13c5ce8908e036fa0986b6d05b30aa0bb6dbf08af60766a71a77d9f573d29b5060bad54d7b48247')

build() {
  cd deepin-music-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-music-$pkgver
  DESTDIR="$pkgdir" ninja install
}
