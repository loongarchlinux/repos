# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-75dpi
pkgver=1.0.3
pkgrel=8
pkgdesc="X.org 75dpi fonts"
arch=(any)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-alias-75dpi')
makedepends=('xorg-util-macros' 'xorg-mkfontscale' 'xorg-bdftopcf' 'xorg-font-util')
groups=('xorg')
source=(${url}/releases/individual/font/font-adobe-75dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-adobe-utopia-75dpi-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-bh-75dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bh-lucidatypewriter-75dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bitstream-75dpi-1.0.3.tar.bz2)
md5sums=('6c9f26c92393c0756f3e8d614713495b'
         'e99276db3e7cef6dccc8a57bc68aeba7'
         '565494fc3b6ac08010201d79c677a7a7'
         'f6d65758ac9eb576ae49ab24c5e9019a'
         'd7c0588c26fac055c0dd683fdd65ac34')
sha256sums=('c6024a1e4a1e65f413f994dd08b734efd393ce0a502eb465deb77b9a36db4d09'
            '8732719c61f3661c8bad63804ebfd54fc7de21ab848e9a26a19b1778ef8b5c94'
            '3486aa51ac92c646a448fe899c5c3dae0024b1fef724d5100d52640d1cac721c'
            '4ac16afbe205480cc5572e2977ea63488c543d05be0ea8e5a94c845a6eebcb31'
            'ba3f5e4610c07bd5859881660753ec6d75d179f26fc967aa776dbb3d5d5cf48e')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/75dpi
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-75dpi-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/75dpi/fonts.*
}
