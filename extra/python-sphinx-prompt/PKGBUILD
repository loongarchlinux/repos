# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-prompt
pkgver=1.7.0
pkgrel=1
pkgdesc='Sphinx directive to add unselectable prompt'
arch=('any')
url='https://github.com/sbrunner/sphinx-prompt'
license=('BSD')
depends=(
  'python'
  'python-sphinx'
  'python-pygments'
  'python-docutils'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
_commit='8f375dd5e2af9de39cebdcaf8d9f68efc6d4f77b'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'pyproject.toml-template.patch'
  'remove-unnecessary-dependencies.patch'
)
b2sums=('SKIP'
        'b84be01426ca3463a80cf3e179e7f2eb3a15bc5a41dbf2a47f9b7ca9f50dd0c60a05bde8966cb404717a3035fefd5d054101342cbe0d27a614d3c93ae52fd811'
        '1552cd699b1f273f6bd0b6bf59ad23341285e645b28fee805f122b9babb7d6facb0b501825ae6f54be472b3325b7c07fa637d183c0f08ff8e15672056794e28b')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # manually set version
  patch -p1 -i "$srcdir/pyproject.toml-template.patch"

  # remove useless dependencies
  patch -p1 -i "$srcdir/remove-unnecessary-dependencies.patch"
}

build() {
  cd "$pkgname"

  # set version
  sed -e "s/@PKGVER@/$pkgver/" -i pyproject.toml

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/sphinx_prompt-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
