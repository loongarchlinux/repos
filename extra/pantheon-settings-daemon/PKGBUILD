# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=1.3.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/settings-daemon.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  fwupd
  geoclue
  glib2
  granite
)
makedepends=(
  git
  meson
  vala
)
_tag=d68bed27e3d95707c00f22c6c58e71a700f74d4e
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
