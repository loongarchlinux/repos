# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has a "rolling release" where you should follow git HEAD
_commit=41fb94defa8f830ce69a8122b03f6ac3216d392a
# The patch version is the timestamp of the above git commit, obtain via `git show -s --format=%ct`
_ct=1693350652
pkgver="2.1.${_ct}"
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('loong64' 'x86_64')
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://repo.or.cz/luajit-2.0.git/snapshot/${_commit}.tar.gz")
md5sums=('1fe40d7b9449c99e8015793b41f818b0')
sha256sums=('8036b6a7078385f3db5c6280cdcbfa5790189349b542f26a10d8d5ed87adaa58')
b2sums=('34948ecd0735a78f4fdfd4dea38c1f1f451c58a90a7794a0e6ee2b11e19ac5fce76fc451d13b08424814713f3a846faf516751a97a6fa585b751f19811f608ee')

build() {
  cd "luajit-2.0-${_commit::7}"

  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

check() {
  cd "luajit-2.0-${_commit::7}"

  # Make sure that _ct was updated
  test "${_ct}" == "$(cat .relver)"
}

package() {
  cd "luajit-2.0-${_commit::7}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
