# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has a "rolling release" where you should follow git HEAD
_commit=54ef81f864e18729f6dafb6b1c9f9176fcb67bb5
# The patch version is the timestamp of the above git commit, obtain via `git show -s --format=%ct`
_ct=1692616192
pkgver="2.1.${_ct}"
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('loong64' 'x86_64')
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://repo.or.cz/luajit-2.0.git/snapshot/${_commit}.tar.gz")
md5sums=('fcb8280cc1d5f92bfa055d913b7b05b4')
sha256sums=('c0cd956f0d1e54af216b94f797e0b485d5aa1e30b5d405b8b6c2e7602d5f211e')
b2sums=('2eeadd0112590987bb1146e273d6b07c487571985446d2d64ff794c53a986a868015531b81890441190ae56946b419f727383f4bc41ba9d0df320ae4ff76fa62')

prepare() {
  cd "luajit-2.0-${_commit::7}"

  echo "${_ct}" > .relver
}

build() {
  cd "luajit-2.0-${_commit::7}"
  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

package() {
  cd "luajit-2.0-${_commit::7}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"

  ln -sf "luajit-2.1.${_ct}" "$pkgdir/usr/bin/luajit"
}
