# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=simde
pkgver=0.7.2
pkgrel=1
pkgdesc="Implementations of SIMD instruction sets"
arch=(any)
url="https://github.com/simd-everywhere/simde"
license=(MIT)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::https://github.com/simd-everywhere/simde/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e4ee5a4a7e8453f116f1325d147f91d358a300f41ea5566ea30fc19649bababf3b87e3dc838d5608b578bd152207df4156200ffc7fb98141bc7c0bb60c75f1f5')
b2sums=('37e1f8715bbdde1b082f13b2f778c3fe57653cfe2c70f90b09cae6da74729f0ab2c8c5b91f6d90e4078f2a7cde4b06aacdcdcaba2c86b0ae16f738d87c9dfb3e')

build() {
  arch-meson build "$pkgname-$pkgver"
  ninja -vC build
}

package() {
  DESTDIR="$pkgdir/" meson install -C build
  install -vDm 644 "$pkgname-$pkgver/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "$pkgname-$pkgver/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
