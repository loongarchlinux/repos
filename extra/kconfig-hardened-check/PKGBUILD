# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=kconfig-hardened-check
pkgver=0.6.1
pkgrel=3
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kconfig-hardened-check"
arch=(any)
license=(GPL3)
depends=(python)
makedepends=(python-setuptools python-build python-installer python-wheel)
source=(https://github.com/a13xp0p0v/kconfig-hardened-check/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('0d4885c4cdeb6351ce1c2e663e4373e9bcd7fe9ae7a58eaa3558040f7edc5467')
b2sums=('fdf67fffa7b72f7d1f75d4e6ccced060b2232335bad8ee6d53114257131b10ab7092667cf772491507e4514177ea1adc67be5db35b85bbe69152befa22aef0e4')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/docs/$pkgname"
}
