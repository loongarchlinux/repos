# Maintainer: David Runge <dvzrv@archlinux.org>

_upstream_name=cyclonedx-rust-cargo
pkgname=cargo-cyclonedx
pkgver=0.4.1
pkgrel=1
pkgdesc="Creates CycloneDX Software Bill of Materials (SBOM) from Rust (Cargo) projects"
arch=(loong64 x86_64)
url="https://github.com/CycloneDX/cyclonedx-rust-cargo"
license=(Apache-2.0)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgname-$pkgver.tar.gz)
sha512sums=('8bab9031a179bd5d0fbcb9f30013efbd1fc5073bef881a108d0f4b91aab93673e759a5578a367f4ccd056f039f7ead21ecb965561116caf283f26d940b0bb36f')
b2sums=('c1907710867b3c1342cc5c9661a095c5c4f62c52a8284eccba8ff71398d933667924df645ba0fa287e947845d8eb8e299c810756a5f94a6dd991733f79dbeb45')

prepare() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # NOTE: use different target dir to have reproducible executable
  export CARGO_TARGET_DIR=target_test
  cargo test --frozen --all-features --workspace
}

package() {
  cd $_upstream_name-$pkgname-$pkgver
  install -vDm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
