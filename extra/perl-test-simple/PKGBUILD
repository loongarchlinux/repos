# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-simple
pkgver=1.302196
pkgrel=1
pkgdesc="Basic utilities for writing tests"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Simple"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test-Simple-$pkgver.tar.gz")
sha512sums=('a08b51e306dedd0da09f68fc0cd6d67d4b1c99b72adcbc5713649fb8a3f0017b26b1d08e8618799d44039d653e833f5179a0d7de5c42b5818c7be8f30d454bcf')

build() {
  cd Test-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Simple-$pkgver
  make test
}

package() {
  cd Test-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
