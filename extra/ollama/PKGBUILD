# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Matt Harrison <matt@harrison.us.com>

pkgname=ollama
pkgdesc='Create, run and share large language models (LLMs)'
pkgver=0.0.16
pkgrel=1
arch=(loong64 x86_64)
url='https://github.com/jmorganca/ollama'
license=(MIT)
makedepends=(go)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('a22f7c481f3713803bd625494a93f0d6300ad7756eac66eab595e277be8258afd603ef2c77e8bc55a2fe0487903e3c380d5574234a9a810f2b29ede138c573de')

build() {
  cd $pkgname-$pkgver
  GOFLAGS='-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw' \
    CGO_CFLAGS="$CFLAGS" \
    CGO_CPPFLAGS="$CPPFLAGS" \
    CGO_CXXFLAGS="$CXXFLAGS" \
    CGO_LDFLAGS="$LDFLAGS" \
    go build
}

check() {
  cd $pkgname-$pkgver
  go test ./...
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
