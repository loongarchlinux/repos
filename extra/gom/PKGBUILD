# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.4
pkgrel=7
pkgdesc='GObject to SQLite object mapper library'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Projects/Gom'
license=('LGPL')
depends=('glib2' 'glibc' 'sqlite')
makedepends=('gdk-pixbuf2' 'gobject-introspection' 'gtk-doc' 'meson' 'python-gobject')
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('68d08006aaa3b58169ce7cf1839498f45686fba8115f09acecb89d77e1018a9d')
 
build() {
  arch-meson $pkgname-$pkgver build -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
