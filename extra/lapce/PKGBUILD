# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=lapce
pkgver=0.3.0
pkgrel=1
pkgdesc='Lightning-fast and Powerful Code Editor'
arch=(loong64 x86_64)
url=https://lapce.dev
_url="https://github.com/$pkgname/$pkgname"
license=(Apache)
depends=(expat
         fontconfig
         freetype2
         gcc-libs
         gtk3
         libxcb
         libxkbcommon)
makedepends=(cargo
             python
             cmake)
options=('!lto')
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('0940cdc3d40c298efec7b09e5cab7b44bdf64eecaa0ee5ac2baac53b56bdaf52')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	# See https://github.com/lapce/lapce/pull/2374 and https://bugs.archlinux.org/task/78922
	export RELEASE_TAG_NAME="v$pkgver"
	cargo build --frozen --profile release-lto --all-features
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release-lto/$pkgname"{,-proxy}
	local lname=dev.lapce.lapce
	install -Dm0644 -t "$pkgdir/usr/share/applications/" extra/linux/$lname.desktop
	install -Dm0644 -t "$pkgdir/usr/share/metainfo/" extra/linux/$lname.metainfo.xml
	install -Dm0644 extra/images/logo.png "$pkgdir/usr/share/pixmaps/$lname.png"
}
