# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2023.07.02
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(loong64 x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
makedepends=(python python-setuptools swig)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('1A3C7F70E08FAB1707809BBF147C39FF9634B72C')
options=(!lto)
sha1sums=('c91ee79635c55721fa00fde9bba7ef6e4fd39f74'
          'SKIP')
sha256sums=('6b6a48581c14abb0f95bd87c1af4d740922406d7b801002a9f94727fdde021d5'
            'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all KBUILD_HOSTLDFLAGS=${LDFLAGS}
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ u-boot-$pkgver/doc/{mkimage,dumpimage}.1
}
