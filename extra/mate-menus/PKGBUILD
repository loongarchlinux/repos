# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-menus
pkgver=1.26.1
pkgrel=1
pkgdesc="MATE menu specifications"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL' 'LGPL')
depends=('glib2')
makedepends=('gobject-introspection')
groups=('mate')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('458d599ae5b650c7d21740f9fe954c4a838be45ed62ab40e20e306faf5dd1d8c')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --enable-python

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
