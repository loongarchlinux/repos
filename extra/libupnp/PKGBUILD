# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.7
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url="https://pupnp.sourceforge.io/"
arch=('loongarch64' 'x86_64')
license=('BSD')
depends=('glibc')
provides=('libixml.so' 'libupnp.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz")
sha512sums=('92dd80d1bbaa64e6d3909c5bce73d0340fcd3a3c1d247a634bd48ec077ba0ce0b8224d1a670a66f6a8c591e31a54f15526071d62ae5460e85d582d7b6b67c674')
b2sums=('539388737dda0dd037cb50fbc1cca8bcdc43d1d706bdb7da19d1a184cfd61f3d585d9beef2a9c8c33f2cb2640a8479210ad6c358abd7193f304509df574e912c')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
