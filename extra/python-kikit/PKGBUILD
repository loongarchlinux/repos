# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=KiKit
pkgname=python-kikit
pkgver=1.4.0
pkgrel=1
pkgdesc='Automation tools for KiCAD'
arch=('any')
url='https://github.com/yaqwsx/KiKit'
license=('MIT')
depends=('python' 'kicad' 'python-numpy' 'python-shapely' 'python-click' 'python-markdown2'
         'python-pybars3' 'python-solidpython' 'python-pcbnewtransition' 'python-commentjson')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('739b584a213b34d2ceaeb5223dc5530d2e5438cd04d50bd5d08feca186a21add3f29226a23edf30ec7db6d468ea2b6c88a209e5d6115c83789b7d4db3493f2f7')

prepare() {
  sed -i '/versioneer/d' $_pkgname-$pkgver/setup.py
}

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

# vim:set ts=2 sw=2 et:
