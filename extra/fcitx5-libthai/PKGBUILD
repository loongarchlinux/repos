# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-libthai
pkgver=5.1.1
pkgrel=1
pkgdesc="Libthai Wrapper for Fcitx5"
arch=('loong64' 'x86_64')
url="https://github.com/fcitx/fcitx5-libthai"
license=('GPL')
depends=('fcitx5' 'libthai')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('5c74002fab1940b7545865d2e9c72603848c68213db0e9782087d072b348478ab5a5ebe66187c5153590590f179246f4c9aa7ce2ed4e8c90cc933bff6b363416'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
