# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Sieweck

pkgname=cvc4
_pkgname=cvc5
pkgver=1.8
pkgrel=3
pkgdesc="An automatic theorem prover"
arch=('loong64' 'x86_64')
url="https://cvc5.github.io/"
depends=('libantlr3c' 'cln')
makedepends=('cmake' 'java-runtime' 'python-toml')
optdepends=('readline: for an improved interaction support')
license=('GPL' 'BSD' 'MIT' 'custom:HPND')
source=("https://github.com/cvc5/cvc5/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        "https://www.antlr3.org/download/antlr-3.4-complete.jar"
        cvc4-fix-headers.patch
        cvc4-gcc7.patch)
noextract=('antlr-3.4-complete.jar')
sha512sums=('ae7be9856c22f430bffdf686dca3351372537a3887ccb36cc739312fd175686f76bc3783882d0c03c69cbbf039dcb4f561aa53a539dcb885bab981c1dbdd8c41'
            '04be4dfba3a21f3ab9d9e439a64958bd8e844a9f151b798383bd9e0dd6ebc416783ae7cb1d1dbb27fb7288ab9756b13b8338cdb8ceb41a10949c852ad45ab1f2'
            'f6a60c2b95c348f7e8abffc79befef0910ff962143b0b5f89f9b3c9798fff575a9bfc71ae8271c9e56e2f5e400539879b440df23aef7c23664eaec9cd5d6fe10'
            'ff5d60141f97394b625c336614afa5a6429d775bed12e277ad1bc64b84f865a958fb075185fb9726ea8ca443b55aedb1a8d114840f016ed8e639dbcf77aa3783')

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../cvc4-fix-headers.patch
  patch -p1 -i ../cvc4-gcc7.patch

  mkdir -p antlr-3.4/bin
  tee antlr-3.4/bin/antlr3 <<EOF
#!/bin/bash
export CLASSPATH=$srcdir/antlr-3.4-complete.jar:\$CLASSPATH
exec java org.antlr.Tool "\$@"
EOF
  chmod a+x antlr-3.4/bin/antlr3
}

build() {
  cd $_pkgname-$pkgver
  export PATH="$PATH:$PWD/antlr-3.4/bin"
  ./configure.sh --prefix=/usr --cln --gpl
  cd build
  make
}

check() {
  cd $_pkgname-$pkgver
  make -C build check
}

package() {
  cd $_pkgname-$pkgver
  make -C build DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/$_pkgname-$pkgver/COPYING" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
