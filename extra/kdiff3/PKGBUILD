# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kdiff3
pkgver=1.10.6
pkgrel=1
pkgdesc='A file comparator/merge tool'
arch=(loong64 x86_64)
url='https://kde.org/applications/development/kdiff3/'
license=(GPL)
depends=(gcc-libs
         glibc
         kconfig5
         kcoreaddons5
         kconfigwidgets5
         kcrash5
         ki18n5
         kio5
         kparts5
         kwidgetsaddons5
         kxmlgui5
         qt5-base)
makedepends=(boost
             extra-cmake-modules
             kdoctools5)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1333aef9d6636c6b3466a17fd2c5d96e919d4eb521ea2b235282541136aefb31'
            'SKIP')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
