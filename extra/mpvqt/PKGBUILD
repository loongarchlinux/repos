# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mpvqt
pkgver=1.0.0
pkgrel=1
pkgdesc='A libmpv wrapper for QtQuick2 and QML'
license=(LGPL)
arch=(loong64 x86_64)
url='https://invent.kde.org/libraries/mpvqt'
depends=(gcc-libs
         glibc
         mpv
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9131d2a925e5f33e19b9d081dfd5f30d576abd87464d67c70bef41a486f54eb9'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
