# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=21.04.2
pkgrel=1
pkgdesc='KDE Personal Information Manager'
arch=(loongarch64 x86_64)
url='https://apps.kde.org/kontact/'
license=(GPL2)
depends=(kontactinterface kmail-account-wizard)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c3b10078f2ea54b3368f8a55ddcefaef1c5f6785727dd626bff0af101bc52313'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
