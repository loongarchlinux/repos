# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mpfi
pkgver=1.5.4
pkgrel=4
pkgdesc='C library for interval arithmetic'
arch=(loong64 x86_64)
url='http://perso.ens-lyon.fr/nathalie.revol/software.html'
license=(GPL)
depends=(mpfr)
source=(https://github.com/arpra-project/mpfi/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('32e6ad529c97aa5ce03e28d01c921d1bce1a464fb4c57fbc248d7be21e652782')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared 
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
