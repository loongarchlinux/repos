# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.21
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-build-on-dtk-5.6.20.patch::https://github.com/linuxdeepin/deepin-compressor/pull/151.patch)
sha512sums=('cb3d02935032c1867b11c032316e721b43da47c7acda3e52975b8b8c987240fd13df5989b0dcc60a31bc5904866e4324494f0f012951d6d3d60f06f97e6c4dc3'
            'c58c39a6355bb2292b333c6e7eb5abf1d98ea657c920b9287b3207ef38e16c6414ed8c77b2722fca91acfff0629c574e41eef01100154efcb8b3d5db70bc11ed')

prepare() {
  cd deepin-compressor-$pkgver
  patch -p1 -i ../$pkgname-fix-build-on-dtk-5.6.20.patch
}

build() {
  cd deepin-compressor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-compressor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
