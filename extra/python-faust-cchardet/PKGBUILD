# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-faust-cchardet
pkgver=2.1.18
pkgrel=1
pkgdesc="Fork of the original cChardet"
url="https://pypi.org/project/faust-cchardet/"
arch=(loong64 x86_64)
license=(
  GPL
  LGPL
  MPL
)
depends=(
  python
)
makedepends=(
  cython
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-wheel
)
provides=("python-cchardet=$pkgver")
conflicts=(python-cchardet)
source=(
  https://files.pythonhosted.org/packages/source/f/faust-cchardet/faust-cchardet-$pkgver.tar.gz
)
sha256sums=('d374eecad23c68383e549c83e90b16788bde6b0354fd5ece4020b046df5a7c7e')

prepare() {
  cd faust-cchardet-$pkgver
}

build() {
  cd faust-cchardet-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd faust-cchardet-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set sw=2 sts=-1 et:
