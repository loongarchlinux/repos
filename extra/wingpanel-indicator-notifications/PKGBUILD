# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-notifications
pkgver=7.0.0
pkgrel=1
pkgdesc='Notifications indicator for Wingpanel'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-notifications
license=(GPL3)
groups=(pantheon)
depends=(
  dbus-glib
  glib2
  gtk3
  libdbus
  libgranite.so
  libhandy-1.so
  libwingpanel.so
  libwnck3
)
makedepends=(
  git
  meson
  vala
)
_tag=db62ed845b3560a81480e2d60cc158de896237c0
source=(git+https://github.com/elementary/wingpanel-indicator-notifications.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-notifications

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
