# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=python-rdflib
pkgver=6.1.1
pkgrel=1
pkgdesc="A Python library for working with RDF, a simple yet powerful language for representing information"
arch=('any')
url="http://rdflib.net/"
license=('BSD')
depends=('python-isodate' 'python-pyparsing')
optdepends=('python-html5lib: for html support'
            'python-requests: for sparql support')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-six' 'python-html5lib' 'python-requests' 'python-networkx'
              'python-doctest-ignore-unicode')
source=(rdflib-$pkgver.tar.gz::https://github.com/RDFLib/rdflib/tarball/$pkgver)
sha512sums=('2381ff71b09ec31a5802fac62ec161d89ec25f695981821541d0b959f803442442e3f6eac14a60722012536e02979cc53d55be42d931024c4d09606e75575163')

build() {
  cd RDFLib-rdflib-*
  python setup.py build
}

check() {
  cd RDFLib-rdflib-*
  pytest
}

package() {
  cd RDFLib-rdflib-*
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644  LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
