# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-conduit
pkgname=haskell-http-conduit
pkgver=2.3.8.2
pkgrel=3
pkgdesc="HTTP client package with conduit interface and HTTPS support"
url="https://www.yesodweb.com/book/http-conduit"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-resourcet'
         'haskell-unliftio-core')
makedepends=('ghc' 'haskell-blaze-builder' 'haskell-case-insensitive' 'haskell-cookie'
             'haskell-crypton-connection' 'haskell-data-default-class' 'haskell-hunit'
             'haskell-hspec' 'haskell-lifted-base' 'haskell-network' 'haskell-streaming-commons'
             'haskell-temporary' 'haskell-unliftio' 'haskell-utf8-string' 'haskell-wai'
             'haskell-wai-conduit' 'haskell-warp' 'haskell-warp-tls')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2cb1372e065efc84209519ecd227064ea235bb11351fd89b31312146aecf10d7df32baf475e79db36a9169cd7adb8c16f99bc77403c42fa6592743c0aafa0852')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
