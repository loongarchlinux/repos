# Maintainer: Campbell Jones <arch at serebit dot com>

pkgname=waycheck
pkgver=1.0.0
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("loong64" "x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("https://gitlab.freedesktop.org/serebit/$pkgname/uploads/bf8ae524f86e36b7c51bcbcc5b811a10/${pkgname}-v${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/serebit/$pkgname/uploads/1a13a2b066c8514a06de7d8d2e21a374/${pkgname}-v${pkgver}.tar.xz.asc")
b2sums=("a1137766082b0def57152b217f2e521b3d303e7ec51adc12c058c3d01611d2453954becd0256a1fe2dd8175da21497d3f6c77b8679888f5a40642425d2c0dd90"
        "SKIP")
validpgpkeys=("8A59B135826B2DA453B9707408AD7343E6BD4B56")

build() {
	arch-meson "$pkgname-$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
