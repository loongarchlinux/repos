# Maintainer: Campbell Jones <arch at serebit dot com>

pkgname=waycheck
pkgver=0.2.0
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("loong64" "x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("https://gitlab.freedesktop.org/serebit/$pkgname/uploads/d7f0da18d012f5bdfb773b3a9f47194c/${pkgname}-v${pkgver}.tar.xz"
        "https://gitlab.freedesktop.org/serebit/$pkgname/uploads/827c37a88ac6f0c92322b5224ebe90ad/${pkgname}-v${pkgver}.tar.xz.asc")
b2sums=("c164f7dde4c2791ed09d3c94d72b5d65abe8b924b8666475bb476f67c438e90306917ce120746caa212b21c277b23cbcc1b0d50561e1526e98bea2b7e9f15f93"
        "SKIP")
validpgpkeys=("8A59B135826B2DA453B9707408AD7343E6BD4B56")

build() {
	arch-meson "$pkgname-$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
