# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.14.2
pkgrel=2
pkgdesc="A desktop manager for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck3' 'exo' 'thunar' 'garcon' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        fix-memory-leak-when-reconnecting-DP-monitor.patch)
sha256sums=('a30534461fea907f969f608a11c84be0b1aaad687c591c32cd56a9d274ea3e74'
            'e6e14ec1d3fd594b0003b3724cc039d882c5f85ee0d9ece7c873d57310e8aec9')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../fix-memory-leak-when-reconnecting-DP-monitor.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
