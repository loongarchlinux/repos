# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=2.1.0
pkgrel=1
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('loong64' 'x86_64')
url='https://duplicity.gitlab.io/'
license=('GPL')
depends=(
  'gnupg'
  'librsync'
  'python-paramiko'
  'python-fasteners'
  'python-future'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
optdepends=(
  'lftp: backend for ftp, ftps, fish'
  'ncftp: alternative backend for ftp'
  'python-b2sdk: Backblaze B2 backend'
  'python-boto3: Amazon S3 and Google Cloud Storage backend'
  'python-dropbox: Dropbox backend'
  'python-gobject: GIO backend'
  'gvfs: GIO backend'
  'python-pydrive2: Google Drive backend'
  'python-mediafire: Mediafire backend'
  'python-requests-oauthlib: OneDrive backend'
  'rsync: rsync backend'
)
_commit='80cba3758f6e3562697bb6cc042ca1ae7df0eca4'
source=(
  "$pkgname::git+https://gitlab.com/duplicity/duplicity#commit=$_commit"
  'fix-documentation-directory.patch'
)
b2sums=('SKIP'
        'ca3e295183f6a484f1483bcd20b537c02d37b9190469c36dd21a9fa53510e5ab3f5bf9b01a5b9ce7e251bfdd418bcc32a448cc94fede1ed4201fb5f063034f0f')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^rel\.//'
}

prepare() {
  cd "$pkgname"

  # move documentation to the correct directory
  patch -p1 -i "$srcdir/fix-documentation-directory.patch"
}

build() {
  cd "$pkgname"

  # FS#76307, FS#79399 - ensure version is correct
  sed -e "s:\$version:$pkgver:" -i "$pkgname/__init__.py"

  SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver" python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
