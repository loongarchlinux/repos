# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.3.0
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/archlinux/archinstall"
license=('GPL3')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
provides=('python-archinstall')
conflicts=('python-archinstall')
replaces=('python-archinstall')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha512sums=('9a2cd808e90ec39df328b2cfec4f499201bc3962cf743002c0ddd941811e943e1b04edc06e4b874dac8edbec308e44dfce857d388bcc43a1bf05030285daf0ae'
            'SKIP')
b2sums=('748bfabc9e51fe18ad23be76d6f7c4f005e9fa35ef53f3f21a73ae934f290b47309675bc3118d0b3014a8068c756054b6312817b84ff9d62ec377267cee496a1'
        'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  make man -C docs
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -vDm 644 docs/_build/man/archinstall.1 -t "${pkgdir}/usr/share/man/man1/"
}
