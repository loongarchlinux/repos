# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.7.1
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  e2fsprogs
  glibc
  kbd
  pciutils
  procps-ng
  python
  python-pyparted
  python-simple-term-menu
  python-sphinx_rtd_theme
  systemd
  util-linux
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=('b88a301ff747f10f5b2e23b8c9217f28b54a5846123580eba06d1960a64e56357ec85414f3c0ce8b641cc2038fd5244608bdbac3ee00bef0a55928a51add0c05'
            'SKIP')
b2sums=('105b297c649e08edb67e36f1675a65c4b9a930cee154e7bdf9cde8d0a5af25ca8045f31fe2cdcccc076dc278dd9f952fbc03fe57f7436118e623c96cddcc8a4a'
        'SKIP')
validpgpkeys=('8AA2213C8464C82D879C8127D4B58E897A929F2E') # torxed@archlinux.org

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
