# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.5.8
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools-scm')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('ab2f413e399810e98bd61c837ac90674e7c6d8e2f4d32f3141b21bf1360c785831fc4afd347e755c97627a04cb482aa4e455897e1c02eec8c5fcd28c54c1e9fc')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-$pkgver
  pytest
}

package() {
  cd astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
