# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=python-fastimport
pkgver=0.9.12
pkgrel=1
pkgdesc='VCS fastimport/fastexport parser'
arch=(any)
url=https://github.com/jelmer/python-fastimport
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=7359e3fba62baf2c0d1af6bf9a4b6aba3b068bd4
source=(git+https://github.com/jelmer/python-fastimport.git?signed#tag=${_tag})
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>
sha256sums=(SKIP)

pkgver() {
  cd python-fastimport
  git describe --tags | sed 's/fastimport-//'
}

build() {
  cd python-fastimport
  python setup.py build
}

package() {
  cd python-fastimport
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
