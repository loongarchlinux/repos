# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-app-static
pkgname=haskell-wai-app-static
pkgver=3.1.8
pkgrel=51
pkgdesc="WAI application for static serving"
url="https://www.yesodweb.com/book/web-application-interface"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-crypton'
         'haskell-file-embed' 'haskell-http-date' 'haskell-http-types' 'haskell-memory'
         'haskell-mime-types' 'haskell-old-locale' 'haskell-optparse-applicative'
         'haskell-unix-compat' 'haskell-unordered-containers' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp' 'haskell-zlib')
makedepends=('ghc' 'haskell-hspec' 'haskell-mockery' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('19268c7f7cbdb8b3e8233856f27d2b99ae30b5cffcad6bdb99c9b680fb1128ee9dd248a24568cec693399f339fa4e885167996583a709478cf9d9a257e62a6cc')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-print
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
