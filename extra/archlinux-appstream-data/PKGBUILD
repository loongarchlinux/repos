# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20211210
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('81bdaf023e7763099a776b1f6e8e368d79461bc40deca562e0ceafbebc641dff'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'd2664603921fb6fb50815306d3d2b5538df6a7619e2e203a8c0ae43272b6011c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'd602b6abef1599f9c1a5fdbd4422c65fa19c8bacf9377c91e837706ba922e8c6'
            '6843831004ed50f60d9e1f8556183d5b15ca1275086fa15d6a0566cdc3ecb9f0'
            'ffa2f68bc942c787ccdcbbb1a69005879bdd04ec2587c2cdb2e2528c3e9fc55e'
            '61841a69d79059b29bbe9f8fe109cabe2812be0dcd5e6a1aaad861d33f458fff'
            '7748843f97c6103d4e0924d981eb1e772919dda5d3691748002e83e626bfb097'
            'c92036a689c8c0524dd932843276ab39ea1a0fd17856749829a1040bbdf6a407'
            '4cb1f510dc3a74845d9ddae6a6ec165a744caa2aee4529e3b1d94a57fbea4b5b'
            'cadc4b2bad4cd4d2481b7acdbd70970e37a5a2a67da66d130b55d30ed771dc43'
            '1ca579ff12cc527be544b32c1e465d42edc73620a72d7b37511162a5a89ef67c'
            '53498ac465e879d2c79b8e005e92d52c1fd84fe3be5a416bbec8d6c2da587661'
            '6e8358fe4ac10febbbc57534b43aa0b55f573430fdefb1b3cf9b98c19a338523'
            '737481a72745446c5c878c37480013f88b23c587729b3e53bdc3fd15385ae531')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community,multilib}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
