# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.88
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-file-find-rule' 'perl-test-inter' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-$pkgver.tar.gz")
sha512sums=('49c571b1f9a69c241700ba327385ec5c44e2868e9e23b167e03cc90e4779c915ba5468a612e125b9a2cca37ab5249192c01d539a6c813313ab81ba5b4d606b0c')

build() {
  cd Date-Manip-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-$pkgver
  make test
}

package() {
  cd Date-Manip-$pkgver
  make DESTDIR="$pkgdir" install
}
