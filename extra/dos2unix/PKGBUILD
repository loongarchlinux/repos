# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Renato Garcia <fgar.renatoATgmailDOTcom>
# Contributor: Gerson E. Ruotolo <gersonruotolo@globo.com>

pkgname=dos2unix
pkgver=7.5.0
pkgrel=1
pkgdesc='Text file format converter'
arch=('loong64' 'x86_64')
url='https://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('glibc')
makedepends=('perl')
conflicts=('hd2u')
source=(https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('F8F1BEA490496A09CCA328CC38C1F572B12725BE')
sha256sums=('7a3b01d01e214d62c2b3e04c3a92e0ddc728a385566e4c0356efa66fd6eb95af'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  make LDFLAGS_USER="${LDFLAGS}"
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
