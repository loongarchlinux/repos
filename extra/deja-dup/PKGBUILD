# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=44.2
pkgrel=1
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Apps/DejaDup'
license=('GPL')
depends=(
  'duplicity'
  'gvfs'
  'libsecret'
  'org.freedesktop.secrets'
  'python-gobject'
  'python-pydrive2'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'itstool'
  'meson'
  'vala'
)
source=("https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c20767e3ba13d6be64c38919a374cfefce6707e5147ef8a6509e82fa207b0db0d705f7fd5e92d848ce95e189c668ce760b0e914d66002335747797217fb40575')
b2sums=('5485d61481901cd544cf17c57c2a1520c8e55b85fde67f17c559be8fd811e6b83a8ba5b37e8de472c1053005a12a9836fcf45626ec517d6f6aa13ab67e7bee00')
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry <mike@mterry.name>

prepare() {
  find "$pkgname-$pkgver" -name meson.build | xargs sed -e 's|C.UTF-8|C|' -i
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
