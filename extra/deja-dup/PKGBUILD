# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=45.2
pkgrel=1
pkgdesc='A trivial-to-use personal backup tool to protect against accidental data loss'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Apps/DejaDup'
license=('GPL')
depends=(
  'duplicity'
  'gvfs'
  'libsecret'
  'org.freedesktop.secrets'
  'python-gobject'
  'python-pydrive2'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'itstool'
  'meson'
  'vala'
)
source=("https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7abfbb32d8d99b3042f16ced8dba8cebdd152c95015722fcae457629b2ca2f50dcbb6f7551fc3f44018a972f814b3461eb2a5d2984afebae0649bc9894e8c356')
b2sums=('18dcb5121617928bcc11473a87d28733547a4b03c4d548a41cbafd57b02b03117ed9b8432f6d75ac51c1d37b97c207efd58ab3b5d857e8de950d751bc4638101')
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry <mike@mterry.name>

prepare() {
  find "$pkgname-$pkgver" -name meson.build | xargs sed -e 's|C.UTF-8|C|' -i
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
