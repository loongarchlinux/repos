# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=2.7.1
pkgrel=1
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(loong64 x86_64)
url="https://gitlab.freedesktop.org/mesa/adriconf"
license=(GPL3)
depends=(gtkmm-4.0 pciutils pugixml)
makedepends=(cmake)
source=(https://gitlab.freedesktop.org/mesa/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha256sums=('7b9a3b560109e82090965449230ad75abb37a75f0f9fcea09e7267dc5463ca95')

build() {
  cmake -B build -S $pkgname-v$pkgver \
    -G 'Unix Makefiles' \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_UNIT_TESTS=OFF \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-v$pkgver/flatpak/org.freedesktop.adriconf.metainfo.xml \
    "$pkgdir/usr/share/metainfo/org.freedesktop.adriconf.metainfo.xml"
  install -Dm644 $pkgname-v$pkgver/flatpak/org.freedesktop.adriconf.desktop \
    "$pkgdir/usr/share/applications/org.freedesktop.adriconf.desktop"
  install -Dm644 $pkgname-v$pkgver/flatpak/org.freedesktop.adriconf.png \
    "$pkgdir/usr/share/icons/hicolor/256x256/apps/org.freedesktop.adriconf.png"
}
