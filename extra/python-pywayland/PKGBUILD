# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.16
pkgrel=1
pkgdesc="Python bindings for the libwayland library"
arch=(loong64 x86_64)
url="https://github.com/flacjacket/pywayland"
license=(Apache-2.0)
depends=(
  glibc
  python
  python-cffi
  wayland
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
  wayland-protocols
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('1098e0ae88ffc874d1e49f6441f06db05d1bfebead06d65e1214d69542b9ada0ec45bfccd6fd11dd188b2ebcf3d0ac2a2370927af8a0838f4e683ec50fc5e01a')
b2sums=('4b299fe1b3067e2e222012db921b4ae5fdfd103b0ca2b796522236b6b8b2ffc1e54ba8c97d30970cb5df35c9da18ca217ea5d97a37b11d7112d93c59a1df164f')

build() {
  cd $_name-$pkgver
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python -m build --wheel --no-isolation
}

check() {
  local _py_ver="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"

  cd $_name-$pkgver
  mkdir -vp temp
  export PYTHONPATH="build/lib.linux-$CARCH-$_py_ver:$PYTHONPATH"
  export XDG_RUNTIME_DIR="$PWD/temp"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
