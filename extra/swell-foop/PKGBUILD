# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=41.0.1
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://wiki.gnome.org/Apps/Swell%20Foop"
arch=(loongarch64 x86_64)
license=(GPL)
depends=(clutter-gtk librsvg libgnome-games-support libgee)
makedepends=(yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
_commit=4ca2598ff41490c27d3769ec00327780736d70d4  # tags/41.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/swell-foop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
