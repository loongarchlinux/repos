# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-screensaver
pkgver=5.0.18
pkgrel=1
pkgdesc='Deepin screensaver viewer and tools'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-screensaver"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkwidget' 'qt5-base' 'qt5-declarative'
         'qt5-x11extras' 'libx11' 'libxcb' 'libxss' 'xscreensaver' 'gcc-libs')
makedepends=('qt5-tools' 'deepin-desktop-base')
groups=('deepin')
source=("https://github.com/linuxdeepin/deepin-screensaver/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('21bd666dc7edc9ffaecdccac0feca0d9db3d71046d4fd638e5fd8f756385722a5e5ea995bc8e73b616e4b51a00cd97de7bfb0ba2c994c183f99a175db5807eaf')

prepare() {
  cd deepin-screensaver-$pkgver
  sed -i 's|/etc/os-version|/etc/deepin-version|' common.pri
}

build() {
  cd deepin-screensaver-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screensaver-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
