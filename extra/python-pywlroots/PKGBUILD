# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.16.5
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(loong64 x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(NCSA)
depends=(
  glibc
  python
  python-cffi
  python-pywayland
  python-xkbcommon
  wlroots
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('891da30ed9949a0535cca6e12787258f689cdba1314853d2e5665a040e67a9f45036d500526ce247dda260355b573e8260b67e115700af0df5cb1e760307de9b')
b2sums=('8ee637b4900590b17c0ee4dc07659ed2f6b44d087fabe29f0e74075a9be1be6cc3a71876bfc0333266b0484b463fc14d243c4189b4c40dd468e3f9f2cf7c91b0')

build() {
  cd $_name-$pkgver
  python wlroots/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
