# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-crypt-openssl-guess
pkgver=0.14
pkgrel=1
pkgdesc='Guess OpenSSL include path'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
makedepends=('perl-extutils-makemaker>=6.64')
url=https://metacpan.org/release/Crypt-OpenSSL-Guess
source=("https://cpan.metacpan.org/authors/id/A/AK/AKIYM/Crypt-OpenSSL-Guess-$pkgver.tar.gz")
md5sums=(716e445a75aaf0d57b66e36292d9374d)
sha512sums=(cf15b8775bf3279063c4b9e6865abbc28cef2d02b1b38fd4f4b5098eb19ef10905bd69dc1814d4122e274e4b04fd16697e17f18f0b5db15cd695befff2cef2fd)
_ddir="Crypt-OpenSSL-Guess-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
