# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.12.25
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(loongarch64 x86_64)
license=(Apache)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git 'java-environment>=9')
_commit=add673f8f7927f684bd3b7867590575a141ec87e  # tags/v8.12.25^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"  # this is nuts
  cmake -S $pkgname/cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  depends+=(libicu{uc,i18n}.so libprotobuf.so libboost_thread.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}
