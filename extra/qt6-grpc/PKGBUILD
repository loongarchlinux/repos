# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-grpc
_qtver=6.5.3
pkgver=${_qtver/-/}
pkgrel=1
arch=(loong64 x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides the modules QtGrpc and QtProtobuf'
depends=(qt6-base grpc)
makedepends=(cmake ninja protobuf)
groups=(qt6)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        protobuf-23.patch)
sha256sums=('6ebf7a73daa6388e15e95a1de021dc4d097fe0f6b71b106178c388cc7ec18b83'
            '838bb8aba6f2f855e16fb08741248c8524ea165b546c186c8fb80c9943fa4b98')

prepare() {
  patch -d $_pkgfn -p1 < protobuf-23.patch # Fix build with protobuf 23
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
