# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>

pkgname=just
pkgver=1.16.0
pkgrel=1
pkgdesc="A handy way to save and run project-specific commands"
arch=("loong64" "x86_64")
url="https://github.com/casey/just"
license=("custom:CC0")
depends=("gcc-libs")
makedepends=("cargo")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/casey/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('80c07b7a92b24ac9661fa312f287f0900bbe19b9c3a2f4c2739a2f242a5558f9')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo check --frozen --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "completions/just.zsh" "${pkgdir}/usr/share/zsh/site-functions/_just"
  install -Dm644 "completions/just.bash" "${pkgdir}/usr/share/bash-completion/completions/just"
  install -Dm644 "completions/just.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/just.fish"
}
