# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky
pkgver=1.18.1
_gitcommit=87858b6ae8f0b5e195faec5c79048ac4171bfc30  # refs/tags/v1.18.1
pkgrel=2
pkgdesc='Light-weight system monitor for X, Wayland, and other things, too'
url='https://github.com/brndnmtthws/conky'
license=(
  'BSD-3-Clause'
  'GPL-3.0-or-later'
)
arch=('loong64' 'x86_64')
makedepends=(
  'catch2'
  'cmake'
  'docbook2x'
  'docbook-xsl'
  'git'
  'man-db'
  'pandoc'
  'python-yaml'
  'python-jinja'
  'wayland-protocols'
)
depends=(
  'cairo'
  'curl' libcurl.so
  'fontconfig'
  'gcc-libs'
  'glib2'
  'glibc'
  'hicolor-icon-theme'
  'imlib2'
  'libpulse' libpulse.so
  'libx11'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxft'
  'libxinerama'
  'libxml2'
  'libxnvctrl' libXNVCtrl.so
  'lua'
  'ncurses' libncursesw.so
  'pango'
  'systemd-libs' libsystemd.so
  'wayland'
  'wireless_tools'
)
source=("git+$url.git#tag=${_gitcommit}?signed")
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=(
  'EB2E847572A71BE20F59AFBEB49ABB7270D9D4FD' # Brenden Matthews
)

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
# Unbundle catch2 to fix build with glibc 2.35
  rm -r tests/catch2
  ln -s /usr/include/catch2 tests
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -D MAINTAINER_MODE=OFF \
    -D BUILD_TESTS=ON \
    -D BUILD_DOCS=ON \
    -D BUILD_EXTRAS=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_XSHAPE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_PULSEAUDIO=ON \
    -D BUILD_JOURNAL=ON \
    -D BUILD_WAYLAND=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -S .

  make -C build
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 build/extras/vim/syntax/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -Dm 644 extras/vim/ftdetect/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/ftdetect
}

# vim: ts=2 sw=2 et:
