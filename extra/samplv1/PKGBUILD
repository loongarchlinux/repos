# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=samplv1
pkgname=(
  samplv1
  samplv1-lv2
  samplv1-standalone
)
pkgver=0.9.32
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(loong64 x86_64)
url="https://samplv1.sourceforge.io/"
license=(GPL-2.0-or-later)
makedepends=(
  alsa-lib
  cmake
  hicolor-icon-theme
  jack
  liblo
  libsndfile
  lv2
  qt6-base
  qt6-svg
  qt6-tools
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('c2028aa615403bf7c0f751b5f7ce7999edea0158f7a3618e2c8dbe2905f1ff622bb4a724f91ad1aa433b9eaa874581572d5f69c4c6e0f65367f9600dc83d207d')
b2sums=('d0f28dd659062b8d2e11afbab2ce989774380bf9a94c567628d3ff570f5d3fbd73b7a9710d251ee5be37fa383481f4aade027d12e8ca4cf990c4b764e89bc0cb')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package_samplv1() {
  depends=(
    $pkgbase-{lv2,standalone}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"

    # have find-libdeps resolve dependencies properly:
    # https://gitlab.archlinux.org/archlinux/devtools/-/issues/102
    find usr/lib -type f -iname "*.so" -exec chmod +x {} \;

    _pick $pkgbase-lv2 usr/lib/lv2
    _pick $pkgbase-standalone usr/{bin,share}
  )
}

package_samplv1-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    libsndfile libsndfile.so
    lv2-host
    qt6-base
    qt6-svg
  )
  optdepends=(
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_samplv1-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    liblo
    libsndfile libsndfile.so
    qt6-base
    qt6-svg
  )
  optdepends=(
    'new-session-manager: for session management using the standalone application'
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
