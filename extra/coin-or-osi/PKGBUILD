# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.108.9
pkgrel=1
pkgdesc='COIN-OR Open Solver Interface'
arch=(loong64 x86_64)
url='https://github.com/coin-or/Osi'
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Osi/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('8b09802960d7d4fd9579b3e4320bfb36e7f8dca5e5094bf1f5edf1b7003f5562')

build() {
  cd Osi-releases-$pkgver
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/"
  make
}

package() {
  cd Osi-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
