# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.6.1
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(loong64 x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('10f61bafde21d94251bad76cd338196f246fd1953ba2d8eeebcec771aad231fd3ec2a59427d18c239dbc442a46f52fd7f3434e099f4bb0c9409de55d1c6d1f7a')
b2sums=('0641717f2edcec5537f2865519bf8da8f0973ecc63f27c8f9b71a12b11af02f2b211657d79d30d73cbd0ca1865682f43a3de93ba81dc49637701eb12c3a5cde2')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
  alsa-lib libasound.so
  libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
