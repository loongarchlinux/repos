# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.6.2
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(loong64 x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('92870fe98b94971babe66e6c7d90231f41d1f73e35aa4ca4e913ab5a3a13f5364194cd68d09fcf26880f86444eff8881ac9a89640b89a86e274afb55e4764ee0')
b2sums=('2754f31009b80a6321e55d53ec50ba965cd93bb7adcf3d40f909ec4b913ff045b81d4e265d66954db1e0c7fe32d91698f15a78085ad4b5d3b8097c21219c4dc8')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
