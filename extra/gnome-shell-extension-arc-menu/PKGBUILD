# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=45
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=('any')
url='https://gitlab.com/arcmenu/ArcMenu'
license=('GPL')
depends=('gnome-menus' 'gnome-shell')
source=("https://gitlab.com/arcmenu/ArcMenu/-/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz"
        'gnome-shell-extension-arc-menu-system-install-paths.patch')
sha256sums=('65e783d35605860f5e29037ba197179ab702283af0153126a5d6ee9cc0700421'
            'c818fc383b88e2e00da936c6763cb562504166658614dc67a04a702cdcc3afb8')

prepare() {
  cd $_pkgname-v$pkgver
  # https://gitlab.com/arcmenu/ArcMenu/-/merge_requests/189
  patch -Np1 -i ../gnome-shell-extension-arc-menu-system-install-paths.patch
}

build() {
  cd $_pkgname-v$pkgver
  make
}

package() {
  cd $_pkgname-v$pkgver
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
