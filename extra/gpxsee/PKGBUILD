# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=13.14
pkgrel=2
pkgdesc='GPS log file viewer and analyzer'
arch=(loong64 x86_64)
url='https://www.gpxsee.org/'
license=(GPL3)
depends=('qt6-imageformats' 'qt6-5compat' 'qt6-positioning' 'qt6-svg'
         'qt6-serialport' 'hicolor-icon-theme')
makedepends=('qt6-tools')
optdepends=('qtpbfimageplugin-qt6: Mapbox vector tiles (MVT) files support')
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('3a3e60d6cfd87c43d77a0a5af4f2d463eea62f0f7becdb1974f1c3deb35c1355')

build() {
  cd $_pkgname-$pkgver
  /usr/lib/qt6/bin/lrelease gpxsee.pro
  qmake6 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
