# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Ila√Ø Deutel

pkgname=typst
pkgver=0.8.0
pkgrel=1
epoch=1
pkgdesc='A markup-based typesetting system for the sciences'
arch=(loong64 x86_64)
url="https://$pkgname.app"
_url="https://github.com/$pkgname/$pkgname"
license=(Apache)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             git)
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('4b7886e312171b66fb33b7ad49c4df76748231d0047fb96b407c926070a5d9f9')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	local hash=$(gunzip < "$_archive.tar.gz" | git get-tar-commit-id)
	cd "$_archive"
	CFLAGS+=" -ffat-lto-objects"
	export TYPST_VERSION="$pkgver (${hash::8})"
	export GEN_ARTIFACTS=artifacts/
	cargo build -p typst-cli --frozen --release --all-features
}

check() {
	cd "$_archive"
	cargo test --workspace --frozen --all-features
}

package() {
	cd "$_archive"
	local _artifacts='crates/typst-cli/artifacts'
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" "$_artifacts/$pkgname"*.1
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "$_artifacts/_$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$_artifacts/$pkgname.fish"
	install -Dm0644 "$_artifacts/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
