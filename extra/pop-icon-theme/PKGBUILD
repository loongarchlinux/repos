# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christopher Reimer <mail@c-reimer.de>

pkgname=pop-icon-theme
pkgver=3.3.0.r14.3126c6a3f6
pkgrel=1
pkgdesc='System76 Pop Icon Theme'
arch=(any)
url=https://github.com/pop-os/icon-theme
license=(CCPL)
depends=(adwaita-icon-theme)
makedepends=(
  git
  meson
)
_commit=3126c6a3f605923c648e74e7276b9de59d4ab67a
source=(pop-icon-theme::git+https://github.com/pop-os/icon-theme.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd pop-icon-theme
  git describe --tags | sed 's/^v//;s/-/.r/;s/-g/./'
}

build() {
  arch-meson pop-icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
