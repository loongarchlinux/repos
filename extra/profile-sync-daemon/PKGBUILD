# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.48
pkgrel=1
epoch=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=(any)
url="https://github.com/graysky2/profile-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('zsh-completions: for completion when using zsh')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('5a0fa7a671e31024b60338e7ac3e4fe84f9237bb080bd7063255ca458bbd7bd844d2b6063b641eb9f66f8f2baa01698d16bfcfe331a2a97fdf9d383ddbdbe5e8')
b2sums=('3c9e65d732aed4accf8fcc199f8b2cf03fc998a7ba1915435f0efd3f492d1918e9fff1ebb6dba865bcd72acc7731d22a28d547d243338c3b3475d4b4b59dda2c')

build() {
  make -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
