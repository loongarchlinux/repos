# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=6.4.0
_commit=1da22d0de41ee83be8f290d72d7cdcc65ccffafd
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cryptography' 'python-cliff' 'python-iso8601' 'python-openstacksdk'
         'python-osc-lib' 'python-oslo-i18n' 'python-keystoneclient' 'python-novaclient'
         'python-cinderclient' 'python-stevedore')
makedepends=('git')
checkdepends=('python-oslotest' 'python-requests' 'python-requests-mock' 'python-stestr'
              'python-testtools' 'python-wrapt' 'python-ddt')
source=("git+https://github.com/openstack/python-openstackclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-openstackclient
  python setup.py build
}

check() {
  cd python-openstackclient
  stestr run
}

package() {
  cd python-openstackclient
  python setup.py install --root="$pkgdir" --optimize=1
}
