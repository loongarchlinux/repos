# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tools
pkgver=0.9.4
pkgrel=1
pkgdesc="CLI tool to inspect, manage and use OpenPGP cards"
arch=(loong64 x86_64)
url="https://codeberg.org/openpgp-card/openpgp-card-tools"
license=('Apache-2.0 OR MIT')
depends=(
  ccid
  gcc-libs
  glibc
  gmp
)
makedepends=(
  bzip2
  cargo
  clang
  nettle
  pcsclite
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('ba446e3c99a1b09a978d9718aab6df43715fe13e573400c8ccfb2f839330782fb10386cd570c9e273b240ddda4b7573beb9913a6662d3b78abba3e21b5e1c125')
b2sums=('ce0d45892e741a635429507f1538c7ab1b1f8efa4e08a547e6a338dd0374b250ae43886bcb60fe7eba29f75384bccd23dafd4964b3fc378530085eabe123435c')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --bin opgpcard
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libhogweed.so libnettle.so
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 755 target/release/opgpcard -t "$pkgdir/usr/bin/"
  install -vDm 644 {scripting,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
