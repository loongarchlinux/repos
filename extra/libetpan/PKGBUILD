# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libetpan
pkgver=1.9.4
pkgrel=2
pkgdesc="A portable middleware for email access"
arch=('x86_64')
url="https://www.etpan.org/libetpan.html"
license=("custom:etpan")
depends=('libgcrypt' 'gnutls' 'libsasl' 'zlib')
makedepends=('expat')
source=($pkgname-$pkgver.tar.gz::https://github.com/dinhviethoa/${pkgname}/archive/${pkgver}.tar.gz)
sha256sums=('82ec8ea11d239c9967dbd1717cac09c8330a558e025b3e4dc6a7594e80d13bb1')

prepare() {
  cd "${pkgname}"-${pkgver}
  libtoolize --force --copy
  aclocal -I m4
  autoheader
  autoconf
  automake --add-missing --foreign --force --copy
}

build() {
  cd "${pkgname}"-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --disable-db \
    --with-openssl=no \
    --with-gnutls=yes
  make
}

check() {
  cd "${pkgname}"-${pkgver}
  make check
  cd tests
  make imap-sample
}

package() {
  cd "${pkgname}"-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYRIGHT "${pkgdir}"/usr/share/licenses/$pkgname/license.txt
}
