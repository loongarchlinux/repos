# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=quicklisp
pkgver=20150128
pkgrel=1
pkgdesc='A library manager for Common Lisp'
arch=('any')
url='https://www.quicklisp.org'
license=('MIT')
install=quicklisp.install
source=(
  'https://beta.quicklisp.org/quicklisp.lisp'{,.asc}
  'LICENSE'
  'README.md'
  'FAQ.md'
)
b2sums=('57d0d29e08d77176fea4f01e644fc11bdb96e9efbd132cbb9720192d3b47599f5ce3c6e0307b44c33add06bd70c28004b8efb8968f1d9deb881a2db4ac615772'
        'SKIP'
        'e7a64d5d86ea97cd694c9d72c602036003657b471c278e0116cc55aa72c817190446e542abf359f7424962b65bd33f4c9c8aa168de0870b9f9fc11e14801577f'
        'b01b32e0149a45ebcc1d9a6013b7e85733d219e65ba0545b1c4194e16ac0f5901317dc66eb1c437175b104f899a73a4af278d542c35e2d0ca37caadb99b4520c'
        '532cda6cdb5dd65ea0c091350f9fa2e6c3fa8abb6737179be495d6f3eafbda6b16253df666f51889aa76fb1c7fa4aecc9d7a499fcdcb62749f10e41cd9b21fff')
validpgpkeys=('D7A3489DDEFE32B7D0E7CC61307965AB028B5FF7') # release@quicklisp.org

pkgver() {
  grep 'defvar qlqs-info' quicklisp.lisp | sed -e 's/.*"\(.*\)".*/\1/' -e 's/-//g'
}

package() {
  # library
  install -vDm644 -t "$pkgdir/usr/share/$pkgname" quicklisp.lisp

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
