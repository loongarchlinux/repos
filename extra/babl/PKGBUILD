# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
_tag='9e1bfc364011d0dc49b4554ca95e53f170719702'
pkgver=0.1.88
pkgrel=2
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=(loongarch64 x86_64)
url="https://gegl.org/babl/"
license=(LGPL3)
depends=(glibc lcms2)
makedepends=(git meson gobject-introspection vala)
source=("git+https://gitlab.gnome.org/GNOME/babl.git#tag=$_tag")
sha256sums=('SKIP')

# Don't port to meson until babl's runtime cpu detection works there

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BABL_//;s/_$//;s/_/./g;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
