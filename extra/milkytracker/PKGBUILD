# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=1.04.00
pkgrel=1
pkgdesc='Music tracker inspired by Fast Tracker II'
arch=(loong64 x86_64)
url='https://milkytracker.titandemo.org/'
license=(GPL3)
depends=(alsa-lib lhasa rtmidi sdl2 zlib zziplib)
makedepends=(cmake jack ninja)
optdepends=('jack: JACK audio support')
# Disable stripping, see FS#30018
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/milkytracker/MilkyTracker/archive/v$pkgver.tar.gz")
b2sums=('da4885592ae4aae87080c8979f92a77b05cb3cc4594fe328346deef949375ad3abb1910f5cc834f41b6d7278312deff45550300b96e3afbd89eedebe1fadd854')

prepare() {
  mv MilkyTracker-$pkgver $pkgname-$pkgver

  # Remove explicit use of C++98
  sed -e '/CMAKE_CXX_STANDARD/d' -e '/CMAKE_CXX_EXTENSIONS/d' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake \
    -B build \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -G Ninja \
    -S $pkgname-$pkgver \
    -W nodev
  ninja -C build
}

package() {
  depends+=(libasound.so librtmidi.so)
  DESTDIR="$pkgdir" ninja install -C build
  cd $pkgname-$pkgver/resources
  install -Dm644 pictures/carton.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # Also rename from .appdata to .appdata.xml
  install -Dm644 $pkgname.appdata "$pkgdir/usr/share/appdata/$pkgname.appdata.xml"
}
