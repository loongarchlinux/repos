# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.8.1
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(loong64 x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulseaudio-alsa upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a5e92aa9ba463cff7db02e9d377a28b39e4c1dd25226ab9d4d59b5fd7b38267c6da5f31cfa052f1a8243de601c7484040be158e633b8932e2e68726860c7bfc3')
b2sums=('50d8e6fb1fde50db7cc40c931e0904c0abfa429dc5757160fdb87ee2753f4d9e732ba4a0837c0480a73a3caffb084881c8aac336f8600afe976adc519b09aa01')

build() {
  arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
