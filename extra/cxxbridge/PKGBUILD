# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.97
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('loong64' 'x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('2e94d169a97b0abc490e901868562318c814034194d7ca0dfe3d976db6c361b6'
            '415c19c0487a56618632db7d3813746d8ab007f5b37ec4e23cee6f36fc7bdb18')
b2sums=('1fb9b1f31646815fcd19411579c54172cac88610fdc9822d182d80ad90863b6ed1afb803491c7c65b354f86b6b8ad35867670354e03ca3dd79aba2c0b2ceffad'
        'e1c841b4ec2967ae6c65dad9b2d47493bb973b6e635cc52d96989c0001d88829c7f91b2e071bbfed3f5aff05235a216136daeb7a0c452fc9410f9a399f4cc9ed')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
