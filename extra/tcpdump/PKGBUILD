# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.9.3
pkgrel=2
pkgdesc='Powerful command-line packet analyzer'
url='https://www.tcpdump.org/'
arch=('x86_64')
license=('BSD')
depends=('libpcap' 'libcap-ng' 'openssl')
source=("https://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz"{,.sig})
#source=("https://sources.archlinux.org/other/packages/tcpdump/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D')
sha256sums=('2cd47cb3d460b6ff75f4a9940f594317ad456cfbf2bd2c8e5151e16559db6410'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \

	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm -f "${pkgdir}"/usr/bin/tcpdump.${pkgver}
}
