# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>

pkgname=libxklavier
pkgver=5.4
pkgrel=5
pkgdesc='High-level API for X Keyboard Extension (deprecated)'
arch=('loong64' 'x86_64')
url='https://www.freedesktop.org/wiki/Software/LibXklavier/'
license=('LGPL')
depends=('glib2' 'glibc' 'iso-codes' 'libx11' 'libxi' 'libxkbfile' 'libxml2' 'xkeyboard-config' 'xorg-xkbcomp')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
source=("https://gitlab.freedesktop.org/archived-projects/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz")
sha256sums=('e1638599e9229e6f6267b70b02e41940b98ba29b3a37e221f6e59ff90100c3da')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
