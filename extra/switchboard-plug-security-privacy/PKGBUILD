# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=7.0.0
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL3)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=b9a68af3c665f863a0fbf049d975a5a8f389e2d2
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-security-privacy
  git describe --tags
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
