# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=2.1.1
pkgrel=2
pkgdesc="A new approach to API documentation in Sphinx."
arch=(any)
url="https://github.com/readthedocs/sphinx-autoapi"
license=(MIT)
depends=(
  python-anyascii
  python-astroid
  python-jinja
  python-sphinx
  python-yaml
)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-beautifulsoup4 python-pytest)
# TODO: package python-go and python-dotnet
# optdepends=(python-go python-dotnet)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('fbadb96e79020d6b0ec45d888517bf816d6b587a2d340fbe1ec31135e300a6c8')
b2sums=('d068acd78bb6899981739654f159429b0e977332a90018f2aff7212dce90b3e31800c148a104887050257ebdfa11e3dced7139ed5dc9d768505f6fabd0535bd1')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/sphinx_autoapi-$pkgver.dist-info/LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
