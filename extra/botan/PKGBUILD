# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

pkgname=botan
pkgver=2.18.2
pkgrel=3
pkgdesc='Crypto library written in C++'
arch=(loongarch64 x86_64)
url='https://botan.randombit.net/'
license=(BSD)
depends=(xz sqlite)
makedepends=(python boost openssl)
optdepends=('python: for using botan2.py' 'boost-libs: for the botan executable')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tar.xz"{,.asc})
sha256sums=('541a3b13f1b9d30f977c6c1ae4c7bfdfda763cda6e44de807369dce79f42307e'
            'SKIP')
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC') # Botan Distribution Key

build() {
  cd "${pkgname^}-$pkgver"

  ./configure.py \
    --prefix=/usr \
    --with-bzip \
    --with-lzma \
    --with-zlib \
    --with-boost \
    --with-openssl \
    --with-sqlite3 \
    --with-os-feature=getrandom
  make
}

check() {
  cd "${pkgname^}-$pkgver"

  LD_LIBRARY_PATH="$PWD" ./botan-test
}

package() {
  DESTDIR="$pkgdir" make -C "${pkgname^}-$pkgver" install
  install -Dm644 "${pkgname^}-$pkgver/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
