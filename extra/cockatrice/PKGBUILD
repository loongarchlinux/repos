# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.8.0
_date=2021-01-26
_tag=$_date-Release-$pkgver
pkgrel=12
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('loong64' 'x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt5-base' 'protobuf' 'qt5-tools' 'qt5-svg' 'qt5-multimedia' 'qt5-websockets')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle'
	    'mariadb-libs: MySQL/MariaDB connection')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag"
         protobuf-23.patch)
sha512sums=('SKIP'
            'd2dc7c73d43d26ad6a8e36c4e67f3ed3f5cf0c2e9155a763c62df39bec9070a5c31d08f9019e9b9e889f26056ab9bf5495b4a28695d32af368c49b3e31b72082')

prepare() {
  patch -d $_pkgname -p1 < protobuf-23.patch # Fix build with protobuf 23
}

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_CXX_STANDARD=17 \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

check() {
  cd $_pkgname/build

  make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
