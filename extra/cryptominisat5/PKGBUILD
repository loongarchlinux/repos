# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=cryptominisat5
pkgver=5.11.11
pkgrel=1
pkgdesc='A modern, multi-threaded, feature-rich, simplifying SAT solver'
arch=(loong64 x86_64)
url='https://github.com/msoos/cryptominisat/'
license=(MIT)
depends=(zlib gcc-libs boost-libs sqlite m4ri)
makedepends=(boost cmake vim help2man python-build python-installer python-setuptools python-wheel python-toml)
optdepends=('python: python module')
source=(https://github.com/msoos/cryptominisat/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('20efedfab285293eb8c9804939b5298e43071489c8b09e135e118aec54f682bc')

prepare() {
  sed -e 's|, \"toml\", \"pathlib\"||' -i cryptominisat-$pkgver/pyproject.toml
}

build() {
  cmake -B build -S cryptominisat-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cd cryptominisat-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd cryptominisat-$pkgver
  _pythonpath=`echo build/lib.*`
  PYTHONPATH="$PWD/$_pythonpath" python python/tests/test_pycryptosat.py
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 cryptominisat-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd cryptominisat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
# Remove stray source files
  rm -r "$pkgdir"/usr/lib/python*/site-packages/{msvc,oracle,picosat}
}
