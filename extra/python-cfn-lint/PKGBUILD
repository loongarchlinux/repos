# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
# https://github.com/aws-cloudformation/cfn-lint/blob/main/CHANGELOG.md
pkgver=0.77.5
# curl https://api.github.com/repos/aws-cloudformation/cfn-lint/git/ref/tags/v$pkgver | jq -r .object.sha
_tag=43e42b2dfd7b0881ef5071c93d0e842137faba27
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-lint'
license=('custom:MIT No Attribution')
depends=(python python-yaml python-aws-sam-translator
         python-jsonpatch python-jsonschema python-networkx
         python-junit-xml python-jschema-to-python python-sarif-om
         python-sympy python-regex)
makedepends=(git python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pydot)
optdepends=(
  'python-pydot: for building graphs from templates'
)
source=("git+https://github.com/aws-cloudformation/cfn-lint.git#tag=$_tag")
sha256sums=('SKIP')

pkgver() {
  cd cfn-lint
  git describe --tags | sed 's/^v//'
}

build() {
  cd cfn-lint
  python -m build --wheel --no-isolation
}

check() {
  cd cfn-lint

  # Tests in test/integration need the cfn-lint binary
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl

  export PYTHONPATH="$PWD/src"
  export PATH="$PATH:$PWD/tmp_install/usr/bin"
  # Using unittest following upstream. `test/unit/module/core/test_run_cli.py`
  # fails if run after `test/unit/module/test_api.py`, which is the case for pytest.
  # The conflict among tests exist after https://github.com/aws-cloudformation/cfn-lint/pull/2646,
  # where the new runner.transform() pollutes environment variables via transform.transform_template()
  python -m unittest discover test
}

package() {
  cd cfn-lint
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
