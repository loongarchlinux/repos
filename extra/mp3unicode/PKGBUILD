# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Zhukov Pavel <gelios@gmail.com>

pkgname=mp3unicode
pkgver=1.2.1
pkgrel=6
pkgdesc="A command line utility to convert ID3 tags in mp3 files between different encodings"
arch=('loong64' 'x86_64')
url="https://github.com/alonbl/mp3unicode"
license=('GPL')
depends=('taglib')
source=(https://github.com/alonbl/mp3unicode/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2
        taglib-2.patch)
sha256sums=('375b432ce784407e74fceb055d115bf83b1bd04a83b95256171e1a36e00cfe07'
            'dbfe1b0be4d4b37f0e48ca5788d16bd65b224e57b97d0a50bf67881df6dd710f')

prepare() {
  patch -d $pkgname-$pkgver -p1 < taglib-2.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
