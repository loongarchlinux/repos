# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc
pkgver=2.5.2
pkgrel=8
epoch=1
pkgdesc="An ncurses console audio player designed to be powerful and easy to use"
arch=('loong64' 'x86_64')
url="https://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libltdl' 'file')
makedepends=('speex' 'ffmpeg4.4' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug' 'faad2')
optdepends=('speex: for using the speex plugin'
	    'ffmpeg4.4: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin'
	    'libmpcdec: for using the musepack plugin'
            'wavpack: for using the wavpack plugin'
            'faad2: for using the aac plugin'
	    'libmodplug: for using the modplug plugin')
source=(http://ftp.daper.net/pub/soft/moc/stable/${pkgname}-${pkgver}.tar.bz2{,.sig}
        moc-ffmpeg4.patch
        moc-https.patch)
sha256sums=('f3a68115602a4788b7cfa9bbe9397a9d5e24c68cb61a57695d1c2c3ecf49db08'
            'SKIP'
            'f1a12d7d2e8269974487a2ffb011e87d9a6c5c06e2a47d1312d5965c98f050ea'
            'd5ace9820974912e4f3d9361ad5d03d1d655348f17c9710dff527020101e98fa')
validpgpkeys=('59359B80406D9E73E80599BEF3121E4F2885A7AA')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../moc-ffmpeg4.patch # Fix build with ffmpeg 4
  patch -p0 -i ../moc-https.patch   # Allow https for urls https://moc.daper.net/node/1872
}

build() {
  cd ${pkgname}-${pkgver}
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  ./configure --prefix=/usr --without-rcc \
    --with-oss --with-alsa --with-jack --with-aac --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl  --disable-cache --disable-debug
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
