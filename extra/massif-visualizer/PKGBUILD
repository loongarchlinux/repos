# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Matz Radloff <matzradloff@googlemail.com>

pkgname=massif-visualizer
pkgver=0.7.0
pkgrel=5
pkgdesc='Visualizer for Valgrind Massif data files'
arch=(loong64 x86_64)
url='https://www.linux-apps.com/p/1127160/'
license=(GPL)
depends=(gcc-libs
         glibc
         karchive5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kdiagram5
         ki18n5
         kio5
         kparts5
         kwidgetsaddons5
         kxmlgui5
         qt5-base
         qt5-svg)
makedepends=(extra-cmake-modules
             kgraphviewer
             qt5-xmlpatterns)
optdepends=('kgraphviewer: call graph tab')
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f8a4cc23c80a259a9edac989e957c48ed308cf9da9caeef19eec3ffb52361f6d'
            'SKIP')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
