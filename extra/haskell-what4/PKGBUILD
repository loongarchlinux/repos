# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=what4
pkgname=haskell-what4
pkgver=1.5
pkgrel=4
pkgdesc="Solver-agnostic symbolic values support for issuing queries"
url="https://github.com/GaloisInc/what4"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-attoparsec' 'haskell-bimap' 'haskell-bifunctors'
         'haskell-boundedchan' 'haskell-bv-sized' 'haskell-concurrent-extra' 'haskell-config-value'
         'haskell-deriving-compat' 'haskell-fingertree' 'haskell-hashable' 'haskell-hashtables'
         'haskell-io-streams' 'haskell-lens' 'haskell-libbf' 'haskell-megaparsec'
         'haskell-ordered-containers' 'haskell-panic' 'haskell-parameterized-utils'
         'haskell-prettyprinter' 'haskell-s-cargot' 'haskell-scientific' 'haskell-temporary'
         'haskell-th-lift' 'haskell-th-lift-instances' 'haskell-unliftio'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector' 'haskell-versions'
         'haskell-zenc')
makedepends=('ghc' 'uusi' 'haskell-contravariant' 'haskell-hedgehog' 'haskell-lumberjack'
             'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-checklist'
             'haskell-tasty-expected-failure' 'haskell-tasty-hedgehog' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-sugar')
checkdepends=('cvc4' 'yices' 'z3')
optdepends=('cvc4' 'yices' 'z3')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9e2f516cec88efb70fe0421ebd7fdb7d9576e1af5d747a1d34cfdf10c361122c4b421f05b6489956c8ec9370c8dd0cd6070b6434db8795e2456840cb7f4af2d5')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # TODO: package cvc5
    runhaskell Setup test --show-details=direct || echo "Ignoring test failures"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
