# Maintainer:
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=stlink
pkgver=1.7.0
pkgrel=2
pkgdesc='Open source STM32 MCU programming toolset'
arch=(loong64 x86_64)
url='https://github.com/texane/stlink'
license=(BSD-3-Clause)
depends=(
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libusb
)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('58b73c7a73809f36ab8ef53370bcbdba7456433c8559f350349febd282188541184b5c171eb2ca1ad38ec4b3ae32d3a89895a208bcacc264ec0aed5f61e6bcd0')
b2sums=('7e00a01912e1d21fa29a7299fbb21f8e85540d3e4f4c90be8fcd9b4a8ef14bae2c0cd460463babf04fd52a11ebeac9601a5f31874dd4cce3077434c2bfc96168')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D STLINK_MODPROBED_DIR=/usr/lib/modprobe.d
    -D STLINK_UDEV_RULES_DIR=/usr/lib/udev/rules.d
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
