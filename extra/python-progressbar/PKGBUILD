# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=4.3.2
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-progressbar"
license=(BSD-3-Clause)
depends=(
  python
  python-utils
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-sphinx
  python-wheel
)
checkdepends=(
  python-dill
  python-pytest
  python-freezegun
)
source=($url/releases/download/v$pkgver/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('d885a8b568149acf7a2cb5e5698f8677ee777c11a907f187c70c4ff4b52c656c40439ac8af00b652e5ddd4325721863c66b9f1a1b9a95d84f5fe081f94e4e0bf'
            'SKIP')
b2sums=('d5e0149fc54f3c4397c3de946db1aeef5c41e104ead1b0db1b19f81071a685c87ab3b425ea5eb3098db046db3a2ebfb29bce9a954f653327b5735fbb4f2c1f7f'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
