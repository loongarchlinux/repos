# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=xdg-desktop-portal-hyprland
pkgver=1.2.5
_protocolver=4d29e48433270a2af06b8bc711ca1fe5109746cd
pkgrel=1
pkgdesc='xdg-desktop-portal backend for hyprland'
url="https://github.com/hyprwm/$pkgname"
arch=(loong64 x86_64)
license=(MIT)
provides=(xdg-desktop-portal-impl)
depends=(libinih
         libuuid.so
         pipewire
         qt6-base
         qt6-wayland
         sdbus-cpp
         xdg-desktop-portal)
makedepends=(cmake
             meson
             scdoc
             wayland
             wayland-protocols)
optdepends=('grim: required for the screenshot portal to function'
            'slurp: support for interactive mode for the screenshot portal')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/v$pkgver.tar.gz"
        "https://github.com/hyprwm/hyprland-protocols/archive/$_protocolver.tar.gz"
        hyprland-portals.conf) # TODO move this to hyprland where it belongs
sha256sums=('c8ba3d173a580d8b4ec9a5ee15e7780d266e536a78a12aa28d8234e0854ae86d'
            '5f6be4d870e94314f05ec7ff9c9c1f028748230ff80a7f89aeaf66c08e5c70e0'
            '20bc215211f16a361086d59fa051df7337d95f91c695a29d8c5d23d40407fad5')

prepare() {
    cd "$_archive/subprojects"
    rm -rf hyprland-protocols sdbus-cpp
    ln -sfT "$srcdir/hyprland-protocols-$_protocolver" hyprland-protocols
}

build() {
    cd "$_archive"
    arch-meson build
    ninja -C build
    make -C hyprland-share-picker all
}

package() {
    cd "$_archive"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm0755 -t "$pkgdir/usr/bin/" hyprland-share-picker/build/hyprland-share-picker
    install -Dm0644 -t "$pkgdir/usr/share/xdg-desktop-portal/" "${srcdir}/hyprland-portals.conf"
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
