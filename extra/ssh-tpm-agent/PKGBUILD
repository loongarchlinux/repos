# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=ssh-tpm-agent
pkgver=0.2.0
pkgrel=1
pkgdesc="ssh-agent for TPMs"
arch=("loong64" "x86_64")
url="https://github.com/Foxboron/ssh-tpm-agent"
license=("MIT")
depends=("glibc")
makedepends=("go" "git" "systemd")
source=("$pkgname-$pkver.tar.gz::https://github.com/Foxboron/$pkgname/archive/v${pkgver}.tar.gz"
        "$pkgname-remove-run.patch::https://github.com/Foxboron/ssh-tpm-agent/commit/1dbece450a99cb03a5e88c7a1776cb56322bbb87.patch"
        "$pkgname-fix-perms.patch::https://github.com/Foxboron/ssh-tpm-agent/commit/053fc5258b6cdc5083aa4df0e4acf26a641cb3d0.patch")
sha256sums=('bfa9ec26a9683ca1ca2f0d96db1b39be02632414ce5e478fb11d717c9a7a4289'
            'df6b94e7c393f8a1dbfff1f7ba8fd84cc9abbec377de8452147be6daedc59615'
            '8183d615dbe77925dc844b357b55fec0707ff46cb049daf385cac6a98c383575')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/ssh-tpm-agent-remove-run.patch"
    patch -Np1 < "$srcdir/ssh-tpm-agent-fix-perms.patch"
}

build(){
    cd "${pkgname}-${pkgver}"
    export GOFLAGS="-buildmode=pie -modcacherw"
    make
}

package(){
    cd "${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" PREFIX="/usr" install
}
