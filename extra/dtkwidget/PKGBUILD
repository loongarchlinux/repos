# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.6.12
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
# qt5-imageformats is needed for webp (dci) icons
depends=('dtkcore' 'dtkgui' 'gcc-libs' 'glibc' 'gsettings-qt' 'libx11' 'libxcb' 'libxext' 'libxi'
         'qt5-base' 'qt5-imageformats' 'qt5-svg' 'qt5-x11extras' 'startup-notification' 'xcb-util')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
source=("https://github.com/linuxdeepin/dtkwidget/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('674ddb51c7c06a9b3dd33286771d0138b28081ba1009ef4b971b23582d5e3cbe60fe4d533e106cf3323693d68469b1e3bd90c8193c9c21f5e81985c93b6620e5')

build() {
  cd dtkwidget-$pkgver
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DBUILD_PLUGINS=OFF \
      -DQCH_INSTALL_DESTINATION=share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DVERSION=$pkgver \
      -DCMAKE_BUILD_TYPE=None
  ninja
}

package() {
  cd dtkwidget-$pkgver
  DESTDIR="$pkgdir" ninja install
}
