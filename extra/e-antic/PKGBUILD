# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=e-antic
pkgver=2.0.0
pkgrel=1
pkgdesc='Embedded algebraic number fields (on top of antic)'
arch=(loong64 x86_64)
url='https://github.com/flatsurf/e-antic/'
license=(LGPL)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=(boost)
source=(https://github.com/flatsurf/e-antic/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('a38d2ab62c1b00fa2ee78f39039cc4d9d8e83de5fa5e2a18529ad34ab8976fe1'
            'ee2c5da1540987e939b7539a761ad4165c309fea67ef2ba3c4ff323f8dcb1d87')

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-build.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --without-benchmark \
    --without-byexample \
    --without-pytest \
    --without-doc
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libeantic/libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
