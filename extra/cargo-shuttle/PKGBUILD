# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Stijn Seghers <stijnseghers at gmail dot com>

pkgname=cargo-shuttle
_commit=5b9885ce5dfdf29b1bc60ad26c63aa3abc58eb99
pkgver=0.17.0
pkgrel=1
pkgdesc='Cargo command for the shuttle platform'
arch=('loong64' 'x86_64')
url="https://github.com/shuttle-hq/shuttle"
license=('Apache')
depends=('gcc-libs' 'openssl' 'zlib' 'curl')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url.git#commit=$_commit"
        "$pkgname-$pkgver-lockfile.patch::$url/commit/8adf67b4ebfde1d9cfb52f98ffa53f5e6e764a14.patch"
        "$pkgname-examples::git+https://github.com/shuttle-hq/examples.git")
sha512sums=('SKIP'
            'ae1aec1df493d3800567eabd152885d44b23264edfb40d74694c3664a563ec718eb8dd2a255f6ed1da31cf8a5a36c8ffb5709a7bc073c4fc7894a9269100863e'
            'SKIP')
options=('!lto')

prepare() {
  cd "$pkgname"
  patch -Np1 -i "../$pkgname-$pkgver-lockfile.patch"
  git submodule init
  git config submodule."examples".url "${srcdir}/${pkgname}"-examples
  git -c protocol.file.allow=always submodule update --init --recursive
  mkdir completions

  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname/$pkgname"
  cargo build --release --frozen
  cd ..
  local compgen="target/release/$pkgname generate -s"
  $compgen bash >"completions/$pkgname"
  $compgen fish >"completions/$pkgname.fish"
  $compgen zsh >"completions/_$pkgname"
}

check() {
  cd "$pkgname/$pkgname"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 "$pkgname/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 "completions/$pkgname" -t "$pkgdir/usr/share/bash-completion/completions/"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions/"
}

# vim:set ts=2 sw=2 et:
