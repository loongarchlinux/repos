# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-launchpad
pkgver=0.2.1
pkgrel=1
pkgdesc='The "launcher" or "start menu" component for DDE - next generation'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-launchpad"
license=('GPL3')
# dtkdeclarative: qml: org.deepin.dtk
# kitemmodels: qml: org.kde.kitemmodels
depends=('qt5-base' 'qt5-declarative' 'qt5-quickcontrols2' 'qt5-svg' 'dtkcore' 'dtkgui'
         'glib2' 'appstream-qt5' 'dtkdeclarative' 'kitemmodels5' 'glibc' 'gcc-libs')
makedepends=('cmake' 'ninja' 'gtest' 'qt5-tools')
provides=('deepin-launcher')
conflicts=('deepin-launcher')
replaces=('deepin-launcher')
source=("https://github.com/linuxdeepin/dde-launchpad/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         appstream-1.0.patch)
sha512sums=('f8bad872f4ceb38699e1da90077bea6d6aab2a55a060feb0f970b3350ef0d713f942464b1de124d38e804c90513cd22c6360bed0d69219936f60f6cce7d437eb'
            '84ce82aa281133786f98531010641ec703b95ade428ab14b4c8549c6b26d44af995d49ecc03f86ae68424ade5f92dffefac319af360585553acf6bf23e4d12fb')

prepare() {
  cd dde-launchpad-$pkgver
# Support appstream 1.0
  patch -p1 -i ../appstream-1.0.patch
}

build() {
  cd dde-launchpad-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-launchpad-$pkgver
  DESTDIR="$pkgdir" ninja install
}
