# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mintsuki <mintsuki@protonmail.com>

pkgname=limine
pkgver=5.20230830.0
pkgrel=1
pkgdesc="An advanced, portable, multiprotocol bootloader"
arch=('loong64' 'x86_64')
url="https://limine-bootloader.org/"
license=('BSD')
depends=('glibc')
makedepends=('nasm' 'mtools' 'llvm' 'lld' 'clang')
source=("https://github.com/limine-bootloader/limine/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ddd417f9caab3ef0f3031b938815a5c33367c3a50c09830138d208bd3126c98f')

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-all TOOLCHAIN_FOR_TARGET=llvm
	make
}

package() {
	cd "${pkgname}-${pkgver}"
  	make DESTDIR="${pkgdir}" install
  	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
