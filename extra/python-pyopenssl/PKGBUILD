# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=python-pyopenssl
pkgver=22.0.0
pkgrel=1
arch=('any')
pkgdesc="Python3 wrapper module around the OpenSSL library"
url='https://github.com/pyca/pyopenssl'
license=('Apache')
depends=('python-cryptography')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pretend' 'python-flaky')
source=(https://github.com/pyca/pyopenssl/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('9e2aabdc4591662520320a22f75a3fa2d68576508f4c87a5a03ab1f14e02b099fbd054d72212ca6e780ee0146aae142365f7f79ec38557ceba0062ea72ee2178')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd pyopenssl-$pkgver
  python setup.py build
}

check() {
  cd pyopenssl-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd pyopenssl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
