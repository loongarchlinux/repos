# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=41.0
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://gitlab.gnome.org/GNOME/gnome-screenshot"
arch=(loongarch64 x86_64)
license=(GPL2)
depends=(gtk3 libhandy)
makedepends=(git meson appstream-glib)
groups=(gnome)
_commit=a867bea20ec621f42e8bd815311654cb687d6733  # tags/41.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-screenshot.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
