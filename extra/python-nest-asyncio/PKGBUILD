# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-nest-asyncio
pkgver=1.6.0
pkgrel=1
pkgdesc="Patch asyncio to allow nested event loops"
url="https://github.com/erdewit/nest_asyncio"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
provides=('python-nest_asyncio')
replaces=('python-nest_asyncio')
source=("https://github.com/erdewit/nest_asyncio/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b594bb2b245624032585af34540025bdc6b8224a6d59bd8ffcc5d5673d47c49736ac96cd20b5ca5ce311f8e9274df68560a4b2904273d4036f1560eeb74cfdf7')

build() {
  cd nest_asyncio-$pkgver
  python setup.py build
}

check() {
  cd nest_asyncio-$pkgver
  PYTHONPATH="$PWD/build/lib" python tests/nest_test.py
}

package() {
  cd nest_asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
