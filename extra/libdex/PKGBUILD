# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=0.2.0
pkgrel=3
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(loong64 x86_64)
license=(LGPL)
depends=(
  glib2
  liburing
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
_commit=c59ec85110954d8e9a1bf31322944225e1dc6874  # tags/0.2.0^0
source=("git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libdex
  git describe --tags | sed -r 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libdex

  # Build fixes
  git cherry-pick -n dd2453dab189643a73e8 2194086a5bf83a56a5bf
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  arch-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
