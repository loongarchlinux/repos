# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Aleksana QWQ <me@aleksana.moe>

pkgname=hyfetch
pkgver=1.4.9_1
pkgrel=1
pkgdesc="Neofetch with LGBTQ+ pride flags!"
arch=('any')
url='https://github.com/hykilpikonna/hyfetch'
license=('MIT')
depends=('python>=3.7.0' 'python-setuptools' 'python-typing_extensions' 'python-importlib-metadata')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver//_/-}.tar.gz")
sha256sums=('81f2b8fd25871e87888259680012c6dcd670eddb61a81aea22fc780c914fb6e3')

build() {
	cd "${pkgname}-${pkgver//_/-}"
  	python setup.py build
}

package() {
	cd "${pkgname}-${pkgver//_/-}"
  	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build 

	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 "${pkgname}/scripts/autocomplete.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
	install -Dm 644 "${pkgname}/scripts/autocomplete.zsh" "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
