# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: M0Rf30

pkgname=clonezilla
pkgver=3.35.2
pkgrel=5
pkgdesc="ncurses partition and disk imaging/cloning program"
arch=('any')
url="https://clonezilla.org"
license=('GPL2')
depends=('drbl' 'partclone' 'ntfs-3g' 'partimage' 'pigz' 'sshfs' 'parted'
         'gptfdisk' 'dosfstools' 'gzip' 'bzip2' 'pbzip2' 'lbzip2' 'lrzip'
         'xz' 'pixz' 'lzop' 'ecryptfs-utils' 'screen' 'perl' 'cifs-utils')
backup=('etc/drbl/drbl-ocs.conf')
source=(https://free.nchc.org.tw/drbl-core/src/stable/$pkgname-$pkgver.tar.xz
        usrbin.patch)
md5sums=('aaf2f2c009b26cbaf0a2edcdab4f9cfc'
         'a976c9209f7bbccd352a9d0a5c8b41ae')
sha256sums=('fa9d83d74f22a1993590c85c773ec7c374b9b0d2b1a8658e365171f80bee982e'
            '9340f18b27a1b3b41c4935c5831f6503e4cadf333f03433769906da40a6e38e6')

prepare() {
  cd $pkgname-$pkgver

  patch -Np0 -i "$srcdir"/usrbin.patch
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install  
}
