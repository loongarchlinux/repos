# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-clocks
pkgver=41.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(loongarch64 x86_64)
license=(GPL)
depends=(gtk3 libgweather gnome-desktop geoclue2 geocode-glib gsound libhandy)
makedepends=(vala gobject-introspection yelp-tools git meson)
groups=(gnome)
_commit=90d36119a5ac0bb86586e261a26562baea4d577c  # tags/41.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-clocks.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
