# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=66
_commit=0b3647a3ce19530cd3bbec88a9da72c12586f2ef  # refs/tags/v66
pkgrel=1
pkgdesc="Tools for creating Arch Linux live and install iso images"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/archiso"
license=(GPL3)
depends=(arch-install-scripts bash dosfstools e2fsprogs erofs-utils grub
libarchive libisoburn mtools squashfs-tools)
makedepends=(git)
checkdepends=(shellcheck)
optdepends=(
  'edk2-ovmf: for emulating UEFI with run_archiso'
  'openssl: for codesigning support when building netboot artifacts'
  'qemu-desktop: for run_archiso'
)
source=(git+https://gitlab.archlinux.org/archlinux/archiso.git#tag=$_commit?signed)
sha256sums=('SKIP')
validpgpkeys=(
  'C7E7849466FE2358343588377258734B41C31549' # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF' # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgbase
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgbase
}
