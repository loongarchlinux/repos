# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.1.4
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(Apache)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake)
source=(https://github.com/scipopt/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3526f3f9a6036c4b51f324f24535b5ee63e26cbc5d3f893a765cbc9cd721fac9')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
