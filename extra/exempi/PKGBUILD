# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: André Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.3
pkgrel=2
pkgdesc='Library to parse XMP metadata'
arch=('loong64' 'x86_64')
url='https://libopenraw.freedesktop.org/exempi/'
license=('BSD')
depends=('expat' 'gcc-libs' 'glibc' 'zlib')
makedepends=('boost')
source=("https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('80b3aa0b7f54978c6a49cc5f5f1d3b6e9524b59de74ef92b3bf0abeba4c28fb1'
            'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
