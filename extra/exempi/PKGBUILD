# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: André Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.4
pkgrel=1
pkgdesc='Library to parse XMP metadata'
arch=('loong64' 'x86_64')
url='https://libopenraw.freedesktop.org/exempi/'
license=('BSD')
depends=('expat' 'gcc-libs' 'glibc' 'zlib')
makedepends=('boost')
source=("https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('fa9c2456bf570172f0877c4eb13cd204c62ae1fdf65c3d6b71b41d88e1066f84'
            'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
