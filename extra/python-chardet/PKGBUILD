# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python-chardet
pkgver=4.0.0
pkgrel=5
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
depends=('python')
pkgdesc="Python3 module for character encoding auto-detection"
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/chardet/chardet-${pkgver}.tar.gz")
sha512sums=('ebd7f420e1094445270db993f6373ffe7370419e002b0bb13299dc6c9b0f7c4e77b0f44f871fba6371e6869e7c86728514367db377e3137487a3acf50cb81e96')

build() {
   cd "$srcdir"/chardet-$pkgver
   python setup.py build
}

check() {
   cd "$srcdir"/chardet-$pkgver
   python -m pytest
}

package() {
   cd chardet-$pkgver
   python setup.py install --root="$pkgdir" --optimize=1
}
