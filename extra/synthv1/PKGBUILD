# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=synthv1
pkgname=(
  synthv1
  synthv1-lv2
  synthv1-standalone
)
pkgver=0.9.31
pkgrel=1
pkgdesc="Old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=(loong64 x86_64)
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=(GPL2)
makedepends=(
  alsa-lib
  cmake
  jack
  liblo
  lv2
  qt6-base
  qt6-svg
  qt6-tools
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('0d09c2beb53e646ac0c1b840357b4d7ce25c2506ab26dbe4e00a0543ddda1588c02a6b24de4322c54eafc813a58d5ae156927310bf9a93aafe9f78f30211419e')
b2sums=('2f803e11731b491ba6f18f74aa0ca72f350cc22b5972dec0d4c9269dc09339ec8f0d6e4a2c76833efbe2156da2c7c3f8a53a67bb63ecce559bb3094f9adf017e')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCONFIG_QT6=ON
    -S $pkgname-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package_synthv1() {
  depends=(
    $pkgbase-{lv2,standalone}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"

    # have find-libdeps resolve dependencies properly:
    # https://gitlab.archlinux.org/archlinux/devtools/-/issues/102
    find usr/lib -type f -iname "*.so" -exec chmod +x {} \;

    _pick $pkgbase-lv2 usr/lib/lv2
    _pick $pkgbase-standalone usr/{bin,share}
  )
}

package_synthv1-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    gcc-libs
    glibc
    lv2-host
    qt6-base
    qt6-svg
  )
  optdepends=(
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_synthv1-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends=(
    alsa-lib
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    liblo
    qt6-base
    qt6-svg
  )
  optdepends=(
    'new-session-manager: for session management using the standalone application'
    'qt6-wayland: for native wayland support'
  )

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
