# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kmidimon
pkgver=1.4.0
pkgrel=1
pkgdesc="A MIDI monitor for Linux using ALSA sequencer"
arch=(loong64 x86_64)
url="https://kmidimon.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-5compat
  qt6-base
)
makedepends=(
  alsa-lib
  cmake
  drumstick
  pandoc
  qt6-tools
)
source=($pkgname-$pkgver.tar.gz::https://github.com/pedrolcl/$pkgname/archive/refs/tags/RELEASE_${pkgver//./_}.tar.gz)
sha512sums=('bcfc38f82da1bd7f646d15e72b477fb942f12a49411fd5f9635035b2ca214d6c8e5227b2514d3dee22038d1b24ae53892e80f14d057b61ebe7866c73ce4ae08c')
b2sums=('12a841661545f31d49d698d47f1ff74005ada4976407820281e0a8a85bd951eb2a2c6de7aa1bb56fdc47e174e5f0295d5173ec53b3362204718e2155d051c846')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-RELEASE_${pkgver//./_}
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    drumstick libdrumstick-alsa.so libdrumstick-file.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-RELEASE_${pkgver//./_}/{AUTHORS,ChangeLog,{INSTALL,NEWS,README,TODO}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
