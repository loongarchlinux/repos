# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sof-firmware
pkgver=1.6
pkgrel=1
pkgdesc="Sound Open Firmware"
arch=(any)
url="https://www.sofproject.org/"
license=(custom:BSD custom:ISC)
options=(!strip)
_tarname=sof-bin-stable-v$pkgver
source=($_tarname.tar.gz::https://github.com/thesofproject/sof-bin/archive/stable-v$pkgver.tar.gz)
sha256sums=('eda78eff9b0734a05f70edf1fd7406ab0965bef96d9e530258772fc756f57cea')

package() {
  cd $_tarname

  mkdir -p "$pkgdir/usr/lib/firmware"
  ROOT="$pkgdir/usr" SOF_VERSION=v$pkgver ./go.sh

  install -Dt "$pkgdir/usr/bin" tools/v$pkgver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
