# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.7.5
pkgrel=3
pkgdesc='An easy to use and easily extensible IDE/GUI for R'
url='https://rkward.kde.org/'
arch=(loong64 x86_64)
license=(GPL)
depends=(gcc-libs
         glibc
         karchive5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         ki18n5
         kio5
         kjobwidgets5
         kparts5
         kservice5
         ktexteditor5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         qt5-base
         qt5-declarative
         qt5-webengine
         r)
makedepends=(extra-cmake-modules
             kdoctools5)
source=(https://download.kde.org/stable/rkward/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('eb7d385b0b9e694fd639c858340ac14cdc7c932ec5e06b61adffbf77d12cd8c8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
