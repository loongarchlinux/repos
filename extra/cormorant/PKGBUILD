# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgbase=cormorant
pkgname=("otf-$pkgbase" "ttf-$pkgbase")
pkgver=3.609
pkgrel=2
pkgdesc='Open-source display font family'
arch=(any)
url="https://github.com/CatharsisFonts/${pkgbase^}"
license=(OFL)
_archive="${pkgbase^}-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('78fff4efb4d97069959a2b03a90b3e7c0c8f6e48349bf93c4ad80c3db779ef0b')

package_otf-cormorant() {
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" "2. OpenType Files/"*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt
}

package_ttf-cormorant() {
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/fonts/TTF/" "1. TrueType Font Files/"*.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt
}
