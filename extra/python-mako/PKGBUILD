# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mako
pkgver=1.1.6
pkgrel=3
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python-markupsafe' 'python-beaker' 'python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-babel' 'python-pygments'
              'python-dogpile.cache')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha512sums=('e87d883d7a549a902bb2e16d20306ccd3dd7b3bf855dfdaff20477dd182a782a5cecbdc447e4b1607f190a7b0febcca001333137f7cac6ffa674824c2a2fd1bb'
            'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
  cd Mako-$pkgver
  sed -e '/\[tool:pytest\]/a filterwarnings =\n    error\n    ignore:Creating a LegacyVersion has been deprecated and will be removed in the next major release:DeprecationWarning' \
      -e 's/-W error//' \
      -i setup.cfg
}

build() {
  cd Mako-$pkgver
  python setup.py build
}

check() {
  cd Mako-$pkgver
  python -m pytest
}

package() {
  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
