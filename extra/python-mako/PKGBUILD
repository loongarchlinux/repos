# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mako
pkgver=1.2.2
pkgrel=1
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python-markupsafe')
makedepends=('python-setuptools')
# python-lingua isn't packaged
checkdepends=('python-pytest' 'python-babel' 'python-beaker' 'python-dogpile.cache'
              'python-pygments')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha512sums=('4a9303b18710ad8111f4a83e963289df490cf1d01bf3b262e89fb2e9bb98a96fe2c83f8900597d836411d136b6ccb686f67b6e1554c4fe4868e68af1c864bcfe'
            'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1'   # Michael Bayer
              '2CA9B722133AF20453401FD1A33AC2044BFDF51E')  # Mako Maintainer

build() {
  cd Mako-$pkgver
  python setup.py build
}

check() {
  cd Mako-$pkgver
  python -m pytest
}

package() {
  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
