# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=23.11
pkgrel=1
pkgdesc='Shared library for MediaInfo'
arch=('loong64' 'x86_64')
url='https://mediaarea.net'
license=('BSD')
depends=(
  'glibc'
  'gcc-libs'
  'curl'
  'glib2'
  'libmms'
  'libzen'
  'tinyxml2'
  'graphviz'
  'zlib'
)
source=("$pkgname-$pkgver.tar.xz::https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz")
sha512sums=('8801146063524783bda61d2fd9d55d8176b914953a11e9ece93d36729f4bb195bf882f3061b0f4e508509e0c956da2c953b96e0100e1873d64cf44e5d29c5ffc')
b2sums=('3c8d1f848ab021e647ae64f8f91d091e9aad804b95c6d44bc3faffa4634dccf28cf5eaff7e2ce7b23221d6f3e42c46989966f41605199d74f86d5096eb23d779')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/MediaInfoLib/LICENSE"
}
