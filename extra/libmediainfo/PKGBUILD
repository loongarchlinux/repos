# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=23.06
pkgrel=1
pkgdesc='Shared library for MediaInfo'
arch=('loong64' 'x86_64')
url='https://mediaarea.net'
license=('BSD')
depends=(
  'glibc'
  'gcc-libs'
  'curl'
  'glib2'
  'libmms'
  'libzen'
  'tinyxml2'
  'graphviz'
  'zlib'
)
source=("$pkgname-$pkgver.tar.xz::https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz")
sha512sums=('0f5f37f0e04903cbe32454c3cc88c20b2f7be15472f81d19879c65bc6bdeabddaf5cf146b713b05acad12ef69fcb88c18cbf284b8b342a0dff3d3ce1f4838ccc')
b2sums=('d43f874fcfba166e68e13c6938de94a14020c909927adca979395a20529ae7c4e7e39ffc8022715f771f12974905341962eb8a11100c7eb59a07288d3848ec3d')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/MediaInfoLib/LICENSE"
}
