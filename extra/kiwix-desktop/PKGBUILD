# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=kiwix-desktop
pkgver=2.3.1
pkgrel=4
pkgdesc="Offline reader for Web content, especially intended to make Wikipedia available offline"
url="https://github.com/kiwix/kiwix-desktop"
arch=("loong64" "x86_64")
license=("GPL3")
depends=("qt5-base" "qt5-webengine" "libkiwix" "aria2")
makedepends=("qt5-tools")
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"${url}/commit/1b322d8f01c787846546a6473f153cf1daa41e65.patch"
)
sha256sums=('799d887efd6a855aefe27452b1e3c02da3b443b1ea19c961b7d58dc55e019342'
            '439e841e002644eba76f948de64fbbefb69bae41c3e8490ce62d39315b94bfd2')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i "../1b322d8f01c787846546a6473f153cf1daa41e65.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	export CFLAGS+=" ${CPPFLAGS}"
	export CXXFLAGS+=" ${CPPFLAGS}"
	qmake PREFIX="/usr"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	make INSTALL_ROOT="$pkgdir/" install
}
