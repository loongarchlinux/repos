# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=aalib
pkgver=1.4rc5
_pkgver=1.4.0
pkgrel=16
pkgdesc="ASCII art graphic library"
arch=('loong64' 'x86_64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('glibc' 'gpm' 'libx11' 'ncurses' 'slang')
optdepends=('xorg-fonts-misc: x11 driver'
            'xorg-mkfontscale: x11 driver')
source=("https://downloads.sourceforge.net/aa-project/$pkgname-$pkgver.tar.gz"
        'aalib.m4.patch')
sha256sums=('fbddda9230cf6ee2a4f5706b4b11e2190ae45f5eda1f0409dc4f99b35e0a70ee'
            '0202545881ec34bd637c04b5896b9b3093609530cc4f85e1d5ffe9a3136d9483')

prepare() {
  cd $pkgname-$_pkgver

  # https://sourceforge.net/p/aa-project/patches/9/
  # https://bugs.archlinux.org/task/53482
  patch -Np0 -i ../aalib.m4.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$_pkgver
  ./configure --prefix=/usr --with-curses-driver
  make
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install
}
