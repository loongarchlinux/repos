# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: George Rawlinson <george@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=proxytunnel
pkgver=1.12.0
pkgrel=1
pkgdesc="Creates tunnels through HTTP(S) proxies for any TCP based protocol"
arch=('loong64' 'x86_64')
url="https://github.com/proxytunnel/proxytunnel"
license=('GPL')
depends=('glibc' 'openssl')
makedepends=('asciidoc' 'xmlto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('3d5090a7f8f38fc928d68222e703913fcaade652c4d3cbf28b68682923c3558a7b3e17f00893a60b0cea4e27af185b50cf1ab52064e5125922c50716d5d9c0b7')

prepare() {
  cd "$pkgname-$pkgver"
  # append -DOPENSSL11 to CFLAGS via SSL-specific OPTFLAGS: See ptstream.c
  sed -i '/^OPTFLAGS += -DUSE_SSL$/ s/$/ -DOPENSSL11/' Makefile
  # fixup modes otherwise strip fails
  sed -i 's|555|755|g' Makefile
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make prefix=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
