# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='domain_name'
pkgname="ruby-${_gemname}"
pkgver=0.6.20231109
pkgrel=1
pkgdesc='Domain Name manipulation library for Ruby'
arch=('any')
url='https://github.com/knu/ruby-domain_name'
license=('BSD' 'MPL2')
makedepends=('ruby-rdoc' 'ruby-bundler')
checkdepends=('ruby-rake' 'ruby-minitest' 'ruby-test-unit')
depends=('ruby' 'ruby-unf')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('8dddd2e251b3c878187949705cd6ca881b9f51916c2bacc8ad7a8b28aa80853af08ebf0fb844ce715706dc8bc6352b1a3f21bd4ab583cc9dbf2ef2e41e787d9b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  # we built with a tar ball, so `git` won't wwork
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
