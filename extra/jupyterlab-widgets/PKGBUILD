# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=3.0.8
pkgrel=2
pkgdesc='JupyterLab extension to use ipywidgets'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyterlab python-ipywidgets)
makedepends=(python-build python-installer python-setuptools python-jupyter_packaging)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
replaces=(jupyter-widgetsnbextension)
sha256sums=('d428ab97b8d87cc7c54cbf37644d6e0f0e662f23876e05fa460a73ec3257252a')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
