# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-util-dfm
pkgver=1.2.18
pkgrel=1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('loong64' 'x86_64')
license=('LGPL')
url="https://github.com/linuxdeepin/util-dfm"
depends=('libisoburn' 'libmediainfo' 'qt5-base' 'udisks2' 'glib2' 'gcc-libs' 'util-linux-libs'
         'libsecret')
makedepends=('cmake' 'ninja')
source=("https://github.com/linuxdeepin/util-dfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5116c7d5e47444472b7f913e9ae611fbca5c72583295872f060a82f55d76c0691ce2ef7e695c0d17dc83fb86598e475ed21d3a4fcdbbe4a5b6950ef2d96c33a8')

build() {
  cmake -G Ninja -B build -S util-dfm-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
