# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apko
pkgver=0.13.1
pkgrel=1
pkgdesc="Declarative APK-based container building tool with support for sigstore signatures"
url="https://github.com/chainguard-dev/apko"
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=('apk-tools' 'alpine-keyring')
makedepends=('go')
source=("https://github.com/chainguard-dev/apko/archive/v${pkgver}/apko-${pkgver}.tar.gz")
sha256sums=('a0f501d26f0c61686429dce4177e3c3d67030e405544e7c278f7fa235fb1b06e')
b2sums=('fe359df450c0c8598fe7b1b50c70ec48f6e28d329eb641335ae4d87f7c3e80b1c242e39e751d5be07039ea18a71cff7cbac6d44fe7dcc99d16dbdb31f9b36e0b')

build() {
  cd "${pkgname}-${pkgver}"
  mkdir build
  CGO_LDFLAGS="${LDFLAGS}" \
    GOFLAGS="-trimpath -buildmode=pie -mod=readonly" \
    go build -o build/ -tags -tags=pivkey,pkcs11key ./...

  for i in bash fish zsh; do
    build/apko completion ${i} > ./apko.${i}
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  # skip TestPublish: https://github.com/chainguard-dev/apko/issues/881
  go test ./... -skip 'TestPublish|TestBuild'
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/apko "${pkgdir}"/usr/bin/apko
  install -Dm644 apko.bash "${pkgdir}"/usr/share/bash-completion/completions/apko
  install -Dm644 apko.fish "${pkgdir}"/usr/share/fish/completions/apko.fish
  install -Dm644 apko.zsh "${pkgdir}"/usr/share/zsh/site-functions/_apko
}

# vim: ts=2 sw=2 et:
