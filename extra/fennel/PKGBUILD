# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Katie Wolfe <wolfe@katie.host>

pkgname=fennel
pkgver=1.4.0
pkgrel=1
pkgdesc='A Lua Lisp language'
arch=('any')
url='https://fennel-lang.org'
license=('MIT')
depends=('lua')
makedepends=('git')
_commit='29aa58a0333fe2a0d776499bdc44e21c46b4ecf6'
source=("$pkgname::git+https://git.sr.ht/~technomancy/fennel#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  make build
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
