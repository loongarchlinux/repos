# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.22.5
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(loong64 x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(
  glibc
  gumbo-parser
  jbig2dec
  libmupdf
  openjpeg2
  python
)
makedepends=(
  freetype2
  git
  libjpeg-turbo
  python-build
  python-distro
  python-installer
  python-setuptools
  python-wheel
  swig
  systemd
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
)
optdepends=(
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
)
sha512sums=('683b713dd1db45c387de205700185722634fd6fdf499007927710a14c037f11be8dffa12843620bfe12d8cd93efa4b3289ff216f27eed49099851d5595f28d85')
b2sums=('c6e460a25cf60aba95ff8336d9712f1ccea72b258d935f625413821a841da348022bcbafae6dc05a47aef497ffc808db9f92ba620cc039b4836ad8d2d81dfa81')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  # we package swig and don't need python-swig
  sed -e 's/, "swig"//' -i $_name-$pkgver/pyproject.toml
}

build() {
  cd $_name-$pkgver
  # NOTE: build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=""
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  depends+=(
    freetype2 libfreetype.so
    libjpeg-turbo libjpeg.so
  )

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
