# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-net-http-persistent
pkgver=4.0.1
pkgrel=1
pkgdesc='Thread-safe persistent connections with Net::HTTP'
arch=(any)
url='https://github.com/drbrain/net-http-persistent'
license=(MIT)
depends=(ruby-connection_pool)
makedepends=(ruby-hoe ruby-rake)
checkdepends=(ruby-minitest)
options=(!emptydirs)
source=(https://github.com/drbrain/net-http-persistent/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3e5678c0befdcf26679c16e7583b2627fa72483f778fc131c17ba5cde34d918c')

prepare() {
  cd net-http-persistent-$pkgver
  sed 's/~>/>=/' -i Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd net-http-persistent-$pkgver
  rake gem
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/net-http-persistent-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd net-http-persistent-$pkgver
  # https://github.com/drbrain/net-http-persistent/issues/141
  GEM_HOME="tmp_install/$_gemdir" rake test || echo "Tests failed"
}

package() {
  cd net-http-persistent-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
