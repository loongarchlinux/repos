# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=pv
pkgver=1.7.0
pkgrel=1
pkgdesc='A terminal-based tool for monitoring the progress of data through a pipeline.'
arch=('loong64' 'x86_64')
url='https://www.ivarch.com/programs/pv.shtml'
# _url="https://github.com/a-j-wood/$pkgname"
license=('custom:Artistic 2.0')
depends=('glibc')
_archive="$pkgname-$pkgver"
source=("${url%/*}/sources/$_archive.tar.bz2"
        "$_archive.tar.bz2.sig::${url%/*}/sources/$_archive.tar.bz2.txt")
# source=("$_url/releases/download/v$pkgver/$_archive.tar.gz")
sha256sums=('1372b41053881a05e2df10cb054304decc0233261c0aa0e96185842fa5a422ad'
            'SKIP')
validpgpkeys=(2189D5C29F041F2277AFD50AC1596BA33FC56F51) # andrew.wood@ivarch.com

build() {
  cd "$_archive"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_archive"
  make DESTDIR="$pkgdir" install
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" doc/COPYING
}
