# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Morgenstern <charles [at] charlesbwise [dot] com>

pkgname=cherrytree
pkgver=0.99.55
pkgrel=1
pkgdesc="Hierarchical note-taking application"
arch=('loong64' 'x86_64')
url="https://www.giuspen.com/cherrytree/"
license=('GPL3')
depends=('fmt' 'gspell' 'gtksourceviewmm' 'libxml++2.6' 'uchardet' 'vte3')
makedepends=('cmake' 'python' 'spdlog')
checkdepends=('xorg-server-xvfb')
source=("https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz"{,.asc})
sha512sums=('3c22df32fe0a4449607862c87aac46f52047a37a6404c848ce9a1e5308f87de7c5ca47c8e4abb21d1ecbe53bb7fc714fa7e0de0302caabd18e6eb2137c3438b6'
            'SKIP')
validpgpkeys=('C7BF38CE0BD442C2369AA984049128A20CE0648D') # Giuseppe Penone <giuspen [at] gmail [dot] com>

build() {
  cmake -B build -S "${pkgname}_${pkgver}" \
  -DINSTALL_GTEST:BOOL='OFF' \
  -DAUTO_RUN_TESTING:BOOL='OFF' \
  -DCMAKE_BUILD_TYPE='None' \
  -DCMAKE_INSTALL_PREFIX='/usr' \
  -Wno-dev
  cmake --build build
}

check() {
  xvfb-run build/run_tests_with_x_1
  xvfb-run build/run_tests_with_x_2
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
