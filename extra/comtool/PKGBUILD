# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=2.2.0
pkgrel=1
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-requests' 'python-babel' 'python-qtawesome')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/v$pkgver.tar.gz")
sha512sums=('e73263a96c4dd7bdf8c510a0ea78fc3912b21fb3c14a17c739b916efaeb4ae04c1810101cc17ef4b252d9f908d29a7aa8c4f2b199870487b42cdba1d81370772')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  rm "$pkgdir"/usr/{LICENSE,README.MD}
}

# vim:set ts=2 sw=2 et:
