# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=koko
pkgver=23.04.2
pkgrel=1
pkgdesc='Image gallery application'
url='https://apps.kde.org/koko/'
arch=(loong64 x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-graphics)
depends=(kirigami2 kdeclarative qt5-location kfilemetadata kquickimageeditor)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://download.geonames.org/export/dump/cities1000.zip
        https://download.geonames.org/export/dump/admin1CodesASCII.txt
        https://download.geonames.org/export/dump/admin2Codes.txt)
sha256sums=('420b1f091edd13b9494b2ab0488a530fbb652a78e627dc4419c6041de985cf6e'
            'SKIP'
            'b524b9f1df159879e266be551ecac78e674c46a080ca6108d84f057e18b64799'
            'b81ddb93812c3c7e0e26d5d7520fa224992f22c2ef3d6422f5f6ca0a6c8e2ffe'
            '8fbf2f16ed3cb97ac62f48b9bfdf51af20fe0ea054c9b836c511a322e13b5782')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cp -L cities1000.zip admin1CodesASCII.txt admin2Codes.txt $pkgname-$pkgver/src
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
