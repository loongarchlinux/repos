# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.85.0
_commit=a6f62e05ae5b4e9ba4027fbfffd21025a898783e
pkgrel=1
pkgdesc='A new type of shell'
arch=('loong64' 'x86_64')
url='https://www.nushell.sh'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'libcrypto.so'
  'libssl.so'
  'zlib'
)
makedepends=('cargo' 'git')
install=nushell.install
source=("git+https://github.com/nushell/nushell.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  CFLAGS+=" -ffat-lto-objects"

  cargo build --release --frozen --workspace --features=extra,dataframe
}

check() {
  cd "$pkgname"

  # https://github.com/nushell/nushell/issues/10468
  cargo test --frozen --workspace --features=extra,dataframe -- \
    --skip commands::table::table_expand_exceed_overlap_0 \
    --skip commands::table::table_expand_padding_not_default \
    --skip commands::table::test_collapse_big_0 \
    --skip commands::table::test_expand_big_0
}

package() {
  cd "$pkgname"

  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "nu*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
