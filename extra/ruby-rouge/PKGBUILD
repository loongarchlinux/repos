# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=4.2.0
pkgrel=1
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='http://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b0dd5fb3ec2ebb1e7cd50460ea589026e70e5ee8b7b55a9a9c48990f22d3845a5a1bb7ce83001f5316852568c8bc7bca3ee2db9deacd60cde2b42ea9ba391d18')
b2sums=('b6add86c73729ed3df8f24ff2b454c520416f0640e090ca04372f8f00184bbf432119993ea6b809e1aec50899bd05bccebde0ca6d35f420c8d2a1e57c61a84d2')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
