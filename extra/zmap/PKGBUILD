# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Arch3y <arch3y[at]archstrike[dot]org>

pkgname=zmap
pkgver=3.0.0
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
arch=('loong64' 'x86_64')
license=('Apache')
depends=('gmp' 'hiredis' 'json-c' 'libpcap' 'libmongoc')
makedepends=('byacc' 'cmake' 'gengetopt' 'pkg-config')
backup=('etc/zmap/blocklist.conf' 'etc/zmap/zmap.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz)
sha512sums=('3ea4d0e4052c2e4ab303f1ac8c35339bd3e282993b7bbd6b75d2a039df934e57f4d93883b5034c5d25e3f8b2e81f3a036cf1bf1fb2ad6620afa6f7fd3366b0f1')

build() {
  cd ${pkgname}-${pkgver}
  cmake -DWITH_JSON=ON -DWITH_REDIS=ON -DWITH_MONGO=ON
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 src/{zmap,ztee,zblocklist} -t "${pkgdir}/usr/bin"
  install -Dm 644 src/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 conf/* -t "${pkgdir}/etc/zmap"
  install -Dm 644 AUTHORS CHANGELOG.md INSTALL.md README.md \
    -t "${pkgdir}/usr/share/doc/zmap"
  cp -a examples "${pkgdir}/usr/share/doc/zmap"
}

# vim: ts=2 sw=2 et:
