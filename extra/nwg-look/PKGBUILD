# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-look
pkgver=0.2.6
pkgrel=1
pkgdesc="GTK3 settings editor adapted to work on wlroots-based compositors"
url="https://github.com/nwg-piotr/nwg-look"
arch=('loong64' 'x86_64')
license=('MIT')
depends=(
  'glibc'
  'gtk3'
  'libatk-1.0.so'
  'libcairo-gobject.so'
  'libcairo.so'
  'libfontconfig.so'
  'libfreetype.so'
  'libgdk-3.so'
  'libgdk_pixbuf-2.0.so'
  'libgio-2.0.so'
  'libglib-2.0.so'
  'libgmodule-2.0.so'
  'libgobject-2.0.so'
  'libgtk-3.so'
  'libharfbuzz.so'
  'libpango-1.0.so'
  'libpangocairo-1.0.so'
  'libz.so'
  'xcur2png'
)
makedepends=(
  'go'
)
source=(https://github.com/nwg-piotr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ac3cb75ece7058120a5ed6b4ec8a321af0a66b522fdc7627bbab44dc1aaac8f8')
b2sums=('23235ec75bdf89fb59f4893597b059fbee778fcf6b380a7348835141a779bc2bff12fd424e74b79e85cb5848af6029d451cf0831f138613b95e4542257f60415')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" nwg-look
  install -Dm 644 -t "${pkgdir}/usr/share/${pkgname}" stuff/main.glade
  install -Dm 644 -t "${pkgdir}/usr/share/${pkgname}/langs" langs/*
  install -Dm 644 -t "${pkgdir}/usr/share/applications" stuff/nwg-look.desktop
  install -Dm 644 -t "${pkgdir}/usr/share/pixmaps" stuff/nwg-look.svg
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim: ts=2 sw=2 et:
