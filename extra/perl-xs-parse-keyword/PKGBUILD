# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-xs-parse-keyword
pkgver=0.39
pkgrel=1
pkgdesc="XS functions to assist in parsing keyword syntax"
arch=('loong64' 'x86_64')
url="https://metacpan.org/dist/XS-Parse-Keyword"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-extutils-cchecker>=0.11' 'perl-module-build>=0.4004')
checkdepends=('perl-test2-suite')
source=("https://cpan.metacpan.org/authors/id/P/PE/PEVANS/XS-Parse-Keyword-${pkgver}.tar.gz")
sha512sums=('41ac31707b473d729a7e63d4607caa77fca9da00580b931576361416d78002f56acd04a0fee0c4dd09ffc1796a914f6ae934bdbce8fcc4c9222064c6e419b7ce')

build() {
  cd "XS-Parse-Keyword-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  /usr/bin/perl Build.PL
  ./Build
}

check() {
  cd "XS-Parse-Keyword-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  ./Build test
}

package() {
  cd "XS-Parse-Keyword-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install --installdirs=vendor --destdir="$pkgdir"
  find "$pkgdir" "(" -name .packlist -o -name perllocal.pod ")" -delete
}
