# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=rekor
pkgver=1.3.4
pkgrel=1
pkgdesc="Signature Transparency Log -- Sigstore client and server tools"
arch=('loong64' 'x86_64')
url="https://github.com/sigstore/rekor"
license=('Apache')
makedepends=('go' 'git')
checkdepends=('openssh')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/rekor/archive/v${pkgver}.tar.gz")
sha256sums=('08e220b6fbc473ecd3561e88c4fde2ca259f9daa895a17bed1f458c33c33a2b9')
b2sums=('0bfb01a750149e4d9d61a7bc46f49176f4b3639eefed0ceb3ba4753522660fd3cc8ba54fcb9ac98c27849b748d3cd5ff4ecb9f9c1492cb909dca92473238c4ea')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw -ldflags=-X=sigs.k8s.io/release-utils/version.gitVersion=v$pkgver"
  go build -o cmd/rekor-cli/rekor-cli cmd/rekor-cli/main.go
  go build -o cmd/rekor-server/rekor-server cmd/rekor-server/main.go
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 cmd/rekor-cli/rekor-cli "${pkgdir}/usr/bin/rekor-cli"
  install -Dsm755 cmd/rekor-server/rekor-server "${pkgdir}/usr/bin/rekor-server"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  "${pkgdir}/usr/bin/rekor-cli" completion bash > "${pkgdir}/usr/share/bash-completion/completions/rekor-cli"
  "${pkgdir}/usr/bin/rekor-cli" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_rekor-cli"
  "${pkgdir}/usr/bin/rekor-cli" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rekor-cli.fish"

  "${pkgdir}/usr/bin/rekor-server" completion bash > "${pkgdir}/usr/share/bash-completion/completions/rekor-server"
  "${pkgdir}/usr/bin/rekor-server" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_rekor-server"
  "${pkgdir}/usr/bin/rekor-server" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rekor-server.fish"
}
