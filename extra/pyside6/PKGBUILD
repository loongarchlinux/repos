# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside6
_qtver=6.1.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(loongarch64 x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='Enables the use of Qt6 APIs in Python applications'
depends=(shiboken6 qt6-declarative)
makedepends=(cmake python-setuptools python-numpy qt6-tools qt6-quickcontrols2 qt6-svg qt6-3d qt6-scxml qt6-datavis3d qt6-charts)
optdepends=('qt6-tools: QtHelp, QtUiTools bindings'
            'qt6-quickcontrols2: QtQuickControls2 bindings'
            'qt6-svg: QtSvg bindings'
            'qt6-3d: Qt3D bindings'
            'qt6-scxml: QtScxml bindings'
            'qt6-datavis3d: QtDataVisualization bindings'
            'qt6-charts: QtCharts bindings')
_pkgfn=pyside-setup-opensource-src-$_qtver
source=(https://download.qt.io/official_releases/QtForPython/pyside6/PySide6-$pkgver-src/${_pkgfn}.tar.xz)
sha256sums=('9136c98742443626c142beb77a777a0e2e6fa6653b5451fce54047f64d5f7d43')

build() {
  cmake -B build -S $_pkgfn/sources/pyside6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install egg-info
  export PATH="/usr/lib/qt6/bin:$PATH"
  cd $_pkgfn
  python setup.py egg_info --build-type=pyside6
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r PySide6.egg-info "$pkgdir"/$_pythonpath
}
