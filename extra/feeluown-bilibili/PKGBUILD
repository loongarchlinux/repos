# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-bilibili
pkgver=0.2.0
pkgrel=1
pkgdesc="Bilibili provider for FeelUOwn"
arch=('any')
url="https://github.com/feeluown/feeluown-bilibili"
license=('GPL3')
depends=('feeluown' 'python-cachetools' 'python-beautifulsoup4' 'python-pycryptodome')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown_bilibili/feeluown_bilibili-$pkgver.tar.gz")
sha512sums=('c9c1360b3257a9fc7cc12f87a1fc3d875882f173c51ff91abd40112c0f47a2896cebc7d84474c62f110dd85402f5fa1945980154928edf29eb25b4fc1a2b337b')

build() {
  cd feeluown_bilibili-$pkgver
  python -m build -nw
}

package() {
  cd feeluown_bilibili-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
