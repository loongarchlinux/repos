# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-bilibili
pkgver=0.3
pkgrel=1
pkgdesc="Bilibili provider for FeelUOwn"
arch=('any')
url="https://github.com/feeluown/feeluown-bilibili"
license=('GPL3')
depends=('feeluown' 'python-cachetools' 'python-beautifulsoup4' 'python-pycryptodome')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-bilibili/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7f59d3ace36780221574da59ead955b7dbbfc703b7f3a30b91bb8db06d6bbf80038dac4cca99e473049c86f2c56bef06f47089c62f1c150c50478e2418d41c9f')

build() {
  cd feeluown-bilibili-$pkgver
  python -m build -nw
}

package() {
  cd feeluown-bilibili-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
