# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-charts
pkgname=(python-pyqt6-charts)
pkgver=6.2.0
pkgrel=2
pkgdesc='Python bindings for QtChart'
arch=(loongarch64 x86_64)
url='https://www.riverbankcomputing.com/software/pyqtchart/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-charts python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-Charts/PyQt6_Charts-$pkgver.tar.gz)
sha256sums=('4ea4b6b2a6c2ae7643a33534acda9bee0b5308748a34529c9f09523167b3379c')

build() {
  cd PyQt6_Charts-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-charts(){
  cd PyQt6_Charts-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
