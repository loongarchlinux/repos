# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-libwww
pkgver=6.55
pkgrel=1
pkgdesc="The World-Wide Web library for Perl"
arch=('any')
url="https://metacpan.org/release/libwww-perl"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-encode-locale' 'perl-file-listing'
         'perl-html-parser' 'perl-http-cookies' 'perl-http-daemon'
         'perl-http-date' 'perl-http-negotiate' 'perl-lwp-mediatypes'
         'perl-net-http' 'perl-try-tiny' 'perl-uri' 'perl-www-robotrules')
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
checkdepends=('perl-test-fatal' 'perl-test-needs' 'perl-test-requiresinternet')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/libwww-perl-$pkgver.tar.gz)
sha512sums=('4a26bbcea343af513cc7d22761b7b9412da864bc7ff5e6d8cc6494978ceb829f001d1e221482a215e363e7f7857002120dbd12fda14b1b5778b1f437a5e338eb')

build() {
  cd libwww-perl-$pkgver
  perl Makefile.PL --aliases INSTALLDIRS=vendor
  make
}

check()  {
  cd libwww-perl-$pkgver
  make test
}

package() {
  cd libwww-perl-$pkgver
  make DESTDIR="$pkgdir" install
}
