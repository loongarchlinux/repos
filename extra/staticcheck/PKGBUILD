# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2023.1.5
pkgrel=1
pkgdesc="The advanced Go linter"
arch=('loong64' 'x86_64')
url="https://staticcheck.io"
license=("MIT")
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('1d5c1ed121c3e75ef2adfd4d95eed166b0d96c4820c248153fc6b5301547bc590e83bd7a30d5a80f730f754126f517e347caffb28502109a5b5ebcf165e610f5')
b2sums=('bf874bac03880845bb389f73c2ec52b7dcb50ec42e3ba9159c112137915287342a9e5ba6a6b7fd4361e3448bb3da0feae48312dbe95100ec004209014d23bf80')

prepare(){
	cd "go-tools-$pkgver"
	mkdir -p bin
}

build(){
	cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags ${LDFLAGS}" \
    -o bin\
    ./cmd/staticcheck
}

check(){
	cd "go-tools-$pkgver"
	GOROOT="/usr/lib/go" go test -v ./...
}

package(){
	cd "go-tools-$pkgver"
	install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
	install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
