# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=1.12
pkgrel=1
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("loong64" "x86_64")
license=('GPL')
url="https://www.gnu.org/software/libextractor/"
depends=('libltdl')
makedepends=('exiv2' 'file' 'flac' 'gst-plugins-base-libs' 'libgsf' 'libmpeg2' 'libvorbis'
             'poppler' 'libsmf' 'giflib')
optdepends=('exiv2: metadata support'
            'file: mime support'
            'flac: flac support'
            'giflib: GIF support'
            'gst-plugins-base-libs: gstreamer support'
            'libarchive: archive support'
            'libgsf: (optional) for ODF support'
            'libmpeg2: mpeg support'
            'libvorbis: ogg/vorbis support'
            'poppler: pdf support'
            'libsmf: midi support')
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E'
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC')
source=("https://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('8128464643aaa232117c9483cc4f42b9c200c1286dddf7bccbde000ccb77daf5b0daa7dc6510a8445575840252e6205b5dab6da7ef25a5248183b65c5b1603a8'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
