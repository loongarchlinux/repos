# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-log-log4perl
pkgver=1.56
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=('any')
url="https://metacpan.org/release/Log-Log4perl"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/E/ET/ETJ/Log-Log4perl-$pkgver.tar.gz")
sha512sums=('aa2a6991c6e2e97627e9a70018029777a793da09763cd2ca287c8552c0e77c6617db03145fbd0e2bcf294b3399f216b9b9812e0f73aaaa08072562e991dcfc7d')

build() {
  cd Log-Log4perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Log-Log4perl-$pkgver
  make test
}

package() {
  cd Log-Log4perl-$pkgver
  make DESTDIR="$pkgdir" install
}
