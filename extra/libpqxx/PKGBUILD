# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=7.8.1
pkgrel=2
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('loong64' 'x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python')
provides=("libpqxx-${pkgver%.*}.so")
source=(https://github.com/jtv/libpqxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0f4c0762de45a415c9fd7357ce508666fa88b9a4a463f5fb76c235bc80dd6a84')
b2sums=('ca2a9abd138113967dea6aedbb20687c0126f081f4adb2b8c0e03a3bf04b4f528206314b1d44b5d2243228eef41d0380ff67d66747161bf43efa6da8aab50017')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-doc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
