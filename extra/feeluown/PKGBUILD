# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=4.0.1
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-packaging' 'python-pydantic' 'python-mutagen' 'qt5-svg'
         'xdg-user-dirs')
makedepends=('python-setuptools')
checkdepends=('python-pytest-benchmark' 'python-pytest-cov' 'python-pycryptodome')
optdepends=('python-secretstorage: browser cookie extraction support'
            'python-pycryptodome: browser cookie extraction support'
            'dbus-python: mpris2 server support'
            'python-sanic: webserver support'
            'python-json-rpc: webserver support'
            'python-websockets: webserver support'
            'feeluown-netease' 'feeluown-qqmusic' 'feeluown-ytmusic' 'feeluown-bilibili')
conflicts=('feeluown-local')
provides=('feeluown-local')
replaces=('feeluown-local')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop")
sha512sums=('76db9bc20f73e4fdafe9da10928e936d478b06bcaf04f262dc75ccdb85fb7f7e51c88eda1f02a7bfd67ef0e91a11f3d9430e1a5c765ee70aa8132fd27cde7994'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/gui/assets/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
