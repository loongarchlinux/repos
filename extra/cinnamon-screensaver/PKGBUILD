# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=6.0.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=(loong64 x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(accountsservice cinnamon-desktop libgnomekbd
         python-cairo python-gobject python-setproctitle python-xapp xapp xdotool)
optdepends=('cinnamon-translations: i18n')
makedepends=(gobject-introspection meson samurai)
backup=(etc/pam.d/cinnamon-screensaver)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1981722974f8fab065958279ca159e0e91abe072f180356c39aae940343e19e7f04d8583ce811c632faaf66d6520a3ad18ddba03bbb471b22e208ccf30bb1d57')
b2sums=('0812940d7865e85ae0cd5b5dfe802c3774cd85bddf7b05709e8cd71876e02c3aed2c0e2da681abe9537c908d34363d54a72f24ecacd75c2412d72adc78cfb9d0')

build() {
  arch-meson --libexecdir=lib/cinnamon-screensaver build ${pkgname}-${pkgver}
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
