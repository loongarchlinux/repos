# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-devel-docs
pkgver=3.38.1
pkgrel=1
pkgdesc="Developer documentation for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-devel-docs"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools git)
groups=(gnome-extra)
_commit=b6ea619eb1686bb432ed469a95873e9afdcd0fb2  # tags/3.38.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-devel-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
