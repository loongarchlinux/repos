# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=alligator
pkgver=23.08.4
pkgrel=1
pkgdesc='Kirigami-based RSS reader'
url='https://apps.kde.org/alligator/'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(kirigami-addons5 syndication5 ki18n5 kconfig5 kcoreaddons5 hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('01feedd91cb74c2b4a0916b34a1ea48ec09f1244de34abf26d3cb03c37d1173d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
