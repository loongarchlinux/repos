# Maintainer: Alad Wenter <alad@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Lorenzo Nizzi Grifi Gargiolli <lorenzo.nizzi.grifi@gmail.com>
# Contributor: marc0s <marc0s@fsfe.org>

pkgname=pcsc-tools
pkgver=1.7.0
pkgrel=1
pkgdesc='PC/SC Architecture smartcard tools'
arch=('loong64' 'x86_64')
url='https://pcsc-tools.apdu.fr/'
license=('GPL')
depends=('pcsclite' 'pcsc-perl' 'glib-perl')
makedepends=('pkg-config')
optdepends=('perl-gtk3: gscriptor')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <rousseau@debian.org>
source=("https://pcsc-tools.apdu.fr/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('8de48526306b23e9dea6d99d29ec5f1230e3959bfe82a4d1b7d65f9767369757a1f440ceca7debeba68a8492607c91d0dcf7bec366cd213f00be45a5ef018d67'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
