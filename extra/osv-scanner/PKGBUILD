# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=osv-scanner
pkgver=1.3.4
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url='https://github.com/google/osv-scanner'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
makedepends=('go')
source=(https://github.com/google/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('19229149eca937a4da60ca29416fe1ea6997655fd10e273b833082ef5977a3de')
b2sums=('21dda3954175816420c3410a93e518b7d0066df3a5faf1b1bc232d90727fe50e90dc8fdc1d4a3ce14d8e6f562ca84907ea5c9c4728f44eae3d2c8314a5c394f0')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/osv-scanner
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    osv-scanner
}

# vim: ts=2 sw=2 et:
