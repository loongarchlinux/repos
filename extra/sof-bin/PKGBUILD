# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(
  sof-firmware
  sof-tools
)
pkgver=2.2.6
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(loong64 x86_64)
license=(
  custom:BSD
  custom:ISC
)
_binver=v${pkgver}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz)
b2sums=('8fa0fb97cfdaa13b30f34f46789bbb1700f4a3be994f11a55226bf617c9ade7ccde931eeb2f263448e355e82f281bbb70c66624a71645062cdfb68bd81a9d3a1')

package_sof-firmware() {
  options+=(!strip)

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cd sof-bin-$_binver

  cp -at "$fwdir" sof-$_binver
  ln -sr "$fwdir"/{sof-$_binver,sof}

  cp -at "$fwdir" sof-tplg-$_binver
  ln -sr "$fwdir"/{sof-tplg-$_binver,sof-tplg}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  install -Dt "$pkgdir/usr/bin" tools-$_binver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 sts=-1 et:
