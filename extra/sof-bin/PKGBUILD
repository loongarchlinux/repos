# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.0
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(loongarch64 x86_64)
license=(custom:BSD custom:ISC)
makedepends=(rsync)
source=(https://github.com/thesofproject/sof-bin/releases/download/v$pkgver/sof-bin-v$pkgver.tar.gz)
sha256sums=('80a245ba59ba826e4394206e39f4d7049e2bea139583e97923c7fbb8411ce922')

package_sof-firmware() {
  options=(!strip)

  cd sof-bin-v$pkgver

  (
    export FW_DEST="$pkgdir/usr/lib/firmware/intel"
    export TOOLS_DEST="$srcdir/tools/usr/bin"

    mkdir -p "$FW_DEST" "$TOOLS_DEST"
    ./install.sh v$pkgver
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-v$pkgver

  mv "$srcdir"/tools/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
