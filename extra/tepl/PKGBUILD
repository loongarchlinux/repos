# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>

pkgname=tepl
pkgver=6.4.0
pkgrel=4
epoch=1
pkgdesc='Library that eases the development of GtkSourceView-based text editors and IDEs'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/swilmet/tepl'
license=('LGPL3')
depends=('amtk' 'cairo' 'glib2' 'glibc' 'gsettings-desktop-schemas' 'gtk3' 'gtksourceview4' 'icu'
         'pango')
makedepends=('gobject-introspection' 'gtk-doc' 'meson')
checkdepends=('xorg-server-xvfb')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('5e56b20669d0cf05fa1d64b58c8c342c59158122dc518100d093d59df9b87321')

build() {
  arch-meson $pkgname-$pkgver build -D gtk_doc=true
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
