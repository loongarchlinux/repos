# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=10.18
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('loong64' 'x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom:BSD')
depends=('glibc' 'gcc-libs')
makedepends=('cmake' 'python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('c0307ed3db0871156589b3e2737066f01420e634493a931f14e3705ab83d55f37ddff44c9278fc19b31cec784bbfe5b9e15a1ad08ac06822279d32ae91489248'
            '58a6bcd5ce84f7faa465f1ca039bd5f2d1b48db62ca2904a5855b292af53406d556794d429640d0548023721af75d40ce44a805c67bd8675c99c3080ae7223c9')
b2sums=('621a60883d8c606356c8caface69529aedb099e52bd48054f6e9145913fb3f15b98a49e9bcb26bd11cbc59725613dd0567c2f61a89cc0b96da0bc3bb73363097'
        'b3092b2b22eb5e5652a81cc0c7ac176dc2849acdfd7cf447b0c0340aa34d9043d4d213b72f19e1432869f06095ac3fb2fa22f6302eae0915b0c136df27d9dcdf')

build() {
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr'

  cmake --build build

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
