# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=adobe-source-sans-fonts
pkgver=3.042
pkgrel=1
pkgdesc="Sans-serif font family for user interface environments"
url="https://adobe-fonts.github.io/source-sans/"
arch=(any)
license=(custom)
provides=("adobe-source-sans-pro-fonts=$pkgver")
replaces=('adobe-source-sans-pro-fonts<=3.028-1')
_relver=${pkgver}R
_tarname=source-sans-$_relver
_oldver=3.006R
_oldname=source-sans-$_oldver
source=("$_tarname.tar.gz::https://github.com/adobe-fonts/source-sans/archive/$_relver.tar.gz"
        "$_oldname.tar.gz::https://github.com/adobe-fonts/source-sans/archive/$_oldver.tar.gz")
sha256sums=('264e6272c2c197277584672e24914c40577aa08663be471be9138990b8c5aaff'
            '45dbc4994892710367e801869d8ad6b26589030977b660f6a968de36147ffd32')

package() {
  # "Source Sans Pro"
  cd $_oldname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  cd ..

  # "Source Sans 3"
  cd $_tarname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE.md
}

# vim:set sw=2 et:
