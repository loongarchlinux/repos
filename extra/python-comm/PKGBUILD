# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=comm
pkgname=python-$_pyname
pkgver=0.1.4
pkgrel=1
pkgdesc='Python Comm implementation for the Jupyter kernel protocol'
arch=(any)
url='https://pypi.org/project/comm/'
license=(BSD)
depends=(python-traitlets)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest)
source=(https://github.com/ipython/$_pyname/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('4b7c0b56439d6835c1f4abf31b81c931fd6a6eba9e38f11f74ac80d1a720bed7')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  PYTHONPATH="$PWD" \
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
