# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-incremental
pkgver=21.3.0
pkgrel=5
pkgdesc='A small library that versions your Python projects'
arch=('any')
license=('MIT')
depends=('python-click' 'python-twisted')
url='https://github.com/hawkowl/incremental'
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://github.com/hawkowl/incremental/archive/incremental-$pkgver.tar.gz")
sha512sums=('99a9e27b8513c4a13a74d37ae3d2c912af78ff9c6c528de1cba84767e2c08c9fc00cb54832efcccc70b922ac1582dd116cf8e8ba3f69926b424cf204ba4f0e7d')

build() {
  cd incremental-incremental-$pkgver
  python setup.py build
}

check() {
  cd incremental-incremental-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py ptr
}

package() {
  cd incremental-incremental-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
