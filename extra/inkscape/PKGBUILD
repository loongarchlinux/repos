# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.1.1
_tag='3bf5ae0d25c673abe152dedf4789a2d6f0cc60ff' # git rev-parse INKSCAPE_${pkgver/./_}
pkgrel=5
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('loongarch64' 'x86_64')
makedepends=('cmake' 'boost' 'git')

depends=(
	'dbus-glib'
	'double-conversion'
	'gc'
	'graphicsmagick'
	'gsl'
	'gspell'
	'gtkmm3'
	'lcms2'
	'lib2geom'
	'libcdr'
	'libjpeg-turbo'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'ttf-font'
)

optdepends=(
	'fig2dev: xfig input'
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python-lxml: some extensions'
	'python-numpy: some extensions'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("git+https://gitlab.com/inkscape/inkscape.git#tag=${_tag}"
        poppler-21.11.0.patch)
sha256sums=('SKIP'
            '76bd1cc64ce7abef0f1758a7f05d9ca9e2cf44506318fb8ae9da029543aa1762')

prepare() {
	cd "${pkgname}"
	git submodule init
	git submodule update

	# https://gitlab.com/inkscape/inkscape/-/merge_requests/3622
	patch -Np1 -i ../poppler-21.11.0.patch
}

build() {
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DWITH_DBUS=ON \

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install
}
