# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=3.4.0
_commit=167b07208d78c796a2435fc5f8b97c2ef6dbe1bb
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib' 'python-prettytable'
         'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils' 'python-keystoneauth1'
         'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
makedepends=('git')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("git+https://github.com/openstack/python-heatclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-heatclient
  python setup.py build
}

check() {
  cd python-heatclient
  stestr run
}

package() {
  cd python-heatclient
  python setup.py install --root="$pkgdir" --optimize=1
}
