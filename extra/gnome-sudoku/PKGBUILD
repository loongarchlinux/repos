# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=45.3
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  gtk4
  json-glib
  libadwaita
  libgee
  qqwing
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=55916878b713ea06da10526b177b5082ba9ac934  # tags/45.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
