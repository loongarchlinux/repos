# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-setuptools-gettext
pkgver=0.1.8
pkgrel=2
pkgdesc='Setuptools plugin for building .mo files'
arch=(any)
url=https://github.com/breezy-team/setuptools-gettext
license=(GPL2)
depends=(
  python
  python-setuptools
  python-tomli
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
_tag=580829c3d7b398e58f60751549028b33afa58119
#source=(git+https://github.com/breezy-team/setuptools-gettext.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/setuptools-gettext.git#tag=${_tag})
b2sums=(SKIP)
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd setuptools-gettext
  git describe --tags | sed 's/^v//'
}

build() {
  cd setuptools-gettext
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" setuptools-gettext/dist/*.whl
}

# vim: ts=2 sw=2 et:
