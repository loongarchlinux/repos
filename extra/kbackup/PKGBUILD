# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BluePeril <blueperil (at) blueperil _dot_ de>

pkgname=kbackup
pkgver=20.08.2
pkgrel=1
pkgdesc="A program that lets you back up any directories or files"
arch=(x86_64)
url="https://www.kde.org/applications/utilities/kbackup/"
license=(GPL)
groups=(kde-applications kde-utilities)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('bb97dd10a64c42ae80c9c87442d3a06d3c18f4f5997bd3b6d1460dec655b3e0a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>


build() {

  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
