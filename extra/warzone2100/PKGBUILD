# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=warzone2100
pkgver=4.4.2
pkgrel=2
pkgdesc="3D realtime strategy game on a future Earth"
url="https://wz2100.net/"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('sdl2' 'openal' 'libvorbis' 'libtheora' 'libsodium' 'physfs' 'sqlite' 'freetype2' 'opus' 'fmt')
makedepends=('zip' 'unzip' 'libzip' 'asciidoc' 'mesa' 'vulkan-headers' 'shaderc' 'cmake' 'ninja')
source=(${pkgname}-${pkgver}_src.tar.xz::https://github.com/Warzone2100/warzone2100/releases/download/${pkgver}/${pkgname}_src.tar.xz)
sha256sums=('3b962ac6aa75bca62bf2ebc067549db08fe4a1c3b38342910a2ac2aaabcbacde')

#prepare() {
# fix build with vulkan 1.3.260
#  sed -e 's|vk::throwResultException|vk::detail::throwResultException|g' -i $pkgname/lib/ivis_opengl/gfx_api_vk.cpp
#}

build() {
  # /usr/bin/warzone2100 uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cmake -B build -S ${pkgname} \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWZ_DISTRIBUTOR="ArchLinux" \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build

  # why is this installed ? Just remove it !
  rm -rfv ${pkgdir}/usr/include
  rm -rfv ${pkgdir}/usr/lib
}
