# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Thomas Booker <tw.booker@outlook.com>

libgmobile_commit=f4d4e5740c3e4b2de40864ab8a9e7b7f957d6aec

pkgname=phosh-mobile-settings
pkgver=0.31.0
pkgrel=1
pkgdesc='A settings app for mobile specific things'
arch=(loong64 x86_64 aarch64)
url='https://gitlab.gnome.org/guidog/phosh-mobile-settings'
license=(GPL3)
depends=(feedbackd libadwaita lm_sensors phoc phosh)
makedepends=(git meson wayland-protocols)
source=("${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
	https://gitlab.gnome.org/guidog/gmobile/-/archive/${libgmobile_commit}/gmobile-${libgmobile_commit}.tar.gz)
sha256sums=('574bf44c5f232b1401b00b1082f1afb1f2b19d7e87b04a1662c5164b1f7c3eea'
            'b071e21a30eb8649b2bd9f416eec97110f0f08090b6c230e928ef1c3339ac344')

prepare() {
  mv "gmobile-${libgmobile_commit}" "${pkgname}-v${pkgver}/subprojects/gmobile"
}

build() {
  arch-meson "${pkgname}-v${pkgver}" build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
