# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.16.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('loong64' 'x86_64')
url='https://www.oilshell.org'
license=('Apache')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('cc8a49beecd85063dc72eddc03a0f0289625f2efcf8a0406d5155066fa30a462')
b2sums=('6a6d3439a2c629df137436493bae5b646809250d951729b7c40c3df8beccdb10b71bffa3fdca4d4b6a693c33e57c8a39db880c5ada968d8946ecc92325935af7')
# https://www.oilshell.org/release/latest/

prepare() {
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
