# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.7
pkgrel=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(loong64 x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(
  Apache-2.0
  GPL-2.0-or-later
)
depends=(
  glibc
  lz4
  zlib
)
makedepends=(
  fuse2
  util-linux
  xz
)
optdepends=(
  'fuse2: for erofsfuse'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('0f204cd40644bde28f9bd0c5c234d93e68db3a3998bb089f43bfb3ce9a9db1e1cc2fa65919205cbd4a484fa4388cffadf5b395bc4363de3882e1a19778c2d62b')
b2sums=('f22183fbc3346db0632f0fc842586251d0e17ea19e1de3be51cd807dfac4a6150a080d6b81625c3e08eeebc2ae28d7840f8209c905ca87fc70481d3d8c3913ec')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-fuse --enable-lzma
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    util-linux libuuid.so
    xz liblzma.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
