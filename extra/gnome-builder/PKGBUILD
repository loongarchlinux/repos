# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-builder
pkgver=3.40.2
pkgrel=2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(loongarch64 x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject clang sysprof
         desktop-file-utils ctags libpeas vte3 vala python-jedi autoconf-archive
         flatpak-builder gspell libdazzle template-glib jsonrpc-glib
         python-sphinx webkit2gtk glade python-lxml meson libportal)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git mm-common
             python-sphinx_rtd_theme)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=9bd87348c1a33d43a78d3c823300bec5656762d5  # tags/3.40.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i "s/'sysprof-4'/'nosysprof'/" meson.build  # Hack sysprof-capture away
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libgladeui-2.so)

  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
