# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=libshout
pkgver=2.4.3
pkgrel=3
epoch=1
pkgdesc="Library for accessing a shoutcast/icecast server"
arch=('x86_64')
depends=('libvorbis' 'libtheora' 'speex' 'openssl')
url="https://www.icecast.org/"
options=('!emptydirs')
license=('LGPL')
source=("https://downloads.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
         corrected-blocking-state.patch
         icy-port-increment.patch)
sha512sums=('fe36778c6640e620f27504cb21234940e18a65a33f9178bf7e77d4baf854a9affc82d85db8ed8c1c8e2d80399685d8be10106fff5be1ddf76e03e2ffeeed1d34'
            '53aab9c3fd5b044eff731f46a51f646366ab48a2c910493035599ef0e1d65c1f7655753d12a2c7958125a1e834476763838547c44a110b85fea9d9b8650a440c'
            'cc00b0e2b41e28fc5bd13f1d159a22b8475d90448b51aac619e7d317a404b88c7631562056cc0c320ad2634daca592e68f3ac8b58e8a6ab166d913bd8625ba9a')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../corrected-blocking-state.patch
  patch -Np1 -i ../icy-port-increment.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
