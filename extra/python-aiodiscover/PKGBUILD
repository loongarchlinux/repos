# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=1.5.1
pkgrel=1
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(loong64 x86_64)
url=https://github.com/bdraco/aiodiscover
license=(APACHE)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-pytest-runner
  python-setuptools
  python-wheel
)
_tag=2261122a1390f9eb3f074e4b213867fb486f1c6c
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python setup.py build
}

package() {
  cd aiodiscover
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
