# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-aiodiscover
pkgver=1.6.0
pkgrel=2
pkgdesc='Discover Hosts via ARP and PTR lookup'
arch=(any)
url=https://github.com/bdraco/aiodiscover
license=(APACHE)
depends=(
  python
  python-dnspython
  python-ifaddr
  python-netifaces
  python-pyroute2
)
makedepends=(
  git
  python-setuptools
  python-wheel
)
_tag=decb249a746740b94b04d04b763880e5d50791f5
source=(git+https://github.com/bdraco/aiodiscover.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  sed '/pytest-runner/d' -i aiodiscover/setup.py
}

pkgver() {
  cd aiodiscover
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiodiscover
  python setup.py build
}

package() {
  cd aiodiscover
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
