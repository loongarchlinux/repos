# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-resolvelib
pkgver=0.5.5
pkgrel=4
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
license=('ISC')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-commentjson' 'python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sarugaku/resolvelib/archive/$pkgver.tar.gz")
sha512sums=('3da39201c4472d45466750119282fc34591b6d79eb4e6e71ca3f221e43dceead213058b4b72bee00083e9885650a47486bd7496264fba10f623aec7c19dabf00')

build() {
  cd resolvelib-$pkgver
  python setup.py build
}

check() {
  cd resolvelib-$pkgver
  python setup.py pytest
}

package() {
  cd resolvelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
