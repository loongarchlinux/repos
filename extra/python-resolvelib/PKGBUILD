# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=resolvelib
pkgname=python-resolvelib
pkgver=0.9.0
pkgrel=1
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
license=(custom:ISC)
arch=(any)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-commentjson python-packaging python-pytest)
# test files not in pypi sdist tarballs because of reasons: https://github.com/sarugaku/resolvelib/issues/108
source=($_name-$pkgver.tar.gz::https://github.com/sarugaku/resolvelib/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('cb263a60e958e190c92dc333afd61eba76e3f1b1cf8f825c39292ea37c90f440afaacfc2d574987e26f6bb1d3a155ea8d878dd29cf82e78d912df36f4a10eb7e')
b2sums=('a5f0ab89f8b44125da59bc93bbfd78e8f35c3af82459732317097a79db81d5366e9fdfdf75ed12d1726441d1d0275304a953c489e987832134be936b92766878')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
