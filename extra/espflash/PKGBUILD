# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=espflash
pkgname=(espflash cargo-espflash)
pkgver=2.0.0
pkgrel=1
pkgdesc="Cargo subcommand for flashing Espressif devices over serial"
url='https://github.com/esp-rs/espflash'
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
depends=('libudev.so' 'libuv')
makedepends=('cargo' 'udev')
options=(!lto)
source=(https://github.com/esp-rs/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('01b65bdd7c22b422d18db6cf186cb788151f0ca0eda9533ff9478aaf7255a2cf')
b2sums=('81abecdde300a4ac1bda62f8ae20fc02c16208383cfa65cd866af51b4198ad3966a5760a36e08418cf81a28f0ee7480d167f7f722a948eb3a750713f1f41d399')

prepare() {
  cd ${pkgbase}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgbase}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgbase}-${pkgver}
  cargo test --frozen --workspace
}

package_espflash() {
  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/espflash
}

package_cargo-espflash() {
  cd ${pkgbase}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-espflash
}

# vim: ts=2 sw=2 et:
