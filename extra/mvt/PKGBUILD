# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.4.2
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-appdirs' 'python-click' 'python-cryptography' 'python-iosbackup'
         'python-libusb1' 'python-packaging' 'python-pyahocorasick' 'python-requests' 'python-rich' 'python-tld' 'python-tqdm'
         'python-simplejson' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b077e2d6a787d1b5f79cd64a46618cb4395c76dc9a367044624591c838a522b6a7c5b1b32f139aac41f945a2c2d71c9feb0ed1cd1786ccace5793519d0a67845')
b2sums=('0899df4a46060c0e2fd28d7c09a90c820a2020a7148401ac61a49d680ec432261e3abaff4095590133c09e37c60b80f8fca4c42ef445bb5d1040b75bf73ce06b')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

check() {
  cd "mvt-${pkgver}"
  dev/mvt-android --help
  dev/mvt-ios --help
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
