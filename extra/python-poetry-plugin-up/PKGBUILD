# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matthew Gamble <git@matthewgamble.net>

pkgname=python-poetry-plugin-up
pkgver=0.3.0
pkgrel=1
pkgdesc='Poetry plugin that updates dependencies and bumps their versions in pyproject.toml file'
url=https://github.com/MousaZeidBaker/poetry-plugin-up
license=(MIT)
depends=(
  python
  python-poetry
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=3860f26d42e6c3a2bd6d28c587707763717a53fd
source=(git+https://github.com/MousaZeidBaker/poetry-plugin-up.git#tag=${_tag})
b2sums=(SKIP)
arch=(any)

pkgver() {
  cd poetry-plugin-up
  git describe --tags
}

build() {
  cd poetry-plugin-up
  python -m build -wn
}

package() {
  python -m installer -d "${pkgdir}" poetry-plugin-up/dist/*.whl
  install -Dm 644 poetry-plugin-up/LICENSE -t "${pkgdir}"/usr/share/licenses/python-poetry-plugin-up/
}

# vim: ts=2 sw=2 et:
