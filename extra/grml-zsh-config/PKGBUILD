# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.19.3
pkgrel=2
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'grep' 'sed' 'procps')
makedepends=('txt2tags')
source=("https://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz"
https://github.com/grml/grml-etc-core/commit/e27d36095d863e19f2d50cbf6423607518778df5.patch)
sha256sums=('e9f2ffc99d1c8709455ec8c223b5a711d94c0419bf2258b795f62301ce89d915'
            '45463d03c6ba636dc1fa5c6a6a72827c24a8514dc665bf0052648de1ceb14257')

prepare() {
	# https://bugs.archlinux.org/task/75788
	cd "${srcdir}/grml-etc-core-${pkgver}/"
	patch -Np1 -i "${srcdir}/e27d36095d863e19f2d50cbf6423607518778df5.patch"
}

build() {
	cd "${srcdir}/grml-etc-core-${pkgver}/doc"
	make
}

package() {
	cd "${srcdir}/grml-etc-core-${pkgver}"
	install -D -m644 etc/skel/.zshrc "${pkgdir}/etc/skel/.zshrc"
	install -D -m644 etc/zsh/keephack "${pkgdir}/etc/zsh/keephack"
	install -D -m644 etc/zsh/zshrc "${pkgdir}/etc/zsh/zshrc"

	install -D -m644 doc/grmlzshrc.5 "${pkgdir}/usr/share/man/man5/grmlzshrc.5"
	ln -sf grmlzshrc.5.gz "${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz"
}
