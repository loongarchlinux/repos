# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.2.1
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('loong64' 'x86_64')
url='http://doomretro.com'
license=('GPL3')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
_commit='7c788ad3e1f42ce948f52ce55b3ef0c01a24e77a'
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
