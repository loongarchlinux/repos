# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=montserrat-font
_name=Montserrat
pkgname=({otf,ttf}-montserrat)
pkgver=7.222
pkgrel=2
pkgdesc="The Montserrat project"
arch=(any)
url="https://github.com/JulietaUla/Montserrat"
license=(OFL-1.1)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('1de20125ba1b40f2acf248f2825ad8d806a6975ae646c5ec62dc4f77821770718e81f720cb9514ab188a498f580bdef2d95cee569c70c00d5554c884f978bfd0')
b2sums=('bd0015e3ffe915cecc60868a734f4944317d831ae652f5789506d497bc60a88fce9bdab3184d1ec57693b1335e134e03941a9b603f64a4f600c352dbc2658f55')

package_otf-montserrat() {
  pkgdesc+=" - OTF"
  conflicts=(montserrat-otf)
  replaces=(montserrat-otf)
  install -vDm 644 $_name-$pkgver/fonts/otf/*.otf -t "$pkgdir/usr/share/fonts/OTF/"
  install -vDm 644 $_name-$pkgver/OFL.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name-$pkgver/{{AUTHORS,CONTRIBUTORS}.txt,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_ttf-montserrat() {
  pkgdesc+=" - TTF"
  conflicts=(montserrat-ttf)
  replaces=(montserrat-ttf)
  install -vDm 644 $_name-$pkgver/fonts/ttf/*.ttf -t "$pkgdir/usr/share/fonts/TTF/"
  install -vDm 644 $_name-$pkgver/OFL.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_name-$pkgver/{{AUTHORS,CONTRIBUTORS}.txt,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
