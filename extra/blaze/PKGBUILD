# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Afnan Enayet <afnan at afnan.io>
pkgname=blaze
pkgver=3.8.2
pkgrel=2
pkgdesc='An open-source, high-performance C++ math library for dense and sparse arithmetic.'
url='https://bitbucket.org/blaze-lib/blaze'
arch=('any')
license=('BSD-3-Clause')
depends=('blas' 'lapack')
makedepends=('cmake')
sha256sums=('13a2199e75b7a8540e84032b0b4aaff93148a8d221e2d410230d7a58f5dfbbd5')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/blaze-lib/blaze/get/v${pkgver}.tar.gz")
_dir='blaze-lib-blaze-3156507a4b7a'

build() {
    local cmake_args=(
        -Wno-dev
        -B build
        -S "$_dir"
        -D CMAKE_INSTALL_PREFIX=/usr
        -D CMAKE_BUILD_TYPE=None
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 "$_dir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
