# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.9.0
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=(loong64 x86_64)
url="https://vmpk.sourceforge.io/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
)
makedepends=(
  cmake
  docbook-xsl
  drumstick
  qt6-tools
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/${pkgver/a/}/$pkgname-$pkgver.tar.bz2)
sha512sums=('7e84daa0664bc9d403df6c7bd5a226ea37c4804c49e4bcfb2e4f141e257e0081b752e9c2bddc29a9d2eda8bcb06b8ed1c79d129e55d601dfb58951449ebf454f')
b2sums=('80827907ac32788c687ff71170afec2de32a02cc9f1096b5af5e1cc6a43c513efbbd5f9dc443cf2d56a536c158b343a362fccb57814c99d9891a5a5b7c83d5ff')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    drumstick libdrumstick-rt.so libdrumstick-widgets.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname"
}
