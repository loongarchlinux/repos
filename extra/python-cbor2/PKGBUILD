# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=cbor2
pkgname=python-cbor2
pkgver=5.6.0
pkgrel=1
pkgdesc="Pure Python CBOR (de)serializer with extensive tag support"
arch=(loong64 x86_64)
url="https://github.com/agronholm/cbor2"
license=(MIT)
depends=(
  glibc
  python
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-setuptools-scm
  python-toml
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('46f7af76c20832b76d5eed5a8d6816a26b52d13793605fef0894b545c650311ea938fc8e436c25fa1d372e69b01d725a62b5b4910a233b37792af55ff5f35125')
b2sums=('2b55b76ce883ae1422250e32afe2e4968374ee6a1c6b8e8d26a403755f71bc7d3c985290cc3f3d8e2042df3fdb8521435538a6dcd584ae44bfbe84a7c5ad188c')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build -wn
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -c /dev/null tests
}

package() {
  cd $_name-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
