# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=6.0.12
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
depends=('dconf' 'glib2' 'glibc' 'gtk3' 'libgnome-keyring' 'libgudev' 'libx11' 'libxi'
         'deepin-wloutput-daemon')
makedepends=('go' 'git')
groups=('deepin')
source=("https://github.com/linuxdeepin/startdde/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('cba1c60b229900699ea051d93c931533d13791beea58af01b66e6afc4b17d08d62f86957d7bb91bb6af5b02a62c206456c669dee2bb833455a1fc41c145972f6')

prepare() {
  cd startdde-$pkgver
  sed -i 's/sbin/bin/' Makefile
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install
}
