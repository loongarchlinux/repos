# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=6
pkgrel=2
pkgdesc="Matrix group messaging app"
arch=(loong64 x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libpipewire
  libshumate
  openssl
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  meson
  rust
  xdg-desktop-portal
)
optdepends=('org.freedesktop.secrets: password storage')
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('4a57aedba9cd6e79999457837d2932ddbe5b62a2cb7d22dc8afd15c509c30484a663697cec91c2f5b40ab5153aadb0fac65a3d2ce59f444ff6ffe1f566d566ab')
b2sums=('32fab0786046ad683bc359f49e2c2b48f9608e3c5c57c3f1c3c0b844563661c11652c1411f0374c099430fff1ff37ceaeff8e2d450610ffe46258fcb28685330')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
