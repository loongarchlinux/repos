# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gthumb
pkgver=3.12.2
pkgrel=7
pkgdesc="Image browser and viewer for the GNOME Desktop"
url="https://wiki.gnome.org/Apps/gthumb"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  clutter-gtk
  dconf
  gsettings-desktop-schemas
  gst-plugin-gtk
  gst-plugins-base-libs
  json-glib
  libheif
  libjxl
  librsvg
  libsecret
  libsoup
  libwebp
  webkit2gtk
)
makedepends=(
  brasero
  docbook-xsl
  exiv2
  git
  liboauth
  libraw
  meson
  yelp-tools
)
optdepends=(
  'brasero: burn discs'
  'exiv2: metadata support'
  'liboauth: web albums'
  'libraw: read RAW files'
)
_commit=3132c5f25ffe6f22314ab0fbfb08e63ba523afab  # tags/3.12.2^0
source=("git+https://gitlab.gnome.org/GNOME/gthumb.git#commit=$_commit"
         exiv2-0.28.patch)
b2sums=('SKIP'
        '24fdf49b5ab9c73b6b7b60dd7c8e45ed074a6ae61ab63a876ff103c2b01b5533f0e7cee7dae4fbec28894e726a981163c9247d474540545a2b7d30c14af4a69d')

pkgver() {
  cd gthumb
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gthumb

  # Fix build with libraw 0.21
  git cherry-pick -n da0d3f22a5c3a141211d943e7d963d14090011ec
  # Fix build with exiv2 0.28
  patch -p1 -i ../exiv2-0.28.patch
}

build() {
  arch-meson gthumb build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
