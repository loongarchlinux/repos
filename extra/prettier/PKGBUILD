# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=prettier
pkgver=3.0.0
pkgrel=1
pkgdesc="An opinionated code formatter for JS, JSON, CSS, YAML and much more"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("jq" "yarn")
checkdepends=("git" "npm")
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('0a6384cedb4539330d23480a6aa156c64f9be83a76d3f0138d3c5490975587bb')

prepare() {
    cd "$pkgname-$pkgver"
    yarn --frozen-lockfile
}

build() {
    cd "$pkgname-$pkgver"
    yarn build
}

check() {
    cd "$pkgname-$pkgver"
    yarn test:dist
}

package() {
    local _npmdir=/usr/lib/node_modules/$pkgname
    install -d "$pkgdir"/{usr/bin,usr/share/licenses/$pkgname,$_npmdir}
    ln -s "$_npmdir/bin/$pkgname.cjs" "$pkgdir/usr/bin/$pkgname"
    ln -s "$_npmdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd "$pkgname-$pkgver"
    cp -a dist/* "$pkgdir"/$_npmdir
}
