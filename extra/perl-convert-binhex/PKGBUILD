# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Techlive Zheng <techlivezheng [at] gmail [dot] com>
# Contributor: Jakub Fi≈°er <mr [at] MikyMaus [dot] org>
# Contributor: Filip S. Adamsen <fsa [at] fsadev [dot] com>

pkgname=perl-convert-binhex
pkgver=1.125
pkgrel=9
pkgdesc="Perl module to extract data from Macintosh BinHex files"
arch=('any')
url="https://search.cpan.org/dist/Convert-BinHex/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-file-slurp' 'perl-test-most')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/ST/STEPHEN/Convert-BinHex-$pkgver.tar.gz")
md5sums=('2f485e1283387ead5b92243e1aacff9d')

build() {
  cd Convert-BinHex-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Convert-BinHex-$pkgver
  make test
}

package() {
  cd Convert-BinHex-$pkgver
  make DESTDIR="$pkgdir" install
}
