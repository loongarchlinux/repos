# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.10.5
pkgrel=1
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(loong64 x86_64)
url='https://igraph.org/c/'
license=(GPL2)
depends=(glpk arpack libxml2 plfit)
makedepends=(cmake)
source=(https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1c2725122fda9b72065095794bc7748939d90d942d312cb63c6160cd98d50f1e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
