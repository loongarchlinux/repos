# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Levy <blevy@protonmail.com>

pkgname=pwninit
pkgver=3.3.1
pkgrel=2
pkgdesc='Automate starting binary exploit challenges'
url='https://github.com/io12/pwninit'
arch=(loong64 x86_64)
license=(MIT)
depends=(
  elfutils
  gcc-libs
  glibc
  openssl
  patchelf
  xz
  zstd
)
makedepends=(
  git
  cargo
)
options=(!lto)
source=("git+${url}.git?signed#commit=${pkgver}")
sha256sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=(
  05A4711013D373A1F2FEE261AAB1132642EF3045 # Benjamin Levy <blevy@protonmail.com>
)

prepare() {
  cd "${pkgname}"
  cargo fetch --locked --target "${CARCH}-unknown-linux-gnu"
}

build() {
  cd "${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}"
  cargo test --frozen
}

package() {
  cd "${pkgname}"
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
