# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-ciso8601
pkgver=2.3.1
pkgrel=1
pkgdesc='Fast ISO8601 date time parser for Python written in C'
arch=(loong64 x86_64)
url=https://github.com/closeio/ciso8601
license=(MIT)
depends=(
  glibc
  python
)
makedepends=(
  cython
  git
  python-setuptools
)
_tag=b17984f5b4c6db0ef596b4d31456c8a39ae278ee
source=(git+https://github.com/closeio/ciso8601.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd ciso8601

  git describe --tags | sed 's/^v//'
}

build() {
  cd ciso8601

  python setup.py build
}

package() {
  cd ciso8601

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-ciso8601/
}

# vim: ts=2 sw=2 et:
