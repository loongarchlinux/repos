# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-multimedia
_qtver=6.2.2
pkgver=${_qtver/-/}
pkgrel=1
arch=(loongarch64 x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=(qt6-base gst-plugins-base)
makedepends=(cmake ninja qt6-declarative qt6-shadertools)
optdepends=('qt6-declarative: QML bindings'
            'gst-plugins-good: camera support, additional plugins'
            'gst-plugins-bad: camera support, additional plugins'
            'gst-plugins-ugly: additional plugins'
            'gst-libav: ffmpeg plugin')
groups=(qt6)
_pkgfn="${pkgname/6-/}-everywhere-src-$_qtver"
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('37ec84e7e35a818e86eee5059bd245d6c42cc11f9fa4110df35b3644b7494810')

build() {
  cmake -B build -S $_pkgfn -G Ninja
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
