# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.9.12
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(loong64 x86_64)
url="https://github.com/rncbc/liblscp"
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
source=($url/archive/${pkgname}_${pkgver//./_}/$pkgname-${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('391b4c59073393a0170b68ea2e7186df2b8ab9586ad7cbf496af1d7f9a1a1886421fd674716b1271b1842e0f0195abd0adffc78fad7bf7c1ae26d7cd733e89e6')
b2sums=('19eea6aff4d782262d03c72363ab743996a89cef972ee435b9884675d5b290cf7c01942e7f5b7eea5271ce905870101d830ead078ae5c03ad6615c79337162e3')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-${pkgname}_${pkgver//./_}
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

