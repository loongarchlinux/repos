# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=kernel-hardening-checker
pkgver=0.6.6
pkgrel=1
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kernel-hardening-checker"
arch=(any)
license=(GPL3)
depends=(python)
makedepends=(python-setuptools python-build python-installer python-wheel)
replaces=(kconfig-hardened-check)
conflicts=(kconfig-hardened-check)
source=(https://github.com/a13xp0p0v/kernel-hardening-checker/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5b437b6fc08b2f923c3db6c1814c043f3d699754f3433b514b7dcaa3b3d0a9df')
b2sums=('b3dd5ab4732755b4ea56a65a89d923e8a70eb7c9446231b30f5ca27753883602dbda2ec64e5cabb00f074e3edad584d7819454d0ad5cd214b5ac0d6856979588')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
