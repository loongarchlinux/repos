# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-a11y
pkgver=1.0.2
pkgrel=1
pkgdesc='Wingpanel Universal Access Indicator'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-a11y
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  libwingpanel.so
)
makedepends=(
  git
  meson
  vala
)
_tag=ccf803ed1d91b1daaa23457d33eef6fa571863bb
source=(git+https://github.com/elementary/wingpanel-indicator-a11y.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-a11y
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-a11y build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
