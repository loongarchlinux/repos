# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Oliver Herold <oliver - at - akephalos - dot - de>

pkgname=scrypt
pkgver=1.3.2
pkgrel=1
pkgdesc='key derivation function and encryption utility'
arch=('loong64' 'x86_64')
depends=('glibc' 'openssl')
url='https://www.tarsnap.com/scrypt.html'
license=('BSD')
source=(https://www.tarsnap.com/scrypt/scrypt-${pkgver}.tgz)
sha256sums=('d632c1193420ac6faebf9482e65e33d3a5664eccd643b09a509d21d1c1f29be2')
b2sums=('aac8dc8360652d5d4d3654d47de961eec99675ee20d8507abf1ffb4de0b7bc8f040cd58cabfd5c0b8c2c197b2302206ea2c747aee1f62a2fe3eaa64e108bce6e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
