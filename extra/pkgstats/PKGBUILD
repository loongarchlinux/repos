# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=2.4.2
pkgrel=2
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('any')
license=('GPL')
depends=('bash' 'curl' 'pacman' 'sed' 'coreutils' 'systemd' 'grep')
makedepends=('git')
checkdepends=('bash-bats' 'shellcheck')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}")
sha1sums=('SKIP')
validpgpkeys=('4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC')

check() {
	cd ${srcdir}/${pkgname}

	make test
}

build() {
	cd ${srcdir}/${pkgname}

	make
}

package() {
	cd ${srcdir}/${pkgname}

	make DESTDIR=${pkgdir} install
}
