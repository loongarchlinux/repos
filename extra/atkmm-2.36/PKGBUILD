# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=atkmm-2.36
pkgname=(
  atkmm-2.36
  atkmm-2.36-docs
)
pkgver=2.36.3
pkgrel=1
pkgdesc="C++ bindings for ATK"
url="https://www.gtkmm.org/"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  atk
  glibmm-2.68
)
makedepends=(
  git
  glibmm-2.68-docs
  meson
  mm-common
)
options=(!emptydirs)
_commit=75f79502f700614a3c35d9c4eaa4c395ac6c7d66  # tags/2.36.3^0
source=("git+https://gitlab.gnome.org/GNOME/atkmm.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd atkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd atkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson atkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_atkmm-2.36() {
  depends+=(
    libglibmm-2.68.so
    libsigc-3.0.so
  )
  provides=(libatkmm-2.36.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_atkmm-2.36-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
