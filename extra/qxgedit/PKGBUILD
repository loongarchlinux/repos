# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.10
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(loong64 x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('7aa95ba2e5dd1c3f7e900563d31337066bb71c074a3fa1e5ff2371fb07e4038f8e0eb28915415e17181f8e4caf1a92afaf5b47f7aea86035fd97913168848142')
b2sums=('0bb652a579f7f887a63ce55464212aa84063349cf1b4a32ee0329b6ce5fdebf4554676dc1ff01528fd4c22f4bc2c605587c2c9de1c882c3696e46acce3185d37')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
