# Maintainer: christian rebischke <chris.rebischke@archlinux.org>

pkgname=gopass-summon-provider
pkgver=1.15.11
pkgrel=1
pkgdesc="summon provider for gopass"
arch=('loong64' 'x86_64')
url="https://github.com/gopasspw/gopass-summon-provider"
license=('MIT')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass-summon-provider/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ae80aabe2216f37e1ac59509b3f7b2d785c0c0e20e7cc9fd42c170f7924be900e2b827daf117e174c69257595650ab287489017981c2988e6a5c1e4007ab1720')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
