# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apk-tools
pkgver=2.14.0
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('loong64' 'x86_64')
license=("GPL2")
depends=(zlib openssl)
makedepends=(scdoc lua53 lua53-zlib)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('4c6db13039280814a10a3b3d89b29837b59769c69214a1861997e088eac107a5')
b2sums=('b760f87ee002ee7446463d94260b4ba84e31aa6d7f750e73f18c27619c79dab83e97fd0bd2b04421ddf8af7cf3a65a68ead581c9f7b48133673d2b6ed951f999')

prepare() {
  cd "${pkgname}-v${pkgver}"
  sed -i -e 's:-Werror::' Make.rules
}

build() {
  cd "${pkgname}-v${pkgver}"
  make SBINDIR=/usr/bin LIBDIR=/usr/lib
}

check() {
  cd "${pkgname}-v${pkgver}"
  make test
}

package() {
  cd "${pkgname}-v${pkgver}"
  make install DESTDIR="${pkgdir}" SBINDIR=/usr/bin LIBDIR=/usr/lib
}

# vim: ts=2 sw=2 et:
