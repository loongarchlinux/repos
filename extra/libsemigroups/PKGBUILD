# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=2.7.3
pkgrel=1
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io'
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(fmt
         gcc-libs
         glibc)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d4d88a11651c7d7d497f847fea97e3ff60a39b25b851c1d0d7ccf41e052612be')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --disable-hpcombi \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
