# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.7
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
url="https://wiki.gnome.org/Projects/Folks"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution-data-server
  libgee
  libxml2
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  libsysprof-capture
  meson
  python-dbusmock
  vala
)
provides=(
  libfolks.so
  libfolks-{dummy,eds}.so
)
options=(!lto)
_commit=fd570fa84f2e5e549187490ee89a9b77e9371785  # tags/0.15.7^0
source=("git+https://gitlab.gnome.org/GNOME/folks.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd folks
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd folks

  # Fix tests: python-dbusmock 0.30.1+ dropped the class_ parameter again
  git revert -n b6b7d41aad9e36e0d0e6ec9822ed2e310923c8fd
}

build() {
  local meson_options=(
    -D b_lto=false  # LTO copies some GType constructors
    -D docs=true
    -D profiling=true
    -D telepathy_backend=false
  )

  arch-meson folks build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 4
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
