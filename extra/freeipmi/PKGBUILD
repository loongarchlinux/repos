# Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Phillip Smith <pkgbuild@phs.id.au>
# Contributor: Nathan Owe <ndowens04 at gmail>

pkgname=freeipmi
pkgver=1.6.14
pkgrel=1
pkgdesc='IPMI remote console and system management software'
arch=('loong64' 'x86_64')
url='https://www.gnu.org/software/freeipmi'
license=('GPL')
depends=('libgcrypt')
optdepends=('perl: for contrib scripts')
backup=(
  "etc/$pkgname/$pkgname.conf"
  "etc/$pkgname/${pkgname}_interpret_sel.conf"
  "etc/$pkgname/${pkgname}_interpret_sensor.conf"
  "etc/$pkgname/ipmidetect.conf"
  "etc/$pkgname/ipmidetectd.conf"
  "etc/$pkgname/ipmiseld.conf"
  "etc/$pkgname/libipmiconsole.conf"
)
options=('!libtool')
source=(
  "https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
  'tmpfiles.conf'
)
sha512sums=('5dab6430d257776cad1aed39cb2e2536af42c56e3f53774111efc15b0dfc19241eedfbcbfb1f088dd8be38ce694bff9779050daaec9ad0b2d4dad329ce8f3fe3'
            'SKIP'
            '3516d972d0b078e2cdedc1362f98bd061fbee583c7ac97c1c728baf2dbe41b62847dbe04e0381422d70c55101240176443ce15e422dbc6d6a583321bb0d39b22')
b2sums=('b0b3fdc45c910f5fd3e837b4186103c754fc399c05be6b3406346a9629de4392334dfce5320fabfa895f8d398399a54b58c8b6663979e428c10269dd57b35067'
        'SKIP'
        '5354e0b716b0806ac6f82dbbae533cb86f302d1952b948df6b5ab5bd41bf194ec927c9c39fd4d5969c2f4de8cfdbf3b66a4a1c1faaee4e5768201eaef83ca991')
validpgpkeys=('A865A9FB6F0387624468543A3EFB7C4BE8303927') # Albert Chu <chu11@llnl.gov>

prepare() {
  cd "$pkgname-$pkgver"

  autoreconf -vi

  ./configure \
    --prefix=/usr \
    --exec-prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --disable-init-scripts \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemconfigdir=/etc/conf.d
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # systemd-tmpfiles integration
  install -Dm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # delete cache
  rm -rf "$pkgdir/var/cache"
}
