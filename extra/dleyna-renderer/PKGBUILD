# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-renderer
pkgver=0.7.0
pkgrel=2
pkgdesc="Library to discover and manipulate Digital Media Renderers"
url="https://github.com/phako/dleyna-renderer"
arch=(loongarch64 x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson docbook-xsl)
_commit=7032c94c4c41a56fead1668440d75462f25d3ac4  # tags/v0.7.0^0
source=("git+https://github.com/phako/dleyna-renderer#commit=$_commit"
        fix-rpath.diff)
sha256sums=('SKIP'
            '70b6cc24399248cfaf8582932351c911d60a885784c9aeb70df027ff358e8c4d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../fix-rpath.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
