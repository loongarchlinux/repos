# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.26.1
pkgrel=1
pkgdesc="Power management tool for the MATE desktop"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gettext' 'libcanberra' 'libnotify' 'upower' 'polkit' 'libsecret')
makedepends=('itstool' 'mate-panel' 'python')
optdepends=('mate-panel: Set brightness and inhibit power management from the panel')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
sha256sums=('20cd9d22ed04babf98bb50e71a0ec5d78a8a476287723278f87da76cabfb1042')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--sbindir=/usr/bin \
        	--enable-applets \
        	--disable-strict \
        	--without-keyring
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
