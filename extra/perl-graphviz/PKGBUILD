# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# https://github.com/fukawi2/aur-packages
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: antoniolucas <antoniolucasnobar at gmail dot com>

pkgname=perl-graphviz
pkgver=2.26
pkgrel=1
pkgdesc="GraphViz - Interface to AT&T's GraphViz. Deprecated. See GraphViz2"
arch=('any')
url="https://search.cpan.org/dist/GraphViz"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('graphviz' 'perl-file-which' 'perl-ipc-run' 'perl-libwww' 'perl-parse-recdescent'
         'perl-xml-twig' 'perl-xml-xpath')
checkdepends=('perl-test-pod')
source=("https://cpan.metacpan.org/authors/id/E/ET/ETJ/GraphViz-$pkgver.tar.gz")
sha512sums=('99bca9e8e8d9487ba72baffc398c0952cf30d18dae00fe83e996822302a1835b85ed4d1201087f3cafe486207e2c771fdb510c45a7e6f783d6c9a25ef77667d7')

build() {
  cd GraphViz-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd GraphViz-$pkgver
  make test
}

package() {
  cd GraphViz-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
