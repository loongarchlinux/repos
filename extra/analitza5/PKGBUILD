# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=analitza
pkgname=${_name}5
pkgver=23.08.3
pkgrel=4
pkgdesc='A library to add mathematical features to your program'
url='https://www.kde.org/'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-svg qt5-declarative)
makedepends=(extra-cmake-modules kdoctools5 eigen qt5-tools)
conflicts=(analitza)
replaces=('analitza<24')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('519776157bad10cd60c8bd3a26e36185a559065049769fac269e65d20b4f8f92'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_analitza5() {
  DESTDIR="$pkgdir" cmake --install build
}
