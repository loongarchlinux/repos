# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=shadowsocks-v2ray-plugin
pkgver=5.13.0
pkgrel=1
pkgdesc='A SIP003 plugin based on v2ray for shadowsocks'
arch=('loong64' 'x86_64')
url='https://github.com/teddysun/v2ray-plugin'
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("https://github.com/teddysun/v2ray-plugin/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0e1db1cab6acc7ceed8f3a359ea1e8b2ae87f85c5122435dc365d91030934cb26dcf89b4ba046ced2c33c3bb1b101c0d2c5dc5c436df9b97bf66f3b06b3c10e2')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd v2ray-plugin-$pkgver
  go build
}

package() {
  cd v2ray-plugin-$pkgver
  install -Dm755 v2ray-plugin "$pkgdir"/usr/bin/v2ray-plugin
}
