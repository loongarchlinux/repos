# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.4.0
pkgrel=1
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  appstream
  binutils
  breezy
  cpio
  debugedit
  elfutils
  flatpak
  fuse3
  git
  patch
  rpmextract
  tar
  unzip
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  xmlto
)
checkdepends=(valgrind)
replaces=('flatpak<0.9.10')
_commit=7d621d6eec7fe523c68d46f3853cdc86561b5637  # tags/1.4.0^0
source=(
  "git+https://github.com/flatpak/flatpak-builder#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  fusermount3.diff
)
b2sums=('SKIP'
        'SKIP'
        '7d54ba89b3481722026ef556aabfc8b157069701ec89e900a5616899b937eacc077e620fb2a5f9d4faf5f7189414bc25e9a0fc57a0d8d2952bb24fb22dc445f3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../fusermount3.diff

  git submodule set-url subprojects/libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always submodule update --init -- subprojects/libglnx
}

build() {
  local meson_options=(
    -D fuse=3
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
