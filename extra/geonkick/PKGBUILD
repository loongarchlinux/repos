# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=geonkick
pkgname=(geonkick geonkick-{common,lv2,standalone})
pkgver=2.10.1
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=(loong64 x86_64)
url="https://geonkick.org/"
_url="https://gitlab.com/Geonkick-Synthesizer/geonkick"
license=(GPL-3.0-or-later)
makedepends=(
  cairo
  cmake
  gcc-libs
  glibc
  hicolor-icon-theme
  jack
  libsndfile
  libx11
  lv2
  openssl
  rapidjson
  vst3sdk
)
checkdepends=(lv2lint)
# issues with visible symbols during check() if LTO is enabled: https://github.com/free-sm/geonkick/issues/164
options=(!lto)
source=($_url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('bb1e6bba313d3bfc58e74e805ecfcf81aa78dd100187ec024ca5a99b895af0ac63034552845df654dfa6f1a812c80c68991f52ad1b9fec3734713ecd62994789')
b2sums=('9543f60730db32bfca6588f46495832149439f8b92e2effe6727b788e463fc4289144d89cb10d342f02701e72c08fc207b36e02a34ab997031377d3c360f8f36')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    # vst3 plugin build broken: https://gitlab.com/Geonkick-Synthesizer/geonkick/-/issues/2
    # -D GKICK_VST3=ON
    # -D VST3_SDK_PATH=/usr/src/vst3sdk
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cp -av build/src/plugin/lv2/*.so $pkgname-v$pkgver/src/plugin/lv2/$pkgname.lv2/
  lv2lint -Mpack -I $pkgname-v$pkgver/src/plugin/lv2/$pkgname.lv2/ "http://geontime.com/geonkick"
  rm -v $pkgname-v$pkgver/src/plugin/lv2/$pkgname.lv2/*.so
}

package_geonkick() {
  depends=(
    $pkgbase-lv2=$pkgver
    $pkgbase-standalone=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/data/$pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-v$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgbase-common usr/share/$pkgbase
    _pick $pkgbase-common usr/share/doc

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share/{applications,icons,man,mime}
  )
}

package_geonkick-common() {
  pkgdesc+=" - common files"

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    libx11
    lv2-host
    openssl
  )
  
  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    hicolor-icon-theme
    jack
    libsndfile
    libx11
    openssl
  )
  
  mv -v $pkgname/* "$pkgdir"
}
