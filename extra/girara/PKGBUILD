# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.4.1
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('loong64' 'x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-glib' 'pango')
makedepends=('meson' 'ninja' 'doxygen' 'check')
checkdepends=('xorg-server-xvfb')

source=(girara-$pkgver.tar.gz::https://github.com/pwmt/girara/archive/$pkgver.tar.gz)

sha512sums=('8ee9f1701bbd73c28fdafb203325eb1a178ddbf077ebb75d9edb290bb09299d5c708d5a3409ca8a02759c4e94cc371cb5a47b00df33263e7863075ed568f4805')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

check() {
  cd girara-$pkgver/build

  ninja test
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
