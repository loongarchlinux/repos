# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh
pkgver=3.0.6
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot'
arch=('any')
url='https://github.com/acmesh-official/acme.sh'
license=('GPL3')
depends=('bash')
optdepends=('socat: for standalone mode')
source=("$pkgname-$pkgver.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('2ddd561356586a289bcd08770b7347ef2e1cb121948987031a9e53d19abd5beda433e50c659aa3ee5dc3b06bdf8e479b5b31ba0b52c4a283d2c491aead4d10ac')

package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share/acme.sh/deploy
  install -dm755 "$pkgdir"/usr/share/acme.sh/dnsapi
  install -dm755 "$pkgdir"/usr/share/acme.sh/notify
  install -dm755 "$pkgdir"/usr/bin

  install -m755 acme.sh "$pkgdir"/usr/share/acme.sh/
  install -m755 deploy/*.sh "$pkgdir"/usr/share/acme.sh/deploy/
  install -m755 dnsapi/*.sh "$pkgdir"/usr/share/acme.sh/dnsapi/
  install -m755 notify/*.sh "$pkgdir"/usr/share/acme.sh/notify/

  ln -s /usr/share/acme.sh/acme.sh "$pkgdir"/usr/bin/acme.sh
}
