# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.7.0
pkgrel=1
pkgdesc='Unit conversion tool and library written in rust'
arch=('loong64' 'x86_64')
url=https://rinkcalc.app
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('git' 'rust')
source=("git+https://github.com/tiffany352/$pkgname-rs.git#tag=v$pkgver")
b2sums=('SKIP')

prepare() {
  cd $pkgname-rs
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-rs
  cargo build --release --locked --offline
}

check() {
  cd $pkgname-rs
  cargo test --locked --offline
}

package() {
  cd $pkgname-rs
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
