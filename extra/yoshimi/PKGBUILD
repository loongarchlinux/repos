# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.3.0
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(loong64 x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(
  cairo
  gcc-libs
  glibc
  hicolor-icon-theme
  zlib
)
makedepends=(
  alsa-lib
  cmake
  fftw
  fltk
  jack
  lv2
  mxml
  ncurses
  readline
)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
# plugin exposes symbols globally if built with LTO:
# https://github.com/Yoshimi/yoshimi/issues/164
options=(!lto)
source=(https://github.com/$pkgname/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('a5ff3234a1cfa6fefe93eb1088b61e939fb2d89636dacb6ee7f5082532cc0bc04a701d6326b61a3d7d7ba3f4bd58acbf4cc806e9a693f4b89a6db1a8192d872c')
b2sums=('3c415c4e0460a5a625eda3cd50324cd234f8c1b3c69fe46e01af6a5c3c7b896caba1f4950148bff35433a616163722913db213b2c40b84eba6956213e8ec86fb')

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -Wno-dev
    -B build
    -S $pkgname-$pkgver/src
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cp -v build/LV2_Plugin/${pkgname}_lv2.so $pkgname-$pkgver/src/LV2_Plugin/
  lv2lint -Mpack -I $pkgname-$pkgver/src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v $pkgname-$pkgver/src/LV2_Plugin/${pkgname}_lv2.so
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    fltk libfltk.so libfltk_images.so
    jack libjack.so
    mxml libmxml.so
    ncurses libncursesw.so
    readline libreadline.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{Changelog,Dependencies,README.txt,Yoshimi_Helpers} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
