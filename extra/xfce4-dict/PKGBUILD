# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-dict
pkgver=0.8.6
pkgrel=1
pkgdesc="A dictionary plugin for the Xfce panel"
arch=('loong64' 'x86_64')
url="https://docs.xfce.org/apps/xfce4-dict/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'librsvg' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('ae5db7ca70354d3293fc08eaf7ca40cdbc91799a219f199d824684b39e6c0a41')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
