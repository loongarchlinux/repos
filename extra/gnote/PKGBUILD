# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=43.0
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(loong64 x86_64)
license=(GPL3)
depends=(gtkmm3 gspell libxslt libsecret)
makedepends=(yelp-tools meson git)
options=(debug)
_commit=6298d00ad8f33b35b0a4c008a7fb96ddfc7b6c0b  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gnote
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gnote
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
