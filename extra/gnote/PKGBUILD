# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=45.1
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(loong64 x86_64)
license=(GPL3)
depends=(
  glibmm-2.68
  gtkmm-4.0
  libsecret
  libxml2
  libxslt
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
_commit=580326d58b5e3476077af1563c31e88489fc5a53  # tags/45.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd gnote
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gnote
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
