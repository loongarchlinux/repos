# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=44.1
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(loong64 x86_64)
license=(GPL3)
depends=(
  gspell
  gtkmm3
  libsecret
  libxslt
)
makedepends=(
  git
  meson
  yelp-tools
)
_commit=bc6b56da9b2e6155beb4af5b055a25ab4f6582a3  # tags/44.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd gnote
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gnote
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
