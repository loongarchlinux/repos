# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-aioquic
_name=aioquic
pkgver=0.9.25
pkgrel=1
pkgdesc='QUIC and HTTP/3 implementation in Python'
url='https://github.com/aiortc/aioquic'
arch=('loong64' 'x86_64')
license=('BSD-3-Clause')
depends=(
  'openssl'
  'python'
  'python-certifi'
  'python-cryptography'
  'python-pylsqpack'
  'python-pyopenssl'
  'python-service-identity'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('70795c78905326d855c2ae524072234aae586c789b81292e272d021e9b0430a3')
b2sums=('50b12f7d274e47172af88185e58f4debb6961724446e47b03bea2ed5379ef20093e86fcc1d482a8a9f5574e541fe855eadd14c504468b6fe9c9f258e5bd3a3cc')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_name}-${pkgver}
  python -m installer --destdir=test_dir dist/*.whl

  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" python -c '
import aioquic
import aioquic.h3.connection
import aioquic.quic.packet
'
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
