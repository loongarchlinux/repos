# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=42.1
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD)
arch=(loong64 x86_64)
depends=(gtk3 gtk4 libadwaita)
makedepends=(vala appstream-glib gobject-introspection gnome-shell
             gnome-control-center git meson)
optdepends=("wgetpaste: Upload clipboard contents")
options=(debug)
_commit=5265371559fbd54426255938d5dd1172ddb4e92c  # tags/v42.1^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gpaste
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gpaste
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}
