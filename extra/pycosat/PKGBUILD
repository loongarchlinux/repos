# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Daniel Maslowski <info@orangecms.org>

pkgbase=pycosat
pkgname=(python-pycosat)
pkgver=0.6.4
pkgrel=3
arch=(loong64 x86_64)
url="https://github.com/ContinuumIO/pycosat"
pkgdesc="Python bindings to picosat (a SAT solver)"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://github.com/ContinuumIO/$pkgbase/archive/$pkgver/$pkgbase-$pkgver.tar.gz
        https://github.com/conda/pycosat/commit/537448e4.patch)
sha256sums=('79ffa99156810e58876dd6374af0b7bff88f9a4580ca8b8468c414c5c2454bb8'
            '8fd2ca7b7385f7514a63cb99e2512f4dd3b0e38acc854ec46f9ec7fc2fe165a4')

prepare() {
  patch -d $pkgbase-$pkgver -p1 < 537448e4.patch # Fix memory leak
}

build() {
  cd $pkgbase-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgbase-$pkgver
  PYTHONPATH="$PWD"/$(ls -d build/lib*) \
  python test_pycosat.py
}

package_python-pycosat() {
  cd $pkgbase-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
