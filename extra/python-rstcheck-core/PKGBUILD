# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-rstcheck-core
_name=${pkgname#python-}
pkgver=1.1.1
pkgrel=1
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=('any')
url=https://github.com/rstcheck/rstcheck-core
license=('MIT')
depends=('python-docutils' 'python-pydantic' 'python-typing_extensions')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('python-pytest' 'python-pytest-mock' 'python-sphinx')
optdepends=(
  'python-sphinx: for Sphinx support'
  'python-tomli: for pyproject.toml support'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('SKIP')

prepare() {
  cd "$_name"
  # Remove include list https://github.com/pypa/wheel/issues/92
  sed -i '/^include =/d' pyproject.toml
}

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
 cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name//-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
