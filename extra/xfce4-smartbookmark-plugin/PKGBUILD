# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-smartbookmark-plugin
pkgver=0.5.1
pkgrel=2
pkgdesc="Plugin for the Xfce4 panel that lets you quicksearch from selected websites"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-smartbookmark-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool' 'libxt')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        xfce4-smartbookmark-plugin-archlinux.patch)
sha256sums=('3e28fb7cd2e2251e89a8715684081b862406e3fb4e6d8c0caa1b798a97703600'
            '61f35a8e93f1aaf7f46b2c90af9788155bea67f045185d74c6b384a97deeacab')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/xfce4-smartbookmark-plugin-archlinux.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
