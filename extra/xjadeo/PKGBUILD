# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.13
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport"
arch=(loong64 x86_64)
url="https://github.com/x42/xjadeo"
license=(GPL-2.0-or-later)
depends=(
  glibc
  imlib2
  libx11
  libxext
  libxpm
  libxv
)
makedepends=(
  alsa-lib
  ffmpeg
  gendesk
  git
  glu
  jack
  libglvnd
  liblo
  libltc
  portmidi
)
backup=(etc/xjadeorc)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('ce0822d4a9a477bba3926fbf27ee4c84af1afd67782ccc7c07971b5004ab0843d3587a126621fa0b87f6f28e58d96dde22a26d5f478b77665a6d74d491b6dc68')
b2sums=('88f8f8e1603eab7fdc21b443b3bcd2cadd212ba5a3af78bf309394be3ad70b9381e2a39163ed24dd1262d035201d2c73351ca32cf67ad9f0a37d92136f7f2887')

prepare() {
  cd $pkgname-$pkgver
  # https://github.com/x42/xjadeo/issues/54
  # libporttime.so is replaced by libportmidi.so
  sed -e 's/ -lporttime//' -i configure.ac
  gendesk -n \
          --pkgname $pkgname \
          --pkgdesc "$pkgdesc" \
          --categories "AudioVideo;Audio"
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  depends+=(
    alsa-lib libasound.so
    ffmpeg libavcodec.so libavformat.so libavutil.so libswscale.so
    freetype2 libfreetype.so
    jack libjack.so
    libglvnd libGL.so
    liblo liblo.so
    libltc libltc.so
    portmidi libportmidi.so
  )

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # config
  install -vDm 644 doc/xjadeorc -t "$pkgdir/etc/"
  # desktop file
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  # icon
  install -vDm 644 src/$pkgname/icons/xjadeoH128.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
