# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=5.1.0
pkgrel=1
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('loong64' 'x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL')
depends=('fcitx5' 'anthy')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-anthy/fcitx5-anthy-$pkgver.tar.xz"{,.sig})
sha512sums=('9bd27c5734126302594c93eefcd50444e41cddf7a5b1d80057e39ebf18f6b3c90f59b3fa5236a7faed09744613efe3459b5d094402f81d01b74bf441a7be6e9d'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
