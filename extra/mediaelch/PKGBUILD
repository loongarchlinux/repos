# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.10.6
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('loong64' 'x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia'
         'qt5-svg' 'quazip-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c37464e4cf956e9abec14126eb681633988f31ba2b0f937817c7829c25737e22')
b2sums=('f0db7f9c8bd4b88664180024ed9e64c56218c8c975c6f35c1031434958f0ddf0654116e463d5f3d927087a42a06279dd0d77de3220ef6d9460db9f277efc6fe5')

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
