# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-weather
pkgver=45.0
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(
  geoclue
  gjs
  gtk4
  libadwaita
  libgweather-4
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
)
groups=(gnome)
_commit=15ee3492b8e8c20cf1e63d7cbe962571ea98d0ca  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
