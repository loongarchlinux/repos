# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.13.11
pkgrel=1
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=(any)
url="https://www.sciss.de/mellite/"
_url=https://codeberg.org/sciss/Mellite
license=(AGPL-3.0-or-later)
groups=(pro-audio)
_java_version=17
depends=(
  hicolor-icon-theme
  java-runtime=$_java_version
  sh
  supercollider
)
makedepends=(
  gendesk
  java-environment=$_java_version
  sbt
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/v$pkgver.tar.gz
  $pkgname.sh
)
sha512sums=('198c200c8654fde10894ec7d243eeba38f5f1e01a6bbb5f1290934ddd6f146de649593bf748f173cd9211306d2e8899eb3bed1ddd2f7110fa7f3deada5a9e813'
            'a4d046463774393fdbe2ff10d545d0a46ff62761effdca9e1beb7e7f8d43aebe7ee4dc48602c951b59dfa0c4add517c1c8b58284f5c10c751969826ff512529a')
b2sums=('85941ace3aea8597d71247deb69270e96603a1450cde238de938ed6cba9525ee44f4362bdf984ed86b4ff94c9a345bf0269e3ca9df61cea8faee7b0aa28c2213'
        '6ca9fa81baf35d0c93ac4d192c591320858e70bc7f981f5fed0a100bcc99d3595c7fbfba177747f7dd2e93e9e399ecb26041937eabacab2654b247df6f4827b9')

prepare() {
  sed "s/JAVA_VERSION/$_java_version/" $pkgname.sh > $pkgname/$pkgname

  gendesk -n \
          --pkgname "$_app_name" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "$pkgname" \
          --icon "$_app_name" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd $pkgname
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  install -vDm 644 $pkgname/app/$_name.jar -t "$pkgdir/usr/share/scala/$pkgname"
  install -vDm 644 $pkgname/icons/application.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/de.sciss.mellite.Mellite.png"
  install -vDm 644 $pkgname/icons/application.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/de.sciss.mellite.Mellite.svg"

  install -vDm 755 $pkgname/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 ./*.desktop -t "$pkgdir/usr/share/applications/"
}
