# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-networkauth
pkgname=(python-pyqt6-networkauth)
pkgver=6.6.0
pkgrel=1
pkgdesc='Python bindings for QtNetworkAuth'
arch=(loong64 x86_64)
url='https://www.riverbankcomputing.com/software/pyqtnetworkauth/intro'
license=(GPL3)
groups=(pyqt6)
depends=(gcc-libs
         glibc
         python
         python-pyqt6
         qt6-base
         qt6-networkauth)
makedepends=(pyqt-builder
             sip)
source=(https://pypi.python.org/packages/source/P/PyQt6-NetworkAuth/PyQt6_NetworkAuth-$pkgver.tar.gz)
sha256sums=('cdfc0bfaea16a9e09f075bdafefb996aa9fdec392052ba4fb3cbac233c1958fb')

build() {
  cd PyQt6_NetworkAuth-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-networkauth(){
  cd PyQt6_NetworkAuth-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
