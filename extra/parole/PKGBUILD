# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=parole
pkgver=4.18.1
pkgrel=2
pkgdesc='Modern media player based on the GStreamer framework'
arch=('loong64' 'x86_64')
url='https://docs.xfce.org/apps/parole/start'
license=('GPL')
groups=('xfce4-goodies')
depends=('dbus-glib' 'gst-plugins-base' 'gst-plugins-good' 'libnotify' 'libxfce4ui')
makedepends=('intltool' 'python')
optdepends=('gst-libav: Extra media codecs'
	    'gst-plugins-bad: Extra media codecs'
	    'gst-plugins-ugly: Extra media codecs')
source=("https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('0c7364a484812f69cf2b20a2323864203334cc854fd8103d1d1131814ac55a66')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
