# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2022.07.26
pkgrel=2
epoch=2
pkgdesc='Base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-base/archive/$pkgver.tar.gz"
        distribution.info)
sha512sums=('ed9f3152e19f7c1de12b734f3db1698f7730d18663cdd6a91096d3b9d7fe17932f4efa163665468544db31de92e8757552b053b33e8f969c72e1e1192dfe2ee2'
            '27625e6d0786b8adacdb7c52806d4faa28d2ab6b319a593b3ea9bcb69f0cc18ea19b258d629e3a0069ef9a69503589b0285289caef39a1e85bbd99e915c7cd7d')

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/distribution.info -t "$pkgdir"/usr/share/deepin/

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version

  # Install os-version and rename to uos-version
  install -Dm644 files/os-version "$pkgdir"/etc/uos-version

  # Remove apt-specific templates
  rm -r "$pkgdir"/usr/share/python-apt
}
