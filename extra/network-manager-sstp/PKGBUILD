# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Echizen Ryoma <echizenryoma.zhang@gmail.com>
# Contributor: Martchus <martchus@gmx.net>

pkgname=network-manager-sstp
pkgver=1.3.2
pkgrel=1
pkgdesc="SSTP support for NetworkManager"
arch=('loong64' 'x86_64')
url="https://gitlab.gnome.org/GNOME/network-manager-sstp"
license=('GPL2')
depends=('libsecret' 'ppp' 'sstp-client')
optdepends=('libnma: GUI support (GTK 3)'
            'libnma-gtk4: GUI support (GTK 4)')
makedepends=('gtk3' 'gtk4' 'libnma' 'libnma-gtk4' 'python')
source=("https://gitlab.gnome.org/GNOME/network-manager-sstp/-/releases/release-$pkgver-1/downloads/dists/NetworkManager-sstp-$pkgver.tar.xz")
b2sums=('58489e093092b119ca105f78aca7f48f846778baf4245d9cece045404cf2d35b87beb13106363705ef800caae3351f3bdaf7992237b264446c69ddb52268df11')

prepare() {
  cd NetworkManager-sstp-$pkgver
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
    --with-gtk4
  )

  cd NetworkManager-sstp-$pkgver
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-sstp-$pkgver
  make DESTDIR="$pkgdir" dbusservicedir=/usr/share/dbus-1/system.d install
}
