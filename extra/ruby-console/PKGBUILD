# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-console
pkgver=1.23.2
pkgrel=1
pkgdesc='Beautiful logging for Ruby'
arch=(any)
url='https://github.com/socketry/console'
license=(MIT)
depends=(ruby-fiber-annotation ruby-fiber-local)
checkdepends=(ruby-bake ruby-bake-modernize ruby-bake-test ruby-bake-test-external ruby-covered
              ruby-sus)
options=(!emptydirs)
source=(https://github.com/socketry/console/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f44dd72ea430e7fdf18890d0d33865483e4dbc2758e9c98ca9cc123b4b0ec9e8')

prepare() {
  cd console-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i console.gemspec
  sed -i '/bake-gem/d;/bake-github-pages/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd console-$pkgver
  gem build console.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    console-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd console-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd console-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
