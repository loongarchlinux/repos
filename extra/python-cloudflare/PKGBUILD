# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cloudflare
pkgver=2.11.2
pkgrel=1
pkgdesc='Python wrapper for the Cloudflare v4 API'
arch=('any')
license=('MIT')
url='https://github.com/cloudflare/python-cloudflare'
depends=('python-requests' 'python-future' 'python-yaml' 'python-jsonlines' 'python-beautifulsoup4')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/cloudflare/python-cloudflare/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('53bc37c74e9c0b31ff2ef1cf32450874f12d1fe97d9e13358e73559e78af8f5c93223dbfe8b83f3e95b8a0561af0a346a71427b26d5204a88dd30e9d82a67d35')

build() {
  cd python-cloudflare-$pkgver
  python setup.py build
}

check() {
  cd python-cloudflare-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest tests/test1.py
}

package() {
  cd python-cloudflare-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -r "$pkgdir$site_packages"/examples
}

# vim:set ts=2 sw=2 et:
