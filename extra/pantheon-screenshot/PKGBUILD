# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=6.0.4
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(loong64 x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=462d429797e71146b9037c956c988eb311699dd3
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-screenshot
  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
