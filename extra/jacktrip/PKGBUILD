# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=2.2.0
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(loong64 x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(
  LGPL-3.0-only
  GPL-2.0-or-later
  GPL-3.0-or-later
  MIT
)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-5compat
  qt6-base
  qt6-declarative
  qt6-quickcontrols2
  qt6-shadertools
  qt6-svg
  qt6-webchannel
  qt6-webengine
  qt6-websockets
  qt6-webview
)
makedepends=(
  help2man
  meson
  jack
  python-jinja
  python-yaml
  rtaudio
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('45eba420f39f204c04d1de00fcbc44a6f1b04790a196a6c4274c4de6f63563e2f0006184c94c769837aa7b824f8f916804df47cd853bac9a95f9b7fd76a4ff54')
b2sums=('8f9f7f300b04e26cb5aefdc3a43bc4c73c66cc6f02dc1e99de900d2b56fd074d92d3817d85606303b13e50d7234b5bdc26247e5e238d97ffbb238a6897822fd4')

build() {
  local meson_options=(
    -D noupdater=true
  )

  arch-meson "${meson_options[@]}" build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    jack libjack.so
    rtaudio librtaudio.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
