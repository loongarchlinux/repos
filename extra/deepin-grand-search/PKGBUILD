# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-grand-search
pkgver=5.4.5
pkgrel=2
pkgdesc='System-wide desktop search for DDE'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-grand-search"
license=('GPL3')
# deepin-application-manager: org.deepin.dde.Application1.Manager :/
depends=(deepin-anything deepin-application-manager taglib ffmpeg icu deepin-pdfium ffmpegthumbnailer
         deepin-qt-dbus-factory)
makedepends=(cmake deepin-dock ninja qt5-tools)
groups=(deepin-extra)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-grand-search/archive/$pkgver.tar.gz")
sha512sums=('7d2bd203b9c0dfef57a0667690252a9b3b3f3b5b2e30f44f6706de0d98885908f21f982fd19257812b92a0564e4e7888f8a6789bee2aa5ac2c573a2cadf0b838')

prepare() {
  cd dde-grand-search-$pkgver
  # https://github.com/linuxdeepin/dde-grand-search/pull/65
  sed -i 's/-fPIE -pie//g' src/*/CMakeLists.txt
}

build() {
  cd dde-grand-search-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd dde-grand-search-$pkgver
  DESTDIR="$pkgdir" ninja install
}
