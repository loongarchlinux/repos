# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-sideload
pkgver=6.2.1
pkgrel=1
pkgdesc='Sideload Flatpaks on elementary OS'
arch=(loong64 x86_64)
url=https://github.com/elementary/sideload
license=(GPL3)
depends=(
  flatpak
  gtk4
  libgranite-7.so
  libxml2
)
makedepends=(
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=e5311b8a77f13ea52d0e3de3e09808717be51bf7
source=(pantheon-sideload::git+https://github.com/elementary/sideload.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-sideload
  git describe --tags
}

build() {
  arch-meson pantheon-sideload build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
