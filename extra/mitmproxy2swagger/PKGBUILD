# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=mitmproxy2swagger
pkgver=0.10.0
pkgrel=1
pkgdesc='Automagically reverse-engineer REST APIs via capturing traffic'
url='https://github.com/alufers/mitmproxy2swagger'
arch=('any')
license=('MIT')
depends=(python mitmproxy python-ruamel-yaml python-json-stream)
makedepends=(python-build python-installer python-wheel python-poetry-core)
source=(https://github.com/alufers/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('9b8ad0688be53ee0f0072cc41218a8bf0eb7bf05bc7d1aea792e35bd4449b71b4b160add03728f35ca80f0ae1c7204f97b48c32feaeb116b2dd9f33526213c22')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
