# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.3.3
pkgrel=1
pkgdesc="A spatial database extender for PostgreSQL"
arch=(loong64 x86_64)
url="https://postgis.net/"
license=(GPL)
depends=(postgresql gdal geos json-c libxml2 proj protobuf-c pcre2)
optdepends=('perl: for contrib script postgis_restore.pl')
makedepends=(clang llvm)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('74eb356e3f85f14233791013360881b6748f78081cc688ff9d6f0f673a762d13')
b2sums=('d5390e3e0713b2f74db0c6d7f953681b49611937442f89cd316b43315257db0e0734b6cd23b750a88d9fe503c5348c98578e12cfe6fd8a382fc7817d641e89cf')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
