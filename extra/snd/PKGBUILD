# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=23.5
pkgrel=1
pkgdesc="An advanced sound editor"
arch=(loong64 x86_64)
url="https://ccrma.stanford.edu/software/snd/"
license=(custom:free)
groups=(pro-audio)
depends=(
  glibc
  glu
  gmp
  gsl
  libglvnd
  libmpc
  libx11
  libxext
  libxpm
  libxt
  mpfr
  openmotif
)
# TODO: maybe replace mpg123 with mpg321
makedepends=(
  alsa-lib
  fftw
  flac
  gendesk
  jack
  ladspa
  libsamplerate
  mpg123
  ruby
  speex
  timidity++
  vorbis-tools
  wavpack
)
optdepends=(
  'flac: For flac support'
  'mpg123: For MPEG support'
  'ruby: for snd-ruby'
  'speex: For speex support'
  'timidity++: For MIDI to WAVE support'
  'vorbis-tools: For OGG support'
  'wavpack: For wav support'
)
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('7dd68e9dbca3a3c5558d7ec331288468b1d8837636c83625a5227dd01f1bdf552e8620fe52d3e26462ff2ff36e5edf95de3737e3e2a3068dc72d1df79e0f4d0e')
b2sums=('9aad12276899b7810908fd188272a633d5382c52c52ba82a8c5b823b250d265b2cdb0214976670908132b3b045f772809cba45d9f33867c3c938b0775c41d6d7')

prepare() {
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-s7" \
          --name "$pkgname (s7)" \
          --exec "$pkgname-s7" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname-ruby" \
          --name "$pkgname (ruby)" \
          --exec "$pkgname-ruby" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.$pkgname" \
          --name $pkgname \
          --exec $pkgname \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  cp -av $pkgname-$pkgver $pkgname-ruby-$pkgver
}

build() {
  local options=(
    --prefix=/usr
    --with-alsa
    --with-gl
    --with-gmp
    --with-jack
    --with-ladspa
    --with-motif
  )
  (
  cd $pkgname-$pkgver
  ./configure "${options[@]}" --with-s7
  make
  )
  (
  cd $pkgname-ruby-$pkgver
  ./configure "${options[@]}" --with-ruby
  make
  )
}

package() {
  depends+=(libasound.so libfftw3.so libjack.so libsamplerate.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-s7}

  make DESTDIR="$pkgdir" install -C $pkgname-ruby-$pkgver
  mv -v "$pkgdir/usr/bin/snd"{,-ruby}

  # set s7 version as default
  ln -sv snd-s7 "$pkgdir/usr/bin/snd"

  # XDG desktop integration
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  # license
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  # docs
  install -vDm 644 $pkgname-$pkgver/{{HISTORY,README}.Snd,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/*.html -t "$pkgdir/usr/share/doc/$pkgname/html"
  install -vDm 644 $pkgname-$pkgver/pix/*.png -t "$pkgdir/usr/share/doc/$pkgname/html/pix"

  # remove examples of unsupported extension language forth (not in the repos)
  rm -v "$pkgdir/usr/share/$pkgname/"*.fs
}
