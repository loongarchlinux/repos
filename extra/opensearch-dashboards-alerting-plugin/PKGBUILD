# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-alerting-plugin'
_pluginname='alerting-dashboards-plugin'
pkgver=2.8.0.0
_dashboardsver=2.8.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Alerting Plugin'
url='https://opensearch.org/docs/latest/monitoring-plugins/alerting'
arch=('loong64' 'x86_64')
license=('Apache')
depends=("opensearch-dashboards=${_dashboardsver}" "opensearch-dashboards-notifications-plugin" 'coffeescript')
makedepends=('yarn' 'npm' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
  skip-failed-test.patch
)
sha256sums=('SKIP'
            'SKIP'
            '28bd80e87ad8973831d1bab68f3ad02c78018ffaa99c670f5d27fbb2d923b9e6')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "${_pluginname}/package.json" \
    "OpenSearch-Dashboards/package.json"
  
  cd "${srcdir}/OpenSearch-Dashboards"
  git cherry-pick -n -m1 "986eb8c82a6be22bfb838a7dc1db997905737c88"
  
  cd "${srcdir}/${_pluginname}"
  patch -Np1 -i "$srcdir"/skip-failed-test.patch
}

build() {
  mv "${_pluginname}" "OpenSearch-Dashboards/plugins"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn test:jest -u
}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
