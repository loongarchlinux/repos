# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=proxmark3
pkgver=4.16717
pkgrel=1
pkgdesc='Software for the the Proxmark3, an RFID swiss-army tool'
arch=('loong64' 'x86_64')
url='https://github.com/RfidResearchGroup/proxmark3'
license=('GPL2')
depends=('qt5-base' 'bluez-libs' 'jansson')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'lua52' 'python')
optdepends=('lua52: lua scripts'
            'python: some scripts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('189cc2a38982f689364a458610e5d78f3a2aae383552e8c740e5dbee3ab3ca2fcfae9c27c3ed9329f6f13a59a164b25452bce38229ae318066ced4779abe3d95')

prepare() {
  cp -r $pkgname-$pkgver{,-rdv4}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC SKIPQT=0 all

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 common bootrom recovery fullimage
}

package() {
  export PREFIX=/usr
  export DESTDIR="$pkgdir"
  export UDEV_PREFIX=/usr/lib/udev/rules.d
  export INSTALLTOOLSRELPATH=bin

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 {bootrom,recovery,fullimage}/install

  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC install
}
