# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=proxmark3
pkgver=4.17511
pkgrel=1
pkgdesc='Software for the the Proxmark3, an RFID swiss-army tool'
arch=('loong64' 'x86_64')
url='https://github.com/RfidResearchGroup/proxmark3'
license=('GPL2')
depends=('qt5-base' 'bluez-libs' 'jansson')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'lua52' 'python')
optdepends=('lua52: lua scripts'
            'python: some scripts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('01c78915c4479ec44d2e58a20e20b02da548be48b0e92e072a4e81057ae6139afa843904e9006751d3fcc6476bc208a2e699aab25931ac20a3edf0cc68b151da')

prepare() {
  cp -r $pkgname-$pkgver{,-rdv4}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC SKIPQT=0 all

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 common bootrom recovery fullimage
}

package() {
  export PREFIX=/usr
  export DESTDIR="$pkgdir"
  export UDEV_PREFIX=/usr/lib/udev/rules.d
  export INSTALLTOOLSRELPATH=bin

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 {bootrom,recovery,fullimage}/install

  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3GENERIC install
}
