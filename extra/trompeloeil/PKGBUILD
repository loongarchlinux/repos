# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=trompeloeil
pkgver=46
pkgrel=1
pkgdesc="Thread-safe header-only mocking framework for C++11/14"
arch=('loong64' 'x86_64')
url="https://github.com/rollbear/trompeloeil"
license=('Boost')
makedepends=(cmake ninja)
checkdepends=(catch2)
source=("$pkgname-$pkgver.tar.gz::https://github.com/rollbear/trompeloeil/archive/v${pkgver}.tar.gz")
sha256sums=('dc2c856ab7716ef659f8df7fc5f6740a40e736089f05e0a8251d4ad3ad17ad83')
b2sums=('e9b1e87f6b57cade01e16f21977cd8342d9b24944173552a71c04f30984bb8ad24a85245e4313f45d33504e223d3cb7f9a27bca98415db7c6bd71b04d1b95027')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='Debug' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DTROMPELOEIL_INSTALL_TARGETS=ON \
    -DTROMPELOEIL_INSTALL_DOCS=OFF \
    -DTROMPELOEIL_BUILD_TESTS=yes \
    -Wno-dev
  cmake --build build --verbose --target self_test thread_terror custom_recursive_mutex
}

check() {
  ./build/test/self_test || exit -1
  ./build/test/thread_terror || exit -1
  ./build/test/custom_recursive_mutex || exit -1
}


package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE_1_0.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
