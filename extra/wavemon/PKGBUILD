# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.9.5
pkgrel=1
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='https://github.com/uoaerg/wavemon'
arch=('loong64' 'x86_64')
license=('GPL3')
depends=('glibc' 'ncurses' 'libncursesw.so' 'libcap' 'libcap.so' 'libnl')
source=(https://github.com/uoaerg/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c8abd0d0a2971b937eb2ea51470ec30c9a0e656161a7884cae257a226ef39e0daa8399ef349bbcb370b91a28b6ad0f42acb831524e1f7f5ce43ec244adcb7767')
b2sums=('492ac0f16da1400b4e9e06af43b1ec8b96303bd33a62f355820975db61efc3b0603e1e3f97be05a69597b9546aeb95e3a2655cb0c45c90aa9f2bd765a06b8884')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e '/^CFLAGS=/d' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  sed -r 's|\?=|=|g' -i Makefile.in
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
