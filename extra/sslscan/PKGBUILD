# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.1.2
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('loong64' 'x86_64')
license=('GPL3')
depends=('glibc' 'openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('2d76c1908b64dc291e8e6688746544386823a2e53d61e13a3ea3109d6ccb4f7dc0199b4c4d8a2ac6d420cf85183d9c9ca42051e351531c54eb138ede01a13277')
b2sums=('b4c297b9b8619f559bae28debaeab5fae89ba60823b0bd2d043436e73bbafd3ce688e505678bb4d0182c78caa0f4c086f71e40e900e40d4bea93649f4ab8baca')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
