# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.1.1
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('loong64' 'x86_64')
license=('GPL3')
depends=('glibc' 'openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('5178119d67d41cc72afb7d7ae294d7eb8e68b75932038c09bf07936f53807b06bc27b036b105e655be40f025f3afd20c9c4c0d0b2bd28cfa8e6234c5cee53c3d')
b2sums=('c31a16e7a174f8c7c476ea525846963cfeff590d92063a7b0c4f2cfd95129517f32d4450e1d839dea648f9f8990fef715692caf331186b0f3cfafe8372927076')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
