# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=arianna
pkgver=23.08.4
pkgrel=1
pkgdesc='EPub Reader for mobile devices'
url='https://apps.kde.org/arianna/'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(kirigami-addons5 qqc2-desktop-style5 baloo5 qt5-webengine qt5-websockets kquickcharts5)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1fb077535beb4d0a1cb0bb784e1ab15a4603b155946bc2940fca96a3c3153213'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
