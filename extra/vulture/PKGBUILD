# Maintainer: Leonidas Spyropoulos <artafinde at archlinux dot org>

pkgname=vulture
pkgver=2.8
pkgrel=1
pkgdesc="Finds dead code in Python projects"
arch=('any')
url="https://github.com/jendrikseipp/vulture"
license=('MIT')
depends=(python-coverage python-setuptools python-pint python-toml)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-tox)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('c6a5adc34e229e3b309cf3398a0996d80270ac1658fdf9ba546c07285e51646a')
b2sums=('5a6b1fed08d739a626e9ce4a3525775070690dcc7ac8e2151edf4d0c7aeb7eaff5e7bdfcf8e21214d431126f4b599a4ff130a31a5d994c4f2051f66a0df4f44d')

build() {
  cd "${pkgname}"-"${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"-"${pkgver}"
  python -m tox -e py
}

package() {
  cd "${pkgname}"-"${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
