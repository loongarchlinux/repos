# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: sysrq

pkgname=picard
pkgver=2.9
pkgrel=1
pkgdesc="Official MusicBrainz tagger"
arch=(loong64 x86_64)
url="https://github.com/metabrainz/picard"
license=(GPL2)
depends=(
  glibc
  hicolor-icon-theme
  python
  python-dateutil
  python-discid
  python-fasteners
  python-markdown
  python-mutagen
  python-pyjwt
  python-pyqt5
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
optdepends=(
  'chromaprint: fingerprinting'
  'qt5-multimedia: media player toolbar'
  'qt5-translations: full UI translation'
)
source=(https://data.musicbrainz.org/pub/musicbrainz/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('837f70b239acc0d2846be79fb225e5bc7a3b7c3d2e928c34bd01730b6049809a58c274a4e49c5cc42f253f5c113c19b331ac1489427f8dbb3c31515d2511e4dc'
            'SKIP')
b2sums=('d843cbb249f79a7817f0d5a62f7f851a3acc9943c93e0130fa487c385d1dda3b14a51b7070212a4562553c39ac54e8b743391a28c3e916ceb11e5070f3801985'
        'SKIP')
# NOTE: GPG signed tags and artifacts are being evaluated: https://tickets.metabrainz.org/browse/PICARD-1934
validpgpkeys=('68990DD0B1EDC129B856958167997E14D563DA7C') # MusicBrainz Picard Developers <picard@metabrainz.org>

build() {
  cd $pkgname-release-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-release-$pkgver
  pytest -vv
}

package() {
  cd $pkgname-release-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS.txt,{CONTRIBUTING,NEWS,README}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
