# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.4
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtk6gui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ecee00733eab24856af1e7cac335043dd7bcde1ce276fe5ce413ab259067deccd98451c634e4ed3db006ce3a1a7afe12daa451e7b5be77117bf3dcf6c47da096')

build() {
  cd dtk6gui-$pkgver
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui-$pkgver
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
