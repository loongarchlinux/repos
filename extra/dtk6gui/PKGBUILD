# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.2
pkgrel=2
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtk6gui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('12605b7403bc59e9f5644d7b08ad38c015a373363aaf9ba8c943150beb57787f4cb647fe89516b3638ca44e20b666e23f68d89d81fbe6556664d92ccce25ee12')

build() {
  cd dtk6gui-$pkgver
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui-$pkgver
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
