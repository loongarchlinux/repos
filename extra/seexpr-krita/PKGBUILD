# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=seexpr-krita
pkgver=3.4.4.0
pkgrel=3
pkgdesc="An embeddable expression evaluation engine (Krita fork)"
arch=(x86_64)
url="https://krita.org"
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools boost python-pyqt5 sip python-sip)
optdepends=('boost-libs: for the Python bindings' 'python: for the Python bindings')
source=("https://download.kde.org/stable/seexpr/seexpr-$pkgver.tar.gz")
sha256sums=('3f5b5330667d398285c9f7086cc3fd7e1363f3fa75f79b39766c34fe2e233656')

build() {
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cmake -B build -S seexpr-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYQT_SIP_DIR="$_pythonpath/PyQt5/bindings" \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
