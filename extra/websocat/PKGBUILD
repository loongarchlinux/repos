# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=websocat
pkgver=1.12.0
pkgrel=1
pkgdesc='Command-line client for web sockets, like netcat/curl/socat for ws://'
url='https://github.com/vi/websocat/'
arch=('loong64' 'x86_64')
license=('MIT')
depends=('glibc' 'openssl' 'gcc-libs')
makedepends=('cargo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('043e175d557fee92d381ed1f1aa510056e1d59d0bd25c8f1cc9bf78c3ef59e9e76c5ac283f6bf4e8c9563365cd57deaf5e6f3b0040e6d680a9e8dc80ad931610')
b2sums=('e83675c1bd421add00a3a2409b51e98edacb52c018b3f617cc78808e14d8a2af0a7ce9d6db1a6ebc740a6c620213e70fb3dda91c2f8a9bcd9e93d05c8043b313')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked --features=ssl
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked --features=ssl
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/websocat -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
