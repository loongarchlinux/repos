# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=opentimelineio
pkgver=0.15
pkgrel=4
pkgdesc='Open Source API and interchange format for editorial timeline information'
url='http://opentimeline.io/'
license=(Apache)
arch=(loong64 x86_64)
depends=(gcc-libs
         glibc
         python
         python-aaf2)
makedepends=(cmake
             git
             imath
             pybind11
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/PixarAnimationStudios/OpenTimelineIO#tag=v$pkgver
        opentimelineio-rapidjson::git+https://github.com/Tencent/rapidjson
        unbundle.patch)
sha256sums=('SKIP'
            'SKIP'
            '1239ca9258b9c828bff3024564d67968e6e2986ecb0d3b4d900320405cc0d197')

prepare() {
  cd OpenTimelineIO

  git submodule init src/deps/rapidjson
  git submodule set-url src/deps/rapidjson "$srcdir"/opentimelineio-rapidjson
  git -c protocol.file.allow=always submodule update src/deps/rapidjson

  git cherry-pick -n d1e78073d4fc321b667ceffc2d95251015af5fea # Fix build with GCC 13
  patch -p1 -i ../unbundle.patch # Use C++17 any and optional
  sed -e '/cmake/d' -i pyproject.toml
}

build() {
  cmake -B build -S OpenTimelineIO \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOTIO_FIND_IMATH=ON \
    -DOTIO_AUTOMATIC_SUBMODULES=OFF \
    -DOTIO_DEPENDENCIES_INSTALL=OFF
  cmake --build build

# Workaround messy build system
  export CXXFLAGS+=" -I/usr/include/Imath"
  cd OpenTimelineIO
  python -m build --wheel --no-isolation
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd OpenTimelineIO
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -fr "$pkgdir"/build
}
