# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.1.2
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('loong64' 'x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=(
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('2625c9f2d0e771ebe9b778c099107ac86f48c8c068d50b8e4fea9765553c3686f3a01bdca53e98ac9447efbfa49191b59576a5debba286940b97edf2ab3ee38b'
            '049413a46313bfac760f79b13af483d89310b015efbbfb52b0b177c70889099de8431076129419ba12109190a86f900af1f4e39c3ef864f47beb260bfd79d4f4')
b2sums=('32526d40ffa30c96fe1ee662415963670b4b35dd88a6720fbf8b728b84e48f81f3ce5fa544b660a2b780c5e49b036f3a36b9290b365cf412d313b85f41fa1ba8'
        '86b44ced8832c931e2ea19c82bd9679165ba2c93f487f8185a7c6467d02e2e8efd17a4ab6cf50f535f27b3115f2cad0f15c230083aa7d8eeb04f846165f77988')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
