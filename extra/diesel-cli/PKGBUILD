# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=2.1.1
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('loong64' 'x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=(
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('bc802930c20a679f162324148bcaa95b57116a7906d7ed9fc21773c2ecf727352bbbe086d879e1ea030f2368813d3fc26bd623f41a977e2e95f8d429d5476bd0'
            '7ddf5cc0ff37d0d3b365d94a8ba365a19d46556169c3b1140d5a2c7382fa6716c7c561a5137a628310b8d460e9749d2f77e3191a1907eb70b048540080f76261')
b2sums=('ed02a608c7266dc6a3f54af93d7f56ff1436b7e3b966606c54e8e5be8439257ca3995afa346d4a51aa4e9c9df8854635e2387e8340894cd607a114c9c21bca43'
        '192aa243f9fc95b43bea89bb92c4ec3a199eb9e1c47d3e0b43855ce436d4e30baedbc7c471fb563235e95c89c9e962c825a7c0c0a0110d3b3d647e91c152d8a4')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
