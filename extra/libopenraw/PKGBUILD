# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: <boromil@gmail.com>

pkgname=libopenraw
pkgver=0.3.6
pkgrel=1
pkgdesc='Library for decoding RAW files'
arch=('loong64' 'x86_64')
url='https://libopenraw.freedesktop.org/'
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('boost' 'libxml2' 'cargo')
provides=('libopenraw.so' 'libopenrawgnome.so')
source=("https://libopenraw.freedesktop.org/download/${pkgname}-${pkgver}.tar.bz2"{.asc,})
b2sums=('SKIP'
        '25a874630d4058104eeef4d3c27504eb897ae86f859beab4ff3e024fc5d15bc40d8c49272511e0a90e692007ce9262b6dd72ce8cccc90b9daeddf6a9cb820dfe')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere <hub@mozilla.com>

prepare() {
# Fix libopenraw dependency in -gnome pc file
  sed -e 's|libopenraw-0.1|libopenraw-0.3|' -i $pkgname-$pkgver/gnome/libopenraw-gnome-0.3.pc.in
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" install
}
