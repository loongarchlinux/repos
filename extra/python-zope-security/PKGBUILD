# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-security
pkgver=6.2
_commit=dfdce15e432208fd8f01cc2072818a2847663bc6
pkgrel=1
pkgdesc="Zope Security Framework"
arch=('loong64' 'x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
         'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')
makedepends=('git')
checkdepends=('python-btrees' 'python-nose' 'python-zope-configuration' 'python-zope-testing'
              'python-zope-testrunner')
source=("git+https://github.com/zopefoundation/zope.security.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd zope.security
  python setup.py build
}

check() {
  cd zope.security
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-311" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.security
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
