# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Nikola
pkgname=nikola
pkgver=8.3.0
pkgrel=2
pkgdesc="A static site and blog generator"
arch=(any)
url="https://getnikola.com/"
_url="https://github.com/getnikola/nikola"
license=(MIT)
depends=(
  python
  python-babel
  python-blinker
  python-doit
  python-dateutil
  python-docutils
  python-logbook
  python-lxml
  python-mako
  python-markdown
  python-natsort
  python-piexif
  python-pillow
  python-pygments
  python-pyrss2gen
  python-requests
  python-setuptools  # https://github.com/getnikola/nikola/issues/3743
  python-unidecode
)
makedepends=(
  jupyter-notebook
  python-aiohttp
  python-build
  python-ghp-import
  python-husl
  python-html5lib
  python-installer
  python-ipykernel
  python-jinja
  python-micawber
  python-phpserialize
  python-pygal
  python-pyphen
  python-ruamel-yaml
  python-toml
  python-typogrify
  python-watchdog
  python-wheel
  zeromq
)
checkdepends=(
  libwebp
  python-feedparser
  python-freezegun
  python-pytest
)
optdepends=(
  'libwebp: for WEBP image file support'
  'jupyter-notebook: for .ipynb support'
  'python-aiohttp: for nikola auto'
  'python-ghp-import: for upload to Github pages'
  'python-hsluv: for section color mixing'
  'python-html5lib: for HTML5 minify and format support'
  'python-husl: for optional color support'
  'python-ipykernel: for .ipynb support'
  'python-jinja: for jinja2 based themes'
  'python-micawber: for embedding media in posts'
  'python-phpserialize: for Wordpress import'
  'python-pygal: for SVG graph plotting'
  'python-pyphen: for hyphenation support'
  'python-ruamel-yaml: for YAML support'
  'python-toml: for TOML support'
  'python-typogrify: for typographical enhancements'
  'python-watchdog: for nikola auto'
)
source=($_url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('9b25eb90fa560b45e8cd0b03f24523859548353f2a4d2ac91b23b05bfb614d20456bb71f1aaa5702b6d6ab7980696aeb2f5ccc3c00ebc648cfcad49619481247'
            'SKIP')
b2sums=('67ab76c670fd00d992e6359bcaa11c59dfc904c78c935d1739e6ed7118e38c8f1c4590782a75e85c722f4fcb2c23efc032cb5d58d0c1587069b80920f3bd20fa'
        'SKIP')
validpgpkeys=('DF32C99CFF84282CF9D4CF7171023DBD5EAAEA16') # Chris Warrick <kwpolska@gmail.com>

prepare() {
  sed -e '/addopts/d' -i $_name-$pkgver/setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation

  # shell completions
  python -m $pkgname tabcompletion --shell bash --hardcode-tasks > ${pkgname}_bash
  python -m $pkgname tabcompletion --shell zsh --hardcode-tasks > ${pkgname}_zsh
  # ensure that the shell completion is assigned to the correct executable
  sed -e "s/__main__.py/$pkgname/g" -i "${pkgname}_"{bash,zsh}
}

check() {
  local pytest_options=(
    -vv
  )
  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ${pkgname}_bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 ${pkgname}_zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
