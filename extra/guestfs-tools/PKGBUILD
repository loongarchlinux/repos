# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=guestfs-tools
pkgver=1.52.0
pkgrel=1
pkgdesc="Tools for accessing and modifying guest disk images"
arch=(loong64 x86_64)
url="https://libguestfs.org/"
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  gcc-libs
  glibc
  jansson
  libosinfo
  openssl
  perl
  perl-libintl-perl
)
makedepends=(
  bash-completion
  cdrtools
  libguestfs
  libisoburn
  libvirt
  libxcrypt
  libxml2
  ncurses
  ocaml-findlib
  perl-module-build
  xz
  zstd
)
checkdepends=(
  linux
  rpm-tools
)
backup=(
  etc/virt-builder/repos.d/libguestfs.conf
  etc/virt-builder/repos.d/libguestfs.gpg
  etc/virt-builder/repos.d/opensuse.conf
  etc/virt-builder/repos.d/opensuse.gpg
)
source=(
  https://download.libguestfs.org/$pkgname/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig}
)
sha512sums=('afef6163c9005ca9fab5aa414c9b73f08f5c6403448b9a513c56eb2f15e9d3a3444046c5a1677fbd7334189cc7739545dff8412a85cb896bc46a44b42e0d0172'
            'SKIP')
b2sums=('cb009c298aff391984a3f2340312d1e0da7012b7a64f84ef60251ca29196b67c0a3f2fdf02f39385f94369af5218a65eb24fbe65cc3d72684711066ca273c000'
        'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  export LIBGUESTFS_DEBUG=1 LIBGUESTFS_TRACE=1  # verbose libguestfs output
  export SKIP_TEST_VIRT_FILESYSTEMS_SH=1  # mismatch in test 2
  export SKIP_TEST_VIRT_INSPECTOR_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LUKS_ON_LVM_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LVM_ON_LUKS_SH=1  # requires KVM
  export SKIP_TEST_VIRT_BUILDER_SH=1  # virt-resize: error: /dev/sda2: physical volume not returned by pvs_full
  export SKIP_TEST_VIRT_DRIVERS_LINUX_SH=1  # requires rpm-tools during libguestfs compile time?
  
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    glib2 libgobject-2.0.so libglib-2.0.so
    libguestfs libguestfs.so
    libvirt libvirt.so
    libxcrypt libcrypt.so
    libxml2 libxml2.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
    xz liblzma.so
    zstd libzstd.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
