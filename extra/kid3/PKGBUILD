# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3-common' 'kid3-qt' 'kid3')
pkgver=3.9.4
pkgrel=3
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor"
arch=('loong64' 'x86_64')
url="https://kid3.kde.org/"
license=('GPL')
depends=('chromaprint' 'flac' 'id3lib' 'kxmlgui5' 'libmp4v2'
         'qt5-declarative' 'qt5-multimedia' 'taglib' 'kio5')
makedepends=('cmake' 'docbook-xsl' 'extra-cmake-modules' 'kdoctools5' 'python' 'qt5-tools')
changelog=$pkgbase.changelog
source=(https://prdownloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz{,.sig}
        https://invent.kde.org/multimedia/kid3/-/commit/3ed2e5f7.patch
        https://invent.kde.org/multimedia/kid3/-/commit/98524900.patch
        taglib-2.patch)
validpgpkeys=('7D09794C2812F62194B081C14CAD34426E354DD2') # Urs Fleisch
sha256sums=('6d67d941604c3d96e42b2bafbe7ec4a60004620b729ccbe2550af211eb59dd7b'
            'SKIP'
            '7f59595a638056a780db5ce3b4266760e7f478e7b11c4c5b703772fddc8bdb5c'
            '6edd2ab211d92c2e4ab6c5c757686bd730f2f54cb26b23e4c513d18cbc341900'
            '11bd60246fe7c8066c5b33b3884b2008f19afbc8b7c0df3a5631e39258642e0c')

prepare() {
  patch -d $pkgbase-$pkgver -p1 < 3ed2e5f7.patch
  patch -d $pkgbase-$pkgver -p1 < 98524900.patch # Fix build with taglib 2
  patch -d $pkgbase-$pkgver -p1 < taglib-2.patch
}

build() {
  export CXXFLAGS+=' -DNDEBUG' # FS#69904
  cmake -B build -S ${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_APPS="CLI;Qt;KDE" \
    -DWITH_MP4V2=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_GSTREAMER=ON \
    -Wno-dev
  cmake --build build
}

package_kid3-common() {
  pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, CLI version and common files"
  depends=('chromaprint' 'flac' 'id3lib' 'libmp4v2' 'libvorbis' 'qt5-base' 'taglib')
  optdepends=('qt5-declarative: GUI support library'
              'qt5-multimedia: GUI support library')

  make -C build DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/usr/bin/kid3{,-qt} \
        "$pkgdir"/usr/share/{applications,icons,kxmlgui5,metainfo}
}

package_kid3-qt() {
  pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, Qt version"
  depends=("kid3-common=$pkgver" 'qt5-declarative' 'qt5-multimedia')

  make -C build/src/app/qt DESTDIR="${pkgdir}" install
}

package_kid3() {
  pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
  depends=("kid3-common=$pkgver" 'kxmlgui5' 'qt5-declarative' 'qt5-multimedia' 'kio5')

  make -C build/src/app DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/usr/bin/kid3-{cli,qt} \
        "$pkgdir"/usr/share/applications/org.kde.kid3-qt.desktop \
        "$pkgdir"/usr/share/icons/hicolor/*/apps/kid3-qt.{png,svg} \
        "$pkgdir"/usr/share/metainfo/org.kde.kid3-qt.appdata.xml
}
