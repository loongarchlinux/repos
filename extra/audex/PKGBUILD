# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mightyjaym <jmambrosino@gmail.com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=audex
pkgver=0.96.1
pkgrel=1
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('loong64' 'x86_64')
url="https://userbase.kde.org/Audex"
license=('GPL')
depends=('libkcddb5' 'hicolor-icon-theme' 'kcmutils5')
makedepends=('extra-cmake-modules' 'git' 'qt5-script')
optdepends=('vorbis-tools: OGG encoder'
	'faac: MP4/M4A/AAC encoder'
	'flac: FLAC encoder'
	'lame: MP3 encoder')
source=("git+https://invent.kde.org/multimedia/audex#tag=v$pkgver")
sha256sums=('SKIP')

pkgver() {
  cd audex
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

build() {
  cmake -B build -S audex
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
