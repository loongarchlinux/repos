# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=delve
pkgver=1.21.0
pkgrel=1
pkgdesc="A debugger for the Go programming language."
arch=('loong64' 'x86_64')
url="https://github.com/go-delve/delve"
license=('MIT')
makedepends=('go' 'git')
optdepends=('debuginfod: debuginfod support')
options=(!lto strip)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/go-delve/delve/archive/v${pkgver}.tar.gz")
sha256sums=('f00321e9333a61cb10c18141484c44ed55b1da1c4239a3f4faf2100b64613199')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  export GOPATH="${srcdir}"
  go build -ldflags='-compressdwarf=false -linkmode=external' ./cmd/dlv
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 dlv "${pkgdir}/usr/bin/dlv"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
