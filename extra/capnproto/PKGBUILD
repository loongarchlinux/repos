# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Matthias Blaicher <matthias@blaicher.com>
# Contributor: Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=1.0.1.1
pkgrel=1
pkgdesc="Cap'n Proto serialization/RPC system"
arch=(loong64 x86_64)
url="https://capnproto.org/"
license=(MIT)
depends=(
  gcc-libs
  glibc
  openssl
  zlib
)
makedepends=(cmake)
provides=(
  libcapnp.so
  libcapnp-json.so
  libcapnp-rpc.so
  libcapnpc.so
  libkj-async.so
  libkj-gzip.so
  libkj-http.so
  libkj-test.so
  libkj-tls.so
  libkj.so
)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('4a3b5afd3515ba27aef7f1767de0b0ca34d72d316fdcfbc1d83894857033addd32d10a275bf25f1f00a48f97e413a76f8a4f5b7ce64d7f3e4045dca4d4151631')
b2sums=('78916b1258b895e01dec81fa4af5f0e8c31881243cfe4cad29c5c389c9371f01fc859f65e74309c0ec05eba9630e290377060cefbd881d35a789d3ffa3d32a2c')

build() {
  local cmake_options=(
      -B build
      -D BUILD_SHARED_LIBS=ON
      -D CMAKE_BUILD_TYPE=None
      -D CMAKE_INSTALL_PREFIX=/usr
      -S $pkgname-$pkgver
      -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CONTRIBUTORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
