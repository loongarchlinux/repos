# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=23.08.1
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kmbox akonadi-notes akonadi qca-qt5 qt5-networkauth)
makedepends=(extra-cmake-modules kdoctools libetebase boost kholidays)
optdepends=('libetebase: EteSync resource')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kdepim-runtime/-/commit/fb0d78fb.patch
        https://invent.kde.org/pim/kdepim-runtime/-/commit/2fd3604a.patch
        https://invent.kde.org/pim/kdepim-runtime/-/commit/c99afc75.patch)
sha256sums=('8b1e85b469159234abfd1da99717a697d4f1d7c92557a2c237264a93b7af4cd9'
            'SKIP'
            '14d4a04b8e7c7bd734f51c55dcaa7c6d578c5c0acade5dca9e95e7125b97dcb3'
            '4105d62a2f79f6a285788ef182717e6c03cdbf3d6a06fa441f62a9b5089ea492'
            '231ba2ec5c5d6ddbac89b84b1584b2f8d58ab0e8bfa8b3c7324c80dfce786b73')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
# Fix adding google accounts
  patch -d $pkgname-$pkgver -p1 < fb0d78fb.patch
  patch -d $pkgname-$pkgver -p1 < 2fd3604a.patch
  patch -d $pkgname-$pkgver -p1 < c99afc75.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
