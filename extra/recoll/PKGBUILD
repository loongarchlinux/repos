# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=recoll
pkgver=1.36.2
pkgrel=1
pkgdesc="Full text search tool based on Xapian backend"
arch=('loong64' 'x86_64')
url="https://www.lesbonscomptes.com/recoll/"
license=('GPL')
depends=('xapian-core>=1.0.15-1' 'qt5-base' 'openssl' 'hicolor-icon-theme' 'qt5-webengine'
	 'chmlib' 'aspell')
makedepends=('python' 'python-setuptools' 'qt5-tools'
	     'cmake' 'extra-cmake-modules' 'kcoreaddons5' 'kio5' 'krunner5')
optdepends=('libxslt: for XML based formats (fb2,etc)'
	    'unzip: for the OpenOffice.org documents'
	    'poppler: for pdf'
	    'pstotext: for postscipt'
	    'antiword: for msword'
	    'catdoc: for ms excel and powerpoint'
	    'unrtf: for RTF'
#	    'untex: for dvi support with dvips'
	    'djvulibre: for djvu'
#	    'id3lib: for mp3 tags support with id3info'
	    'python-mutagen: Audio metadata'
	    'perl-image-exiftool: EXIF data from image files'
	    'python-lxml: indexing spreadsheets'
	    'python-pychm: CHM filter'
	    'kcoreaddons5: KDE plugins'
	    'kio5: KDE plugins'
	    'krunner5: KDE plugins'
	    'aspell-en: English stemming support')
source=("https://www.lesbonscomptes.com/$pkgname/$pkgname-${pkgver/_/}.tar.gz")
sha512sums=('4c7ce5b6f6213ba4cb9266dcd6ea26e56850aa984c343552464766c5036ac6fd2ac6e9e71157997e10e8ba0350d23a4ebc3f2d57978805aa2d33674aa2a6c8b0')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  sed -i '1,1i#include <QAbstractItemView>' qtgui/ssearch_w.cpp
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  export CFLAGS="$CFLAGS -fpermissive"
  export CPPFLAGS="$CPPFLAGS -fpermissive"
  QMAKE=qmake-qt5 ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-recollq \
    --disable-webkit \
    --enable-webengine \
    --enable-publiclib
  make

  pushd kde/kioslave/kio_recoll
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DRECOLL_PUBLIC_LIB=ON ..
  make
  popd

  pushd kde/krunner
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DRECOLL_PUBLIC_LIB=ON ..
  make
  popd
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  make DESTDIR="$pkgdir" install
  make -C "kde/kioslave/kio_recoll/build" DESTDIR="$pkgdir" install
  make -C "kde/krunner/build" DESTDIR="$pkgdir" install
}
