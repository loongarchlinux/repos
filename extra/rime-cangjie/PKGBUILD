# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cangjie
pkgver=0.0.0.20231211
_commit=75b10325bf4c590a3ffef7039a6f052a729edc55
pkgrel=1
pkgdesc="Cangjie input for rime"
arch=('loong64' 'x86_64')
url="https://github.com/rime/rime-cangjie"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cangjie/archive/$_commit/rime-cangjie-$_commit.tar.gz")
sha512sums=('617c607dc69c1ab2e468dffe692435da468f18dc0abd99e1f8b8b70011873ab0e6ba13f3212ee313e172834da2fb8083d06fa79e0f359c942bc0acdfa7d23153')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
