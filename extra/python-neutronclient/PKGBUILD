# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-neutronclient
pkgver=10.0.0
_commit=d342171f9eefc16760ce80fb5bebe3aa2f461d21
pkgrel=1
pkgdesc="OpenStack Networking (Neutron) Client"
arch=('any')
url="https://docs.openstack.org/python-neutronclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-debtcollector' 'python-iso8601' 'python-netaddr'
         'python-openstacksdk' 'python-osc-lib' 'python-oslo-i18n' 'python-oslo-log'
         'python-oslo-serialization' 'python-oslo-utils' 'python-os-client-config'
         'python-keystoneauth1' 'python-keystoneclient' 'python-requests' 'python-simplejson')
makedepends=('git')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-openstackclient' 'python-subunit'
              'python-requests-mock' 'python-stestr' 'python-testtools' 'python-testscenarios')
source=("git+https://github.com/openstack/python-neutronclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-neutronclient
  python setup.py build
}

check() {
  cd python-neutronclient
  stestr run
}

package() {
  cd python-neutronclient
  python setup.py install --root="$pkgdir" --optimize=1
}
