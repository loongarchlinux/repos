# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libgnome-games-support
pkgver=1.8.2
pkgrel=3
pkgdesc="Code shared between GNOME games"
url="https://gitlab.gnome.org/GNOME/libgnome-games-support"
arch=(loong64 x86_64)
license=(LGPL-3.0-or-later)
depends=(
  gtk3
  libgee
)
makedepends=(
  git
  meson
  vala
)
provides=("libgnome-games-support-${pkgver%%.*}.so")
_commit=406695a2bd14436cb7ff10001865db9d1c236fad  # tags/1.8.2^0
source=("git+https://gitlab.gnome.org/GNOME/libgnome-games-support.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libgnome-games-support
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgnome-games-support
}

build() {
  arch-meson libgnome-games-support build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
