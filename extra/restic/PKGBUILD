# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=restic
pkgver=0.16.0
pkgrel=1
pkgdesc="Fast, secure, efficient backup program"
arch=('loong64' 'x86_64' 'aarch64')
url="https://restic.net"
license=('BSD')
depends=('glibc')
makedepends=('go' 'git')
checkdepends=('fuse2')
optdepends=('fuse2: mount archive')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/restic/restic/archive/v${pkgver}.tar.gz")
sha512sums=('d51732b611407cc8ebc40c9acb45df8154f87ece26b37d32074b59f5cfb56cbcca71695036923a50739215426dce614fb32d8cd18c581d37dc10d8a25b8c95e5')
b2sums=('f9f28cb5751265368583b8b1ac01fad591cccb13ae0a9c7b8b3d41329783230484d190d763223c2a43d57b001ddcb3cefbfb2a37cf00eaef7b49ddb2c9610375')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
  export CGO_ENABLED=0
  go build -ldflags "-X 'main.version=${pkgver}'" ./cmd/...
}

check(){
  cd "${pkgname}-${pkgver}"
  go test ./cmd/... ./internal/... || true
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 restic "${pkgdir}/usr/bin/restic"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/man/*.1
  install -Dm644 doc/zsh-completion.zsh "$pkgdir/usr/share/zsh/site-functions/_restic"
  install -Dm644 doc/bash-completion.sh "$pkgdir/usr/share/bash-completion/completions/restic"
  install -Dm644 doc/fish-completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/restic.fish"
}
