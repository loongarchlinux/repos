# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lawrence Lee <valheru@facticius.net>

pkgname=rasqal
epoch=1
pkgver=0.9.33
pkgrel=3
pkgdesc="A free C library that handles Resource Description Framework (RDF) query syntaxes, query construction and query execution returning result bindings"
url="http://librdf.org/rasqal"
license=('GPL' 'LGPL')
arch=('x86_64')
depends=('raptor' 'mpfr' 'util-linux')
source=(http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('6924c9ac6570bd241a9669f83b467c728a322470bf34f4b2da4f69492ccfd97c'
            'SKIP')
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A') # Dave Beckett <dave@dajobe.org>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --enable-release
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check || true # still fails some checks
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
