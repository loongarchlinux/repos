# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(gtkmm3 gtkmm3-docs)
pkgver=3.24.2
pkgrel=2
pkgdesc="C++ bindings for GTK+ 3"
url="http://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 pangomm atkmm)
makedepends=(git mm-common glibmm-docs cairomm-docs pangomm-docs atkmm-docs)
_commit=72c50bb8629558e1f0df3d86c16e4290cc94fd5e  # tags/3.24.2^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtkmm
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtkmm
  ./configure --prefix=/usr --enable-maintainer-mode
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtkmm3() {
  cd gtkmm
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd gtkmm
  make -C docs DESTDIR="$pkgdir" install
}
