# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=basket
_pkgver=2.49
pkgver=${_pkgver}b
pkgrel=3
pkgdesc="All-purpose notes taker for KDE."
arch=('x86_64')
url="https://basket-notepads.github.io/"
depends=('kcmutils' 'kfilemetadata' 'kparts' 'phonon-qt5' 'libgit2')
makedepends=('extra-cmake-modules' 'kdoctools')
license=('GPL')
source=(https://launchpad.net/basket/kde5/$pkgver/+download/basket-$_pkgver-beta.tar.bz2
        basket-libgit2-0.99.patch::"https://cgit.kde.org/basket.git/patch/?id=090ac469")
sha256sums=('e760fc68deb1f56ef02cd4a1773c6cd348b79b88ef5ff5dfb05e66947244f186'
            '3e6c47eff6d7087bdac784c689444c33c504d91aaffa97bef2e379918775c73a')

prepare() {
  mkdir -p build

  cd $pkgname-$_pkgver-beta
  patch -p1 -i ../basket-libgit2-0.99.patch # Fix build with libgit 0.99
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver-beta \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
