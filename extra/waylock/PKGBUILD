# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=0.6.4
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(loong64 x86_64)
url="https://github.com/ifreund/waylock"
license=(ISC)
depends=(
  glibc
)
makedepends=(
  git
  libxkbcommon
  pam
  scdoc
  wayland
  wayland-protocols
  zig
)
source=(
  $url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.sig}
  git+https://codeberg.org/ifreund/zig-wayland.git#commit=65475badb37fdfa06ac091554fdc81689a37a72a
  git+https://codeberg.org/ifreund/zig-xkbcommon.git#commit=7b188de0ba794b52eb70340abf2469b858630816
)
sha512sums=('c663223cdfd99c4bc730ae06d75c4a8e75f068f07245d15fa7c463e468b2dcfaae2e8a46f83f380cc4b86059045337ed00b1eae223bc4ea67058831f6f6febd0'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('13db129aa65294f4ec3b955b31581f9ba57bd9af115bc332e0eaaf2d1ea7d425a18c7daf754a097cdf178a104731179bb46f13a9d708e1db8b36515036440902'
        'SKIP'
        'SKIP'
        'SKIP')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

prepare() {
  cd $pkgname-$pkgver
  ln -s ../zig-wayland deps/zig-wayland
  ln -s ../zig-xkbcommon deps/zig-xkbcommon
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.5.15-gnu
    --prefix /usr
    --search-prefix /usr
    --verbose
  )

  cd $pkgname-$pkgver
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon libxkbcommon.so
    pam libpam.so
    wayland libwayland-client.so
  )

  cd $pkgname-$pkgver

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
