# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-control-center
pkgver=1.26.1
pkgrel=1
pkgdesc="The Control Center for MATE"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('mate-menus' 'mate-settings-daemon' 'marco' 'libxss' 'mate-desktop'
         'gettext' 'accountsservice' 'mate-panel' 'libcanberra')
makedepends=('itstool')
groups=('mate')
conflicts=('mate-control-center-gtk3')
replaces=('mate-control-center-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e05f492a3b657aa56fc58f7cf71bc8c80df8e25351fde4db4f523ab8db5b5608')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --disable-update-mimedb

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}/usr/share/applications/mimeinfo.cache"
}

# vim:set sw=4 et:
