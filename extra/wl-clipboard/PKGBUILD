# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Adam Brunnmeier

pkgname=wl-clipboard
pkgver=2.2.0
pkgrel=1
epoch=1
pkgdesc="Command-line copy/paste utilities for Wayland"
arch=('loong64' 'x86_64')
url="https://github.com/bugaevc/wl-clipboard"
license=('GPL3')
depends=('wayland')
optdepends=(
    'xdg-utils: for content type inference in wl-copy'
    'mailcap: for type inference in wl-paste'
)
makedepends=('meson' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bugaevc/wl-clipboard/archive/v$pkgver.tar.gz")
sha256sums=('414005cfa229d0e54f89a0d9a8473938e4c29adc21a9e556847a4d44ad508874')

build() {
    arch-meson "$pkgname-$pkgver" build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
}
