# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-docs
pkgver=1.7.2
pkgrel=2
pkgdesc="X.org documentations"
arch=(any)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('man')
groups=('xorg')
source=(${url}/archive/individual/doc/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('474b7fac5832edbdf6fba319074c3fe8eae6394d251fc8886c013c4e4d9695e92bf59e30aee5e53aed8365255b5d7b16bdbbad7a8ace33901bb0e3158353cdbf'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -vsf /usr/share/doc/xorg-docs/License.xml .
}
