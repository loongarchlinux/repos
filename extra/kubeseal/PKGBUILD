# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: sseneca <me at ssene dot ca>
# Contributor: ml <ml@visu.li>

pkgname=kubeseal
_pkgname=sealed-secrets
pkgver=0.23.0
pkgrel=1
pkgdesc="A Kubernetes controller and tool for one-way encrypted Secrets"
arch=('loong64' 'x86_64')
url="https://github.com/bitnami-labs/sealed-secrets"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('79fb2f66c900e340cb64b2fc5571b2d7cca3debcc8442d8e53139a4fbdcb11ef9be7aad337eced2f9e574d5d7b6af8ee45a5ab8a0229330736a412d415e90952')
options=('!lto')

build() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build -ldflags="-linkmode=external -X=main.VERSION=v${pkgver}" .
}

check() {
  cd "${_pkgname}-${pkgver}"

  go test ./cmd/kubeseal/... ./pkg/...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal/"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
