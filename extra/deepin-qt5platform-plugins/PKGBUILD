# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.6.21
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL3')
depends=('cairo' 'qt5-base' 'qt5-wayland' 'qt5-x11extras' 'dwayland' 'libx11' 'libxi'
         'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('cmake' 'ninja' 'qt5-tools' 'qt5-xcb-private-headers' 'libglvnd' 'gtest'
             'extra-cmake-modules' 'dtkcommon')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ad9f037ba2fcb6b146aef9e9df04727e70174ccdf4922bf46b735dee925ecfbba849f02e73434e6bde263f52dd3d8436f73c4c5ed2ea103acb367139a71150e5')

prepare() {
  cd qt5platform-plugins-$pkgver
  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev
  sed -i '/find_package/i find_package(ECM REQUIRED 1.0.0)\nset(CMAKE_MODULE_PATH ${ECM_MODULE_PATH} ${CMAKE_MODULE_PATH})' wayland/wayland-shell/wayland-shell.cmake
  sed -i 's|qt${QT_VERSION_MAJOR}/plugins|qt/plugins|' xcb/CMakeLists.txt wayland/*/CMakeLists.txt 
}

build() {
  cd qt5platform-plugins-$pkgver
  cmake . -GNinja \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qtxcb-private
  ninja
}

package() {
  cd qt5platform-plugins-$pkgver
  DESTDIR="$pkgdir" ninja install
}
