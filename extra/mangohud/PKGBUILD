# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
_pkgname=MangoHud
pkgname=mangohud
_pkgver=0.6.9-1
_tarpkgver="${_pkgver%-*}"
pkgver=${_pkgver//-/.}
pkgrel=6
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('loong64' 'x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('appstream' 'cmocka' 'glfw-x11' 'glslang' 'libxnvctrl' 'meson' 'nlohmann-json' 'python-mako')
depends=('dbus' 'fmt' 'gcc-libs' 'glew' 'hicolor-icon-theme' 'libglvnd' 'libx11' 'spdlog' 'vulkan-icd-loader')
optdepends=('libxnvctrl: NVIDIA GPU stats by XNVCtrl'
            'glfw-x11: Required for MangoApp'
            'gamescope: Use MangoApp as an overlay within gamescope')
replaces=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
conflicts=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$_pkgver/$_pkgname-v$_pkgver-Source.tar.xz"
        "3f8f036ee8773ae1af23dd0848b6ab487b5ac7de-reversed.patch")
sha256sums=('30d9336b60cbc7fdc2a1ba86ec62b9fb7f2986a2b0f6196ca347f5c13e583c6d'
            '244ef8fffb27042d521993f9e9894f852d3061a980345e5c8c6a3f148dd26707')

prepare() {
    cd "$_pkgname-v$_tarpkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build

    # Fix build with GCC 13
    patch --forward --reverse --strip=1 --input="$srcdir/3f8f036ee8773ae1af23dd0848b6ab487b5ac7de-reversed.patch"
}

build() {
    arch-meson "$_pkgname-v$_tarpkgver" build \
        -Duse_system_spdlog=enabled \
        -Dmangoapp=true \
        -Dmangohudctl=true \
        -Dmangoapp_layer=true

    meson compile -C build
}

package() {    
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$_tarpkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}

check() {
    meson test -C build --print-errorlogs || :
}
