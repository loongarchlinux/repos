# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Hendrik Meyer <archlinux@t4cc0.re>

pkgname=lsof
pkgver=4.93.2
pkgrel=3
pkgdesc='Lists open files for running Unix processes'
url='https://github.com/lsof-org/lsof'
arch=('x86_64')
license=('custom')
depends=('glibc' 'libtirpc')
source=(https://github.com/lsof-org/lsof/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
	"lsof-reproducible.patch::https://github.com/lsof-org/lsof/commit/3e404f637c76949917435f4a086c40e644e0089c.patch"
        license.txt)
sha512sums=('3c4a4d503391b1a648a08a4947c2e72a9629b169eddf50738b16a16a82f2a26967e4db2902e4a9157de5a73ae638db48e392117ffbd591e9fafd6b1725cd01a0'
            'ccb263eb04d3cc6a879f6a6bada680dee473920ea6460c4e048e7551a2716b937007bc91f21751fb3dd0a43320f9b263983e7c04e2bac46d45472ce97e50047b'
            '3bdbc8f213e9bdba946636498d21486e2b79d8ae44a45b284b4dff8875e76f992ab0977e3ed510525a8f74203028acb6b15315fda963666012874be4ce4fdd93')

prepare() {
  cd lsof-${pkgver}

  # patch accepted upstream - in queue to be committed
  patch -p1 -i "${srcdir}/lsof-reproducible.patch"

  sed -i 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h
  sed -i "s|.so ./version|.ds VN ${pkgver}|" -i Lsof.8
}

build() {
  cd lsof-${pkgver}
  ./Configure -n linux
  make CC="cc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS}"
}

package() {
  cd lsof-${pkgver}

  install -Dm 755 lsof -t "${pkgdir}"/usr/bin
  install -Dm 644 Lsof.8 "${pkgdir}"/usr/share/man/man8/lsof.8
  install -Dm 644 "${srcdir}"/license.txt \
       "${pkgdir}"/usr/share/licenses/lsof/LICENSE
}

# vim: ts=2 sw=2 et:
