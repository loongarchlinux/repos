# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=blosc2
pkgver=2.11.3
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(loong64 x86_64)
url="https://www.blosc.org"
license=(BSD)
depends=(lz4 zlib zstd)
makedepends=(cmake)
source=(https://github.com/Blosc/c-blosc2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('51652056e646dd29496d8e7a4c3fc1c84af5f2a389b9057d0189d45fd298d64a581bf9f92f688397ff941dc18e03123cf7cd190fea91e3c127c36ad1d775bf79')
b2sums=('140ca9693f2a779e37d324c6a631cde915b7bd7e16c5d48c779872dbc2a688773c22145bd224b8a7ac8e355b2639d08723ff1e4dc59cd57f2f4da89eea2d2c7c')

build() {
  cmake -B build -S c-blosc2-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 c-blosc2-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
