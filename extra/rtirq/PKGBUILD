# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20240120
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=(any)
url="https://www.rncbc.org/archive/#rtirq"
license=(GPL-2.0-or-later)
groups=(realtime)
depends=(bash)
backup=(etc/rtirq.conf)
source=(https://www.rncbc.org/archive/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('b945bdb29ef3ef551c19edc9d947a7ed50ecd756dec5a3b02a0cd519a0f506e3ca212b1cb629386dd525d565cc6e2d868c558934c59b8d0bc02573a9589deb86'
            'SKIP')
b2sums=('2c1ab9f1824d861e13e49667a35fc048e931afb18084de756dcbabde0d4cdd21e78afc9733eb364cd826d4ffd29a89db862033c9539b7cfe7a7b22f7cb353864'
        'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

package() {
  cd $pkgname-$pkgver
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -vDm 644 $pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 ./*.service -t "$pkgdir/usr/lib/systemd/system/"
}
