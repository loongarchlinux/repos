# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.9.1
pkgrel=9
pkgdesc='A backup software for helping people to keep up-to-date backups'
arch=(loong64 x86_64)
url='https://apps.kde.org/kup/'
license=(GPL2)
depends=(gcc-libs
         glibc
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kdbusaddons5
         ki18n5
         kidletime5
         kinit
         kio5
         kjobwidgets5
         knotifications5
         kwidgetsaddons5
         kxmlgui5
         libgit2
         qt5-base
         solid5)
makedepends=(extra-cmake-modules
             plasma-framework)
optdepends=('bup: bup backend'
            'rsync: support for synced backups')
source=(https://download.kde.org/stable/kup/kup-$pkgver.tar.xz{,.sig})
sha256sums=('5597d215b025bfc76aa290efbe187fb6f92313fbdc40628b851180c535610976'
            'SKIP')
validpgpkeys=(CBA1DCD0B06A2B2E9B133E5C5C7D83CE9812E291)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
