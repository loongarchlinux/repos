# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Padraic Fanning <fanninpm AT miamioh DOT edu>

pkgname=python-exceptiongroup
_pyname=${pkgname#python-}
pkgver=1.1.2
pkgrel=1
pkgdesc='Backport of PEP 654 (exception groups)'
arch=(any)
url="https://github.com/agronholm/$_pyname"
license=(MIT)
depends=('python<=3.11.3')
makedepends=(python-{build,installer,wheel}
             python-flit-scm)
checkdepends=(python-pytest)
_archive="$_pyname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz"
        "$url/commit/821d5eb.patch")
sha256sums=('107e769418fac86697c510aa0e6849ae48d384b20186ec4db5c23e567829589f'
            'a789e9153eb32fcf3c01ec11a220964f3a42be93841ab9fc26f69379794a5c3c')

# Release 1.1.2 has a patch that is specific for Python 3.11.4+, as of now Arch
# is on 3.11.3. This can/should be removed after Python updates.
prepare() {
	cd "$_archive"
	sed -e '15,31d' ../821d5eb.patch | patch -Np1 -R
}

build() {
	cd "$_archive"
	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
	python -m build -wn
}

check() {
	cd "$_archive"
	PYTHONPATH=src pytest
}

package() {
	cd "$_archive"
	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
