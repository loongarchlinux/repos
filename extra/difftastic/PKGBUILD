# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=difftastic
pkgver=0.51.1
_commit=8752af74d4fad7932b34076a1021dd654f1bee28
pkgrel=3
pkgdesc='An experimental diff tool that compares files based on their syntax'
arch=('loong64' 'x86_64')
url='https://difftastic.wilfred.me.uk'
license=('MIT')
depends=('glibc' 'gcc-libs' 'shared-mime-info')
makedepends=('cargo' 'git' 'mdbook')
source=(
  "git+https://github.com/Wilfred/difftastic.git#commit=$_commit"
  'remove-makedepends-jq.patch'
)
b2sums=('SKIP'
        '467d5fc634b3f1d8fa7fe0c8de843e615b6bbfed340ad5d4167d6d5d374185bcb8d54f31f43995beb43966f8d49497956d4a30186ccb974b2cadc01357c9c2bc')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  # remove makedepends on jq
  patch -p1 -i "$srcdir/remove-makedepends-jq.patch"
}

build() {
  cd "$pkgname"
  export CFLAGS+=" -ffat-lto-objects"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  cd manual
  sed -i "s/DFT_VERSION_HERE/$pkgver/g" -i src/introduction.md
  mdbook build
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features

  # run all ignored tests
  # https://github.com/Wilfred/difftastic/commit/34f21c6d9f256f16cc5f77294bcca9e61cee3fd6
  cargo test --frozen --all-features -- --ignored
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/difft

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr manual/book "$pkgdir/usr/share/doc/$pkgname/manual"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
