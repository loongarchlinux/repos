# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgbase=qtcreator
pkgname=(qtcreator
         qtcreator-devel)
pkgver=12.0.1
_clangver=16.0.6
pkgrel=2
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=(loong64 x86_64)
url='https://www.qt.io'
license=(GPL3)
depends=(clang=$_clangver
         clazy
         gcc-libs
         glibc
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-quick3d
         qt6-tools
         qt6-serialport
         qt6-svg
         qt6-webengine
         yaml-cpp
         zstd)
# litehtml syntax-highlighting
makedepends=(cmake
             llvm
             python)
optdepends=('qt6-doc: integrated Qt documentation'
            'qt6-examples: welcome page examples'
            'gdb: debugger'
            'cmake: cmake project support'
            'x11-ssh-askpass: ssh support'
            'git: git support'
            'mercurial: mercurial support'
            'breezy: bazaar support'
            'valgrind: analyze support'
            'perf: performer analyzer'
            'mlocate: locator filter')
source=(https://download.qt.io/official_releases/qtcreator/${pkgver%.*}/$pkgver/qt-creator-opensource-src-$pkgver.tar.xz
        qtcreatorbug-30035.patch::https://code.qt.io/cgit/qt-creator/qt-creator.git/patch/?id=a61f9162f10e54f31dcc5b0221e11b5ca81f186b)
sha256sums=('66fb92c2ced092829e3321c5a0911e2de98309d0310e7ab949a39a50238e03f3'
            '8b918b8ac6784a6e928be88e975ee3df0a99975c3a600993cb1172f96de0d480')
options=(docs)

prepare() {
  patch -d qt-creator-opensource-src-$pkgver -p1 < qtcreatorbug-30035.patch
}

build() {
  cmake -B build -S qt-creator-opensource-src-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DWITH_DOCS=ON \
    -DBUILD_DEVELOPER_DOCS=ON \
    -DBUILD_QBS=OFF \
    -DQTC_CLANG_BUILDMODE_MATCH=ON \
    -DCLANGTOOLING_LINK_CLANG_DYLIB=ON
  cmake --build build
  cmake --build build --target docs
}

package_qtcreator() {
  DESTDIR="$pkgdir" cmake --install build
# Install docs
  cp -r build/share/doc "$pkgdir"/usr/share

  install -Dm644 qt-creator-opensource-src-$pkgver/LICENSE.GPL3-EXCEPT "$pkgdir"/usr/share/licenses/qtcreator/LICENSE.GPL3-EXCEPT
}

package_qtcreator-devel() {
  pkgdesc+=' (development files)'
  depends=(qtcreator)
  optdepends=()

  DESTDIR="$pkgdir" cmake --install build --component Devel
}
