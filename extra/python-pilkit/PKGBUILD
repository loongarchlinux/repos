# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pilkit
pkgname=python-pilkit
pkgver=2.0
pkgrel=13
pkgdesc="A collection of utilities for working with PIL (the Python Imaging Library)"
arch=(any)
url="https://github.com/matthewwithanm/pilkit"
license=(BSD)
depends=(
  python
  python-pillow
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
# NOTE: ticket about the removal of mock: https://github.com/matthewwithanm/pilkit/issues/54
checkdepends=(
  python-mock
  python-nose
  python-pillow
  python-pytest
)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-2.0-pillow_10_antialias_deprecation.patch  # backport of https://github.com/matthewwithanm/pilkit/pull/65
)
sha512sums=('7618d7de125f2813b25d707b02b70ca9cb4e19105ed0eb3be3ff6b926a812ed2a609e54f8e86535641fc4baef265fae58d70290c14011eeea744e90066b7bdae'
            'f9234b002cd00cd5ec36d64ae7af2907f341148b402f9d9b861ab2b3b8a4332d20c57dd22b565c04c4440020a018cb18b38b298c18608e35b9c2b3d2a623db7e')
b2sums=('e612b1f438d17c068dcad5d5150b3593374e7c5b2d08439eb64754cfa3a8c54d298adffa6d57d884bd1b2338b01afb159c238d90d4144608d47d51cc77c14a1c'
        '3e0e9f379128308f8b4f0700d2a32a6eb7eb4a9dbbd8f66cdf088b38dd61d8cb07dfdb53c64c21a611a47cea0a2b798883f7be8f43d57d054388f079c3c44438')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-2.0-pillow_10_antialias_deprecation.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
