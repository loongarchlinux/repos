# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.10.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('loong64' 'x86_64')
url='https://dystroy.org/bacon'
license=('AGPL3')
depends=('glibc' 'gcc-libs')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Canop/bacon/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('5cc996ceabfb03f8f32eb202679f2c886bdb1a29fae9e4448e5d577d24c661d2aa51179b994211fd0cf00b96b018d01132ba1fc2a69f321a513eac9e6fe765a1')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-$pkgver"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -vDm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}
