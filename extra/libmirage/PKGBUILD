# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.2.7
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
url="https://cdemu.sourceforge.io/"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  bzip2
  glib2
  libsamplerate
  libsndfile
  shared-mime-info
  xz
  zlib
)
makedepends=(
  cmake
  gobject-introspection
  gtk-doc
  intltool
  ninja
  vala
)
source=(https://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.xz)
sha256sums=('fa892480d7954bcca8292ad00f2e08b7b3e23e54d288eb14a05c50d454bdb3d3')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D POST_INSTALL_HOOKS=OFF
  )

  cmake -S $pkgname-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
