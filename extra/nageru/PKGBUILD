# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=nageru
pkgver=2.2.1
pkgrel=2
pkgdesc="live video mixer, based around the standard M/E workflow."
arch=(loong64 x86_64)
url="https://nageru.sesse.net/"
license=('GPL3')
depends=('libmicrohttpd' 'ffmpeg' 'libx264' 'protobuf' 
         'movit' 'bmusb' 'qcustomplot' 'pciutils' 'zita-resampler'
         'sqlite3' 'luajit' 'cmake')
makedepends=('meson' 'eigen' 'qt5-tools')
source=("https://nageru.sesse.net/nageru-${pkgver}.tar.gz"
         gcc-13.patch
         svt-av1-1.6.patch)
sha256sums=('16cdf59d36d7500ec3d06a7b7dfd027355446e6ff9ae25f1d30e094f3dbad4b3'
            'b7157e2c2c0431026fd2fc532b39d46f6be605248fa0ee9785f9fba856d279d3'
            'b83a694eaaa77bf2ae35b9127a9cbba75a6abe69cbdfe0de795c77a9236fb289')

prepare() {
  patch -d $pkgname-$pkgver -p1 < gcc-13.patch # Fix build with GCC 13
  patch -d $pkgname-$pkgver -p1 < svt-av1-1.6.patch # Fix build with svt-av1 1.6
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build -v
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
