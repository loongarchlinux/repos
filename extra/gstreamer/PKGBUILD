# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.18.4
pkgrel=1
pkgdesc="Multimedia graph framework - core"
url="https://gstreamer.freedesktop.org/"
arch=(loongarch64 x86_64)
license=(LGPL)
depends=(libxml2 glib2 libunwind libcap libelf python)
makedepends=(gobject-introspection git valgrind bash-completion meson gmp gsl gtk3)
_commit=eacb7aa8d12f66a3ff1970c149210972f6079b56  # tags/1.18.4^0
install=gstreamer.install
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D doc=disabled \
    -D ptp-helper-permissions=capabilities \
    -D dbghelp=disabled \
    -D gobject-cast-checks=disabled \
    -D package-name="GStreamer (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
