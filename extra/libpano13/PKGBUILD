# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libpano13
pkgver=2.9.22
pkgrel=1
pkgdesc="Basic library to calculate panoramical pictures - core functions of the panotools"
arch=(loong64 x86_64)
url="http://panotools.sourceforge.net/"
license=(GPL2)
depends=(libjpeg libpng libtiff suitesparse)
makedepends=(cmake)
source=(https://downloads.sourceforge.net/sourceforge/panotools/${pkgname}-${pkgver}.tar.gz)
sha256sums=('affc6830cdbe71c28d2731dcbf8dea2acda6d9ffd4609c6dbf3ba0c68440a8e3')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SPARSE_LEVMAR=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}"/usr/share/doc
  mv "${pkgdir}"/usr/share/{pano13/doc,doc/pano13}
  rmdir "${pkgdir}"/usr/share/pano13
}
