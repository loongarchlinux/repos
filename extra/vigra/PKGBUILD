# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=vigra
pkgver=1.11.2
pkgrel=1
pkgdesc='Computer vision library'
arch=(loong64 x86_64)
url="https://ukoethe.github.io/$pkgname"
_url="https://github.com/ukoethe/$pkgname"
license=(MIT)
depends=(fftw
         gcc-libs
         hdf5
         libpng
         libtiff
         openexr
         sh)
makedepends=(boost
             cmake
             python-numpy)
checkdepends=(python-nose)
optdepends=('python: for python bindings'
            'boost-libs: for python bindings')
_pkgver="Version-${pkgver//./-}"
_archive="$pkgname-$_pkgver"
source=("$_url/archive/$_pkgver/$_archive.tar.gz"
        vigra-openexr3.patch)
sha256sums=('4841936f5c3c137611ec782e293d961df29d3b5b70ade8cb711374de0f4cb5d3'
            '2572717e39f916c7c463b8f49306b683c5923906be2721f7da181c58b3a2b34b')

prepare() {
	cd "$_archive"
	patch -p1 -i ../vigra-openexr3.patch
}

build() {
	cmake -B build -S "$_archive" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DPYTHON_EXECUTABLE=/usr/bin/python \
		-DDOCINSTALL=share/doc \
		-DWITH_OPENEXR=true \
		-DWITH_VIGRANUMPY=1
		#-DBUILD_DOCS=OFF \
	make -C build
}

check() {
	# https://github.com/ukoethe/vigra/issues/491
	make -C build -j1 -k check || echo "Tests failed"
}

package() {
	  make -C build DESTDIR="$pkgdir" install
	  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "${_archive}/LICENSE.txt"
}
