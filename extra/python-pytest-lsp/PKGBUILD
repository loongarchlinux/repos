# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

_name=pytest-lsp
pkgname=python-pytest-lsp
pkgver=0.3.0
pkgrel=1
pkgdesc='Pytest plugin for end-to-end testing of language servers'
arch=('any')
url=https://swyddfa.github.io/lsp-devtools
license=('MIT')
depends=('python-pygls' 'python-pytest-asyncio')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/swyddfa/lsp-devtools/archive/pytest-lsp-v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('aa0535dc9c819fa80e63410a62cac93071dd376705e5f068d519597011001ef0c456bdeffc2d70bf184b2041ebd7d4deeeb97a4e264597a75f5e111aaac3e26c')

build() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd lsp-devtools-pytest-lsp-v$pkgver/lib/pytest-lsp
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pytest_lsp-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
