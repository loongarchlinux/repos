# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=kubo
pkgver=0.23.0
pkgrel=1
pkgdesc='IPFS implementation in Go'
url='https://github.com/ipfs/kubo'
arch=(loong64 x86_64)
license=(MIT)
depends=(glibc)
makedepends=(go git)
optdepends=('fuse2: for mounting/advanced use')
provides=('go-ipfs')
replaces=('go-ipfs')
conflicts=('go-ipfs')
# manually unpack kubo-source.tar.gz into kubo/ since the archive contains the source directly
noextract=(kubo-$pkgver.tar.gz)
source=(kubo-$pkgver.tar.gz::https://github.com/ipfs/kubo/releases/download/v$pkgver/kubo-source.tar.gz
        ipfs.service
        ipfs@.service)
sha512sums=('091f9673642b6a319f2b7762bcf9cd81612c3d8b3678fa8718189a09f06b82dd66e83ece204e7797ddc3fd575e36318182326215bd079763c04725d5ea57c7fb'
            'b1d0b2ab3008adcd2c1871fe3ff57aa27cf43d6acbd4ab330f0d2efbba661a046b9c8a8cf514ddf131c5fce93ca5d91d10d516c4f39d98f33e4ac46aee1d0abb'
            'a1254e40d0bacfceb92071de7f5331cd87cc454bf70be34e892af35dfc677fcf130903577c2a4e7fe6da6b5cedecb22dc0f73d1ad86f5aa27a8333d6bd85fe8c')

prepare() {
  mkdir -p kubo
  tar -C kubo -xf kubo-$pkgver.tar.gz
}

build() {
  cd kubo

  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw"
  make build
  cmd/ipfs/ipfs commands completion bash > "$srcdir/ipfs-completion.bash"
  cmd/ipfs/ipfs commands completion fish > "$srcdir/ipfs-completion.fish"
}

package() {
  cd kubo

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644 "$srcdir"/ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 "$srcdir"/ipfs@.service "$pkgdir/usr/lib/systemd/system/ipfs@.service"

  install -Dm 644 "$srcdir"/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 "$srcdir"/ipfs-completion.fish "$pkgdir/usr/share/fish/vendor_completions.d/ipfs.fish"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/MIT" LICENSE-MIT
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/APACHE" LICENSE-APACHE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: ts=2 sw=2 et:
