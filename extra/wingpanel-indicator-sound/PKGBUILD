# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-sound
pkgver=6.0.2
pkgrel=2
pkgdesc='Wingpanel Sound Indicator'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-sound
license=(GPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgee
  libgranite.so
  libnotify
  libpulse
  wingpanel
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
_tag=c9b97e8479430a6bbaf51c6eaae14caaccd5eab8
source=(wingpanel-indicator-sound::git+https://github.com/elementary/wingpanel-indicator-sound.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-sound
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
