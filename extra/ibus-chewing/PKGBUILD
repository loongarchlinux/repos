# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.6.2
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('loong64' 'x86_64')
license=('GPL')
url='https://github.com/definite/ibus-chewing'
depends=('ibus' 'libchewing' 'gtk3')
makedepends=('gob2' 'cmake-fedora')
source=("https://github.com/definite/ibus-chewing/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b723202fa844d3f94171a4bf44467f7bacaee7283866698903b705ba26344bf844c5d6df0fd73e875bb5cff0aa6764a881a4633c36ef5585015a63ec01e639bf')

build() {
  cd $pkgname-$pkgver

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DLIBEXEC_DIR=/usr/lib/ibus

  # XXX: updated *.po files contain no translated strings. Maybe because fedora.zanata.org is down
  # https://github.com/definite/ibus-chewing/issues/150
  touch build/po/ibus-chewing.pot
  cp -v po/*.po build/po/

  make -C build -j1
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
