# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.16.5
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('loong64' 'x86_64')
url='https://github.com/dandavison/delta'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'libgit2.so'
  'zlib'
)
makedepends=('cargo' 'mdbook')
checkdepends=('git')
source=("$url/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('76475159ad659261dea3f60e5ee340d64f16031b59387602a6b962f854e5c88e5ad1613715ea0581197d8b829c75326d25c9550d0899957a0ce7e4dd5d3046a9')
b2sums=('28e88c84fc299c3b148eb71bcc3de26dfc5d52140e79d188d71d2a8c902485e06ed62c09826cdf22c7d97f6aeab42e5c39d09b26aac49afcacf4cecd0c690ff3')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"
  install -vDm644 etc/completion/completion.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
