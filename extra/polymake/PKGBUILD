# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
pkgver=4.10
pkgrel=1
_perlver=`perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]).".".($v->[2]);'`
pkgdesc='Open source software for research in polyhedral geometry'
arch=(loong64 x86_64)
url='https://polymake.org/'
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol scip perl=$_perlver)
makedepends=(ninja cddlib ant boost bliss libnsl jdk-openjdk permlib nauty soplex)
optdepends=('java-runtime: java module')
source=(https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2)
sha256sums=('6038b266d6d40bbe9955ede846dccf4417e4114faa87e775656161cd4ca2f8f8')

prepare() {
  sed -e 's|-lgmp|-lgmp -lmpfr|g' -i polymake-$pkgver/bundled/soplex/support/configure.pl # Fix soplex linking
}

build() {
  cd polymake-$pkgver
  ./configure \
    --prefix=/usr \
    --without-native \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-soplex=/usr \
    --with-sympol=/usr
  make
}

package() {
  cd polymake-$pkgver
  make DESTDIR="$pkgdir" install

# Executables are installed in mode 555, which breaks strip
  find "$pkgdir" -exec chmod u+w {} +
}
