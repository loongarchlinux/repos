# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-io-socket-ssl
pkgver=2.075
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/IO-Socket-SSL/"
depends=('perl-net-ssleay' 'perl' 'perl-uri')
checkdepends=('perl-io-socket-inet6')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/S/SU/SULLR/IO-Socket-SSL-${pkgver}.tar.gz)
sha512sums=('ed693aa360e669401d5d2b81d988d6d2bd39f042452d7194cc8a7ec2dccf1dfa213ecb29572178a3abfdd20af999217d19a3d9a4c7978207a2edaac5148c9853')

build() {
  cd IO-Socket-SSL-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Socket-SSL-${pkgver}
  make test || warning "TLS 1.3 related errors"
}

package() {
  cd IO-Socket-SSL-${pkgver}
  make DESTDIR="${pkgdir}" install
}
