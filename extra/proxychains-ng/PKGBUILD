# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgname=proxychains-ng
pkgver=4.17
pkgrel=1
pkgdesc="A hook preloader that allows to redirect TCP traffic of existing dynamically linked programs through one or more SOCKS or HTTP proxies"
arch=('loong64' 'x86_64')
url="https://github.com/rofl0r/proxychains-ng"
license=('GPL')
provides=('proxychains')
replaces=('proxychains')
conflicts=('proxychains')
depends=('glibc')
backup=('etc/proxychains.conf')
source=("https://github.com/rofl0r/proxychains-ng/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('14404449a5ab50920a5d16e70db4e8025075b03146df86ca5833eb22d5bd56428b038ac5daf2a17e0687e036ca496a8bfd88fc654be09b9dfad6d5d0b2273b95')

build() {
  cd proxychains-ng-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd proxychains-ng-$pkgver
  make DESTDIR="$pkgdir/" install install-config
  ln -s proxychains4 "$pkgdir/usr/bin/proxychains"
}

# vim:set ts=2 sw=2 et:
