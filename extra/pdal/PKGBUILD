# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.6.0
pkgrel=1
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('loong64' 'x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.bz2")
sha512sums=('61b2575fc5d2751f1e34925e948aca9936b8de72c0b596f9809cdf9156a15e94f670db22bd68c45e484783ba112a6bc43c7309a62e3aa3158567437195901cbd')

build() {
  cd "PDAL-$pkgver-src/"

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
      -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
