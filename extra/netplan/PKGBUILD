# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor:  Conrad Hoffmann <ch@bitfehler.net>

pkgname=netplan
pkgver=0.106.1
pkgrel=1
pkgdesc="Network configuration abstraction renderer"
arch=('loong64' 'x86_64')
url="https://github.com/CanonicalLtd/netplan"
license=('GPL3')
depends=(
  'glib2'
  'python-yaml'
  'python-netifaces'
  'iproute2'
  'systemd'
)
makedepends=(
  'python'
  'pkgconf'
  'pandoc'
)
source=(
  "https://github.com/CanonicalLtd/${pkgname}/archive/${pkgver}.tar.gz"
)
sha512sums=('0592079888dc6be8802f5d5b01b62e1a570ac1fa340628bab9d7a4a38e796a1cde8c22ec98448e29a83569381035e9d7b06a92418a82547d0b1030151d94a9e2')
b2sums=('3c77c8193269b372f79366bf31160a53fc59a5fff8f18fb12bcba89073da053e174349acd9ccd05c128023dd4e56d379d380a91ed309e651686dbbecfa5c7fb6')

build() {
  cd "${pkgname}-${pkgver}"

  # Drop -Werror to avoid the following error:
  # /usr/include/glib-2.0/glib/glib-autocleanups.h:28:3: error: 'ip_str' may be used uninitialized in this function [-Werror=maybe-uninitialized]
  sed -e "s/-Werror//g" -i Makefile

  env
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" ROOTPREFIX=/usr SBINDIR=/usr/bin install
}
