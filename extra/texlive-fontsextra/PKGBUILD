# Maintainer: RÃ©my Oudompheng <remy.archlinux.org>

pkgname=texlive-fontsextra
pkgver=2021.61408
_revnr=${pkgver#2021.}
pkgrel=1
pkgdesc="TeX Live - all sorts of extra fonts"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "$pkgname.maps"
        "newtx-1.701.diff")
options=('!emptydirs')
sha256sums=('80ffe1c439292f9aaa0b1fe88423b5ab9c2abf730f6d1cc89283cc22f03ea211'
            '1fcc22ee72b40765961a90144d5babe6e902e33e57fe653a9dd4b0b19c6ad399'
            '2a2535aef4902b3b229b012a83274011c707bb8b5a01e7f889e44acef1484feb')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true

   # Fix lastminute bug in newtx
   patch -Np1 -i "${srcdir}/newtx-1.701.diff"
}

package() {
   cd "$srcdir"
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
