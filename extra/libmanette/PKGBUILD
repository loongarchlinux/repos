# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libmanette
pkgver=0.2.6
pkgrel=5
pkgdesc='Simple GObject game controller library'
arch=('loong64' 'x86_64')
url='https://gnome.pages.gitlab.gnome.org/libmanette/'
license=('LGPL2.1')
depends=('gcc-libs' 'glib2' 'glibc' 'libevdev' 'libgudev')
makedepends=('gobject-introspection' 'meson' 'vala')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('63653259a821ec7d90d681e52e757e2219d462828c9d74b056a5f53267636bac')

build() {
  arch-meson $pkgname-$pkgver build -D doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
