# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=143193
pkgrel=1
pkgdesc='ScummVM core'
arch=(loong64 x86_64)
url=https://github.com/libretro/scummvm
license=(GPL)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=0c3e3aaebcaf23de7175a9df5e5c0e13ad2551fd
source=(libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  make -C libretro-scummvm/backends/platform/libretro
}

package() {
  install -Dm 644 libretro-scummvm/backends/platform/libretro/scummvm_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
