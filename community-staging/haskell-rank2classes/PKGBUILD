# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=rank2classes
pkgname=haskell-rank2classes
pkgver=1.4.1
pkgrel=63
pkgdesc="Standard type constructor class hierarchy, only with methods of rank 2 types"
url="https://github.com/blamario/grampa/tree/master/rank2classes"
license=('BSD')
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-distributive')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-markdown-unlit'
             'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        ghc9.patch)
sha512sums=('5c1c271809d98105c9d0bfa0a453871b4ac322d954ac9a1048d8846081782f5080c7051d3f3d58d8620c8221f3ea5b4761311677ff1968daabc4e5a6dfbc8d5e'
            '0b80333b0f463ae5b07c90d453526943448e2e5898936fd9cf308e8c2c0864cd77f534e1579dd53ee40dbeeec47ca41b0c8ac982f6fc0ad1fd2d2c85501924ff')

prepare() {
    patch -d $_hkgname-$pkgver -p2 < ghc9.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
