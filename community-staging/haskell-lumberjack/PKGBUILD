# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lumberjack
pkgname=haskell-lumberjack
pkgver=1.0.0.0
pkgrel=2
pkgdesc="Trek through your code forest and make logs"
url="https://github.com/GaloisInc/lumberjack"
license=("custom:ISC")
arch=('loongarch64' 'x86_64')
depends=('ghc-libs' 'haskell-contravariant' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('d1d584de9548a1cc04ffc1a8345ce561f2df123e6de339ba6b20606635167bab')

prepare() {
  cd $_hkgname-$pkgver
  # https://github.com/GaloisInc/lumberjack/issues/2
  sed -i 's/adjustLogAction = local/adjustLogAction x = local x/' example/ExampleLog.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
