# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.9.365
pkgrel=2
pkgdesc='AWS SDK for C++'
arch=(loong64 x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib util-linux-libs aws-c-common aws-c-event-stream libpulse aws-crt-cpp)
makedepends=(cmake ninja)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('1a447dd9f5be45c150ad918e7bd286eadf8acd70e467ad383c963e3bcae87e2e')

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF \
      -DCMAKE_MODULE_PATH=/usr/lib/cmake \
      -S . -B build -G Ninja
  ninja -C build -j 2 # the build is so heavy that it gets OOM killed even at powerful machines
}

package() {
  cd aws-sdk-cpp-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
