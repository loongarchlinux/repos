# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.3.0
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six' 'patch')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/libgit2/pygit2/archive/v$pkgver.tar.gz
        $pkgname-libgit2v1.1.0.patch)
sha256sums=('1ffbbbfc2f85694b8f395a616ab930e57b2df9d50781e8ba84603d293ae70462'
            'd888082802ebff943986909a9716b302780eb7e9c56b73d3cdd3f9b3740125d4')

prepare() {
	cd "pygit2-$pkgver"
# https://github.com/libgit2/pygit2/commit/b57aa0e889fe07d7b4663d0729a40f4a9ecf5578
    patch -Np1 -i "${srcdir}"/$pkgname-libgit2v1.1.0.patch
}


build() {
	cd "pygit2-$pkgver"
	python setup.py build
}

check() {
	cd "pygit2-$pkgver"
	python setup.py pytest
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
