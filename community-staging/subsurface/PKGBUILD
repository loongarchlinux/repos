# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.9.6
pkgrel=3
pkgdesc='Divelog program'
url='https://subsurface-divelog.org/'
license=('GPL2')
arch=('x86_64')
makedepends=('asciidoc' 'cmake' 'libgit2' 'libusb' 'qt5-tools')
depends=('googlemaps' 'grantlee' 'hicolor-icon-theme' 'libzip' 'libxml2'
'libxslt' 'subsurface-libdc' 'qt5-svg' 'qt5-location' 'qt5-connectivity'
'qt5-webkit' 'sqlite')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha512sums=('6bef6a17e505aad2ab3d395483b3890ab358cc3c352671fc41f8e0fd946daee27f6aac98da3ed6f75cac7a148047581ad069d757714f40814f7bb0d950215564')
b2sums=('2df46f721817c363904dd15f39266ea6d9d29686cad7c541996a28fe05c1c22d67563c2c58f7147c458c5b7746c4cba8bfd0e5bcf277d7360fa85efa80a4800f')

# qt5-webkit still used for: printing, manual

prepare() {
  mv -v "${_pkgname}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DLIBDIVECOMPUTER_INCLUDE_DIR=/usr/include/libdivecomputer \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libgit2.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
