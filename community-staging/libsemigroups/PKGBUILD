# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=1.3.7
pkgrel=2
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io/libsemigroups'
arch=(loongarch64 x86_64)
license=(GPL3)
depends=(fmt)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('94e1a5e4bd175781b09f85604ae7f42267be57c163160414cdfbc2bf85c158a7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
