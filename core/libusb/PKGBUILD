# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libusb
pkgver=1.0.23
pkgrel=2
pkgdesc='Library that provides generic access to USB devices'
arch=(x86_64)
url='https://libusb.info/'
license=(LGPL)
depends=(glibc systemd-libs)
makedepends=(systemd)
replaces=(libusb1 libusbx)
provides=("libusbx=$pkgver" libusb-1.0.so)
conflicts=(libusbx)
source=(https://github.com/libusb/libusb/releases/download/v$pkgver/libusb-$pkgver.tar.bz2)
md5sums=('1e29700f6a134766d32b36b8d1d61a95')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package () {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
