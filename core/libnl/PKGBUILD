# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.8.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=('loong64' 'x86_64')
url='https://github.com/thom311/libnl/'
license=('GPL')
depends=('glibc' 'gcc-libs')
provides=('libnl-3.so' 'libnl-cli-3.so' 'libnl-genl-3.so' 'libnl-idiag-3.so'
          'libnl-nf-3.so' 'libnl-route-3.so' 'libnl-xfrm-3.so')
backup=('etc/libnl/classid'
        'etc/libnl/pktloc')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller
source=("https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('bb726c6d7a08b121978d73ff98425bf313fa26a27a331d465e4f1d7ec5b838c6'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
